toolbar_calc_dimensions = [
    ui_toolbar_height = $ui_tool_button_height_xl
    ui_toolbar_map_button_width = (uiwidth 0.175)
    ui_toolbar_button_space = $ui_tool_elem_space_l
]
tool_calc_ui_dimensions_handler toolbar_calc_dimensions
toolbar_calc_dimensions

// Dynamically calculated
ui_toolbar_tools_width = 0

toolbar_actions_left = []
toolbar_actions_right = []

ui_toolbar_left = [
    uifill $ui_toolbar_tools_width 0 [
        uiclamp 1 1 1 1
        uihlist $ui_toolbar_button_space [
            uialign -1 0
            uiclamp 0 0 1 1
            uifill $ui_toolbar_button_space 0

            looplist action $toolbar_actions_left [
                ui_tool_button [
                    uit_icon = @(tool_action_prop [$action] icon)
                    uit_icon_size = $ui_tool_icon_size_m_unscaled
                    uit_icon_align = 0
                    uit_width = $ui_tool_button_height_l_unscaled
                    uit_height = $ui_tool_button_height_l_unscaled
                    uit_tip_action = $action
                    uit_on_click = [
                        tool_do_action $action
                    ]
                ]
            ]

            uialign* 0 -1
        ]
    ]
]

ui_toolbar_right = [
    uifill $ui_toolbar_tools_width 0 [
        uiclamp 1 1 1 1
        uihlist $ui_toolbar_button_space [
            uialign 1 0
            uiclamp 0 0 1 1

            looplist action $toolbar_actions_right [
                ui_tool_button [
                    uit_icon = @(tool_action_prop [$action] icon)
                    uit_icon_size = $ui_tool_icon_size_m_unscaled
                    uit_icon_align = 0
                    uit_width = $ui_tool_button_height_l_unscaled
                    uit_height = $ui_tool_button_height_l_unscaled
                    uit_tip_action = $action
                    uit_on_click = [
                        tool_do_action $action
                    ]
                ]
            ]

            uialign* 0 -1
            uifill $ui_toolbar_button_space 0
        ]
    ]
]

ui_toolbar_map = [
    ui_tool_button [
        uit_width = $ui_toolbar_map_button_width
        uit_height = $ui_tool_button_height_xl_unscaled
        uit_children = [
            uihlist $ui_tool_elem_space_s [
                uiimage "textures/icons/editing" $ui_tool_accent_colour 0 $ui_tool_icon_size_m $ui_tool_icon_size_m
                uitext $mapname $ui_tool_text_size_m
                uifill $ui_tool_elem_space_s
                uicolourtext (concatword "r" (maprevision)) $ui_tool_dark_accent_colour $ui_tool_text_size_xs
            ]
        ]
        uit_on_click = [
            toolpanel_open tool_map_settings center [
                uit_title = "Map settings"
            ]
        ]
    ]
]

ui_toolbar_controls_settings_contents = []

ui_toolbar_controls_wireframe = [
    tool_edit_outline_sync

    uivlist $ui_tool_elem_space_s [
        uitext "Wireframe settings" $ui_tool_text_size_s

        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        uihlist $ui_tool_elem_space_l [
            uivlist $ui_tool_elem_space_s [
                ui_tool_dropdown tool_edit_outline [
                    "None"
                    "Wireframe"
                    "Outline"
                ] [
                    uit_on_change = [
                        tool_edit_outline_set
                    ]
                ]

                ui_tool_checkbox dtoutline [
                    uit_label = "Outline depth test"
                ]
            ]

            ui_tool_colour outlinecolour [
                uit_label_size = $ui_tool_text_size_xs
                uit_val_format = i
            ]
        ]

        ui_tool_button [
            uit_label = "Close"
            uit_width = $ui_tool_button_width_l_unscaled
            uit_on_click = [
                ui_toolbar_controls_settings_contents = []
            ]
        ]
    ]
]

tool_toolbar_showmat_mask_substs = 0
tool_toolbar_showmat_mask_clips = 0
tool_toolbar_showmat_mask_flags = 0

ui_toolbar_controls_showmat = [
    uivlist $ui_tool_elem_space_s [
        uitext "Material volume overlay" $ui_tool_text_size_s_unscaled

        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        ui_tool_switch showmat [
            uit_label = "Enable overlay"
        ]

        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        uitext "Material visibility" $ui_tool_text_size_xs_unscaled
        uifill 0 $ui_tool_elem_space_s

        ui_tool_switch vismatmask [
            uit_label = "Substances"
            uit_get = [= (& $arg1 0x1C) 0x1C]
            uit_set = [
                if (uit_get $arg1) [
                    & $arg1 (~ 0x1C)
                ] [
                    | $arg1 0x1C
                ]
            ]
        ]

        ui_tool_switch vismatmask [
            uit_label = "Clips"
            uit_get = [= (& $arg1 0xE0) 0xE0]
            uit_set = [
                if (uit_get $arg1) [
                    & $arg1 (~ 0xE0)
                ] [
                    | $arg1 0xE0
                ]
            ]
        ]

        ui_tool_switch vismatmask [
            uit_label = "Other"
            uit_get = [= (& $arg1 0xFF00) 0xFF00]
            uit_set = [
                if (uit_get $arg1) [
                    & $arg1 (~ 0xFF00)
                ] [
                    | $arg1 0xFF00
                ]
            ]
        ]

        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        ui_tool_button [
            uit_label = "Close"
            uit_width = $ui_tool_button_width_l_unscaled
            uit_on_click = [
                ui_toolbar_controls_settings_contents = []
            ]
        ]
    ]
]

ui_toolbar_controls_settings = [
    if $ui_toolbar_controls_settings_contents [
        uiskin 0 0 $ui_tool_base_colour 0 0 [
            uispace $ui_tool_elem_space_l $ui_tool_elem_space_l [
                ui_toolbar_controls_settings_contents
            ]
        ]

        @@(ui_tool_allowinput)
    ]
]

tool_toolbar_focus_wireframe = [
    ui_toolbar_controls_settings_contents = ui_toolbar_controls_wireframe
]

tool_toolbar_focus_showmat = [
    ui_toolbar_controls_settings_contents = ui_toolbar_controls_showmat
]

ui_toolbar_controls = [
    local _space_bottom
    _space_bottom = (+f $ui_tool_elem_space_l 0.02)

    uispace $ui_tool_elem_space_l $_space_bottom [
        uihlist $ui_tool_elem_space_s [
            ui_tool_button [
                uit_label = "WF"
                uit_label_size = $ui_tool_text_size_xs_unscaled
                uit_tip_action = ta_cycle_outline
                uit_colour = @(? (|| $outline $wireframe) $ui_tool_accent_colour $ui_tool_dark_colour)
                uit_width = $ui_tool_button_height_m_unscaled
                uit_height = $ui_tool_button_height_m_unscaled
                uit_on_click = [
                    tool_do_action ta_cycle_outline
                ]
            ]

            ui_tool_button [
                uit_label = "FB"
                uit_label_size = $ui_tool_text_size_xs_unscaled
                uit_tip_action = ta_toggle_fullbright
                uit_colour = @(? $fullbright $ui_tool_accent_colour $ui_tool_dark_colour)
                uit_width = $ui_tool_button_height_m_unscaled
                uit_height = $ui_tool_button_height_m_unscaled
                uit_on_click = [
                    tool_do_action ta_toggle_fullbright
                ]
            ]

            ui_tool_button [
                uit_label = "BG"
                uit_label_size = $ui_tool_text_size_xs_unscaled
                uit_tip_action = ta_toggle_blankgeom
                uit_colour = @(? $blankgeom $ui_tool_accent_colour $ui_tool_dark_colour)
                uit_width = $ui_tool_button_height_m_unscaled
                uit_height = $ui_tool_button_height_m_unscaled
                uit_on_click = [
                    tool_do_action ta_toggle_blankgeom
                ]
            ]

            ui_tool_button [
                uit_label = "MV"
                uit_label_size = $ui_tool_text_size_xs_unscaled
                uit_tip_action = ta_toggle_showmat
                uit_colour = @(? $showmat $ui_tool_accent_colour $ui_tool_dark_colour)
                uit_width = $ui_tool_button_height_m_unscaled
                uit_height = $ui_tool_button_height_m_unscaled
                uit_on_click = [
                    tool_do_action ta_toggle_showmat
                ]
            ]
        ]

        @(ui_tool_allowinput)
    ]
    uialign- -1 1
]

ui_toolbar_sel = [
    local _mat _matname

    uiskin 0 0 $ui_tool_base_colour 0 0 [
        @(ui_tool_allowinput)

        uispace $ui_padsmall $ui_padsmall [
            uiclamp 1 1

            uivlist $ui_tool_elem_space_s [
                uitext "Selection" $ui_tool_text_size_s
                uifill 0 $ui_tool_elem_space_s

                if (< ((getenginestat 41)) 16) [
                    uitext (format "^fAGrid: ^fw%1 (%2cm)" (getenginestat 41) (divf (getenginestat 41) 0.16)) $ui_tool_text_size_xs
                ] [
                    uitext (format "^fAGrid: ^fw%1 (%2m)" (getenginestat 41) (divf (getenginestat 41) 16)) $ui_tool_text_size_xs
                ]

                uitext (format "^fASide: ^fw%1" (getenginestat 40)) $ui_tool_text_size_xs
                uitext (format "^fASize: ^fw%1,%2,%3" (getenginestat 30) (getenginestat 31) (getenginestat 32)) $ui_tool_text_size_xs
                uitext (format "^fAPos: ^fw%1,%2,%3" (getenginestat 27) (getenginestat 28) (getenginestat 29)) $ui_tool_text_size_xs

                if (> $tool_mat_numselmats 0) [
                    uifill 0 $ui_tool_elem_space_s
                    uitext "Materials:" $ui_tool_text_size_xs
                    uifill 0 $ui_tool_elem_space_s

                    loop i $tool_mat_numselmats [
                        _mat = (at $tool_mat_selmats $i)
                        _matname = (at $tool_mat_selmats_names $i)

                        uihlist 0 [
                            uiclamp 1 1

                            uitarget 0 0 [
                                uiclamp 1 1
                                uicolourtext $_matname $ui_tool_dark_accent_colour $ui_tool_text_size_xs
                                uialign- -1

                                uihover [
                                    ui_tool_button [
                                        uit_label = " X "
                                        uit_label_size = $ui_tool_text_size_xs
                                        uit_tip_simple = "Remove material"
                                        uit_pad = 0
                                        uit_on_click = [
                                            editmat air $_mat
                                        ]
                                    ]
                                    uialign- 1
                                ]
                            ]

                        ]
                    ]
                ]

                uialign* -1
            ]
        ]
    ]
]

ui_toolbar_bottom = [
    local _space_bottom
    _space_bottom = (+f $ui_tool_elem_space_l (*f 0.05 $uitoolscale))

    uispace $ui_tool_elem_space_l $_space_bottom [
        uihlist $ui_tool_elem_space_l [
            @@@ui_toolbar_controls_settings
            @@@ui_toolbar_sel
            uialign* -1 1
        ]
    ]
    uialign- -1 1
]

toolbar_calc_layout = [
    ui_toolbar_tools_width = (-f (uiaspect) (*f $ui_toolbar_map_button_width $uitoolscale))
    ui_toolbar_tools_width = (*f $ui_toolbar_tools_width 0.5)
    ui_toolbar_tools_width = (-f $ui_toolbar_tools_width $ui_toolbar_button_space)
]

ui_toolbar = [
    toolbar_calc_layout

    uitarget 0 $ui_toolbar_height [
        uiclamp 1 1
        uialign 0 -1
        uihlist $ui_tool_elem_space_m [
            @@@ui_toolbar_left
            @@@ui_toolbar_map
            @@@ui_toolbar_right
        ]

        @@(ui_tool_allowinput)
    ]

    @ui_toolbar_controls
    @ui_toolbar_bottom
]

toolbar_init = [
    // Left toolbar
    append toolbar_actions_left ta_menu
    append toolbar_actions_left ta_edit_settings
    append toolbar_actions_left ta_save
    append toolbar_actions_left ta_save_as
    append toolbar_actions_left ta_undo
    append toolbar_actions_left ta_redo
    append toolbar_actions_left ta_copy
    append toolbar_actions_left ta_paste
    append toolbar_actions_left ta_search

    // Right toolbar
    append toolbar_actions_right ta_textures
    append toolbar_actions_right ta_env
    append toolbar_actions_right ta_ents
    append toolbar_actions_right ta_mats
]
