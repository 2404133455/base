tool_cursor_icon = ""

TOOL_COPY = 0
TOOL_PASTE = 1

tool_uihover = 0
tool_copy_has_handler = 0
tool_copy_handler_op = -1

tool_copy_do_op = [
    if $tool_copy_has_handler [
        tool_copy_handler_op = $arg1
    ] [
        tool_copy_handler_op = -1
    ]

    result (! $tool_uihover)
]

// 1:<copy code> 2:<paste code>
tool_copy_handler = [
    tool_copy_has_handler = 1

    case $tool_copy_handler_op $TOOL_COPY [
        arg1
    ] $TOOL_PASTE [
        arg2
    ]

    tool_copy_handler_op = -1
]

ui_tool_allowinput = [
    result [
        if $hasinput [
            uihover [
                uiallowinput 1
                tool_uihover = 1
                tool_cursor_icon = ""
            ]
        ]
    ]
]

exec "config/ui/tool/toolview/toolbar.cfg"
exec "config/ui/tool/toolview/toolpanel.cfg"
exec "config/ui/tool/toolview/toolwidgets.cfg"
exec "config/ui/tool/toolview/toolinfo.cfg"

toolview_autoscroll = 0

// 1:<time>
tool_autoscroll = [
    local _period _tension
    _period = 5
    _tension = 2

    clampf (-f (*f (absf (-f (*f (modf (divf $arg1 $_period) 1) 2) 1)) (+f $_tension 1)) (*f $_tension 0.5)) 0 1
]

rl = [
    clearconsole
    clearlog
    exec config/tool.cfg
    exec config/ui/tool.cfg
]

ui_hud_toolview = [
    tool_proc

    tool_copy_has_handler = 0
    tool_uihover = 0
    tool_cursor_icon = ""
    toolview_autoscroll = (tool_autoscroll (divf (getmillis) 1000))

    if $debugruncodestack [
        uitext $runcodestack
    ]

    uifont "tess" [
        uifill (uiaspect) 1
        @@ui_toolbar
        @@ui_toolpanels
        @@ui_toolinfo
    ]

    if (!=s $tool_cursor_icon "") [
        uiimage $tool_cursor_icon 0xffffff 0 0.02 0.02 [
            uiposition (+f (at (uicursorpos) 0) 0.015) (+f (at (uicursorpos) 1) 0.015)
        ]
    ]
]

toolview_init = [
    toolbar_init
]
