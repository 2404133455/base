tool_tip_content_var = []
tool_tip_simple_text = ""
tool_tip_action_name = ""

ui_tool_tip_content_action = [
    local _action_sdesc _action_desc _action_keys _action_type
    _action_sdesc = @(tool_action_prop [$tool_tip_action_name] short_desc)
    _action_desc = @(tool_action_prop [$tool_tip_action_name] long_desc)
    _action_keys = (tool_str_bind_info $tool_tip_action_name)
    _action_type = @(tool_action_prop [$tool_tip_action_name] type)

    uitext $_action_sdesc $ui_toolview_base_text_size
    if (!=s $_action_sdesc $_action_desc) [
        uicolourtext $_action_desc $ui_toolview_mid_accent_colour $ui_toolview_small_text_size
    ]
    if $_action_keys [
        uihlist $ui_toolview_small_elem_space [
            if (= $_action_type $TOOL_ACTION_SCROLL) [
                uiimage "textures/keys/mouse5" $ui_toolview_accent_colour 0 (*f $ui_toolview_small_icon_size 0.8) $ui_toolview_small_icon_size
                _action_keys = (concatword "+" $_action_keys)
            ]
            uicolourtext $_action_keys $ui_toolview_dark_accent_colour $ui_toolview_small_text_size
        ]
    ]
    if (= $_action_type $TOOL_ACTION_SCROLL) [
        uicolourtext "Mouse scroll action" $ui_toolview_dark_accent_colour $ui_toolview_small_text_size
    ]
]

ui_tool_tip_content_simple = [
    uitext $tool_tip_simple_text $ui_toolview_base_text_size
]

ui_tip_tool = [
    if $tool_tip_content_var [
        $tool_tip_content_var
    ]
]

tool_tip_props = [
    [ uit_tip "" ]
    [ uit_tip_simple "" ]
    [ uit_tip_action "" ]
]

// 1:<props>
ui_tooltip_hover = [
    @(tool_props $tool_tip_props arg1)

    tool_tip_content_var = []

    caseif (!=s $uit_tip "") [
        tool_tip_content_var = $uit_tip
    ] (!=s $uit_tip_simple "") [
        tool_tip_simple_text = $uit_tip_simple
        tool_tip_content_var = ui_tool_tip_content_simple
    ] (!=s $uit_tip_action "") [
        tool_tip_action_name = $uit_tip_action
        tool_tip_content_var = ui_tool_tip_content_action
    ]

    if $tool_tip_content_var [
        uitooltip tool
    ]
]

// 1:<props>
ui_tool_tip = [
    uihover [
        ui_tooltip_hover $arg1
    ]
]

T_BUTTON_ICON = 1
T_BUTTON_LABEL = (<< 1 1)

// 1:<action> 2:<has label> 3:<has icon> 4:<align>
ui_tool_action_button = [
    local _code

    _code = (concatword "uit_on_click = [tool_do_action " $arg1 "]^n")
    append _code (concatword "uit_tip_action = " $arg1 "^n")

    if (= $numargs 1) [
        arg2 = 1
        arg3 = 1
    ]

    caseif (!= $arg2) [
        append _code (concatword "uit_label = @(tool_action_prop " $arg1 " short_desc)^n")
        if $arg4 [
            append _code (concatword "uit_label_align = " $arg4 "^n")
        ]
    ] (!= $arg3) [
        append _code "uit_icon_align = 0^n"
    ]

    if (!= $arg3) [
        append _code (concatword "uit_icon = @(tool_action_prop " $arg1 " icon)^n")
    ]

    result $_code
]

ui_tool_preview_spin = [
    result [
        uipreviewyaw (mod (div (getmillis) 10) 360)
    ]
]

tool_vscrollarea_props = [
    [ uit_slider_size $ui_toolpanel_slider_size ]
    [ uit_width $ui_toolpanel_content_width ]
    [ uit_height 0.1 ]
    [ uit_scroll_speed 1 ]
    [ uit_scrollbar_autohide 1 ]
]

// 1:<content> 2:<props>
ui_tool_vscrollarea = [
    @(tool_props $tool_vscrollarea_props arg2)

    local _has_scrollbar
    _has_scrollbar = 0

    uihlist 0 [
        uiscroll 0 $uit_height [
            uialign -1 -1
            doargs @@arg1

            if (|| (! $uit_scrollbar_autohide) [>f (uigetclipvirth) $uit_height]) [
                _has_scrollbar = 1
                uit_width = (-f $uit_width $uit_slider_size)
            ]

            uifill $uit_width
        ]

        if $_has_scrollbar [
            uivscroll $uit_slider_size $uit_height $uit_scroll_speed
        ]
    ]
]

// 1:<w> 2:<h> 3:<children> 4:<disabled>
ui_tool_interactable = [
    result [
        local _highlighting
        _highlighting = (uigethighlight)

        uiborderedimageclamped $skintex $ui_toolview_base_colour 0 $ui_texborder $ui_screenborder @arg1 @arg2 [
            uihold [
                uicolourset $ui_toolview_select_colour
            ] [
                uihover [ _highlighting = 1 ]

                if $_highlighting [
                    uicolourset $ui_toolview_highlight_colour
                ] [
                    uicolourset $ui_toolview_base_colour
                ]
            ]

            @@arg3
        ]
    ]
]

tool_textinput_props = [
    [ uit_label "Text" ]
    [ uit_label_size $ui_toolview_small_text_size ]
    [ uit_prompt "[Enter text here]" ]
    [ uit_immediate 1 ]
    [ uit_on_change [] ]
]

// 1:<var> 2:<length> 3:<props>
ui_tool_textinput = [
    @(tool_props $tool_textinput_props arg3)

    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uitext $uit_label $uit_label_size [ uialign -1 ]
        uiinput $arg1 $arg2 [
            uit_on_change $$arg1
        ] $ui_toolview_small_text_size $uit_immediate $uit_prompt
    ]
]

// 1:<label> 2:<var> 3:<step> 4:<props>
ui_tool_var_input = [
    local _var_type
    _var_type = (getvartype $arg2)

    case $_var_type $ididxvar [
        ui_tool_numinput $arg2 0 0 $arg3 [
            uit_val_format = i
            uit_label = [@arg1]
            uit_immediate = 0
            @arg4
        ]
    ] $ididxfvar [
        ui_tool_numinput $arg2 0 0 $arg3 [
            uit_label = [@arg1]
            uit_immediate = 0
            @arg4
        ]
    ]
]

// 1:<image> 2:<description> 3:<active?>
ui_tool_tab = [
    local _col _img_color _res
    _res = 0
    _col = (? $arg3 $ui_toolview_accent_colour $ui_toolview_base_colour)
    _img_color = (? $arg3 0 $ui_toolview_accent_colour)

    uiborderedimageclamped $skintex $_col 0 $ui_texborder $ui_screenborder 0 0 [
        uispace $ui_toolview_small_elem_space $ui_toolview_small_elem_space [
            uiimage $arg1 $_img_color 0 $uit_icon_size $uit_icon_size
        ]

        uihover [
            if (! $arg3) [
                uicolourset $ui_toolview_highlight_colour
            ]
        ]

        uipress [
            uicolourset $ui_toolview_dark_accent_colour
            _res = 1
        ]

        ui_tool_tip [
            uit_tip_simple = [@@arg2]
        ]
    ]

    result $_res
]

tool_vtabs_props = [
    [ uit_icon_size 0.02 ]
    [ uit_label_size $ui_toolview_small_text_size ]
]

// 1:<id> 2:<icons> 3:<descriptions> 4:<contents> 5:<props>
ui_tool_vtabs = [
    @(tool_props $tool_vtabs_props arg5)

    local _num_tabs _active_idx _is_active _tab_res
    _active_idx = (getalias $arg1)
    _num_tabs = (listlen $arg2)

    uihlist $ui_toolview_base_elem_space [
        uiclamp 1 1

        uivlist $ui_toolview_base_elem_space [
            uihlist $ui_toolview_base_elem_space [
                uiimage (at $arg2 $_active_idx) 0xffffff 0 $uit_icon_size $uit_icon_size
                uitext (at $arg3 $_active_idx) $uit_label_size
            ]
            uialign- -1
            uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
            do $(at $arg4 $_active_idx)
        ]
        uialign- -2 -1

        uicolour $ui_toolview_dark_colour 0 0 [
            uiclamp 0 0 1 1
            uivlist 0 [
                loop tabidx $_num_tabs [
                    _is_active = (= (getalias $arg1) $tabidx)
                    _tab_res = (ui_tool_tab (at $arg2 $tabidx) (at $arg3 $tabidx) $_is_active)
                    if (= $_tab_res 1) [
                        $arg1 = $tabidx
                    ]
                ]
            ]
            uialign- -1 -1
        ]
    ]
]

tool_autoscroll_props = [
    [ uit_width 0.1 ]
    [ uit_text_size $ui_toolview_tiny_text_size ]
]

// 1:<text> 2:<props>
ui_tool_autoscroll_text = [
    @(tool_props $tool_autoscroll_props arg2)

    uiclip $uit_width 0 0 0 [
        uitext $arg1 $uit_text_size
        uiclipoffsetx (*f $toolview_autoscroll (-f (uigetclipvirtw) $uit_width))
    ]
]

tool_param_var = []
tool_param_copy_val = 0
tool_param_copied = 0
tool_param_can_reset = 0
tool_param_reset_val = 0
tool_param_on_change = []

// 1:<value>
tool_param_getter = []

// 1:<new value> 2:<dest var>
tool_param_setter = []

// 1:<var> 2:<value> 3:<on change>
tool_param_set = [
    if (!=s $$arg1 $arg2) [
        set $arg1 $arg2
        doargs $arg3
    ]
]

// 1:<var> 2:<delta> 3:<on change>
tool_param_delta = [
    if $arg2 [
        set $arg1 $arg2
        doargs $arg3
    ]
]

tool_param_copy = [
    tool_param_copy_val = (tool_param_getter $$tool_param_var)
    tool_param_copied = 1
]

tool_param_paste = [
    if $tool_param_copied [
        tool_param_set $tool_param_var (tool_param_setter $tool_param_copy_val $tool_param_var) $tool_param_on_change
    ]
]

tool_param_reset = [
    if $tool_param_can_reset [
        tool_param_set $tool_param_var $tool_param_reset_val $tool_param_on_change
    ]
]

// 1:<var> 2:<can reset> 3:<reset val> 4:<on change> 5:<get> 6:<set>
# tool_param_menu [
    tool_param_var = $arg1
    tool_param_can_reset = $arg2
    tool_param_reset_val = $arg3
    tool_param_on_change = $arg4
    tool_param_getter = (? $arg5 $arg5 [result $arg1])
    tool_param_setter = (? $arg6 $arg6 [result $arg1])

    local _paste_text
    _paste_text = "Paste value"

    if $tool_param_copied [
        _paste_text = (format "Paste value (%1)" $tool_param_copy_val)
    ]

    toolpanel_open_menu [
        uit_width = 0.2
        uit_item_names = [
            "Copy value"
            [#1_paste_text]
            "Reset value"
        ]
        uit_on_select = [
            do (at [
                tool_param_copy
                tool_param_paste
                tool_param_reset
            ] $arg1)
        ]
        uit_disabled = [
            case $arg1 0 [
                result 0
            ] 1 [
                ! $tool_param_copied
            ] 2 [
                ! $tool_param_can_reset
            ]
        ]
    ]
]

tool_confirm_prompt_props = [
    [ uit_width 0.3 ]
    [ uit_noundo_warn 0 ]
]

// 1:<text> 2:<code> 3:<props>
# tool_confirm_prompt [
    @(tool_props $tool_confirm_prompt_props arg3)

    local _menu_items _divider _confirm_idx
    _menu_items = [
        [#1arg1]
    ]
    _divider = 0
    _confirm_idx = 1

    if $uit_noundo_warn [
        append _menu_items ["^foThis operation cannot be undone!"]
        _divider = (+ $_divider 1)
        _confirm_idx = (+ $_confirm_idx 1)
    ]

    append _menu_items "Confirm"

    toolpanel_open_menu [
        uit_width = @uit_width
        uit_nav_enable = 0
        uit_item_names = [#1_menu_items]
        uit_item_dividers = [#1_divider]
        uit_disabled = [!= $arg1 #1_confirm_idx]
        uit_on_select = [#1arg2]
    ]
]

exec "config/ui/tool/toolview/widgets/toolnuminput.cfg"
exec "config/ui/tool/toolview/widgets/tooldropdown.cfg"
exec "config/ui/tool/toolview/widgets/tooltexlist.cfg"
exec "config/ui/tool/toolview/widgets/toolmodellist.cfg"
exec "config/ui/tool/toolview/widgets/toolsearchlist.cfg"
exec "config/ui/tool/toolview/widgets/toolskyboxlist.cfg"
exec "config/ui/tool/toolview/widgets/toolimagelist.cfg"
exec "config/ui/tool/toolview/widgets/toolfilelist.cfg"
exec "config/ui/tool/toolview/widgets/tooltexselect.cfg"
exec "config/ui/tool/toolview/widgets/toolmodelselect.cfg"
exec "config/ui/tool/toolview/widgets/toolsearchselect.cfg"
exec "config/ui/tool/toolview/widgets/toolskyboxselect.cfg"
exec "config/ui/tool/toolview/widgets/toolimageselect.cfg"
exec "config/ui/tool/toolview/widgets/toolfileselect.cfg"
exec "config/ui/tool/toolview/widgets/tooltexinfo.cfg"
exec "config/ui/tool/toolview/widgets/toolcolour.cfg"
exec "config/ui/tool/toolview/widgets/toolcollapsegroup.cfg"
exec "config/ui/tool/toolview/widgets/toolswitch.cfg"
exec "config/ui/tool/toolview/widgets/toolbutton.cfg"
exec "config/ui/tool/toolview/widgets/toolkeyinput.cfg"
exec "config/ui/tool/toolview/widgets/toolmenu.cfg"
