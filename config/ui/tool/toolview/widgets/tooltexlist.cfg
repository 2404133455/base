ui_tool_texlist_sel_colour = 0xFF0000

tool_texlist_groups               = 1
tool_texlist_variants             = 1
tool_texlist_filter_query         = ""
tool_texlist_filter_shader_select = 0
tool_texlist_filter_shader        = []

tool_texlist_shader_filters = [
    [ "B"  "bump"      "Bump map"   ]
    [ "S"  "spec"      "Specular"   ]
    [ "R"  "env"       "Reflective" ]
    [ "G"  "glow"      "Glow"       ]
    [ "P"  "pulse"     "Pulse"      ]
    [ "A"  "alpha"     "Alpha mask" ]
    [ "Px" "parallax"  "Parallax"   ]
    [ "T"  "triplanar" "Triplanar"  ]
    [ "D"  "detail"    "Detail"     ]
]

tool_texlist_shader_filter_bump      = 0
tool_texlist_shader_filter_spec      = 0
tool_texlist_shader_filter_env       = 0
tool_texlist_shader_filter_glow      = 0
tool_texlist_shader_filter_pulse     = 0
tool_texlist_shader_filter_alpha     = 0
tool_texlist_shader_filter_parallax  = 0
tool_texlist_shader_filter_triplanar = 0
tool_texlist_shader_filter_detail    = 0

tool_textlist_shader_make_filter = [
    local _filter_name

    tool_texlist_filter_shader = []

    looplist sfilter $tool_texlist_shader_filters [
        _filter_name = (at $sfilter 1)
        if (getalias [tool_texlist_shader_filter_@_filter_name]) [
            append tool_texlist_filter_shader $_filter_name
        ]
    ]
]

// 1:<slot>
tool_texlist_filter_shader_check = [
    local _shader_name _match
    _shader_name = (getvshadername $arg1)
    _match = 1

    looplist sfilter $tool_texlist_filter_shader [
        if (< (strstr $_shader_name $sfilter) 0) [
            _match = 0
        ]
    ]

    result $_match
]

// 1:<slot>
tool_texlist_filter = [
    local _tex_name _name_match _isvariant_match
    _tex_name = (gettexname $arg1)
    _name_match = (|| [! $tool_texlist_filter_query] [!= (strstr $_tex_name $tool_texlist_filter_query) -1])
    _isvariant_match = (|| [= $tool_texlist_variants 1] [! (istexvariant $arg1)])

    result (&& $_name_match $_isvariant_match (tool_texlist_filter_shader_check $arg1))
]

// 1:<slot>
tool_texlist_decal_filter = [
    local _tex_name
    _tex_name = (getdecalname $arg1)

    || [! $tool_texlist_filter_query] [!= (strstr $_tex_name $tool_texlist_filter_query) -1]
]

// 1:<display type> 2:<slot> 3:<highlight> 4:<var>
ui_tool_texlist_item = [
    local _namefunc _tip
    _namefunc = (? $p_decal getdecalname gettexname)
    _tip = (format "#%1: %2" $arg2 ($_namefunc $arg2))

    $arg1 $arg2 $p_sel_size $p_sel_size [
        uirelease [
            tool_param_set @arg4 @arg2 [@@p_on_change]
            p_on_select
        ]

        uidoublepress $arg2 [
            p_on_doubleclick $arg2
        ]

        uialtrelease [
            p_on_rightclick $arg2
        ]

        uihover [
            if (!=s $p_on_rightclick) [
                tool_rightclickable
            ]
        ]

        ui_tool_tip [
            p_tip_simple = $_tip
        ]

        if $arg3 [
            uioutline $ui_tool_texlist_sel_colour
            uiclamp- 1 1 1 1
        ]
    ]
]

// 1:<var>
ui_tool_texlist_groups = [
    local _curgroup _lastgroup _slot _break
    _curgroup  = ""
    _lastgroup = ""
    _slot      = 0
    _break     = 0

    uivlist $p_sel_space [
        while [< $_slot $num_slots] [
            uigrid $p_sel_cols $p_sel_space $p_sel_space [
                uialign -1 -1
                _break = 0

                while [&& [= $_break 0] [< $_slot $num_slots]] [
                    if ($_filterfunc $_slot) [
                        _curgroup = (gettexgroup $_slot)

                        if (|| [>= $_slot $num_slots] [!=s $_curgroup $_lastgroup]) [
                            _lastgroup = $_curgroup
                            _break     = 1
                            _slot      = (- $_slot 1)
                        ] [
                            ui_tool_texlist_item $_tex_display $_slot (= $_slot $$arg1) $arg1

                            _curgroup = ""
                        ]
                    ]

                    _slot = (+ $_slot 1)
                ]
            ]

            uitext $_curgroup $ui_tool_text_size_xs
            uialign- -1 -1
        ]
    ]
]

// 1:<var>
ui_tool_texlist_simple = [
    uigrid $p_sel_cols $p_sel_space $p_sel_space [
        uialign -1 -1

        loop slot $num_slots [
            if ($_filterfunc $slot) [
                ui_tool_texlist_item $_tex_display $slot (= $slot $$arg1) $arg1
            ]
        ]
    ]
]

tool_texlist_props = [
    [ p_sel_cols            7                         ]
    [ p_sel_size            0.045                     ]
    [ p_sel_area            0.6                       ]
    [ p_sel_space           0.0035                    ]
    [ p_filter_query_length 20                        ]
    [ p_slider_size         $ui_toolpanel_slider_size ]
    [ p_on_change           []                        ]
    [ p_on_select           []                        ]
    [ p_on_doubleclick      []                        ]
    [ p_on_rightclick       []                        ]
    [ p_decal               0                         ]
]

// 1:<select var> 2:<props>
ui_tool_texlist = [
    @(tool_props $tool_texlist_props arg2)

    @(tool_ui_scale p_sel_size)
    @(tool_ui_scale p_sel_space)
    @(tool_ui_scale p_slider_size)

    local _filterfunc _listfunc _tex_display
    _filterfunc = (? $p_decal tool_texlist_decal_filter tool_texlist_filter)

    uivlist $ui_tool_elem_space_s [
        uiclamp 1 1

        uihlist $ui_tool_elem_space_l [
            uialign -1 -1

            if $p_decal [
                _tex_display = uidecalslotview
                num_slots    = $numdecalslots
            ] [
                ui_tool_checkbox tool_texlist_variants [
                    p_label = "Show variants"
                ]
                uialign- -1 -1

                _tex_display = uivslotview
                num_slots    = $numvslots
            ]

            ui_tool_checkbox tool_texlist_groups [
                p_label = "Show groups"
            ]

            uialign* -1 -1
        ]

        _listfunc = (? $tool_texlist_groups ui_tool_texlist_groups ui_tool_texlist_simple)

        uihlist 0 [
            uiclamp 1 1

            ui_tool_textinput tool_texlist_filter_query $p_filter_query_length [
                p_label = "Search:"
            ]

            if $p_decal [] [
                local _shader_filter_label
                _shader_filter_label = (? $tool_texlist_filter_shader "Shader filter (active)" "Shader filter")

                ui_tool_button [
                    p_icon       = "textures/icons/settings"
                    p_tip_simple = $_shader_filter_label
                    p_colour     = @(? $tool_texlist_filter_shader $ui_tool_warn_colour $ui_tool_accent_colour)
                    p_on_click   = [
                        tool_texlist_filter_shader_select = (! $tool_texlist_filter_shader_select)
                    ]
                ]
            ]
        ]

        if (&& (! $p_decal) $tool_texlist_filter_shader_select) [
            uihlist 0 [
                uiclamp 1 1
                uitext "Shader filter:" $ui_tool_text_size_xs
                uialign- -1

                uihlist 0 [
                    uialign 1
                    p_sel_area = (-f $p_sel_area 0.025)

                    looplist sfilter $tool_texlist_shader_filters [
                        ui_tool_compactswitch [tool_texlist_shader_filter_@(at $sfilter 1)] [
                            p_label      = @(at $sfilter 0)
                            p_height     = 0.025
                            p_width      = 0.025
                            p_tip_simple = [@(at $sfilter 2)]
                            p_on_change  = [
                                tool_textlist_shader_make_filter
                            ]
                        ]
                    ]
                ]
            ]
        ]

        uifill 0 $ui_tool_elem_space_l

        uihlist 0 [
            uiclamp 1 1
            uiscroll 0 $p_sel_area [
                uialign -1 -1
                $_listfunc $arg1
            ]
            uivscroll $p_slider_size $p_sel_area 1
            uialign 1
        ]
    ]
]
