ui_tool_texlist_sel_colour = 0xFF0000

tool_texlist_groups = 1
tool_texlist_variants = 1
tool_texlist_filter_query = ""
tool_texlist_filter_shader_select = 0
tool_texlist_filter_shader = []

tool_texlist_shader_filters = [
    [ "B"  "bump"      "Bump map"     ]
    [ "S"  "spec"      "Specular"     ]
    [ "R"  "env"       "Reflective"   ]
    [ "G"  "glow"      "Glow"         ]
    [ "P"  "pulse"     "Pulse"        ]
    [ "A"  "alpha"     "Alpha mask"   ]
    [ "Px" "parallax"  "Parallax"     ]
    [ "T"  "triplanar" "Triplanar"    ]
    [ "D"  "detail"    "Detail"       ]
]

tool_texlist_shader_filter_bump = 0
tool_texlist_shader_filter_spec = 0
tool_texlist_shader_filter_env = 0
tool_texlist_shader_filter_glow = 0
tool_texlist_shader_filter_pulse = 0
tool_texlist_shader_filter_alpha = 0
tool_texlist_shader_filter_parallax = 0
tool_texlist_shader_filter_triplanar = 0
tool_texlist_shader_filter_detail = 0

tool_textlist_shader_make_filter = [
    local _filter_name

    tool_texlist_filter_shader = []

    looplist sfilter $tool_texlist_shader_filters [
        _filter_name = (at $sfilter 1)
        if (getalias [tool_texlist_shader_filter_@_filter_name]) [
            append tool_texlist_filter_shader $_filter_name
        ]
    ]
]

// 1:<slot>
tool_texlist_filter_shader_check = [
    local _shader_name _match
    _shader_name = (getvshadername $arg1)
    _match = 1

    looplist sfilter $tool_texlist_filter_shader [
        if (< (strstr $_shader_name $sfilter) 0) [
            _match = 0
        ]
    ]

    result $_match
]

// 1:<slot>
tool_texlist_filter = [
    local _tex_name _name_match _isvariant_match
    _tex_name = (gettexname $arg1)
    _name_match = (|| [! $tool_texlist_filter_query] [!= (strstr $_tex_name $tool_texlist_filter_query) -1])
    _isvariant_match = (|| [= $tool_texlist_variants 1] [! (istexvariant $arg1)])
    result (&& $_name_match $_isvariant_match (tool_texlist_filter_shader_check $arg1))
]

// 1:<slot>
tool_texlist_decal_filter = [
    local _tex_name
    _tex_name = (getdecalname $arg1)

    || [! $tool_texlist_filter_query] [!= (strstr $_tex_name $tool_texlist_filter_query) -1]
]

// 1:<display type> 2:<slot> 3:<highlight> 4:<var>
ui_tool_texlist_item = [
    local _namefunc _tip
    _namefunc = (? $uit_decal getdecalname gettexname)
    _tip = (format "#%1: %2" $arg2 ($_namefunc $arg2))

    $arg1 $arg2 $uit_sel_size $uit_sel_size [
        uirelease [
            tool_param_set @arg4 @arg2 [@@uit_on_change]
            uit_on_select
        ]

        uidoublepress $arg2 [
            uit_on_doubleclick $arg2
        ]

        uialtrelease [
            uit_on_rightclick $arg2
        ]

        ui_tool_tip [
            uit_tip_simple = $_tip
        ]

        if $arg3 [
            uioutline $ui_tool_texlist_sel_colour
            uiclamp- 1 1 1 1
        ]
    ]
]

// 1:<var>
ui_tool_texlist_groups = [
    local _curgroup _lastgroup _slot _break
    _curgroup = ""
    _lastgroup = ""
    _slot = 0
    _break = 0

    uivlist $uit_sel_space [
        while [< $_slot $num_slots] [
            uigrid $uit_sel_cols $uit_sel_space $uit_sel_space [
                uialign -1 -1
                _break = 0
                while [&& [= $_break 0] [< $_slot $num_slots]] [
                    if ($_filterfunc $_slot) [
                        _curgroup = (gettexgroup $_slot)

                        if (|| [>= $_slot $num_slots] [!=s $_curgroup $_lastgroup]) [
                            _lastgroup = $_curgroup
                            _break = 1
                            _slot = (- $_slot 1)
                        ] [
                            ui_tool_texlist_item $_tex_display $_slot (= $_slot $$arg1) $arg1

                            _curgroup = ""
                        ]
                    ]

                    _slot = (+ $_slot 1)
                ]
            ]

            uitext $_curgroup $ui_toolview_tiny_text_size
            uialign- -1 -1
        ]
    ]
]

// 1:<var>
ui_tool_texlist_simple = [
    uigrid $uit_sel_cols $uit_sel_space $uit_sel_space [
        uialign -1 -1

        loop slot $num_slots [
            if ($_filterfunc $slot) [
                ui_tool_texlist_item $_tex_display $slot (= $slot $$arg1) $arg1
            ]
        ]
    ]
]

tool_texlist_props = [
    [ uit_sel_cols 7 ]
    [ uit_sel_size 0.045 ]
    [ uit_sel_area 0.6 ]
    [ uit_sel_space 0.0035 ]
    [ uit_filter_query_length 20 ]
    [ uit_slider_size $ui_toolpanel_slider_size ]
    [ uit_on_change [] ]
    [ uit_on_select [] ]
    [ uit_on_doubleclick [] ]
    [ uit_on_rightclick [] ]
    [ uit_decal 0 ]
]

// 1:<select var> 2:<props>
ui_tool_texlist = [
    @(tool_props $tool_texlist_props arg2)

    local _filterfunc _listfunc _tex_display
    _filterfunc = (? $uit_decal tool_texlist_decal_filter tool_texlist_filter)

    uivlist $ui_toolview_small_elem_space [
        uiclamp 1 1

        uihlist $ui_toolview_base_elem_space [
            uialign -1 -1

            if $uit_decal [
                _tex_display = uidecalslotview
                num_slots = $numdecalslots
            ] [
                ui_tool_checkbox tool_texlist_variants [
                    uit_label = "Show variants"
                ]
                uialign- -1 -1

                _tex_display = uivslotview
                num_slots = $numvslots
            ]

            ui_tool_checkbox tool_texlist_groups [
                uit_label = "Show groups"
            ]
            uialign* -1 -1
        ]

        _listfunc = (? $tool_texlist_groups ui_tool_texlist_groups ui_tool_texlist_simple)

        uihlist 0 [
            uiclamp 1 1
            ui_tool_textinput tool_texlist_filter_query $uit_filter_query_length [
                uit_label = "Search:"
            ]
            if $uit_decal [] [
                local _shader_filter_label
                _shader_filter_label = (? $tool_texlist_filter_shader "Shader filter (active)" "Shader filter")
                ui_tool_button [
                    uit_icon = "textures/icons/settings"
                    uit_tip_simple = $_shader_filter_label
                    uit_colour = @(? $tool_texlist_filter_shader $ui_toolview_warn_colour $ui_toolview_accent_colour)
                    uit_on_click = [
                        tool_texlist_filter_shader_select = (! $tool_texlist_filter_shader_select)
                    ]
                ]
            ]
        ]

        if (&& (! $uit_decal) $tool_texlist_filter_shader_select) [
            uihlist 0 [
                uiclamp 1 1
                uitext "Shader filter:" $ui_toolview_tiny_text_size
                uialign- -1
                uihlist 0 [
                    uialign 1
                    uit_sel_area = (-f $uit_sel_area 0.025)
                    looplist sfilter $tool_texlist_shader_filters [
                        ui_tool_compactswitch [tool_texlist_shader_filter_@(at $sfilter 1)] [
                            uit_label = @(at $sfilter 0)
                            uit_height = 0.025
                            uit_width = 0.025
                            uit_tip_simple = [@(at $sfilter 2)]
                            uit_on_change = [
                                tool_textlist_shader_make_filter
                            ]
                        ]
                    ]
                ]
            ]
        ]

        uifill 0 $ui_toolview_base_elem_space

        uihlist 0 [
            uiclamp 1 1
            uiscroll 0 $uit_sel_area [
                uialign -1 -1
                $_listfunc $arg1
            ]
            uivscroll $uit_slider_size $uit_sel_area 1
            uialign 1
        ]
    ]
]
