tool_imageselect_var = []
tool_imageselect_images = []

tool_imageselect_menu_props = [
    [ uit_sel_cols 2 ]
    [ uit_sel_size 0.16 ]
    [ uit_sel_area 0.4 ]
    [ uit_sel_space 0.0035 ]
    [ uit_on_change [] ]
]

ui_tool_imageselect_picker = [
    @(tool_props $tool_imageselect_menu_props (toolpanel_attr popup user_data))

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uiclamp 1 1
        ui_tool_imagelist $tool_imageselect_var $tool_imageselect_images [
            uit_sel_cols = @uit_sel_cols
            uit_sel_size = @uit_sel_size
            uit_sel_area = @uit_sel_area
            uit_sel_space = @uit_sel_space
            uit_on_change = [@@uit_on_change]
        ]
    ]
]

tool_imageselect_props = [
    [ uit_disabled 0 ]
    [ uit_sel_cols 2 ]
    [ uit_sel_size 0.16 ]
    [ uit_sel_area 0.4 ]
    [ uit_sel_space 0.0035 ]
    [ uit_can_reset 0 ]
    [ uit_reset_val 0 ]
    [ uit_text_size $ui_tool_text_size_xs_unscaled ]
    [ uit_tip_simple "" ]
    [ uit_size 0.06 ]
    [ uit_picker_width [(uiwidth 0.2)] ]
    [ uit_align 1 ]
    [ uit_on_change [] ]
]

// 1:<var> 2:<images> 3:<props>
ui_tool_imageselect = [
    @(tool_props $tool_imageselect_props arg3)

    @(tool_ui_scale uit_sel_size)
    @(tool_ui_scale uit_sel_space)
    @(tool_ui_scale uit_text_size)
    @(tool_ui_scale uit_size)
    @(tool_ui_scale uit_picker_width)

    local _value
    _value = (? $uit_disabled "" $$arg1)

    uivlist 0 [
        uicolour 0 0 0 [
            uiimage (concatword "<thumbnail:256/256>" $_value) 0xffffff 0 $uit_size $uit_size

            if $uit_disabled [] [
                ui_tool_tip [
                    uit_tip_simple = [@@uit_tip_simple]
                ]

                uirelease [
                    tool_imageselect_var = $arg1
                    tool_imageselect_images = $arg2

                    toolpanel_open tool_imageselect_picker popup [
                        uit_position = (uicursorpos)
                        uit_width = @uit_picker_width
                        uit_user_data = [
                            uit_sel_cols = @@uit_sel_cols
                            uit_sel_size = @@uit_sel_size
                            uit_sel_area = @@uit_sel_area
                            uit_sel_space = @@uit_sel_space
                            uit_on_change = [@@@uit_on_change]
                        ]
                    ]
                ]

                uialtrelease [
                    tool_param_menu $arg1 [
                        uit_can_reset = @@uit_can_reset
                        uit_reset_val = [@@@uit_reset_val]
                        uit_on_change = [@@@uit_on_change]
                    ]
                ]

                uihover [
                    tool_rightclickable

                    tool_copy_handler [
                        tool_param_copy @@arg1
                    ] [
                        tool_param_paste @@arg1 [
                            uit_on_change = [@@@uit_on_change]
                        ]
                    ]
                ]
            ]
        ]

        uitext (? $_value $_value "No texture selected" ) $uit_text_size

        uialign* $uit_align
    ]
]
