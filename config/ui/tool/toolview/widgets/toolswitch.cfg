tool_switch_props = [
    [ uit_disabled 0 ]
    [ uit_options [["Off" "On"]] ]
    [ uit_label "" ]
    [ uit_label_size $ui_tool_text_size_xs_unscaled ]
    [ uit_option_size $ui_tool_text_size_xs_unscaled ]
    [ uit_width 0 ]
    [ uit_height 0 ]
    [ uit_tip "" ]
    [ uit_tip_simple "" ]
    [ uit_on_change [] ]
    [ uit_get [[result $arg1]] ]
    [ uit_set [[! $arg1]] ]
]

// 1:<var> 2:<props>
ui_tool_switch = [
    @(tool_props $tool_switch_props arg2)

    @(tool_ui_scale uit_label_size)
    @(tool_ui_scale uit_option_size)
    @(tool_ui_scale uit_width)
    @(tool_ui_scale uit_height)

    local _check

    if $uit_disabled [
        _check = -1
    ] [
        _check = (uit_get $$arg1)
    ]

    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uitext $uit_label $uit_label_size [ uialign -1 ]
        @(ui_tool_interactable [$uit_width] [$uit_height] [
            uispace $ui_tool_elem_space_l $ui_tool_elem_space_s [
                uihlist 0 [
                    loop state 2 [
                        local _col
                        _col = (? (= $_check $state) $ui_tool_accent_colour $ui_tool_dark_colour)

                        uispace $ui_tool_elem_space_s $ui_tool_elem_space_s [
                            uicolourtext (at $uit_options $state) $_col $uit_option_size
                        ]
                    ]
                ]
            ]

            ui_tool_tip [
                uit_tip = [@@@uit_tip]
                uit_tip_simple = [@@@uit_tip_simple]
            ]

            if $uit_disabled [] [
                uirelease [
                    set $arg1 (uit_set $$arg1)
                    uit_on_change
                ]
            ]
        ] [$uit_disabled])
    ]
]

tool_compactswitch_props = [
    [ uit_disabled 0 ]
    [ uit_label "Enable" ]
    [ uit_label_size $ui_tool_text_size_xs_unscaled ]
    [ uit_width 0 ]
    [ uit_height 0 ]
    [ uit_tip "" ]
    [ uit_tip_simple "" ]
    [ uit_on_change [] ]
    [ uit_get [[result $arg1]] ]
    [ uit_set [[! $arg1]] ]
]

// 1:<var> 2:<props>
ui_tool_compactswitch = [
    @(tool_props $tool_compactswitch_props arg2)

    @(tool_ui_scale uit_label_size)
    @(tool_ui_scale uit_width)
    @(tool_ui_scale uit_height)

    local _check _col
    _check = (uit_get $$arg1)
    _col = (? (&& $_check [! $uit_disabled]) $ui_tool_accent_colour $ui_tool_dark_colour)

    @(ui_tool_interactable [$uit_width] [$uit_height] [
        uispace $ui_padsmall $ui_padsmaller [
            uicolourtext $uit_label $_col $uit_label_size
        ]

        ui_tool_tip [
            uit_tip = [@@uit_tip]
            uit_tip_simple = [@@uit_tip_simple]
        ]

        if $uit_disabled [] [
            uirelease [
                tool_param_set $arg1 (uit_set $$arg1) $uit_on_change
            ]
        ]
    ] [$uit_disabled])
]

tool_checkbox_props = [
    [ uit_disabled 0 ]
    [ uit_label "Enable" ]
    [ uit_label_size $ui_tool_text_size_s_unscaled ]
    [ uit_tip "" ]
    [ uit_tip_simple "" ]
    [ uit_on_change [] ]
    [ uit_size 0.01 ]
    [ uit_get [[result $arg1]] ]
    [ uit_set [[! $arg1]] ]
]

ui_tool_checkbox = [
    @(tool_props $tool_checkbox_props arg2)

    @(tool_ui_scale uit_label_size)
    @(tool_ui_scale uit_size)

    local _check _col
    _check = (uit_get $$arg1)
    _col = (? (&& $_check [! $uit_disabled]) $ui_tool_accent_colour $ui_tool_dark_colour)

    uitarget 0 0 [
        uihlist $ui_tool_elem_space_s [
            @@@(ui_tool_interactable 0 0 [
                uispace $ui_padsmall $ui_padsmall [
                    uicolour $_col $uit_size $uit_size
                ]
            ] [$uit_disabled])

            uicolourtext $uit_label (? $uit_disabled $ui_tool_dark_colour $ui_tool_accent_colour) $uit_label_size
        ]

        if $uit_disabled [] [
            uirelease [
                tool_param_set $arg1 (uit_set $$arg1) $uit_on_change
            ]
        ]

        ui_tool_tip [
            uit_tip = [@@uit_tip]
            uit_tip_simple = [@@uit_tip_simple]
        ]
    ]
]
