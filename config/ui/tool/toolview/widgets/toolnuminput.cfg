tool_numinput_drag_id = []
tool_numinput_focus_id = []
tool_numinput_focus = 0
tool_numinput_drag_old_val = 0
tool_numinput_drag_val = 0
tool_numinput_drag_var = []
tool_numinput_edit = 0
tool_numinput_last_val = 0
tool_numinput_last_distance = 0

tool_numinput_drag_mul = 75
tool_numinput_drag_millis = 0

tool_numinput_get_drag = [
    local _distance _distance_d

    _distance = (toint (round (*f (uimousetrackx) $tool_numinput_drag_mul)))
    _distance_d = (- $_distance $tool_numinput_last_distance)
    tool_numinput_last_distance = $_distance

    result $_distance_d
]

// 1:<pivot val> 2:<min> 3:<max> 4:<step>
tool_numinput_drag_f = [
    local _drag _new_val _key_mods

    uilockcursor
    uicursortype 2

    _drag = (tool_numinput_get_drag)
    _key_mods = (getkeymodifiers)

    if (>f $arg4 0) [
        caseif (& $_key_mods $KMOD_SHIFT) [
            arg4 = (*f $arg4 10)
        ] (& $_key_mods @KMOD_ALT) [
            arg4 = (*f $arg4 0.1)
        ]

        _new_val = (+f $tool_numinput_drag_val (*f $arg4 $_drag))
    ] [
        _new_val = (*f $tool_numinput_drag_val (divf 1 (pow 2 (*f -1 $_drag))))
    ]

    _new_val = (round $_new_val 1e-05)

    if (= $arg2 $arg3) [
        case (getvartype $tool_numinput_drag_var) $ididxvar [
            arg2 = (uit_get (getvarmin $tool_numinput_drag_var))
            arg3 = (uit_get (getvarmax $tool_numinput_drag_var))
        ] $ididxfvar [
            arg2 = (uit_get (getfvarmin $tool_numinput_drag_var))
            arg3 = (uit_get (getfvarmax $tool_numinput_drag_var))
        ]
    ]

    if $uit_circular [
        circclampf $_new_val $arg2 $arg3
    ] [
        clampf $_new_val $arg2 $arg3
    ]
]

// 1:<pivot val> 2:<min> 3:<max> 4:<step>
tool_numinput_drag_i = [
    local _drag _new_val _key_mods

    uilockcursor
    uicursortype 2

    _drag = (tool_numinput_get_drag)
    _key_mods = (getkeymodifiers)

    if (> $arg4 0) [
        caseif (& $_key_mods $KMOD_SHIFT) [
            arg4 = (*f $arg4 10)
        ] (& $_key_mods @KMOD_ALT) [
            arg4 = (max (*f $arg4 0.1) 1)
        ]

        _new_val = (+ $tool_numinput_drag_val (* $arg4 $_drag))
    ] [
        _new_val = (toint (*f $tool_numinput_drag_val (divf 1 (pow 2 (*f -1 $_drag)))))
    ]

    if (= $arg2 $arg3) [
        case (getvartype $tool_numinput_drag_var) $ididxvar [
            arg2 = (uit_get (getvarmin $tool_numinput_drag_var))
            arg3 = (uit_get (getvarmax $tool_numinput_drag_var))
        ] $ididxfvar [
            arg2 = (uit_get (getfvarmin $tool_numinput_drag_var))
            arg3 = (uit_get (getfvarmax $tool_numinput_drag_var))
        ]
    ]

    if $uit_circular [
        toint (circclamp $_new_val $arg2 $arg3)
    ] [
        toint (clamp $_new_val $arg2 $arg3)
    ]
]

// 1:<var> 2:<min> 3:<max> 4:<step> 5:<dir>
tool_numinput_scroll_f = [
    local _new_val

    if (>f $arg4 0) [
        _new_val = (+f $$arg1 (*f $arg4 $arg5))
    ] [
        _new_val = (*f $$arg1 (pow 2 $arg5))
    ]

    if $uit_circular [
        circclampf $_new_val $arg2 $arg3
    ] [
        clampf $_new_val $arg2 $arg3
    ]
]

// 1:<var> 2:<min> 3:<max> 4:<step> 5:<dir>
tool_numinput_scroll_i = [
    local _new_val

    if (> $arg4 0) [
        _new_val = (+ $$arg1 (* $arg4 $arg5))
    ] [
        _new_val = (* $$arg1 (pow 2 $arg5))
    ]

    if $uit_circular [
        circclamp $_new_val $arg2 $arg3
    ] [
        clamp $_new_val $arg2 $arg3
    ]
]

tool_numinput_props = [
    [ uit_disabled 0 ]
    [ uit_has_menu 1 ]
    [ uit_can_reset 1 ]
    [ uit_reset_val [] ]
    [ uit_val_format f ]
    [ uit_circular 0 ]
    [ uit_val_size $ui_toolview_small_text_size_unscaled ]
    [ uit_val_text [[result $arg1]] ]
    [ uit_immediate 1 ]
    [ uit_can_edit 1 ]
    [ uit_label "" ]
    [ uit_label_size $ui_toolview_small_text_size_unscaled ]
    [ uit_input_length 8 ]
    [ uit_width $ui_toolview_numinput_size_unscaled ]
    [ uit_height 0 ]
    [ uit_on_change [] ]
    [ uit_delta 0 ]
    [ uit_interval 0 ]
    [ uit_id 0 ]
    [ uit_round 0.0001 ]
    [ uit_get [[result $arg1]] ]
    [ uit_set [[result $arg1]] ]
    [ uit_menu_props [] ]
]

ui_tool_numinput_field = [
    uifield tool_numinput_edit $uit_input_length [] $uit_val_size 0 [
        uieditorsetfocusable 0

        // Focus once
        if $tool_numinput_focus [
            uieditorsetfocus
            tool_numinput_focus = 0
        ]

        // Hide this field when done interacting
        if (uifocus?) [
            uiallowinput 1
        ] [
            tool_numinput_focus_id = []
            _new_value = $tool_numinput_edit
        ]
    ]
]

// 1:<var> 2:<min> 3:<max> 4:<step> 5:<props>
ui_tool_numinput = [
    @(tool_props $tool_numinput_props arg5)

    @(tool_ui_scale uit_val_size)
    @(tool_ui_scale uit_label_size)
    @(tool_ui_scale uit_width)
    @(tool_ui_scale uit_height)

    local _drag _value _col _new_value _drag_func _clamp_func _millis _id
    _id = (concatword $arg1 $uit_id)
    _drag = (=s $tool_numinput_drag_id $_id)
    _drag_func = (? (=s $uit_val_format f) tool_numinput_drag_f tool_numinput_drag_i)
    _clamp_func = (? (=s $uit_val_format f) clampf clamp)

    if $uit_delta [
        arg2 = -99999
        arg3 = 99999
        set $arg1 (uit_set 0)
    ]

    if $_drag [
        _value = $tool_numinput_drag_val
    ] [
        _value = (? $uit_disabled "-" (uit_get $$arg1))
    ]

    if (&& (!=s $_value "-") (=s $uit_val_format f)) [
        _value = (round $_value $uit_round)
    ]

    _col = (? $uit_disabled $ui_toolview_dark_colour $ui_toolview_accent_colour)
    _new_value = ""

    _millis = (getmillis 1)
    if (< (- $_millis $tool_numinput_drag_millis) $uit_interval) [
        uit_immediate = 0
    ]

    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uitext $uit_label $uit_label_size

        @(ui_tool_interactable [$uit_width] [$uit_height] [
            if (=s $tool_numinput_focus_id $_id) [
                uispace $ui_padsmall $ui_padsmaller [
                    @ui_tool_numinput_field
                ]
            ] [
                uispace $ui_padsmall $ui_padsmaller [
                    uitext (uit_val_text $_value) $uit_val_size
                ]

                uispace $ui_padsmall $ui_padsmaller [
                    uialign -1
                    uitriangle $_col 0.005 0.005 90
                ]

                uispace $ui_padsmall $ui_padsmaller [
                    uialign 1
                    uitriangle $_col 0.005 0.005 -90
                ]
            ]

            uihover [
                if (! $_drag) [
                    tool_copy_handler [
                        tool_param_copy @@@@arg1 [
                            uit_get = [@@@@@uit_get]
                        ]
                    ] [
                        tool_param_paste @@@@arg1 [
                            uit_set = [@@@@@uit_set]
                            uit_on_change = [@@@@@uit_on_change]
                        ]
                    ]
                ]
            ] [
                if $_drag [
                    _drag = 0
                ]
            ]

            uidoublepress $_id [
                if (&& [!= $uit_can_edit 0] [! $uit_disabled]) [
                    // Show input field
                    tool_numinput_focus_id = $_id
                    tool_numinput_focus = 1
                    tool_numinput_edit = (uit_get $$arg1)
                    tool_numinput_last_val = 0

                    if $_drag [
                        tool_numinput_drag_id = []
                    ]
                ]
            ]

            uipress [
                if (&& [!=s $tool_numinput_focus_id $_id] [! $uit_disabled]) [
                    // Init _drag
                    tool_numinput_drag_val = (uit_get $$arg1)
                    tool_numinput_drag_old_val = (uit_get $$arg1)
                    tool_numinput_drag_id = $_id
                    tool_numinput_drag_var = $arg1
                    tool_numinput_last_val = 0
                    tool_numinput_last_distance = 0
                    tool_numinput_drag_millis = $_millis
                ]
            ]

            uirelease [
                if (&& [!= $_drag 0] [! $uit_disabled]) [
                    if $uit_immediate [] [
                        _new_value = $tool_numinput_drag_val
                    ]

                    tool_numinput_drag_id = []
                ]
            ]

            uialtrelease [
                if (! $uit_disabled) [
                    caseif $_drag [
                        // Cancel _drag
                        tool_numinput_drag_id = []
                        _drag = 0
                        _new_value = $tool_numinput_drag_old_val
                    ] $uit_has_menu [
                        local resetval
                        resetval = [@@@@uit_reset_val]

                        if (=s $resetval) [
                            resetval = (? (=s $uit_val_format f) (getfvardef $arg1) (getvardef $arg1))
                        ]

                        tool_param_menu $arg1 [
                            uit_can_reset = @@@@uit_can_reset
                            uit_reset_val = [@@resetval]
                            uit_on_change = [@@@@@uit_on_change]
                            uit_get = [@@@@@uit_get]
                            uit_set = [@@@@@uit_set]
                            @@@@uit_menu_props
                        ]
                    ]
                ]
            ]
        ])
    ]

    if (&& [!= $_drag 0] [! $uit_disabled]) [
        tool_numinput_drag_val = ($_drag_func $tool_numinput_drag_old_val $arg2 $arg3 $arg4)

        if $uit_immediate [
            _new_value = $tool_numinput_drag_val
            tool_numinput_drag_millis = $_millis
        ]
    ]

    if (!=s $_new_value "") [
        if (= $arg2 $arg3) [
            case (getvartype $arg1) $ididxvar [
                arg2 = (uit_get (getvarmin $arg1))
                arg3 = (uit_get (getvarmax $arg1))
            ] $ididxfvar [
                arg2 = (uit_get (getfvarmin $arg1))
                arg3 = (uit_get (getfvarmax $arg1))
            ]
        ]

        _new_value = ($_clamp_func $_new_value $arg2 $arg3)
        if $uit_delta [
            local _delta
            if (=s $uit_val_format f) [
                _delta = (-f $_new_value $tool_numinput_last_val)
            ] [
                _delta = (- $_new_value $tool_numinput_last_val)
            ]
            tool_param_delta $arg1 (uit_set $_delta $arg1) $uit_on_change
            tool_numinput_last_val = $_new_value
        ] [
            tool_param_set $arg1 (uit_set $_new_value $arg1) $uit_on_change
        ]
    ]
]
