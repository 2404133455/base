// 1:<vec> 2:<component> 3:<delta>
tool_vec_get_component = [
    ? $arg3 0 (at $arg1 $arg2)
]

// 1:<vec var> 2:<component> 3:<value> 4:<delta>
tool_vec_set_component = [
    // if $arg3 [echo listsplice "[" 0 0 0 "]" $arg3 $arg2 1]
    listsplice (? $arg4 [0 0 0] $$arg1) $arg3 $arg2 1
]

tool_vec_picker_props = [
    [ uit_on_change [] ]
    [ uit_var [] ]
    [ uit_delta 0 ]
]

# ui_tool_vec_picker [
    @(tool_props $tool_vec_picker_props (toolpanel_attr popup user_data))

    local _step
    _step = (pow 2 $gridpower)

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uiclamp 1 1

        uivlist 0 [
            ui_tool_numinput $uit_var 0 99999999 $_step [
                uit_label = "X"
                uit_on_change = [#1 uit_on_change]
                uit_get = [tool_vec_get_component $arg1 0 #1 uit_delta]
                uit_set = [tool_vec_set_component #1 uit_var 0 $arg1 #1 uit_delta]
                uit_width = $ui_tool_button_width_l_unscaled
                uit_delta = #1 uit_delta
                uit_id = 0
            ]
            ui_tool_numinput $uit_var 0 99999999 $_step [
                uit_label = "Y"
                uit_on_change = [#1 uit_on_change]
                uit_get = [tool_vec_get_component $arg1 1 #1 uit_delta]
                uit_set = [tool_vec_set_component #1 uit_var 1 $arg1 #1 uit_delta]
                uit_width = $ui_tool_button_width_l_unscaled
                uit_delta = #1 uit_delta
                uit_id = 1
            ]
            ui_tool_numinput $uit_var 0 99999999 $_step [
                uit_label = "Z"
                uit_on_change = [#1 uit_on_change]
                uit_get = [tool_vec_get_component $arg1 2 #1 uit_delta]
                uit_set = [tool_vec_set_component #1 uit_var 2 $arg1 #1 uit_delta]
                uit_width = $ui_tool_button_width_l_unscaled
                uit_delta = #1 uit_delta
                uit_id = 2
            ]

            uiclamp* 1 1
        ]
    ]
]

tool_vec_props = [
    [ uit_disabled 0 ]
    [ uit_label "" ]
    [ uit_label_size $ui_tool_text_size_s_unscaled ]
    [ uit_val_size $ui_tool_text_size_xs_unscaled ]
    [ uit_pad $ui_padsmaller ]
    [ uit_width $ui_tool_button_width_s_unscaled ]
    [ uit_on_change [] ]
    [ uit_delta 0 ]
    [ uit_get [[result $arg1]] ]
    [ uit_set [[result $arg1]] ]
]

// 1:<var> 2:<props>
# ui_tool_vec [
    @(tool_props $tool_vec_props arg2)

    local _value
    _value = [- - -]

    if $uit_disabled [] [
        if $uit_delta [
            _value = [0 0 0]
            uit_get = [result 0]
        ] [
            _value = (uit_get $$arg1)
        ]
    ]

    uihlist $ui_tool_elem_space_l [
        uiclamp 1 1

        uitext $uit_label $uit_label_size

        @@(ui_tool_interactable [$uit_width] 0 [
            uispace $uit_pad $uit_pad [
                uigrid 2 $ui_tool_elem_space_s $ui_tool_elem_space_s [
                    uitext "^fAX:" $uit_val_size; uitext (at $_value 0) $uit_val_size; uialign- 1
                    uitext "^fAY:" $uit_val_size; uitext (at $_value 1) $uit_val_size; uialign- 1
                    uitext "^fAZ:" $uit_val_size; uitext (at $_value 2) $uit_val_size; uialign- 1
                ]
            ]

            if $uit_disabled [] [
                uirelease [
                    toolpanel_open tool_vec_picker popup [
                        uit_position = (uicursorpos)
                        uit_width = (uiwidth 0.15)
                        uit_user_data = [
                            uit_on_change = [#1 uit_on_change]
                            uit_var = #1 arg1
                            uit_delta = #1 uit_delta
                        ]
                    ]
                ]

                uialtrelease [
                    tool_param_menu $arg1 [
                        uit_can_reset = 0
                        uit_on_change = [#1 uit_on_change]
                    ]
                ]

                uihover [
                    tool_rightclickable

                    tool_copy_handler [
                        tool_param_copy #1 arg1
                    ] [
                        tool_param_paste #1 arg1 [
                            uit_on_change = [#1 uit_on_change]
                        ]
                    ]
                ]
            ]
        ])
    ]
]
