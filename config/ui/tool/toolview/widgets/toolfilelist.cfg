tool_filelist_filter_query = ""

tool_filelist_curdir = ""
tool_filelist_dirs = []
tool_filelist_files = []
tool_filelist_numdirs = 0
tool_filelist_numfiles = 0
tool_filelist_sel_type = 0
tool_filelist_sel_index = -1
tool_filelist_sel_path = ""

tool_filelist_updir = [
    local _path
    _path = (strreplace $tool_filelist_curdir "/" " ")
    _path = (listsplice $_path "" (- (listlen $_path) 1) 1)
    tool_filelist_curdir = (strreplace $_path " " "/")

    tool_filelist_fetchdir
]

// 1:<var>
tool_filelist_select = [
    local _selname
    _selname = ""

    if (= $tool_filelist_sel_type 0) [
        _selname = (at $tool_filelist_files $tool_filelist_sel_index)
    ] [
        _selname = (at $tool_filelist_dirs $tool_filelist_sel_index)
    ]

    if (!=s $tool_filelist_curdir) [
        _selname = (concatword $tool_filelist_curdir "/" $_selname)
    ]

    if $uit_strip_ext [
        _selname = (filenoext $_selname)
    ]

    tool_param_set $arg1 $_selname $uit_on_change
]

// 1:<index> 2:<name> 3:<selected> 4:<dir> 5:<var>
ui_tool_filelist_item = [
    local _icon_size
    _icon_size = (*f $uit_sel_size 0.8)

    uitarget $uit_sel_size $uit_sel_size [
        if (uidrawn) [
            local _fullfname

            if $uit_strip_ext [
                _fullfname = (filenoext $arg2)
            ] [
                _fullfname = $arg2
            ]

            uivlist 0 [
                local _text_area _fname
                _text_area = (*f $uit_sel_size 0.8)
                _fname = (filename $arg2)

                caseif [!= $arg4 0] [
                    uiimage "textures/icons/action" 0xffffff 0 $_icon_size $_icon_size
                ] [tool_file_isimage $arg2] [
                    uithumbnailclamped (concatword "<thumbnail:64,-1>" $tool_filelist_curdir "/" $arg2) $_icon_size $_icon_size
                ] [&& (=s $_fname "textures") [= $uit_file_type $TOOL_FILE_ASSETPACK]] [
                    uiimage "textures/icons/edit/texture" 0xffffff 0 $_icon_size $_icon_size
                ] [&& (=s $_fname "decals") [= $uit_file_type $TOOL_FILE_ASSETPACK]] [
                    uiimage "textures/icons/edit/decal" 0xffffff 0 $_icon_size $_icon_size
                ] [&& (=s $_fname "models") [= $uit_file_type $TOOL_FILE_ASSETPACK]] [
                    uiimage "textures/icons/edit/cube" 0xffffff 0 $_icon_size $_icon_size
                ] [&& (=s $_fname "sounds") [= $uit_file_type $TOOL_FILE_ASSETPACK]] [
                    uiimage "textures/icons/edit/sound" 0xffffff 0 $_icon_size $_icon_size
                ] () [
                    uiimage "textures/icons/edit/new" 0xffffff 0 $_icon_size $_icon_size
                ]

                ui_tool_autoscroll_text $_fullfname [
                    uit_width = @_text_area
                ]
            ]

            uipress [
                local _selname
                _selname = (? (= $arg4 0) (at $tool_filelist_files $arg1) (at $tool_filelist_dirs $arg1))
                tool_filelist_sel_type = $arg4
                tool_filelist_sel_index = $arg1
                if (!=s $tool_filelist_curdir) [
                    tool_filelist_sel_path = (concatword $tool_filelist_curdir "/" $_selname)
                ] [
                    tool_filelist_sel_path = $_selname
                ]
            ]

            uidoublepress (concatword tool_filelist_item_ (at ["f" "d"] $arg4) _ $arg1) [
                if (!= $arg4 0) [
                    if (!=s $tool_filelist_curdir) [
                        appendword tool_filelist_curdir (concatword "/" $_fullfname)
                    ] [
                        tool_filelist_curdir = $_fullfname
                    ]
                    tool_filelist_fetchdir
                ] [
                    tool_filelist_select $arg5
                ]
            ]

            if (&& (= $tool_filelist_sel_type $arg4) [= $tool_filelist_sel_index $arg1]) [
                uicolour $ui_tool_skyboxlist_sel_colour 0 0 [
                    uichangeblends 0.2
                ]
                uiclamp- 1 1 1 1
            ]

            if (=s $$arg5 (concatword $tool_filelist_curdir "/" $_fullfname)) [
                uioutline $ui_tool_skyboxlist_sel_colour
                uiclamp- 1 1 1 1
            ]
        ]
    ]
]

// 1:<list>
tool_filelist_rem_hidden = [
    local _done _index
    _index = -1
    _done = 0

    while [(= $_done 0)] [
        _index = (listfind i $$arg1 [
            tool_file_ishidden $i
        ])

        if (>= $_index 0) [
            $arg1 = (listsplice $$arg1 "" $_index 1)
        ] [
            _done = 1
        ]
    ]
]

tool_filelist_filter_type = [
    local _done _index
    _index = -1
    _done = 0

    if (!= $uit_file_type @TOOL_FILE_ANY) [
        while [(= $_done 0)] [
            _index = (listfind i $tool_filelist_files [
                ! (tool_file_is_type $i $uit_file_type)
            ])

            if (>= $_index 0) [
                tool_filelist_files = (listsplice $tool_filelist_files "" $_index 1)
            ] [
                _done = 1
            ]
        ]
    ]
]

tool_filelist_asset_types = [
    textures
    sounds
    models
    decals
]

// 1:<dir>
tool_filelist_assetscan = [
    local _dirs _files _assetname
    _files = []
    _dirs = (listfiles (concatword "data/" $arg1) "" 2)

    looplist dir $_dirs [
        if (!=s (substr $dir 0 1) ".") [
            tool_filelist_assetscan $dir
            _files = (listfiles (concatword "data/" $arg1 "/" $dir) "" 1)

            looplist type $tool_filelist_asset_types [
                if (>= (listfind=s $_files (concatword $type ".cfg")) 0) [
                    if (=s $arg1) [
                        _assetname = (concatword $dir "/" $type)
                    ] [
                        _assetname = (concatword $arg1 "/" $dir "/" $type)
                    ]
                    append tool_filelist_files $_assetname
                    tool_filelist_numfiles = (+ $tool_filelist_numfiles 1)
                ]
            ]
        ]
    ]
]

tool_filelist_fetchdir = [
    // Fetch dirs and files only when not scanning for asset packs
    if (!= $uit_file_type $TOOL_FILE_ASSETPACK) [
        tool_filelist_dirs = (listfiles (concatword "data/" $tool_filelist_curdir) "" 2)
        tool_filelist_files = (listfiles (concatword "data/" $tool_filelist_curdir) "" 1)

        tool_filelist_rem_hidden tool_filelist_dirs
        tool_filelist_rem_hidden tool_filelist_files

        tool_filelist_filter_type

        tool_filelist_numdirs = (listlen $tool_filelist_dirs)
        tool_filelist_numfiles = (listlen $tool_filelist_files)
    ] [
        tool_filelist_files = []
        tool_filelist_numdirs = 0
        tool_filelist_numfiles = 0
        tool_filelist_assetscan $tool_filelist_curdir
    ]

    tool_filelist_sel_type = 0
    tool_filelist_sel_index = -1
]

// 1:<item>
tool_filelist_filter = [
    result (|| [! $tool_filelist_filter_query] [!= (strcasestr $arg1 $tool_filelist_filter_query) -1])
]

tool_filelist_props = [
    [ uit_sel_size 0.08 ]
    [ uit_sel_area 0.45 ]
    [ uit_sel_space 0.0035 ]
    [ uit_filter_query_length 32 ]
    [ uit_columns 5 ]
    [ uit_slider_size $ui_toolpanel_slider_size ]
    [ uit_file_type $TOOL_FILE_ANY ]
    [ uit_on_change [] ]
    [ uit_width 0 ]
]

// 1:<var> 2:<props>
ui_tool_filelist = [
    @(tool_props $tool_filelist_props arg2)

    @(tool_ui_scale uit_sel_size)
    @(tool_ui_scale uit_sel_space)
    @(tool_ui_scale uit_slider_size)
    @(tool_ui_scale uit_width)

    if $toolpanel_this_isinit [
        tool_filelist_fetchdir
    ]

    uivlist 0 [
        uiclamp 1 1

        ui_tool_textinput tool_filelist_filter_query $uit_filter_query_length [
            uit_label = "Search:"
        ]

        uifill 0 $ui_tool_elem_space_l

        uihlist $ui_tool_elem_space_s [
            ui_tool_button [
                uit_icon = "textures/icons/shock"
                uit_tip_simple = "Refresh"
                uit_icon_size = 0.015
                uit_on_click = [
                    tool_filelist_fetchdir
                ]
            ]

            if (!= $uit_file_type $TOOL_FILE_ASSETPACK) [
                ui_tool_button [
                    uit_disabled = (=s $tool_filelist_curdir "data")
                    uit_icon = "textures/icons/arrow"
                    uit_tip_simple = "Go up"
                    uit_icon_size = 0.015
                    uit_on_click = [
                        tool_filelist_updir
                    ]
                ]
                uitext "^fADirectory: " $ui_tool_text_size_s
                ui_tool_autoscroll_text $tool_filelist_curdir [
                    uit_width = @(*f $uit_width 0.7)
                    uit_text_size = $ui_tool_text_size_s
                ]
            ] [
                uitext "Assets packs:" $ui_tool_text_size_s
            ]

            uialign -1
        ]


        uifill 0 $ui_tool_elem_space_l
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_l

        local _item_name
        _item_name = ""

        uihlist 0 [
            uiclamp 1 1
            uiscroll 0 $uit_sel_area [
                uialign -1 -1
                uigrid $uit_columns $uit_sel_space $uit_sel_space [
                    loop i $tool_filelist_numdirs [
                        _item_name = (at $tool_filelist_dirs $i)
                        if (tool_filelist_filter $_item_name) [
                            ui_tool_filelist_item $i $_item_name 0 1 $arg1
                        ]
                    ]
                    loop i $tool_filelist_numfiles [
                        _item_name = (at $tool_filelist_files $i)
                        if (tool_filelist_filter $_item_name) [
                            ui_tool_filelist_item $i $_item_name 0 0 $arg1
                        ]
                    ]
                ]
            ]
            uivscroll $uit_slider_size $uit_sel_area 1
            uialign 1
        ]

        uifill 0 $ui_tool_elem_space_l
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_l

        uivlist $ui_tool_elem_space_l [
            uiclamp 1 1
            uigrid 2 0 $ui_tool_elem_space_s [
                uitext "^fASelected: " $ui_tool_text_size_s
                ui_tool_autoscroll_text $tool_filelist_sel_path [
                    uit_width = @(*f $uit_width 0.8)
                    uit_text_size = $ui_tool_text_size_s
                ]

                uitext "^fACurrent: " $ui_tool_text_size_s
                ui_tool_autoscroll_text $$arg1 [
                    uit_width = @(*f $uit_width 0.8)
                    uit_text_size = $ui_tool_text_size_s
                ]

                uialign -1
            ]

            uihlist $ui_tool_elem_space_l [
                ui_tool_button [
                    uit_label = "Ok"
                    uit_width = 0.18
                    uit_on_click = [
                        tool_filelist_select @arg1
                    ]
                ]
                ui_tool_button [
                    uit_label = "Cancel"
                    uit_width = 0.18
                    uit_on_click = [
                        toolpanel_close_this
                    ]
                ]
            ]
        ]
    ]
]
