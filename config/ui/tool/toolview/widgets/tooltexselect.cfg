tool_texselect_var = []

tool_texselect_menu_props = [
    [ uit_on_change [] ]
    [ uit_decal 0 ]
]

ui_tool_texselect_picker = [
    @(tool_props $tool_texselect_menu_props (toolpanel_attr popup user_data))

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uiclamp 1 1
        ui_tool_texlist $tool_texselect_var [
            uit_sel_area = 0.3
            uit_on_change = [@@uit_on_change]
            uit_decal = @uit_decal
        ]
    ]
]

tool_texselect_props = [
    [ uit_disabled 0 ]
    [ uit_can_reset 0 ]
    [ uit_reset_val 0 ]
    [ uit_sky_tex 1 ]
    [ uit_text_size $ui_tool_text_size_xs_unscaled ]
    [ uit_size 0.06 ]
    [ uit_picker_width [(uiwidth 0.2)] ]
    [ uit_align -1 ]
    [ uit_on_change [] ]
    [ uit_decal 0 ]
]

// 1:<var> 2:<props>
ui_tool_texselect = [
    @(tool_props $tool_texselect_props arg2)

    @(tool_ui_scale uit_text_size)
    @(tool_ui_scale uit_size)
    @(tool_ui_scale uit_picker_width)

    local _value _viewfunc
    _value = (? $uit_disabled -1 $$arg1)
    _viewfunc = (? $uit_decal uidecalslotview uivslotview)

    if (= $_value 0) [
        _value = (? $uit_sky_tex $$arg1 -1)
    ]

    uivlist 0 [
        $_viewfunc $_value $uit_size $uit_size [
            if (< $_value 0) [
                uioutline (? $uit_disabled $ui_tool_dark_colour $ui_tool_dark_accent_colour)
                uiclamp- 1 1 1 1
            ]

            if $uit_disabled [] [
                ui_tool_tip [
                    uit_tip_simple = "Click to browse textures"
                ]

                uirelease [
                    tool_texselect_var = $arg1

                    toolpanel_open tool_texselect_picker popup [
                        uit_position = (uicursorpos)
                        uit_width = @uit_picker_width
                        uit_user_data = [
                            uit_on_change = [@@@uit_on_change]
                            uit_decal = @@uit_decal
                        ]
                    ]
                ]

                uialtrelease [
                    tool_param_menu $arg1 [
                        uit_can_reset = @@uit_can_reset
                        uit_reset_val = [@@@uit_reset_val]
                        uit_on_change = [@@@uit_on_change]
                    ]
                ]

                uihover [
                    tool_rightclickable

                    tool_copy_handler [
                        tool_param_copy @@arg1
                    ] [
                        tool_param_paste @@arg1 [
                            uit_on_change = [@@@uit_on_change]
                        ]
                    ]
                ]
            ]
        ]

        ui_tool_texinfo $_value [
            uit_disabled = @uit_disabled
            uit_text_size = @uit_text_size
            uit_decal = @uit_decal
        ]

        uialign* $uit_align
    ]
]
