tool_fileselect_var = []

tool_fileselect_menu_props = [
    [ uit_on_change [] ]
    [ uit_sel_size 0.08 ]
    [ uit_sel_area 0.45 ]
    [ uit_sel_space 0.0035 ]
    [ uit_filter_query_length 32 ]
    [ uit_columns 5 ]
    [ uit_slider_size $ui_toolpanel_slider_size ]
    [ uit_file_type $TOOL_FILE_ANY ]
    [ uit_strip_ext 1 ]
    [ uit_width 0 ]
]

ui_tool_fileselect_picker = [
    @(tool_props $tool_fileselect_menu_props (toolpanel_attr center user_data))

    uispace $ui_toolpanel_elem_space $ui_toolview_base_elem_space [
        uiclamp 1 1
        ui_tool_filelist $tool_fileselect_var [
            uit_on_change = [
                @@uit_on_change
                toolpanel_close_this
            ]
            uit_sel_size = @uit_sel_size
            uit_sel_area = @uit_sel_area
            uit_sel_space = @uit_sel_space
            uit_filter_query_length = @uit_filter_query_length
            uit_columns = @uit_columns
            uit_slider_size = @uit_slider_size
            uit_file_type = @uit_file_type
            uit_strip_ext = @uit_strip_ext
            uit_width = @uit_width
        ]
    ]
]

tool_fileselect_props = [
    [ uit_disabled 0 ]
    [ uit_can_reset 0 ]
    [ uit_reset_val 0 ]
    [ uit_text_size $ui_toolview_tiny_text_size_unscaled ]
    [ uit_dir "" ]
    [ uit_file_type $TOOL_FILE_ANY ]
    [ uit_strip_ext 1 ]
    [ uit_size 0.06 ]
    [ uit_picker_width [(uiwidth 0.25)] ]
    [ uit_align 1 ]
    [ uit_sel_size 0.08 ]
    [ uit_sel_area 0.45 ]
    [ uit_sel_space 0.0035 ]
    [ uit_filter_query_length 32 ]
    [ uit_columns 5 ]
    [ uit_slider_size $ui_toolpanel_slider_size ]
    [ uit_show_value 1 ]
    [ uit_on_change [] ]
]

// 1:<var> 2:<props>
ui_tool_fileselect = [
    @(tool_props $tool_fileselect_props arg2)

    @(tool_ui_scale uit_text_size)
    @(tool_ui_scale uit_size)
    @(tool_ui_scale uit_picker_width)
    @(tool_ui_scale uit_sel_size)
    @(tool_ui_scale uit_sel_space)
    @(tool_ui_scale uit_slider_size)

    local _value
    _value = (? $uit_disabled "" $$arg1)

    uivlist 0 [
        uiclamp 1 1

        if $uit_show_value [
            if (= $uit_file_type $TOOL_FILE_IMAGE) [
                uithumbnailclamped (concatword "<thumbnail:256,-1>" $_value) $uit_size $uit_size
            ]
            uitext (? $_value $_value "No file selected") $uit_text_size
        ]

        ui_tool_button [
            uit_disabled = @uit_disabled
            uit_label = "Browse"
            uit_width = 0.1
            uit_on_click = [
                tool_fileselect_var = @arg1

                if $@arg1 [
                    tool_filelist_curdir = (filedir $@@arg1)
                ] [
                    tool_filelist_curdir = @@uit_dir
                ]

                toolpanel_open tool_fileselect_picker center [
                    uit_title = "File browser"
                    uit_width = @uit_picker_width
                    uit_user_data = [
                        uit_on_change = [@@@uit_on_change]
                        uit_sel_size = @@uit_sel_size
                        uit_sel_area = @@uit_sel_area
                        uit_sel_space = @@uit_sel_space
                        uit_filter_query_length = @@uit_filter_query_length
                        uit_columns = @@uit_columns
                        uit_slider_size = @@uit_slider_size
                        uit_file_type = @@uit_file_type
                        uit_width = @uit_picker_width
                        uit_strip_ext = @uit_strip_ext
                    ]
                ]
            ]
        ]

        uialign* $uit_align
    ]
]
