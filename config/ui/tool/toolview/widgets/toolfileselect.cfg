tool_fileselect_menu_props = [
    [ p_var                 []                        ]
    [ p_on_change           []                        ]
    [ p_sel_size            0.08                      ]
    [ p_sel_area            0.45                      ]
    [ p_sel_space           0.0035                    ]
    [ p_filter_query_length 32                        ]
    [ p_columns             5                         ]
    [ p_slider_size         $ui_toolpanel_slider_size ]
    [ p_file_type           $TOOL_FILE_ANY            ]
    [ p_strip_ext           1                         ]
    [ p_width               0                         ]
]

ui_tool_fileselect_picker = [
    @(tool_props $tool_fileselect_menu_props (toolpanel_attr center user_data))

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uiclamp 1 1
        ui_tool_filelist $p_var [
            p_sel_size            = @p_sel_size
            p_sel_area            = @p_sel_area
            p_sel_space           = @p_sel_space
            p_filter_query_length = @p_filter_query_length
            p_columns             = @p_columns
            p_slider_size         = @p_slider_size
            p_file_type           = @p_file_type
            p_strip_ext           = @p_strip_ext
            p_width               = @p_width
            p_on_change           = [
                @@p_on_change
                toolpanel_close_this
            ]
        ]
    ]
]

tool_fileselect_props = [
    [ p_disabled            0                              ]
    [ p_can_reset           1                              ]
    [ p_reset_val           []                             ]
    [ p_text_size           $ui_tool_text_size_xs_unscaled ]
    [ p_dir                 ""                             ]
    [ p_file_type           $TOOL_FILE_ANY                 ]
    [ p_strip_ext           1                              ]
    [ p_size                0.06                           ]
    [ p_picker_width        [(uiwidth 0.25)]               ]
    [ p_align               1                              ]
    [ p_sel_size            0.08                           ]
    [ p_sel_area            0.45                           ]
    [ p_sel_space           0.0035                         ]
    [ p_filter_query_length 32                             ]
    [ p_columns             5                              ]
    [ p_slider_size         $ui_toolpanel_slider_size      ]
    [ p_show_value          1                              ]
    [ p_on_change           []                             ]
]

// 1:<var>
tool_fileselect_browse = [

]

// 1:<var> 2:<props>
# ui_tool_fileselect [
    @(tool_props $tool_fileselect_props arg2)

    @(tool_ui_scale p_text_size)
    @(tool_ui_scale p_size)
    @(tool_ui_scale p_picker_width)
    @(tool_ui_scale p_sel_size)
    @(tool_ui_scale p_sel_space)
    @(tool_ui_scale p_slider_size)

    local _value _browse
    _value = (? $p_disabled "" $$arg1)

    _browse = [
        if $#1 arg1 [
            tool_filelist_curdir = (filedir $#1 arg1)
        ] [
            tool_filelist_curdir = #1 p_dir
        ]

        toolpanel_open tool_fileselect_picker center [
            p_title     = "File browser"
            p_width     = #1 p_picker_width
            p_user_data = [
                p_var                 = #1 arg1
                p_on_change           = [#1 p_on_change]
                p_sel_size            = #1 p_sel_size
                p_sel_area            = #1 p_sel_area
                p_sel_space           = #1 p_sel_space
                p_filter_query_length = #1 p_filter_query_length
                p_columns             = #1 p_columns
                p_slider_size         = #1 p_slider_size
                p_file_type           = #1 p_file_type
                p_width               = #1 p_picker_width
                p_strip_ext           = #1 p_strip_ext
            ]
        ]
    ]

    uivlist 0 [
        uiclamp 1 1

        if $p_show_value [
            uioutline $ui_tool_dark_accent_colour 0 0 [
                uispace $ui_tool_elem_space_s $ui_tool_elem_space_s [
                    uivlist 0 [
                        if (= $p_file_type $TOOL_FILE_IMAGE) [
                            uithumbnailclamped (concatword "<thumbnail:256,-1>" $_value) $p_size $p_size
                        ]
                        uitext (? $_value $_value "No file selected") $p_text_size

                        uialign* $p_align 1
                    ]
                ]

                uirelease [
                    _browse
                ]

                uialtrelease [
                    if (=s $p_reset_val) [
                        p_reset_val = (getsvardef $arg1)
                    ]

                    tool_param_menu $arg1 [
                        p_can_reset = @p_can_reset
                        p_reset_val = [@@p_reset_val]
                        p_on_change = [@@p_on_change]
                    ]
                ]

                uihover [
                    tool_rightclickable

                    tool_copy_handler [
                        tool_param_copy @arg1
                    ] [
                        tool_param_paste @arg1 [
                            p_on_change = [@@p_on_change]
                        ]
                    ]
                ]
            ]
        ] [
            ui_tool_button [
                p_disabled = @p_disabled
                p_label    = "Browse"
                p_width    = 0.1
                p_on_click = [@@_browse]
            ]
        ]

        uialign* $p_align
    ]
]
