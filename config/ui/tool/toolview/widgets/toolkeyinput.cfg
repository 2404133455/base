tool_keyinput_props = [
    [ uit_disabled 0 ]
    [ uit_width 0 ]
    [ uit_height 0 ]
    [ uit_label_size $ui_toolview_small_text_size_unscaled ]
    [ uit_on_change [] ]
    [ uit_bind_command [] ]
    [ uit_bind_action [] ]
    [ uit_var_update 1 ]
]

tool_keyinput_var = ""
tool_keyinput_keystring = ""

// 1:<var> 2:<props>
ui_tool_keyinput = [
    @(tool_props $tool_keyinput_props arg2)

    @(tool_ui_scale uit_width)
    @(tool_ui_scale uit_height)
    @(tool_ui_scale uit_label_size)

    local _bind_info _text _text_col
    _bind_info = []
    if (=s $arg1 $tool_keyinput_var) [
        _text = "[Awaiting keys]"
        _text_col = $ui_toolview_dark_accent_colour
    ] [
        _text = (prettybindinfo (at $$arg1 0) (at $$arg1 1))
        _text_col = $ui_toolview_accent_colour
    ]

    @(ui_tool_interactable [$uit_width] [$uit_height] [
        uispace $ui_padsmall $ui_padsmaller [
            uicolourtext $_text $_text_col $uit_label_size
        ]

        uirelease [
            tool_keyinput_var = $arg1
            tool_keyinput_keystring = ""
        ]

        if (=s $arg1 $tool_keyinput_var) [
            uicombokeyfield tool_keyinput_keystring 24 [
                _bind_info = (makebindinfo $tool_keyinput_keystring)
                uit_on_change $_bind_info
                // update the variable
                if $uit_var_update [
                    $arg1 = $_bind_info
                ]
                // create new bind
                if (&& (!=s $uit_bind_command "") (!=s $uit_bind_action "")) [
                    $uit_bind_command (at $_bind_info 0) [@uit_bind_action] (at $_bind_info 1)
                ]

                tool_keyinput_var = ""
            ] 0.001 [ // small size to make sure it doesn't extend the layout
                // this UI element exists only when we want to grab the key combination,
                // we don't care about its display
                uichangeblends 0
                uieditorsetfocusable 0
                uieditorsetfocus 1
            ]
        ]
    ] [$uit_disabled])
]
