tool_collapsegroup_props = [
    [ uit_disabled 0 ]
    [ uit_label "" ]
    [ uit_label_size $ui_tool_small_text_size_unscaled ]
    [ uit_force_open 0 ]
]

// 1:<id> 2:<children> 3:<props>
ui_tool_collapsegroup = [
    @(tool_props $tool_collapsegroup_props arg3)

    @(tool_ui_scale uit_label_size)

    local _show
    _show = (|| $uit_force_open [? (getalias $arg1) 1 0])

    uitablerow [] [
        uiclamp 1 1
        @(ui_tool_interactable 0 0 [
            uiclamp 1 1

            uispace $ui_padsmall $ui_padsmaller [
                uitext $uit_label $uit_label_size
            ]

            uispace $ui_padsmall $ui_padsmaller [
                uialign 1
                uitriangle $ui_tool_accent_colour 0.005 0.005 (? $_show 180 90)
            ]

            uirelease [
                $arg1 = (! $_show)
            ]
        ])
    ]

    if $_show [
        doargs $arg2
    ]
]