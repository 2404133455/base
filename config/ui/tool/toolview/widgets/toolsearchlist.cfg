tool_searchlist_filter_query = ""

// 1:<_item>
tool_searchlist_filter = [
    result (|| [! $tool_searchlist_filter_query] [!= (strcasestr $arg1 $tool_searchlist_filter_query) -1])
]

tool_searchlist_item_click = 0

// 1:<_item> 2:<highlight>
ui_tool_searchlist_item = [
    tool_searchlist_item_click = 0

    @(ui_tool_interactable 0 0 [
        uiclamp 1 1
        uispace $ui_toolview_small_elem_space $ui_toolview_small_elem_space [
            uiclamp 1 1
            uicolourtext $arg1 (? $arg2 $ui_toolview_sel_accent_colour 0xffffff) $uit_sel_size
            uialign- -1
        ]

        uirelease [
            tool_searchlist_item_click = 1
        ]
    ])
]

tool_searchlist_props = [
    [ uit_sel_size $ui_toolview_small_text_size_unscaled ]
    [ uit_sel_area 0.6 ]
    [ uit_filter_query_length 24 ]
    [ uit_width 0 ]
    [ uit_slider_size $ui_toolpanel_slider_size ]
    [ uit_on_change [] ]
    [ uit_on_select [] ]
    [ uit_num_items_func [] ]
    [ uit_item_func [] ]
]

// 1:<select var> 2:<props>
ui_tool_searchlist = [
    @(tool_props $tool_searchlist_props arg2)

    @(tool_ui_scale uit_sel_size)
    @(tool_ui_scale uit_width)
    @(tool_ui_scale uit_slider_size)

    uivlist 0 [
        local _item_num _item
        _item_num = (uit_num_items_func)

        ui_tool_textinput tool_searchlist_filter_query $uit_filter_query_length [
            uit_label = "Search:"
        ]

        uifill 0 $ui_toolview_base_elem_space

        uihlist 0 [
            uiscroll 0 $uit_sel_area [
                uialign -1 -1
                uivlist 0 [
                    uifill (-f $uit_width $uit_slider_size)
                    loop i $_item_num [
                        _item = (uit_item_func $i)

                        if (tool_searchlist_filter $_item) [
                            ui_tool_searchlist_item $_item (= $i $$arg1)
                            if $tool_searchlist_item_click [
                                tool_param_set $arg1 $i $uit_on_change
                                uit_on_select
                            ]
                        ]
                    ]
                ]
            ]
            uivscroll $uit_slider_size $uit_sel_area 1
            uialign 1
        ]
    ]
]
