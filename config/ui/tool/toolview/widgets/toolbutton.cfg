tool_button_props = [
    [ uit_disabled 0 ]
    [ uit_label "" ]
    [ uit_label_size $ui_toolview_small_text_size ]
    [ uit_label_align 0 ]
    [ uit_pad $ui_padsmaller ]
    [ uit_icon "" ]
    [ uit_icon_size $ui_toolview_base_icon_size ]
    [ uit_icon_align -1 ]
    [ uit_children [] ]
    [ uit_colour $ui_toolview_accent_colour ]
    [ uit_width 0 ]
    [ uit_height 0 ]
    [ uit_tip "" ]
    [ uit_tip_simple "" ]
    [ uit_tip_action "" ]
    [ uit_highlight 0 ]
    [ uit_on_click [] ]
]

// 1:<props>
ui_tool_button = [
    @(tool_props $tool_button_props arg1)

    local colour
    colour = (? $uit_disabled $ui_toolview_dark_colour $uit_colour)

    @(ui_tool_interactable [$uit_width] [$uit_height] [
        uispace $uit_pad $uit_pad [
            uiclamp 1 1 1 1

            doargs $uit_children

            uicolourtext $uit_label $colour $uit_label_size
            uialign- $uit_label_align

            if $uit_icon [
                uiimage $uit_icon $colour 0 $uit_icon_size $uit_icon_size
                uialign- $uit_icon_align
            ]
        ]

        ui_tool_tip [
            uit_tip = [@@uit_tip]
            uit_tip_simple = [@@uit_tip_simple]
            uit_tip_action = [@@uit_tip_action]
        ]

        if $uit_disabled [] [
            uirelease [
                uit_on_click
            ]
        ]
    ] [$uit_disabled])
]

tool_get_action_button_override_props = [
    [ uit_override_label 0 ]
    [ uit_override_icon 0 ]
    [ uit_override_tip 0 ]
    [ uit_override_on_click 0 ]
]

// 1:<action> 2:<props> 3:<override props>
ui_tool_get_action_button = [
    @(tool_props $tool_get_action_button_override_props arg3)

    local prop

    if (= $uit_override_label) [
        prop = @(tool_action_prop [$arg1] short_desc)
        appendword arg2 [uit_label = [@@prop]]
        appendword arg2 "^n"
    ]

    if (= $uit_override_icon) [
        prop = @(tool_action_prop [$arg1] icon)
        appendword arg2 [uit_icon = [@@prop]]
        appendword arg2 "^n"
    ]

    if (= $uit_override_tip) [
        appendword arg2 [uit_tip_action = @@arg1]
        appendword arg2 "^n"
    ]

    if (= $uit_override_on_click) [
        appendword arg2 [uit_on_click = [tool_do_action @@@arg1]]
    ]

    result [
        ui_tool_button [
            @@arg2
        ]
    ]
]
