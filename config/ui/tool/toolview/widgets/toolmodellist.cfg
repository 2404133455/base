ui_tool_modellist_sel_colour = 0xFF0000

tool_modellist_filter_query = ""

// 1:<slot>
tool_modellist_filter = [
    local _model_name
    _model_name = (mapmodelindex $arg1)
    result (|| [! $tool_modellist_filter_query] [!= (strstr $_model_name $tool_modellist_filter_query) -1])
]

tool_modellist_item_click = 0

// 1:<model path>
ui_tool_modellist_item_text = [
    uicolour $ui_tool_text_bg_colour 0 (*f $uit_sel_size 0.2) [
        uiclamp 1 1

        ui_tool_autoscroll_text $arg1 [
            uit_width = @_text_area
        ]
    ]
    uialign- 0 1
]

// 1:<slot> 2:<highlight>
ui_tool_modellist_item = [
    local _model_path _text_area
    _model_path = (mapmodelindex $arg1)
    _model_name = (filename $_model_path)
    _text_area = (*f $uit_sel_size 0.8)

    tool_modellist_item_click = 0

    uicolour 0 $uit_sel_size $uit_sel_size [
        uimodelpreview $_model_path "" 1 1 $uit_sel_size $uit_sel_size [
            uirelease [
                tool_modellist_item_click = 1
            ]
            if $arg2 [
                uioutline $ui_tool_modellist_sel_colour
                uiclamp- 1 1 1 1
            ]
            @@(ui_tool_preview_spin)
        ]

        if (uidrawn) [
            ui_tool_modellist_item_text $_model_path
        ]
    ]
]

tool_modellist_props = [
    [ uit_sel_cols 3 ]
    [ uit_sel_size 0.11 ]
    [ uit_sel_area 0.6 ]
    [ uit_sel_space 0.0035 ]
    [ uit_filter_query_length 24 ]
    [ uit_slider_size $ui_toolpanel_slider_size ]
    [ uit_on_change [] ]
    [ uit_on_select [] ]
]

// 1:<select var> 2:<props>
ui_tool_modellist = [
    @(tool_props $tool_modellist_props arg2)

    @(tool_ui_scale uit_sel_size)
    @(tool_ui_scale uit_sel_space)
    @(tool_ui_scale uit_slider_size)

    uivlist 0 [
        uiclamp 1 1

        local _model_num
        _model_num = (mapmodelindex)

        ui_tool_textinput tool_modellist_filter_query $uit_filter_query_length [
            uit_label = "Search:"
        ]

        uifill 0 $ui_tool_base_elem_space

        uihlist 0 [
            uiclamp 1 1
            uiscroll 0 $uit_sel_area [
                uialign -1 -1
                uigrid $uit_sel_cols $uit_sel_space $uit_sel_space [
                    loop slot $_model_num [
                        if (tool_modellist_filter $slot) [
                            ui_tool_modellist_item $slot (= $slot $$arg1)
                            if $tool_modellist_item_click [
                                tool_param_set $arg1 $slot $uit_on_change
                                uit_on_select
                            ]
                        ]
                    ]
                ]
            ]
            uivscroll $uit_slider_size $uit_sel_area 3
            uialign 1
        ]
    ]
]