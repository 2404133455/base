// widget flags
TOOL_COLOUR_BLUEFIX = 1
TOOL_COLOUR_ZEROFIX = (<< 1 1)

tool_colour_mode = 0
tool_colour_var = []
tool_colour_val_format = v
tool_colour_flags = 0
tool_colour_max_val = 1.0

// 1:<value> 2:<format> 3:<mode>
tool_colour_conv_rgb_vec = [
    local vec
    vec = $arg1

    if (=s $arg2 i) [
        @@(tool_colour_fromint [vec] [arg1])
    ]

    if $arg3 [] [
        vec = (rgbtohsv $vec)
    ]

    result $vec
]

// 1:<value> 2:<format> 3:<mode> 4:<max val> 5:<flags>
tool_colour_conv_storage = [
    local vec
    vec = $arg1

    if $arg3 [
        vec = [@@(minf $arg4 (at $vec 0)) @@(minf $arg4 (at $vec 1)) @@(minf $arg4 (at $vec 2)) ]
    ] [
        vec = [@@(at $vec 0) @@(at $vec 1) @@(minf $arg4 (at $vec 2)) ]
        vec = (hsvtorgb $vec)
    ]

    if (=s $arg2 i) [
        local bluefix zerofix

        bluefix = (& $arg5 $TOOL_COLOUR_BLUEFIX)
        zerofix = (& $arg5 $TOOL_COLOUR_ZEROFIX)

        tool_colour_toint $vec $bluefix $zerofix
    ] [
        result $vec
    ]
]

// 1:<component idx>
tool_colour_get_component = [
    local vec
    vec = (tool_colour_conv_rgb_vec $$tool_colour_var $tool_colour_val_format $tool_colour_mode)

    at $vec $arg1
]

// 1:<component idx> 2:<value>
tool_colour_set_component = [
    local vec
    vec = (tool_colour_conv_rgb_vec $$tool_colour_var $tool_colour_val_format $tool_colour_mode)
    vec = (listsplice $vec $arg2 $arg1 1)

    tool_colour_conv_storage $vec $tool_colour_val_format $tool_colour_mode $tool_colour_max_val $tool_colour_flags
]

tool_colour_get_int = [
    if (=s $tool_colour_val_format i) [
        result $$tool_colour_var
    ] [
        tool_colour_toint $$tool_colour_var
    ]
]

tool_colour_picker_menu_props = [
    [ uit_on_change [] ]
    [ uit_max_val 1 ]
    [ uit_interval 0 ]
]

ui_tool_colour_picker = [
    @(tool_props $tool_colour_picker_menu_props toolpanel_props_menu)

    local label1 label2 label3 ctrl1max ctrl1step

    if $tool_colour_mode [
        label1 = "Red"
        label2 = "Green"
        label3 = "Blue"
        ctrl1max = $uit_max_val
        ctrl1step = 0.1
    ] [
        label1 = "Hue"
        label2 = "Saturation"
        label3 = "Value"
        ctrl1max = 360
        ctrl1step = 1
    ]

    uispace $ui_toolpanel_elem_space $ui_toolview_base_elem_space [
        uiclamp 1 1

        uivlist 0 [
            uiclamp 1 1

            uicolour (tool_colour_get_int) 0 0.02
            uiclamp- 1 1

            ui_tool_switch tool_colour_mode [
                uit_options = ["HSV" "RGB"]
                uit_tip_simple = "Switch colour mode"
            ]
            ui_tool_numinput $tool_colour_var 0 $ctrl1max $ctrl1step [
                uit_label = $label1
                uit_label_size = $ui_toolview_small_text_size
                uit_val_format = (? $tool_colour_mode f i)
                uit_circular = (? $tool_colour_mode 0 1)
                uit_has_menu = 0
                uit_interval = @uit_interval
                uit_on_change = [@@uit_on_change]
                uit_get = [(tool_colour_get_component 0)]
                uit_set = [(tool_colour_set_component 0 $arg1)]
                uit_id = 0
            ]
            ui_tool_numinput $tool_colour_var 0 1 0.1 [
                uit_label = $label2
                uit_label_size = $ui_toolview_small_text_size
                uit_has_menu = 0
                uit_interval = @uit_interval
                uit_on_change = [@@uit_on_change]
                uit_get = [(tool_colour_get_component 1)]
                uit_set = [(tool_colour_set_component 1 $arg1)]
                uit_id = 1
            ]
            ui_tool_numinput $tool_colour_var 0 $uit_max_val 0.1 [
                uit_label = $label3
                uit_label_size = $ui_toolview_small_text_size
                uit_has_menu = 0
                uit_interval = @uit_interval
                uit_on_change = [@@uit_on_change]
                uit_get = [(tool_colour_get_component 2)]
                uit_set = [(tool_colour_set_component 2 $arg1)]
                uit_id = 2
            ]
        ]
    ]
]

tool_colour_props = [
    [ uit_disabled 0 ]
    [ uit_can_reset 0 ]
    [ uit_reset_val 0 ]
    [ uit_label "Colour" ]
    [ uit_label_size $ui_toolview_small_text_size ]
    [ uit_preview_size 0.02 ]
    [ uit_max_val 1 ]
    [ uit_on_change [] ]
    [ uit_interval 0 ]
    [ uit_val_format v ]
    [ uit_flags 0 ]
    [ uit_picker_width [(uiwidth 0.15)] ]
]

// 1:<var> 2:<props>
ui_tool_colour = [
    @(tool_props $tool_colour_props arg2)

    local colour bordercolour
    colour = 0

    if $uit_disabled [] [
        if (=s $uit_val_format i) [
            colour = $$arg1
        ] [
            colour = (tool_colour_toint $$arg1)
        ]
    ]

    bordercolour = (& (~ $colour) 0xFFFFFF)

    uihlist $ui_toolview_base_elem_space [
        uiclamp 1 1

        uitext $uit_label $uit_label_size

        uicolour $colour (*f $uit_preview_size 2) $uit_preview_size [
            uispace $ui_toolview_tiny_elem_space $ui_toolview_tiny_elem_space [
                uiclamp 1 1 1 1

                uioutline $bordercolour
                uiclamp- 1 1 1 1
            ]

            if $uit_disabled [] [
                ui_tool_tip [
                    uit_tip_simple = "Click to change"
                ]

                uirelease [
                    tool_colour_var = $arg1
                    tool_colour_val_format = $uit_val_format
                    tool_colour_flags = $uit_flags
                    tool_colour_max_val = $uit_max_val

                    toolpanel_show tool_colour_picker "" menu [
                        uit_position = (uicursorpos)
                        uit_width = @uit_picker_width
                        uit_on_change = [@@uit_on_change]
                        uit_max_val = @uit_max_val
                        uit_interval = @uit_interval
                    ]
                ]

                uialtrelease [
                    tool_param_menu $arg1 $uit_can_reset $uit_reset_val $uit_on_change [
                        tool_colour_conv_rgb_vec $arg1 @@@@@uit_val_format 1
                    ] [
                        tool_colour_conv_storage $arg1 @@@@@uit_val_format 1 @@@@@uit_max_val @@@@@uit_flags
                    ]
                ]
            ]
        ]
    ]
]
