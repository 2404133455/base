// widget flags
TOOL_COLOUR_BLUEFIX = 1
TOOL_COLOUR_ZEROFIX = (<< 1 1)

tool_colour_mode = 0
tool_colour_var = []
tool_colour_val_format = v
tool_colour_flags = 0
tool_colour_max_val = 1.0

// 1:<value> 2:<format> 3:<mode>
tool_colour_conv_rgb_vec = [
    local _vec
    _vec = $arg1

    if (=s $arg2 i) [
        @@(tool_colour_fromint [_vec] [arg1])
    ]

    if $arg3 [] [
        _vec = (rgbtohsv $_vec)
    ]

    result $_vec
]

// 1:<value> 2:<format> 3:<mode> 4:<max val> 5:<flags>
tool_colour_conv_storage = [
    local _vec
    _vec = $arg1

    if $arg3 [
        _vec = [@@(minf $arg4 (at $_vec 0)) @@(minf $arg4 (at $_vec 1)) @@(minf $arg4 (at $_vec 2)) ]
    ] [
        _vec = [@@(at $_vec 0) @@(at $_vec 1) @@(minf $arg4 (at $_vec 2)) ]
        _vec = (hsvtorgb $_vec)
    ]

    if (=s $arg2 i) [
        local _bluefix _zerofix

        _bluefix = (& $arg5 $TOOL_COLOUR_BLUEFIX)
        _zerofix = (& $arg5 $TOOL_COLOUR_ZEROFIX)

        tool_colour_toint $_vec $_bluefix $_zerofix
    ] [
        result $_vec
    ]
]

// 1:<component idx>
tool_colour_get_component = [
    local _vec
    _vec = (tool_colour_conv_rgb_vec $$tool_colour_var $tool_colour_val_format $tool_colour_mode)

    at $_vec $arg1
]

// 1:<component idx> 2:<value>
tool_colour_set_component = [
    local _vec
    _vec = (tool_colour_conv_rgb_vec $$tool_colour_var $tool_colour_val_format $tool_colour_mode)
    _vec = (listsplice $_vec $arg2 $arg1 1)

    tool_colour_conv_storage $_vec $tool_colour_val_format $tool_colour_mode $tool_colour_max_val $tool_colour_flags
]

tool_colour_get_int = [
    if (=s $tool_colour_val_format i) [
        result $$tool_colour_var
    ] [
        tool_colour_toint $$tool_colour_var
    ]
]

tool_colour_picker_menu_props = [
    [ uit_on_change [] ]
    [ uit_max_val 1 ]
    [ uit_interval 0 ]
]

ui_tool_colour_picker = [
    @(tool_props $tool_colour_picker_menu_props (toolpanel_attr popup user_data))

    local _label1 _label2 _label3 _ctrl1max _ctrl1step

    if $tool_colour_mode [
        _label1 = "Red"
        _label2 = "Green"
        _label3 = "Blue"
        _ctrl1max = $uit_max_val
        _ctrl1step = 0.1
    ] [
        _label1 = "Hue"
        _label2 = "Saturation"
        _label3 = "Value"
        _ctrl1max = 360
        _ctrl1step = 1
    ]

    uispace $ui_toolpanel_elem_space $ui_tool_elem_space_l [
        uiclamp 1 1

        uivlist 0 [
            uiclamp 1 1

            uicolour (tool_colour_get_int) 0 0.02
            uiclamp- 1 1

            ui_tool_switch tool_colour_mode [
                uit_options = ["HSV" "RGB"]
                uit_tip_simple = "Switch colour mode"
            ]
            ui_tool_numinput $tool_colour_var 0 $_ctrl1max $_ctrl1step [
                uit_label = $_label1
                uit_label_size = $ui_tool_text_size_s
                uit_val_format = (? $tool_colour_mode f i)
                uit_circular = (? $tool_colour_mode 0 1)
                uit_has_menu = 0
                uit_interval = @uit_interval
                uit_on_change = [@@uit_on_change]
                uit_get = [(tool_colour_get_component 0)]
                uit_set = [(tool_colour_set_component 0 $arg1)]
                uit_id = 0
            ]
            ui_tool_numinput $tool_colour_var 0 1 0.1 [
                uit_label = $_label2
                uit_label_size = $ui_tool_text_size_s
                uit_has_menu = 0
                uit_interval = @uit_interval
                uit_on_change = [@@uit_on_change]
                uit_get = [(tool_colour_get_component 1)]
                uit_set = [(tool_colour_set_component 1 $arg1)]
                uit_id = 1
            ]
            ui_tool_numinput $tool_colour_var 0 $uit_max_val 0.1 [
                uit_label = $_label3
                uit_label_size = $ui_tool_text_size_s
                uit_has_menu = 0
                uit_interval = @uit_interval
                uit_on_change = [@@uit_on_change]
                uit_get = [(tool_colour_get_component 2)]
                uit_set = [(tool_colour_set_component 2 $arg1)]
                uit_id = 2
            ]
        ]
    ]
]

tool_colour_props = [
    [ uit_disabled 0 ]
    [ uit_can_reset 1 ]
    [ uit_reset_val [] ]
    [ uit_label "Colour" ]
    [ uit_label_size $ui_tool_text_size_s_unscaled ]
    [ uit_preview_size 0.02 ]
    [ uit_max_val 1 ]
    [ uit_on_change [] ]
    [ uit_interval 0 ]
    [ uit_val_format v ]
    [ uit_flags 0 ]
    [ uit_picker_width [(uiwidth 0.15)] ]
    [ uit_tip "" ]
    [ uit_tip_simple ["Click to change"] ]
    [ uit_tip_action "" ]
]

// 1:<var> 2:<props>
# ui_tool_colour [
    @(tool_props $tool_colour_props arg2)

    @(tool_ui_scale uit_label_size)
    @(tool_ui_scale uit_preview_size)
    @(tool_ui_scale uit_picker_width)

    local _col _bordercol
    _col = 0

    if $uit_disabled [] [
        if (=s $uit_val_format i) [
            _col = $$arg1
        ] [
            _col = (tool_colour_toint $$arg1)
        ]
    ]

    _bordercol = (& (~ $_col) 0xFFFFFF)

    uihlist $ui_tool_elem_space_l [
        uiclamp 1 1

        uitext $uit_label $uit_label_size

        uicolour $_col (*f $uit_preview_size 2) $uit_preview_size [
            uispace $ui_tool_elem_space_xs $ui_tool_elem_space_xs [
                uiclamp 1 1 1 1

                uioutline $_bordercol
                uiclamp- 1 1 1 1
            ]

            if $uit_disabled [] [
                ui_tool_tip [
                    uit_tip = [@@uit_tip]
                    uit_tip_simple = [@@uit_tip_simple]
                    uit_tip_action = [@@uit_tip_action]
                ]

                uirelease [
                    tool_colour_var = $arg1
                    tool_colour_val_format = $uit_val_format
                    tool_colour_flags = $uit_flags
                    tool_colour_max_val = $uit_max_val

                    toolpanel_open tool_colour_picker popup [
                        uit_position = (uicursorpos)
                        uit_width = @uit_picker_width
                        uit_user_data = [
                            uit_on_change = [@@@uit_on_change]
                            uit_max_val = @@uit_max_val
                            uit_interval = @@uit_interval
                        ]
                    ]
                ]

                uialtrelease [
                    if (=s $uit_reset_val) [
                        uit_reset_val = (getvardef $arg1)
                    ]

                    tool_param_menu $arg1 [
                        uit_can_reset = @@uit_can_reset
                        uit_reset_val = [@@@uit_reset_val]
                        uit_on_change = [@@@uit_on_change]
                        uit_get = [
                            tool_colour_conv_rgb_vec $arg1 #1 uit_val_format 1
                        ]
                        uit_set = [
                            tool_colour_conv_storage $arg1 #1 uit_val_format 1 #1 uit_max_val #1 uit_flags
                        ]
                    ]
                ]

                uihover [
                    tool_rightclickable

                    tool_copy_handler [
                        tool_param_copy @@arg1 [
                            uit_get = [
                                tool_colour_conv_rgb_vec $arg1 #1 uit_val_format 1
                            ]
                        ]
                    ] [
                        tool_param_paste @@arg1 [
                            uit_set = [
                                tool_colour_conv_storage $arg1 #1 uit_val_format 1 #1 uit_max_val #1 uit_flags
                            ]
                            uit_on_change = [@@@uit_on_change]
                        ]
                    ]
                ]
            ]
        ]
    ]
]
