tool_modelselect_var = []

tool_modelselect_menu_props = [
    [ uit_on_change [] ]
]

ui_tool_modelselect_picker = [
    @(tool_props $tool_modelselect_menu_props (toolpanel_attr popup user_data))

    uispace $ui_toolpanel_elem_space $ui_tool_base_elem_space [
        uiclamp 1 1
        ui_tool_modellist $tool_modelselect_var [
            uit_sel_area = 0.3
            uit_on_change = [@@uit_on_change]
        ]
    ]
]

tool_modelselect_props = [
    [ uit_disabled 0 ]
    [ uit_can_reset 0 ]
    [ uit_reset_val 0 ]
    [ uit_text_size $ui_tool_tiny_text_size_unscaled ]
    [ uit_size 0.06 ]
    [ uit_picker_width [(uiwidth 0.2)] ]
    [ uit_align -1 ]
    [ uit_on_change [] ]
]

// 1:<var> 2:<props>
ui_tool_modelselect = [
    @(tool_props $tool_modelselect_props arg2)

    @(tool_ui_scale uit_text_size)
    @(tool_ui_scale uit_size)
    @(tool_ui_scale uit_picker_width)

    local _value _model_name _model_info
    _value = (? $uit_disabled -1 $$arg1)
    _model_name = (mapmodelindex $_value)

    if (< $_value 0) [
        _model_info = (? $uit_disabled "-" "No model selected")
    ] [
        _model_info = $_model_name
    ]

    uivlist 0 [
        uicolour 0 $uit_size $uit_size [
            uimodelpreview $_model_name "" 1 1 $uit_size $uit_size [
                if (< $_value 0) [
                    uioutline (? $uit_disabled $ui_tool_dark_colour $ui_tool_dark_accent_colour)
                    uiclamp- 1 1 1 1
                ]

                if $uit_disabled [] [
                    ui_tool_tip [
                        uit_tip_simple = "Click to browse models"
                    ]

                    uirelease [
                        tool_modelselect_var = $arg1

                        toolpanel_open tool_modelselect_picker popup [
                            uit_position = (uicursorpos)
                            uit_width = @uit_picker_width
                            uit_user_data = [
                                uit_on_change = [@@@uit_on_change]
                            ]
                        ]
                    ]

                    uialtrelease [
                        tool_param_menu $arg1 [
                            uit_can_reset = @@uit_can_reset
                            uit_reset_val = [@@@uit_reset_val]
                            uit_on_change = [@@@uit_on_change]
                        ]
                    ]

                    uihover [
                        tool_copy_handler [
                            tool_param_copy @@arg1
                        ] [
                            tool_param_paste @@arg1 [
                                uit_on_change = [@@@uit_on_change]
                            ]
                        ]
                    ]
                ]

                @@@@(ui_tool_preview_spin)
            ]
        ]

        uitext $_model_info $uit_text_size

        uialign* $uit_align
    ]
]
