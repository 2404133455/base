tool_listmenu_props = [
    [ uit_text "" ]
    [ uit_text_size $ui_tool_text_size_s_unscaled ]
    [ uit_icon_size $ui_tool_icon_size_s_unscaled ]
    [ uit_item_text_size $ui_tool_text_size_s_unscaled ]
    [ uit_number_size $ui_tool_text_size_xs_unscaled ]
    [ uit_item_names [] ]
    [ uit_item_icons [] ]
    [ uit_item_extras [] ]
    [ uit_item_actions [] ]
    [ uit_item_dividers [] ]
    [ uit_disabled [] ]
    [ uit_tips [] ]
    [ uit_anim_time 150 ]
    [ uit_on_select [] ]
    [ uit_nav_enable 0 ]
]

ui_tool_listmenu = [
    @(tool_props $tool_listmenu_props arg1)

    @(tool_ui_scale uit_text_size)
    @(tool_ui_scale uit_icon_size)
    @(tool_ui_scale uit_item_text_size)
    @(tool_ui_scale uit_number_size)

    local _numoptions _action _extra _text _icon _anim _disabled
    _anim = (clampf (divf (- (getmillis 1) $toolpanel_this_open_millis) $uit_anim_time) 0 1)
    _numoptions = (max (listlen $uit_item_actions) (listlen $uit_item_names))

    uivlist $ui_tool_elem_space_s [
        uiclamp 1 1

        if $uit_text [
            uispace $ui_tool_elem_space_s $ui_tool_elem_space_s [
                uicolourtext $uit_text $ui_tool_mid_accent_colour $uit_text_size
            ]
            uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        ]

        loop i $_numoptions [
            _action = (at $uit_item_actions $i)

            if (!=s $_action) [
                _text = (tool_action_get_prop $_action short_desc)
                _icon = (tool_action_get_prop $_action _icon)
            ] [
                _text = (at $uit_item_names $i)
                _icon = (at $uit_item_icons $i)
            ]

            _extra = (at $uit_item_extras $i)
            _disabled = (!= (uit_disabled $i))

            @(ui_tool_interactable 0 0 [
                uiclamp 1 1 1 1

                uispace $ui_tool_elem_space_s $ui_tool_elem_space_s [
                    uiclamp 1 1

                    uihlist $ui_tool_elem_space_s [
                        uiclamp 1 1

                        uihlist $ui_tool_elem_space_s [
                            uialign -1 -2
                            if (&& $uit_nav_enable (< $i 10)) [
                                uicolourtext (+ $i 1) $ui_tool_sel_accent_colour $uit_number_size
                            ]
                            uicolourtext $_text $ui_tool_accent_colour $uit_item_text_size [
                                if $_disabled [
                                    uichangeblends 0.3
                                ]
                            ]
                        ]
                        if $_icon [
                            uiimage $_icon $ui_tool_accent_colour 0 $uit_icon_size $uit_icon_size
                        ]
                        if $_extra $_extra
                    ]
                ]


                ui_tool_tip [
                    uit_tip_action = @_action
                    uit_tip_simple = [@(at $uit_tips $i)]
                ]

                uirelease [
                    if (= $_disabled) [
                        toolpanel_close_this
                        uit_on_select $i

                        if (!=s @@_action) [ tool_do_action @@@_action ]
                    ]
                ]
            ])

            if (listhas $uit_item_dividers $i) [
                uifill 0 $ui_tool_elem_space_s
                uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
                uifill 0 $ui_tool_elem_space_s
            ]
        ]
    ]

    uichangeblends $_anim

    if (&& (>= $toolpanel_popop_nav_select 0) [= (uit_disabled $toolpanel_popop_nav_select)]) [
        _action = (at $uit_item_actions $toolpanel_popop_nav_select)

        toolpanel_close_this
        uit_on_select $toolpanel_popop_nav_select

        if (!=s $_action) [ tool_do_action $_action ]
    ]
]

tool_piemenu_props = [
    [ uit_size 0.2 ]
    [ uit_icon_size $ui_tool_icon_size_m_unscaled ]
    [ uit_blob_ratio 2 ]
    [ uit_text "" ]
    [ uit_text_size $ui_tool_text_size_s_unscaled ]
    [ uit_item_text_size $ui_tool_text_size_s_unscaled ]
    [ uit_item_text_resize 1.2 ]
    [ uit_number_size $ui_tool_text_size_xs_unscaled ]
    [ uit_item_names [] ]
    [ uit_item_icons [] ]
    [ uit_item_extras [] ]
    [ uit_tips [] ]
    [ uit_position [[0.5 0.5]]]
    [ uit_anim_time 150 ]
    [ uit_on_select [] ]
]

ui_tool_piemenu = [
    @(tool_props $tool_piemenu_props arg1)

    @(tool_ui_scale uit_size)
    @(tool_ui_scale uit_icon_size)
    @(tool_ui_scale uit_text_size)
    @(tool_ui_scale uit_item_text_size)
    @(tool_ui_scale uit_number_size)

    local _anim _numoptions _piesize _angstep _curang _selyaw _selitem _posx _posy _dirx _diry _dist _icon _extra

    _anim = (clampf (divf (- (getmillis 1) $toolpanel_this_open_millis) $uit_anim_time) 0 1)
    _numoptions = (listlen $uit_item_names)
    _angstep = (divf 360 $_numoptions)

    _posx = (divf (at $uit_position 0) (uiaspect))
    _posy = (at $uit_position 1)
    _piesize = (*f $uit_size $_anim)

    _dirx = (-f (uicursorx) (uiwidth $_posx))
    _diry = (-f (uicursory) $_posy)
    _dist = (sqrt (+f (*f $_dirx $_dirx) (*f $_diry $_diry)))

    _selyaw = (+f (*f (atan2 $_dirx $_diry) -1) 180)
    _selitem = (mod (round (divf $_selyaw $_angstep)) $_numoptions)

    uiradar 1 0 0 $_piesize $_piesize [
        uiradarblip "" 0 0 0 0 0.0001 0.0001 [
            uiimage "textures/blob" 0x88ffffff 0 (*f $_piesize $uit_blob_ratio) (*f $_piesize $uit_blob_ratio)
        ]

        uiradarblip "" 0 0 0 0.3 0.0001 0.0001 [
            uicolourtext $uit_text $ui_tool_mid_accent_colour $uit_text_size
        ]

        uicircleoutline $ui_tool_dark_accent_colour (*f $_anim 0.02)
        uiradarblip "textures/icons/arrow" $ui_tool_accent_colour $_selyaw $_selyaw 0.1 0.015 0.015

        loop i $_numoptions [
            _curang = (* $i $_angstep)
            _icon = (at $uit_item_icons $i)
            _extra = (at $uit_item_extras $i)

            if (= $i $_selitem) [
                uisethighlight
            ]

            uiradarblip "" 0 $_curang 0 1 0.0001 0.0001 [
                @(ui_tool_interactable 0 0 [
                    uispace $ui_tool_elem_space_s $ui_tool_elem_space_s [
                        uihlist $ui_tool_elem_space_s [
                            if (&& $uit_nav_enable (< $i 10)) [
                                uicolourtext (+ $i 1) $ui_tool_sel_accent_colour $uit_number_size
                            ]
                            if $_icon [
                                uiimage $_icon $ui_tool_accent_colour 0 $uit_icon_size $uit_icon_size
                            ]
                            uitext (at $uit_item_names $i) (*f $uit_item_text_size (? (= $i $_selitem) $uit_item_text_resize 1))
                            if $_extra $_extra
                        ]
                        if (!= $i $_selitem) [
                            uichangeblends 0.66
                        ]
                    ]
                ])
            ]
        ]

        uichangeblends $_anim
        uiposition (uiwidth (-f $_posx (*f (divf $_piesize (uiaspect)) 0.5))) (-f $_posy (*f $_piesize 0.5))
    ]

    // uihover is not working correctly for uiradar elements
    // as such, this is a workaround by checking the cursor distance to the ring
    // and displaying a tip if close enough.
    // Additionally, only dispaly tips after the animation is finished
    if (&& (=f $_anim 1) (<f (absf (-f $_dist (*f 0.5 $_piesize))) 0.025)) [
        ui_tool_tip [
            uit_tip_simple = [@(at $uit_tips $_selitem)]
        ]
    ]

    uirelease [
        toolpanel_close_this
        uit_on_select $_selitem
    ]

    if (>= $toolpanel_popop_nav_select 0) [
        toolpanel_close_this
        uit_on_select $toolpanel_popop_nav_select
    ]
]
