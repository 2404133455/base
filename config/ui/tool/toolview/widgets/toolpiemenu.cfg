tool_contextmenu_props = [
    // [ uit_size 0.2 ]
    // [ uit_icon_size $ui_toolview_base_icon_size ]
    // [ uit_blob_ratio 2 ]
    [ uit_text "" ]
    [ uit_text_size $ui_toolview_small_text_size ]
    [ uit_item_text_size $ui_toolview_small_text_size ]
    [ uit_number_size $ui_toolview_tiny_text_size ]
    [ uit_item_names [] ]
    [ uit_item_icons [] ]
    [ uit_item_extras [] ]
    [ uit_tips [] ]
    [ uit_anim_time 150 ]
    [ uit_on_select [] ]
]

ui_tool_contextmenu = [
    @(tool_props $tool_contextmenu_props arg1)

    local numoptions extra icon anim
    anim = (clampf (divf (- (getmillis 1) (toolpanel_gettime)) $uit_anim_time) 0 1)
    numoptions = (listlen $uit_item_names)

    uispace $ui_toolview_small_elem_space $ui_toolview_small_elem_space [
        uiclamp 1 1

        uivlist $ui_toolview_small_elem_space [
            uiclamp 1 1

            if $uit_text [
                uicolourtext $uit_text $ui_toolview_mid_accent_colour $uit_text_size
                uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
            ]

            loop i $numoptions [
                icon = (at $uit_item_icons $i)
                extra = (at $uit_item_extras $i)

                @(ui_tool_interactable 0 0 [
                    uiclamp 1 1 1 1

                    uihlist $ui_toolview_small_elem_space [
                        uialign -1 -2

                        if (&& $uit_nav_enable (< $i 10)) [
                            uicolourtext (+ $i 1) $ui_toolview_sel_accent_colour $uit_number_size
                        ]
                        if $icon [
                            uiimage $icon $ui_toolview_accent_colour 0 $uit_icon_size $uit_icon_size
                        ]
                        uitext (at $uit_item_names $i) $uit_item_text_size
                        if $extra $extra
                    ]

                    ui_tool_tip [
                        uit_tip_simple = [@(at $uit_tips $i)]
                    ]

                    uirelease [
                        menu_click_hide = 1
                        uit_on_select $i
                    ]
                ])
            ]
        ]
    ]

    uichangeblends $anim

    if (>= $tool_menu_select 0) [
        tool_menu_force_close = 1
        uit_on_select $tool_menu_select
    ]
]

tool_piemenu_props = [
    [ uit_size 0.2 ]
    [ uit_icon_size $ui_toolview_base_icon_size ]
    [ uit_blob_ratio 2 ]
    [ uit_text "" ]
    [ uit_text_size $ui_toolview_small_text_size ]
    [ uit_item_text_size $ui_toolview_small_text_size ]
    [ uit_item_text_resize 1.2 ]
    [ uit_number_size $ui_toolview_tiny_text_size ]
    [ uit_item_names [] ]
    [ uit_item_icons [] ]
    [ uit_item_extras [] ]
    [ uit_tips [] ]
    [ uit_position [[0.5 0.5]]]
    [ uit_anim_time 150 ]
    [ uit_on_select [] ]
]

ui_tool_piemenu = [
    @(tool_props $tool_piemenu_props arg1)

    local anim numoptions piesize angstep curang selyaw selitem posx posy dirx diry dist icon extra

    anim = (clampf (divf (- (getmillis 1) (toolpanel_gettime)) $uit_anim_time) 0 1)
    numoptions = (listlen $uit_item_names)
    angstep = (divf 360 $numoptions)

    posx = (divf (at $uit_position 0) (uiaspect))
    posy = (at $uit_position 1)
    piesize = (*f $uit_size $anim)

    dirx = (-f (uicursorx) (uiwidth $posx))
    diry = (-f (uicursory) $posy)
    dist = (sqrt (+f (*f $dirx $dirx) (*f $diry $diry)))

    selyaw = (+f (*f (atan2 $dirx $diry) -1) 180)
    selitem = (mod (round (divf $selyaw $angstep)) $numoptions)

    uiradar 1 0 0 $piesize $piesize [
        uiradarblip "" 0 0 0 0 0.0001 0.0001 [
            uiimage "textures/blob" 0x88ffffff 0 (*f $piesize $uit_blob_ratio) (*f $piesize $uit_blob_ratio)
        ]

        uiradarblip "" 0 0 0 0.3 0.0001 0.0001 [
            uicolourtext $uit_text $ui_toolview_mid_accent_colour $uit_text_size
        ]

        uicircleoutline $ui_toolview_dark_accent_colour (*f $anim 0.02)
        uiradarblip "textures/icons/arrow" $ui_toolview_accent_colour $selyaw $selyaw 0.1 0.015 0.015

        loop i $numoptions [
            curang = (* $i $angstep)
            icon = (at $uit_item_icons $i)
            extra = (at $uit_item_extras $i)

            if (= $i $selitem) [
                uisethighlight
            ]

            uiradarblip "" 0 $curang 0 1 0.0001 0.0001 [
                @(ui_tool_interactable 0 0 [
                    uispace $ui_toolview_small_elem_space $ui_toolview_small_elem_space [
                        uihlist $ui_toolview_small_elem_space [
                            if (&& $uit_nav_enable (< $i 10)) [
                                uicolourtext (+ $i 1) $ui_toolview_sel_accent_colour $uit_number_size
                            ]
                            if $icon [
                                uiimage $icon $ui_toolview_accent_colour 0 $uit_icon_size $uit_icon_size
                            ]
                            uitext (at $uit_item_names $i) (*f $uit_item_text_size (? (= $i $selitem) $uit_item_text_resize 1))
                            if $extra $extra
                        ]
                        if (!= $i $selitem) [
                            uichangeblends 0.66
                        ]
                    ]
                ])
            ]
        ]

        uichangeblends $anim
        uiposition (uiwidth (-f $posx (*f (divf $piesize (uiaspect)) 0.5))) (-f $posy (*f $piesize 0.5))
    ]

    // uihover is not working correctly for uiradar elements
    // as such, this is a workaround by checking the cursor distance to the ring
    // and displaying a tip if close enough.
    // Additionally, only dispaly tips after the animation is finished
    if (&& (=f $anim 1) (<f (absf (-f $dist (*f 0.5 $piesize))) 0.025)) [
        ui_tool_tip [
            uit_tip_simple = [@(at $uit_tips $selitem)]
        ]
    ]

    uirelease [
        uit_on_select $selitem
    ]

    if (>= $tool_menu_select 0) [
        tool_menu_force_close = 1
        uit_on_select $tool_menu_select
    ]
]
