tool_dropdown_items = []
tool_dropdown_var = []

tool_dropdown_menu_props = [
    [ uit_on_change [] ]
    [ uit_tips [] ]
]

ui_tool_dropdown_list = [
    @(tool_props $tool_dropdown_menu_props (toolpanel_attr popup user_data))

    uivlist 0 [
        uiclamp 1 1

        loop i (listlen $tool_dropdown_items) [
            ui_tool_button [
                uit_label = (at $tool_dropdown_items $i)
                uit_label_align = -1
                uit_colour = (? (= $i $$tool_dropdown_var) $ui_toolview_accent_colour $ui_toolview_dark_accent_colour)
                uit_on_click = [
                    toolpanel_close_this
                    tool_param_set @tool_dropdown_var @i @uit_on_change
                ]
                uit_tip_simple = [@@(at $uit_tips $i)]
            ]
        ]

        uiclamp* 1 1
    ]
]

tool_dropdown_props = [
    [ uit_disabled 0 ]
    [ uit_can_reset 1 ]
    [ uit_reset_val [] ]
    [ uit_val_size $ui_toolview_small_text_size_unscaled ]
    [ uit_label "" ]
    [ uit_label_size $ui_toolview_small_text_size_unscaled ]
    [ uit_width $ui_toolview_numinput_size_unscaled ]
    [ uit_menu_width $ui_toolpanel_menu_width ]
    [ uit_height 0 ]
    [ uit_tip "" ]
    [ uit_tip_simple "" ]
    [ uit_tips [] ]
    [ uit_on_change [] ]
]

// 1:<var> 2:<dir> 3:<listlen> 4:<on change>
tool_dropdown_scroll = [
    local _new_val
    _new_val = (clamp (+ $$arg1 $arg2) 0 (- $arg3 1))

    tool_param_set $arg1 $_new_val $arg4
]

// 1:<var> 2:<items> 3:<props>
ui_tool_dropdown = [
    @(tool_props $tool_dropdown_props arg3)

    @(tool_ui_scale uit_val_size)
    @(tool_ui_scale uit_label_size)
    @(tool_ui_scale uit_width)
    @(tool_ui_scale uit_height)

    local _value _col
    _value = (? $uit_disabled "-" (at $arg2 $$arg1))
    _col = (? $uit_disabled $ui_toolview_dark_colour $ui_toolview_accent_colour)

    uihlist $ui_toolpanel_elem_space [
        if (!=s $uit_label "") [
            uiclamp 1 1
            uitext $uit_label $uit_label_size [ uialign -1 ]
        ]
        @@(ui_tool_interactable [$uit_width] [$uit_height] [
            uispace $ui_padsmall $ui_padsmaller [
                uitext $_value $uit_val_size
            ]

            uispace $ui_padsmall $ui_padsmaller [
                uialign 1
                uitriangle $_col 0.005 0.005 180
            ]

            ui_tool_tip [
                uit_tip = [@@uit_tip]
                uit_tip_simple = [@@uit_tip_simple]
            ]

            if $uit_disabled [] [
                uirelease [
                    tool_dropdown_items = $arg2
                    tool_dropdown_var = $arg1
                    toolpanel_open tool_dropdown_list popup [
                        uit_position = (uicursorpos)
                        uit_width = @uit_menu_width
                        uit_user_data = [
                            uit_on_change = [@@@uit_on_change]
                            uit_tips = [@@@uit_tips]
                        ]
                    ]
                ]

                uialtrelease [
                    if (=s $uit_reset_val) [
                        uit_reset_val = (getvardef $arg1)
                    ]

                    tool_param_menu $arg1 [
                        uit_can_reset = @@uit_can_reset
                        uit_reset_val = [@@@uit_reset_val]
                        uit_on_change = [@@@uit_on_change]
                    ]
                ]

                uihover [
                    tool_copy_handler [
                        tool_param_copy @@arg1
                    ] [
                        tool_param_paste @@arg1 [
                            uit_on_change = [@@@uit_on_change]
                        ]
                    ]
                ]

// TODO: Figure out if this can work when inside a scroll area
//                uiscrollup [
//                    tool_dropdown_scroll $arg1 -1 (listlen $arg2) $uit_on_change
//                ]
//
//                uiscrolldown [
//                    tool_dropdown_scroll $arg1 1 (listlen $arg2) $uit_on_change
//                ]
            ]
        ] [$uit_disabled])
    ]
]
