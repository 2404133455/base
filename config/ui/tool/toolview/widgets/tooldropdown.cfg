tool_dropdown_items = []

tool_dropdown_menu_props = [
    [ p_var       [] ]
    [ p_on_change [] ]
    [ p_tips      [] ]
]

ui_tool_dropdown_list = [
    @(tool_props $tool_dropdown_menu_props (toolpanel_attr popup user_data))

    uivlist 0 [
        uiclamp 1 1

        local _colour
        _colour = 0

        loop i (listlen $tool_dropdown_items) [
            _colour = (? (= $i $$p_var) $ui_tool_accent_colour $ui_tool_dark_accent_colour)

            ui_tool_button [
                p_label       = (at $tool_dropdown_items $i)
                p_label_align = -1
                p_colour      = $_colour
                p_on_click    = [
                    toolpanel_close_this
                    tool_param_set @p_var @i @p_on_change
                ]
                p_tip_simple  = [@@(at $p_tips $i)]
            ]
        ]

        uiclamp* 1 1
    ]
]

tool_dropdown_props = [
    [ p_disabled   0                               ]
    [ p_can_reset  1                               ]
    [ p_reset_val  []                              ]
    [ p_val_size   $ui_tool_text_size_s_unscaled   ]
    [ p_label      ""                              ]
    [ p_label_size $ui_tool_text_size_s_unscaled   ]
    [ p_width      $ui_tool_numinput_size_unscaled ]
    [ p_menu_width $ui_toolpanel_menu_width        ]
    [ p_height     0                               ]
    [ p_tip        ""                              ]
    [ p_tip_simple ""                              ]
    [ p_tips       []                              ]
    [ p_on_change  []                              ]
]

// 1:<var> 2:<dir> 3:<listlen> 4:<on change>
tool_dropdown_scroll = [
    local _new_val
    _new_val = (clamp (+ $$arg1 $arg2) 0 (- $arg3 1))

    tool_param_set $arg1 $_new_val $arg4
]

// 1:<var> 2:<items> 3:<props>
ui_tool_dropdown = [
    @(tool_props $tool_dropdown_props arg3)

    @(tool_ui_scale p_val_size)
    @(tool_ui_scale p_label_size)
    @(tool_ui_scale p_width)
    @(tool_ui_scale p_height)

    local _value _col
    _value = (? $p_disabled "-" (at $arg2 $$arg1))
    _col = (? $p_disabled $ui_tool_dark_colour $ui_tool_accent_colour)

    uihlist $ui_toolpanel_elem_space [
        if (!=s $p_label "") [
            uiclamp 1 1
            uitext $p_label $p_label_size [ uialign -1 ]
        ]
        @@(ui_tool_interactable [$p_width] [$p_height] [
            uispace $ui_padsmall $ui_padsmaller [
                uitext $_value $p_val_size
            ]

            uispace $ui_padsmall $ui_padsmaller [
                uialign 1
                uitriangle $_col 0.005 0.005 180
            ]

            ui_tool_tip [
                p_tip        = [@@p_tip]
                p_tip_simple = [@@p_tip_simple]
            ]

            if $p_disabled [] [
                uirelease [
                    tool_dropdown_items = $arg2

                    toolpanel_open tool_dropdown_list popup [
                        p_position  = (uicursorpos)
                        p_width     = @p_menu_width
                        p_user_data = [
                            p_var       = @arg1
                            p_on_change = [@@p_on_change]
                            p_tips      = [@@p_tips]
                        ]
                    ]
                ]

                uialtrelease [
                    if (=s $p_reset_val) [
                        p_reset_val = (getvardef $arg1)
                    ]

                    tool_param_menu $arg1 [
                        p_can_reset = @p_can_reset
                        p_reset_val = [@@p_reset_val]
                        p_on_change = [@@p_on_change]
                    ]
                ]

                uihover [
                    tool_rightclickable

                    tool_copy_handler [
                        tool_param_copy @arg1
                    ] [
                        tool_param_paste @arg1 [
                            p_on_change = [@@p_on_change]
                        ]
                    ]
                ]

// TODO: Figure out if this can work when inside a scroll area
//                uiscrollup [
//                    tool_dropdown_scroll $arg1 -1 (listlen $arg2) $p_on_change
//                ]
//
//                uiscrolldown [
//                    tool_dropdown_scroll $arg1 1 (listlen $arg2) $p_on_change
//                ]
            ]
        ] [$p_disabled])
    ]
]
