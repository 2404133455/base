# ui_tool_ent_soundenv [
    ui_tool_ent_param_group "Type" [
        ui_tool_searchselect #(tool_ent_attr soundenv type) [
            #(ui_tool_ent_attr_props soundenv type)
            p_num_items_func = [ + (soundenvinfo -1) 1]
            p_item_func = [
                if $arg1 [
                    soundenvinfo (- $arg1 1)
                ] [
                    result "<None>"
                ]
            ]
            p_value = "Browse"
        ]

        local _picked
        if $#(tool_ent_attr soundenv type) [
            _picked = (soundenvinfo (- $#(tool_ent_attr soundenv type) 1))
        ] [
            _picked = "<None>"
        ]

        uitext $_picked $ui_tool_text_size_xs
        uialign- 1
    ]

    ui_tool_ent_param_group "Size" [
        ui_tool_numinput #(tool_ent_attr soundenv width) 0 999999 1 [
            #(ui_tool_ent_attr_props soundenv width [] 1)
            p_val_format = i
            p_label = "X"
        ]

        ui_tool_numinput #(tool_ent_attr soundenv length) 0 999999 1 [
            #(ui_tool_ent_attr_props soundenv length [] 1)
            p_val_format = i
            p_label = "Y"
        ]

        ui_tool_numinput #(tool_ent_attr soundenv height) 0 999999 1 [
            #(ui_tool_ent_attr_props soundenv height [] 1)
            p_val_format = i
            p_label = "Z"
        ]
    ]

    ui_tool_ent_param_group "Mute groups" [
        uigrid 8 0 0 [
            loop i 32 [
                ui_tool_compactswitch #(tool_ent_attr soundenv mutemask) [
                    #(ui_tool_ent_attr_props soundenv mutemask)
                    p_label = @(+ $i 1)
                    p_get   = [ & $#(tool_ent_attr soundenv mutemask) (<< 1 @@i) ]
                    p_set   = [ ^ $arg1 (<< 1 @@i) ]
                ]
                uiclamp- 1 1
            ]
        ]
    ]
]
