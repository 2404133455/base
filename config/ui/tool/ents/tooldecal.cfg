# ui_tool_ent_decal [
    uitext Decal

    ui_tool_ent_param_group "Texture" [
        ui_tool_texselect #(tool_ent_attr decal type) [
            #(ui_tool_ent_attr_props decal type)
            uit_align = 1
            uit_decal = 1
        ]
    ]

    ui_tool_ent_param_group "Dimensions" [
        uigrid 2 0 0 [
            ui_tool_numinput #(tool_ent_attr decal yaw) 0 360 1 [
                #(ui_tool_ent_attr_props decal yaw [] 1)
                uit_label = "Yaw"
                uit_val_format = i
                uit_circular = 1
            ]

            ui_tool_button [
                uit_icon = "textures/icons/camera"
                uit_icon_size = 0.015
                uit_icon_align = 0
                uit_tip_simple = "Yaw from camera"
                uit_on_click = [
                    tool_param_set #(tool_ent_attr decal yaw) (toint (round (getcamerayaw))) [
                        tool_ent_attr_change decal $tool_ent_decal_idx_yaw
                    ]
                ]
                uit_disabled = $tool_ent_delta_edit_on
            ]

            ui_tool_numinput #(tool_ent_attr decal pitch) -180 180 1 [
                #(ui_tool_ent_attr_props decal pitch [] 1)
                uit_label = "Pitch"
                uit_val_format = i
                uit_circular = 1
            ]

            ui_tool_button [
                uit_icon = "textures/icons/camera"
                uit_icon_size = 0.015
                uit_icon_align = 0
                uit_tip_simple = "Pitch from camera"
                uit_on_click = [
                    tool_param_set #(tool_ent_attr decal pitch) (toint (round (getcamerapitch))) [
                        tool_ent_attr_change decal $tool_ent_decal_idx_pitch
                    ]
                ]
                uit_disabled = $tool_ent_delta_edit_on
            ]

            ui_tool_numinput #(tool_ent_attr decal roll) -180 180 1 [
                #(ui_tool_ent_attr_props decal roll [] 1)
                uit_label = "Roll"
                uit_val_format = i
                uit_circular = 1
            ]
        ]

        uifill 0 $ui_tool_base_elem_space

        ui_tool_numinput #(tool_ent_attr decal scale) 0 10000 1 [
            #(ui_tool_ent_attr_props decal scale [] 1)
            uit_label = "Scale"
            uit_val_format = i
        ]
    ]

    ui_tool_ent_param_group "Colour" [
        ui_tool_colour #(tool_ent_attr decal colour) [
            #(ui_tool_ent_attr_props decal colour)
            uit_val_format = i
        ]

        uifill 0 $ui_tool_base_elem_space

        ui_tool_numinput tool_ent_attr_blend_val 1 100 1 [
            #(ui_tool_ent_attr_props decal blend [] 1)
            uit_label = "Blend ^%"
            uit_val_format = i
        ]
    ]

    @(ui_tool_ent_palette_group decal)
    @(ui_tool_ent_gamemode_group decal)
    @(ui_tool_ent_variant_group decal 1)
]
