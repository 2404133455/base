tool_ent_dyn_content_h = 0

tool_ent_calc_dyn_content_h = [
    tool_ent_dyn_content_h = (-f $tool_ent_dyn_content_h (uigetlasth))
]

// 1:<type> 2:<attr name list> 3:<subtype> 4:<placer mod menu>
# ui_tool_ent_attr_props [
    local _attr_list _nodelta _disable _placer_mod_menu
    _attr_list = []
    _nodelta = 0
    _placer_mod_menu = (&& $arg4 [= (listlen $arg2) 1])

    looplist attr_name $arg2 [
        append _attr_list (tool_ent_attr_idx_map $arg1 $attr_name $arg3)
        if (& (tool_ent_aflags $arg1 $attr_name) $T_ENT_NODELTA) [
            _nodelta = 1
        ]
    ]

    _disable = []
    if $_nodelta [
        _disable = "uit_disabled = (tool_ent_delta_edit)"
    ]

    _placer_mod_menu = []
    if (&& $arg4 [= (listlen $arg2) 1]) [
        _placer_mod_menu = [
            uit_menu_props = (? (= $tool_ent_ui_mode $T_ENT_UI_PLACER) [
                uit_item_names = ["Placer modifiers"]
                uit_on_select = [
                    // Assuming single element in _attr_list
                    tool_ent_placer_mod_focus = (+ $TOOL_ENT_PLACER_MOD_ATTR_BEGIN #1 _attr_list)
                    tool_ent_placer_mod_attr = (tool_ent_placer_mod_get)
                    toolpanel_open tool_ent_placer_num_modifiers popup [
                        uit_position = (uicursorpos)
                        uit_width = 0.3
                    ]
                ]
            ] [])
        ]
    ]

    result [
        uit_label_size = $ui_tool_text_size_xs
        uit_on_change = [
            tool_ent_attr_change #1 arg1 [#1 _attr_list]
        ]
        uit_can_reset = 1
        uit_reset_val = 0
        @_disable
        uit_has_menu = (! (tool_ent_delta_edit))
        uit_delta = (tool_ent_delta_edit)
        @_placer_mod_menu
    ]
]

// 1:<text> 2:<fields>
ui_tool_ent_param_group = [
    uihlist 0 [
        uiclamp 1 1
        uitext $arg1 $ui_tool_text_size_s
        uivlist 0 [
            @arg2
        ]
    ]
    uifill 0 $ui_tool_elem_space_s
    uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
    uifill 0 $ui_tool_elem_space_s
]

// 1:<type> 2:<subtype> 3:<suffix>
# ui_tool_ent_palette_group [
    result [
        ui_tool_ent_param_group [Palette #1 arg3] [
            ui_tool_dropdown @@(tool_ent_attr $arg1 [palette@arg3] $arg2) [
                "Pulse"
                "Team"
                "Weapon"
            ] [
                #1(ui_tool_ent_attr_props $arg1 [palette@arg3] $arg2)
                uit_label = "Type"
            ]

            ui_tool_dropdown tool_ent_attr_palindex#1[arg3]_val (at $tool_palette_ids $#1(tool_ent_attr $arg1 [palette@arg3] $arg2)) [
                #1(ui_tool_ent_attr_props $arg1 [palindex@arg3] $arg2)
                uit_label = "Id"
            ]

            uifill 0 $ui_tool_elem_space_m

            ui_tool_checkbox tool_ent_attr_palindex#1[arg3]_enforce [
                uit_label = "Enforce"
                uit_label_size = $ui_tool_text_size_xs
                #1(ui_tool_ent_attr_props $arg1 [palindex@arg3] $arg2)
            ]
            uialign- 1
        ]
    ]
]

// 1:<type> 2:<fx>
# ui_tool_ent_variant_group [
    local _fxlevel_group _fxlevel_var

    if $arg2 [
        _fxlevel_group = [
            uihlist 0 [
                uitext "FX Level:" $ui_tool_text_size_xs
                uifill $ui_tool_elem_space_l
                loop i 3 [
                    _fxlevel_var = [tool_ent_attr_fxlevel_@(+ $i 1)]
                    ui_tool_compactswitch $_fxlevel_var [
                        #2(ui_tool_ent_attr_props $arg1 fxlevel $arg3)
                        uit_label = @(+ $i 1)
                        uit_width = 0.02
                        uit_height = 0.02
                    ]
                ]
            ]
            uialign- 1
        ]
    ]

    result [
        ui_tool_ent_param_group "Variant" [
            ui_tool_dropdown @@(tool_ent_attr $arg1 variant $arg3) [
                "All"
                "Default"
                "Alternate"
            ] [
                #1(ui_tool_ent_attr_props $arg1 variant $arg3)
                uit_label = "Map variant"
            ]

            #1 _fxlevel_group
        ]
    ]
]

// 1:<type> 2:<subtype>
# ui_tool_ent_gamemode_group [
    result [
        ui_tool_ent_param_group "Game mode" [
            local _attr_mode

            uigrid 3 0 0 [
                loop i (listlen $tool_modes) [
                    _attr_mode = (at $tool_modes $i)
                    ui_tool_compactswitch [tool_ent_attr_modes_@_attr_mode] [
                        #1(ui_tool_ent_attr_props $arg1 modes $arg2)
                        uit_label = @_attr_mode
                        uit_tip_simple = [@(at $modename (+ $i 2))]
                    ]
                    uiclamp- 1 1
                ]
            ]
        ]

        ui_tool_ent_param_group "Game mutators" [
            local _attr_mut

            uigrid 3 0 0 [
                loop i (listlen $tool_muts) [
                    _attr_mut = (at $tool_muts $i)
                    ui_tool_compactswitch [tool_ent_attr_muts_@_attr_mut] [
                        #1(ui_tool_ent_attr_props $arg1 muts $arg2)
                        uit_label = @_attr_mut
                        uit_tip_simple = [@(at $mutsname $i)]
                    ]
                    uiclamp- 1 1
                ]
            ]
        ]
    ]
]

// 1:<type> 2:<flags> 3:<labels> 4:<attr> 5:<title>
ui_tool_ent_flags_group = [
    local _code _label _var _attr_name _props
    _code = ""
    _attr_name = (? $arg4 $arg4 flags)

    append _code (concatword "ui_tool_ent_param_group ^"" (? $arg5 $arg5 "Flags") "^" [^n")
    append _code "uigrid 2 $ui_tool_elem_space_l $ui_tool_elem_space_s [^n"
    append _code "uiclamp 1 1^n"

    _props = (ui_tool_ent_attr_props $arg1 $_attr_name $arg2)

    loop i (listlen $arg2) [
        _label = (at $arg3 $i)
        _var = [tool_ent_attr_@[_attr_name]_@(at $arg2 $i)]

        append _code (concatword "ui_tool_checkbox " $_var " [^n")
        append _code (concatword "uit_label = [" $_label "]")
        append _code $_props
        append _code "uit_label_size = $ui_tool_text_size_xs"
        append _code "^n]^n"
    ]

    append _code "uialign* -1 -1^n"
    append _code "]^n]^n"

    result $_code
]

exec "config/ui/tool/ents/toollight.cfg"
exec "config/ui/tool/ents/toolmapmodel.cfg"
exec "config/ui/tool/ents/toolplayerstart.cfg"
exec "config/ui/tool/ents/toolenvmap.cfg"
exec "config/ui/tool/ents/toolparticles.cfg"
exec "config/ui/tool/ents/toolsound.cfg"
exec "config/ui/tool/ents/toollightfx.cfg"
exec "config/ui/tool/ents/tooldecal.cfg"
exec "config/ui/tool/ents/toolwind.cfg"
exec "config/ui/tool/ents/toolweapon.cfg"
exec "config/ui/tool/ents/toolteleport.cfg"
exec "config/ui/tool/ents/toolactor.cfg"
exec "config/ui/tool/ents/tooltrigger.cfg"
exec "config/ui/tool/ents/toolpusher.cfg"
exec "config/ui/tool/ents/toolaffinity.cfg"
exec "config/ui/tool/ents/toolcheckpoint.cfg"
exec "config/ui/tool/ents/toolroute.cfg"
exec "config/ui/tool/ents/toolrail.cfg"
exec "config/ui/tool/ents/toolcamera.cfg"

ui_tool_ent_none = []
ui_tool_ent_outline = []

tool_ent_edit_attr = 0

ui_tool_ent_edit_attr = [
    local _type_idx _num_attrs
    _type_idx = (indexof $tool_ent_types $tool_ent_type)
    _num_attrs = (entityattrs $_type_idx 1)

    uivlist $ui_tool_elem_space_s [
        uiclamp 1 1
        loop attridx $_num_attrs [
            ui_tool_numinput [tool_ent_attr_@attridx] $intmin $intmax 1 [
                uit_label_size = $ui_tool_text_size_xs
                uit_label = (getentattr $_type_idx @attridx $tool_ent_attr_0)
                uit_val_format = i
                uit_can_reset = 1
                uit_has_menu = (! (tool_ent_delta_edit))
                uit_delta = (tool_ent_delta_edit)
                uit_on_change = [
                    local _set_func
                    _set_func = (tool_ent_set_func)
                    $_set_func @attridx $[tool_ent_attr_@@attridx]
                ]
            ]
        ]
    ]
]

ui_tool_ent_cur = [
    local _edit_type
    _edit_type = (? $tool_ent_edit_attr ui_tool_ent_edit_attr [ui_tool_ent_@tool_ent_type])

    if (&& $tool_ent_compatible tool_ent_has_sel) [
        ui_tool_vscrollarea [
            uivlist 0 [
                uiclamp 1 1
                _edit_type
            ]
        ] [
            uit_height = $tool_ent_dyn_content_h
        ]
    ]
]

tool_ent_list_height = 0.08
tool_last_sel_ents = 0

ui_tool_ent_list = [
    local _sel_ents _sel_idx _text_col
    _sel_ents = (getenginestat 15)
    _sel_idx = 0

    uitext (concat $_sel_ents (? (= $_sel_ents 1) "entity" "entities") "selected") $ui_tool_text_size_s
    uialign- -1 -2

    uicolour $ui_tool_dark_colour 0 0 [
        uiclamp 1 1

        ui_tool_vscrollarea [
            uigrid 3 0 0 [
                uiclamp 1 1

                entloopread [
                    _text_col = (? (= $_sel_idx (- $_sel_ents 1)) $ui_tool_accent_colour $ui_tool_dark_accent_colour)

                    ui_tool_button [
                        uit_label = (concatword (enttype) " ("(entindex) ")")
                        uit_label_align = -1
                        uit_label_size = $ui_tool_text_size_xs
                        uit_colour = $_text_col
                        uit_on_click = [
                            entautoview @_sel_idx 1
                        ]
                        uit_tip_simple = "Click to view"
                    ]

                    _sel_idx = (+ $_sel_idx 1)
                ]
                uiclamp* 1 1
            ]

            if (!= $tool_last_sel_ents $_sel_ents) [
                uiclipoffsety 1
            ]
        ] [
            uit_height = $tool_ent_list_height
            uit_scroll_speed = 0.2
        ]
    ]

    tool_last_sel_ents = $_sel_ents
]

# ui_tool_ent_controls [
    uifill 0 0 [
        uiclamp 1 1
        uivlist 0 [
            uiclamp 1 1
            uihlist 0 [
                uiclamp 1 1
                #(ui_tool_get_action_button ta_ent_add [
                    uit_label = "New"
                    uit_height = $ui_tool_button_height_m_unscaled
                    uit_icon_size = $ui_tool_icon_size_m_unscaled
                ] [
                    uit_override_label = 1
                ])
                #(ui_tool_get_action_button ta_ent_delete [
                    uit_label = "Delete"
                    uit_height = $ui_tool_button_height_m_unscaled
                    uit_icon_size = $ui_tool_icon_size_m_unscaled
                    uit_disabled = (! (getenginestat 15))
                ] [
                    uit_override_label = 1
                ])
                uiclamp* 1 1
            ]
            uialign- -1 -2
            uihlist 0 [
                #(ui_tool_get_action_button ta_ent_sel_links [
                    uit_icon_align = 0
                    uit_width = $ui_tool_button_height_m_unscaled
                    uit_height = $ui_tool_button_height_m_unscaled
                    uit_icon_size = $ui_tool_icon_size_m_unscaled
                    uit_disabled = (! (tool_ent_has_sel))
                ] [
                    uit_override_label = 1
                ])

                #(ui_tool_get_action_button ta_ent_unify [
                    uit_icon_align = 0
                    uit_width = $ui_tool_button_height_m_unscaled
                    uit_height = $ui_tool_button_height_m_unscaled
                    uit_icon_size = $ui_tool_icon_size_m_unscaled
                ] [
                    uit_override_label = 1
                ])

                #(ui_tool_get_action_button ta_ent_search [
                    uit_icon_align = 0
                    uit_width = $ui_tool_button_height_m_unscaled
                    uit_height = $ui_tool_button_height_m_unscaled
                    uit_icon_size = $ui_tool_icon_size_m_unscaled
                ] [
                    uit_override_label = 1
                ])

                #(ui_tool_get_action_button ta_ent_placer [
                    uit_icon_align = 0
                    uit_width = $ui_tool_button_height_m_unscaled
                    uit_height = $ui_tool_button_height_m_unscaled
                    uit_icon_size = $ui_tool_icon_size_m_unscaled
                ] [
                    uit_override_label = 1
                ])

                ui_tool_button [
                    uit_icon_align = 0
                    uit_icon = "textures/icons/shock"
                    uit_tip_simple = "Link entities"
                    uit_on_click = [
                        toolpanel_open_menu [
                            uit_text = "Link entities"
                            uit_item_actions = [
                                ta_ent_chain_link
                                ta_ent_star_link
                            ]
                        ]
                    ]
                    uit_width = $ui_tool_button_height_m_unscaled
                    uit_height = $ui_tool_button_height_m_unscaled
                    uit_icon_size = $ui_tool_icon_size_m_unscaled
                ]
            ]
            uialign- -1 -2
        ]
        uialign- -1 -2

        uihlist 0 [
            ui_tool_compactswitch tool_ent_edit_attr [
                uit_label = "Atr"
                uit_label_size = $ui_tool_text_size_xs_unscaled
                uit_tip_simple = "Toggle simple attribute view"
                uit_width = $ui_tool_button_height_m_unscaled
                uit_height = $ui_tool_button_height_m_unscaled
            ]
            ui_tool_compactswitch tool_ent_delta_edit_on [
                uit_label = "Inc"
                uit_label_size = $ui_tool_text_size_xs_unscaled
                uit_tip_simple = "Toggle incremental multi-editing"
                uit_width = $ui_tool_button_height_m_unscaled
                uit_height = $ui_tool_button_height_m_unscaled
            ]
        ]
        uialign- 1 1
    ]
]

ui_tool_ent_unify = [
    uitext "Unify entities" $ui_tool_text_size_m

    local _type_idx _num_attrs
    _type_idx = (indexof $tool_ent_types $tool_ent_type)
    _num_attrs = (entityattrs $_type_idx 1)

    if (tool_ent_multiple) [
        uihlist $ui_tool_elem_space_l [
            ui_tool_button [
                uit_label = "Select all"
                uit_on_click = [
                    tool_ent_unify_mask = 0xFFFFFF
                ]
            ]
            ui_tool_button [
                uit_label = "Deselect all"
                uit_on_click = [
                    tool_ent_unify_mask = 0
                ]
            ]
        ]

        uigrid 3 $ui_tool_elem_space_l $ui_tool_elem_space_s [
            loop attridx $_num_attrs [
                ui_tool_checkbox tool_ent_unify_mask [
                    uit_label = (getentattr $_type_idx @attridx $tool_ent_attr_0)
                    uit_get = [
                        & $tool_ent_unify_mask (<< 1 @@attridx)
                    ]
                    uit_set = [
                        ^ $arg1 (<< 1 @@attridx)
                    ]
                ]
            ]
        ]

        uihlist 0 [
            ui_tool_switch tool_ent_unify_type [
                uit_label = "Ent type differences"
                uit_options = ["Skip" "Change"]
                uit_tip_simple = "Whether entities of different types^nshould be skipped or have their types changed"
            ]
        ]
    ] [
        uicolourtext "Must have at least two entities selected" $ui_tool_warn_colour $ui_tool_text_size_s
    ]

    uihlist $ui_tool_elem_space_l [
        ui_tool_button [
            uit_label = "Unify"
            uit_label_size = $ui_tool_text_size_m
            uit_on_click = [
                tool_ent_unify_perform
                tool_ent_ui_mode = $T_ENT_UI_EDIT
            ]
            uit_disabled = (! (tool_ent_multiple))
        ]
        ui_tool_button [
            uit_label = "Cancel"
            uit_label_size = $ui_tool_text_size_m
            uit_on_click = [
                tool_ent_ui_mode = $T_ENT_UI_EDIT
            ]
        ]
    ]
]

ui_tool_ent_search = [
    uivlist $ui_toolpanel_elem_space [
        uitext "Search" $ui_tool_text_size_m

        ui_tool_checkbox tool_ent_search_insel [
            uit_label = "Limit to selection"
            uit_tip_simple = "Search within the selected area"
        ]

        uigrid 2 0 0 [
            ui_tool_button [
                uit_label = "Selected"
                uit_icon = "textures/icons/arrow"
                uit_tip_simple = "Set filters from current entity selection"
                uit_width = 0.12
                uit_height = 0.03
                uit_on_click = [
                    tool_ent_search_get_filters
                ]
            ]
            ui_tool_button [
                uit_label = "Clear"
                uit_icon = "textures/icons/action"
                uit_tip_simple = "Clear filters"
                uit_width = 0.12
                uit_height = 0.03
                uit_on_click = [
                    tool_ent_search_reset_filters
                ]
            ]
            ui_tool_button [
                uit_label = "Search"
                uit_icon = "textures/icons/spectator"
                uit_tip_simple = "Perform search"
                uit_width = 0.12
                uit_height = 0.03
                uit_on_click = [
                    tool_ent_search_find
                ]
            ]
            ui_tool_button [
                uit_label = "Cancel"
                uit_icon = "textures/icons/warning"
                uit_tip_simple = "Cancel"
                uit_width = 0.12
                uit_height = 0.03
                uit_on_click = [
                    tool_ent_ui_mode = $T_ENT_UI_EDIT
                ]
            ]
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        uihlist 0 [
            uiclamp 1 1

            ui_tool_dropdown tool_ent_search_type $tool_ent_types [
                uit_label = "Type"
            ]
            ui_tool_button [
                uit_icon = "textures/icons/arrow"
                uit_tip_simple = "Get type from currently selected entity"
                uit_icon_size = 0.012
                uit_on_click = [
                    tool_ent_search_get_type
                ]
            ]
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uitext "Attribute filters" $ui_tool_text_size_m

        tool_ent_calc_dyn_content_h
    ]

    local _type_idx _num_attrs
    _type_idx = $tool_ent_search_type
    _num_attrs = (entityattrs $_type_idx 1)

    ui_tool_vscrollarea [
        uivlist 0 [
            uiclamp 1 1
            uigrid 3 $ui_tool_elem_space_l $ui_tool_elem_space_s [
                uiclamp 1 1
                loop attridx $_num_attrs [
                    ui_tool_dropdown [tool_ent_search_attr_op_@attridx] $tool_ent_search_operators [
                        uit_width = 0.06
                        uit_menu_width = 0.06
                    ]
                    ui_tool_numinput [tool_ent_search_attr_@attridx] $intmin $intmax 1 [
                        uit_label = (getentattr $_type_idx @attridx $tool_ent_search_attr_0)
                        uit_val_format = i
                        uit_can_reset = 1
                    ]
                    ui_tool_button [
                        uit_icon = "textures/icons/arrow"
                        uit_tip_simple = "Get value from currently selected entity"
                        uit_icon_size = 0.012
                        uit_on_click = [
                            tool_ent_search_get_attr $attridx
                        ]
                    ]
                ]
            ]
        ]
    ] [
        uit_height = $tool_ent_dyn_content_h
    ]
]

tool_ent_placer_template_list_new_name = ""

ui_tool_ent_placer_new_list = [
    if $toolpanel_this_isinit [
        tool_ent_placer_template_list_new_name = ""
    ]

    uispace $ui_tool_elem_space_l $ui_tool_elem_space_l [
        uiclamp 1 1

        uivlist $ui_tool_elem_space_l [
            uiclamp 1 1

            ui_tool_textinput tool_ent_placer_template_list_new_name 16 [
                uit_label = "List name"
            ]

            ui_tool_button [
                uit_label = "Add"
                uit_on_click = [
                    local _new_name
                    _new_name = (tool_ent_placer_add_template_list $tool_ent_placer_template_list_new_name)
                    tool_ent_placer_template_list_focus = (listfind=s $tool_ent_placer_valid_template_lists $_new_name)
                    tool_ent_placer_set_template_list $_new_name

                    toolpanel_close_this
                ]
                uit_width = 0.2
            ]
        ]
    ]
]

ui_tool_ent_placer_num_modifiers = [
    uispace $ui_tool_elem_space_l $ui_tool_elem_space_l [
        uiclamp 1 1

        uivlist $ui_tool_elem_space_l [
            uiclamp 1 1

            ui_tool_numinput tool_ent_placer_mod_attr 0 99999 1 [
                uit_label = "Variance"
                uit_val_format = i
            ]

            ui_tool_button [
                uit_label = "Confirm"
                uit_on_click = [
                    tool_ent_placer_mod_update $tool_ent_placer_mod_attr
                    toolpanel_close_this
                ]
                uit_width = 0.1
            ]
        ]
    ]
]

ui_tool_ent_placer = [
    tool_set_cursor_icon "textures/icons/attack"

    local _template _text_col _template_lists
    _num_lists = 0
    _template_lists = $tool_ent_placer_valid_template_lists
    append _template_lists ["<New list>"]

    uivlist $ui_toolpanel_elem_space [
        uitext "Entity placer" $ui_tool_text_size_m

        uifill 0 $ui_tool_elem_space_l

        uihlist 0 [
            uiclamp 1 1
            uitext "Templates:" $ui_tool_text_size_s
            uihlist 0 [
                ui_tool_button [
                    uit_icon = "textures/hud/exit"
                    uit_icon_size = 0.021
                    uit_icon_align = 0
                    uit_pad = 0
                    uit_tip_simple = "Remove list"
                    uit_on_click = [
                        tool_ent_placer_rem_template_list (at $_template_lists $tool_ent_placer_template_list_focus)
                        tool_ent_placer_reset_focus
                    ]
                    uit_disabled = @(< $tool_ent_placer_template_list_focus 0)
                ]
                ui_tool_dropdown tool_ent_placer_template_list_focus $_template_lists [
                    uit_on_change = [
                        if (>= $tool_ent_placer_template_list_focus $tool_ent_placer_num_valid_template_lists) [
                            tool_ent_placer_reset_focus
                            toolpanel_open tool_ent_placer_new_list popup [
                                uit_position = [@@(uicursorpos)]
                                uit_width = 0.3
                            ]
                        ] [
                            local _sel_name
                            _sel_name = (at $tool_ent_placer_valid_template_lists $tool_ent_placer_template_list_focus)
                            tool_ent_placer_set_template_list $_sel_name
                        ]
                    ]
                ]
            ]
        ]

        uicolour $ui_tool_dark_colour 0 0 [
            uiclamp 1 1

            ui_tool_vscrollarea [
                uigrid 2 0 0 [
                    uiclamp 1 1
                    loop temp_idx (listlen (getalias $tool_ent_placer_templates)) [
                        _template = (at (getalias $tool_ent_placer_templates) $temp_idx)
                        _text_col = (? (tool_ent_placer_template_isfocus $temp_idx) $ui_tool_accent_colour $ui_tool_dark_accent_colour)

                        ui_tool_button [
                            uit_icon = "textures/hud/exit"
                            uit_icon_size = 0.015
                            uit_pad = 0
                            uit_tip_simple = "Remove template"
                            uit_on_click = [
                                tool_ent_placer_template_remove @temp_idx
                            ]
                        ]

                        ui_tool_button [
                            uit_label = [@(concatword (at $_template 0) " (" $temp_idx ")")]
                            uit_label_align = -1
                            uit_label_size = $ui_tool_text_size_xs
                            uit_width = 0.32
                            uit_colour = $_text_col
                            uit_tip_simple = "Select template"
                            uit_on_click = [
                                tool_ent_placer_set_template_focus @temp_idx
                            ]
                        ]
                    ]

                    uialign* -1 -1
                ]
            ] [
                uit_height = $tool_ent_list_height
                uit_scroll_speed = 0.2
                uit_scrollbar_autohide = 0
            ]
        ]

        uihlist $ui_tool_elem_space_s [
            uiclamp 1 1

            uihlist $ui_tool_elem_space_s [
                ui_tool_button [
                    uit_label = "New"
                    uit_on_click = [
                        tool_ent_pick [
                            tool_ent_placer_template_add $arg1
                        ]
                    ]
                    uit_disabled = (< $tool_ent_placer_template_list_focus 0)
                ]
                ui_tool_button [
                    uit_label = "From entity"
                    uit_on_click = [
                        tool_ent_placer_template_froment
                    ]
                    uit_disabled = (|| [! (getenginestat 15)] [< $tool_ent_placer_template_list_focus 0])
                ]
                ui_tool_button [
                    uit_label = "Duplicate"
                    uit_on_click = [
                        tool_ent_placer_template_duplicate
                    ]
                    uit_disabled = (! (tool_ent_placer_template_hasfocus))
                ]
            ]
            uialign- -1

            uihlist 0 [
                ui_tool_switch tool_ent_placer_random [
                    uit_options = [ "Sel" "Random" ]
                    uit_label = "Place mode"
                ]
            ]
            uialign- 1
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        tool_ent_calc_dyn_content_h
    ]

    local _edit_type
    _edit_type = (? $tool_ent_edit_attr ui_tool_ent_edit_attr [ui_tool_ent_@tool_ent_type])

    if (&& (!=s $tool_ent_type "") (tool_ent_placer_template_hasfocus)) [
        ui_tool_vscrollarea [
            uivlist 0 [
                uiclamp 1 1
                ui_tool_numinput tool_ent_placer_mod_zoffset -1000 1000 1 [
                    uit_label = "Z offset"
                    uit_immediate = 0
                    uit_on_change = [
                        tool_ent_placer_mod_update $tool_ent_placer_mod_zoffset 0
                    ]
                    uit_menu_props = [
                        uit_item_names = ["Placer modifiers"]
                        uit_on_select = [
                            tool_ent_placer_mod_focus = $TOOL_ENT_PLACER_MOD_ZOFFSET_VARIANCE
                            tool_ent_placer_mod_attr = (tool_ent_placer_mod_get)
                            toolpanel_open tool_ent_placer_num_modifiers popup [
                                uit_position = (uicursorpos)
                                uit_width = 0.3
                            ]
                        ]
                    ]
                ]

                uifill 0 $ui_tool_elem_space_s
                uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
                uifill 0 $ui_tool_elem_space_s

                _edit_type
            ]
        ] [
            uit_height = $tool_ent_dyn_content_h
        ]
    ]
]

ui_tool_ent_undercursor = [
    if (> $numenthover 1) [
        uitext (concat $numenthover "entities here") $ui_tool_text_size_xs
    ] [
        uivlist $ui_tool_elem_space_s [
            uitext (concat "^fAEntity:^fw" (enttype)) $ui_tool_text_size_xs
            uialign- -1
            uigrid 8 $ui_tool_elem_space_s $ui_tool_elem_space_s [
                local _type_idx
                _type_idx = (indexof $tool_ent_types (enttype))
                loop i (getentattr $_type_idx -1) [
                    uitext (concatword "^fA" (getentattr $_type_idx $i) ":") $ui_tool_text_size_xs
                    uitext (entattr $i) $ui_tool_text_size_xs
                ]
            ]
        ]
    ]
]

tool_ent_undercursor_proc = [
    if $numenthover [
        toolview_undercursor = ui_tool_ent_undercursor

        if (> $numenthover 1) [
            tool_rightclickable
        ]
    ]
]
tool_proc_add tool_ent_undercursor_proc

ui_tool_ent_on_open = [
    if $enteditingonpanel [
        entediting 1
    ]
]

ui_tool_ent_on_close = [
    if $enteditingonpanel [
        entediting 0
    ]
]

ui_tool_ent = [
    tool_ent_edit = 0
    tool_ent_dyn_content_h = $toolpanel_content_max_height

    uivlist 0 [
        uivlist $ui_toolpanel_elem_space [
            uiclamp 1 1 1 1

            uihlist 0 [
                uiclamp 1 1
                ui_tool_checkbox entselsnap [
                    uit_label = "Snap entities to grid"
                ]

                ui_tool_dropdown entdrop [
                    "Drop on camera"
                    "Drop on floor"
                    "Drop on selection"
                    "Drop on sel. floor"
                ] [
                    uit_tip_simple = "Entity drop position"
                    uit_width = 0.15
                ]
                uialign- 1
            ]

            uihlist $ui_tool_elem_space_l [
                ui_tool_checkbox entediting [
                    uit_label = "Select entities"
                    uit_tip_simple = "Allow selecting of entities"
                ]

                ui_tool_checkbox entselmapmodelbox [
                    uit_label = "BB select"
                    uit_tip_simple = "Allow selection of entity bounding boxes"
                ]
            ]
            uialign- -1

            uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]

            ui_tool_ent_list
            ui_tool_ent_controls

            uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]

            tool_ent_calc_dyn_content_h
        ]

        local _ent_uis
        _ent_uis = [
            ui_tool_ent_cur
            ui_tool_ent_unify
            ui_tool_ent_search
            ui_tool_ent_placer
        ]

        if (= $tool_ent_ui_mode @T_ENT_UI_EDIT) [
            tool_ent_edit = 1
        ]

        tool_ent_sync
        do (at $_ent_uis $tool_ent_ui_mode)
    ]
]
