ui_tool_edit_bind_text_width = 0.4
ui_tool_edit_bind_field_width = 0.2

tool_bind_search_query = ""

// 1:<action> 2:<query>
tool_action_filter = [
    > (strcasestr @(tool_action_prop [$arg1] short_desc) $arg2) -1
]

// 1:<action>
ui_tool_edit_cat_bind = [
    local _action_type _desc
    _action_type = @(tool_action_prop [$arg1] type)
    _desc = @(tool_action_prop [$arg1] long_desc)

    uihlist 0 [
        uitarget $ui_tool_edit_bind_text_width 0 [
            uitext @(tool_action_prop [$arg1] short_desc) $ui_toolview_small_text_size
            uialign- -1
            ui_tool_tip [
                uit_tip_simple = [@_desc]
            ]
        ]

        if (= $_action_type $TOOL_ACTION_SCROLL) [
            uitarget 0 0 [
                uiimage "textures/keys/mouse5" $ui_toolview_accent_colour 0 (*f $ui_toolview_base_icon_size 0.8) $ui_toolview_base_icon_size
                ui_tool_tip [
                    uit_tip_simple = "Mouse scroll action"
                ]
            ]
        ]
    ]

    uihlist 0 [
        uiclamp 1 1
        ui_tool_keyinput [tb_@arg1] [
            uit_var_update = 0
            uit_bind_command = toolbind
            uit_bind_action = @arg1
            uit_width = $ui_tool_edit_bind_field_width
        ]
        ui_tool_button [
            uit_label = "X"
            uit_tip_simple = "Clear bind"
            uit_on_click = [
                toolunbind @arg1
            ]
        ]
        uiclamp- 1 1
    ]
    uialign- 1

    uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
    uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]
]

// 1:<category>
ui_tool_edit_cat_bindings = [
    local _num_actions _action_idx _action
    _num_actions = (listlen $[tool_actions_@arg1])

    uigrid 2 $ui_toolview_base_elem_space 0 [
        uiclamp 1 1
        loop i $_num_actions [
            _action_idx = (at $[tool_actions_@arg1] $i)
            _action = (at $tool_actions $_action_idx)

            if (|| (=s tool_bind_search_query "") [tool_action_filter $_action $tool_bind_search_query]) [
                ui_tool_edit_cat_bind $_action
            ]
        ]
    ]
]

ui_tool_edit_binding_presets = [
    uivlist 0 [
        uiclamp 1 1

        ui_tool_button [
            uit_label = "Legacy"
            uit_tip_simple = "Provides old-style, legacy editor key binds"
            uit_on_click = [
                toolpanel_close_this
                exec "config/tool/binds/legacy.cfg"
            ]
        ]

        ui_tool_button [
            uit_label = "Default"
            uit_tip_simple = "Provides new editor key binds, tailored for the overhauled workflow"
            uit_on_click = [
                toolpanel_close_this
                exec "config/tool/binds/default.cfg"
            ]
        ]

        uiclamp* 1 1
    ]
]

ui_tool_edit_bindings = [
    local _num_cats _cat
    _num_cats = (listlen $tool_actions_categories)

    uivlist $ui_toolview_base_elem_space [
        ui_tool_button [
            uit_label = "Presets"
            uit_on_click = [
                toolpanel_open tool_edit_binding_presets popup [
                    uit_position = (uicursorpos)
                ]
            ]
        ]

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        uihlist 0 [
            uitext "Search: " $ui_toolview_small_text_size
            uiinput tool_bind_search_query 32 [] $ui_toolpanel_text_size 0 "[search for action here]"
        ]

        uiclamp* 1 1

        ui_tool_vscrollarea [
            uivlist 0 [
                uiclamp 1 1
                loop i $_num_cats [
                    _cat = (at $tool_actions_categories $i)
                    uifill 0 $ui_toolview_base_elem_space
                    ui_tool_collapsegroup [tool_bind_group_@_cat] [
                        ui_tool_edit_cat_bindings $_cat
                    ] [
                        uit_label = $_cat
                        uit_force_open = @(!=s $tool_bind_search_query "")
                    ]
                ]
            ]
        ] [
            uit_height = 0.5
        ]
    ]
]

# ui_tool_edit_settings [
    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        ui_tool_var_input "Undo size (MB):" undomegs 8

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_var_input "Float speed:" floatspeed 50 [
            uit_immediate = 1
        ]

        ui_tool_var_input "Fast float speed:" shiftfloatspeed 50 [
            uit_immediate = 1
        ]

        ui_tool_var_input "Float coast:" floatcoast 1 [
            uit_immediate = 1
        ]

        ui_tool_var_input "Edit FOV:" editfov 10 [
            uit_immediate = 1
        ]

        ui_tool_checkbox nompedit [
            uit_label = "Lock non-multiplayer functionality"
            uit_tip_simple = "Disables edit functions which cannot be synchronized in multiplayer"
        ]

        ui_tool_dropdown entdragmode [
            "Drag"
            "Grab + drag"
            "Press grab"
        ] [
            uit_label = "Entity move controls"
            uit_tips = [
                "Hold left mouse button and drag an entitiy"
                "Hold grab and then left mouse button, and drag an entity.^nGrab button may be releaseed during movement."
                "Press grab button to toggle entity moving"
            ]
        ]

        ui_tool_checkbox enteditingonpanel [
            uit_label = "Entity edit only with panel open"
            uit_tip_simple = "Requires the entity panel to be open to allow for entity editing"
        ]

        ui_tool_checkbox passthroughentcancel [
            uit_label = "Cancel entity selection in passthrough"
            uit_tip_simple = "Deselects entities when engaging passthrough mode"
        ]

        ui_tool_checkbox texeditdefaultautoapply [
            uit_label = "Default auto-apply during texture edit"
            uit_tip_simple = "Automatically enables auto-apply for the duration of editing texture variants"
        ]

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_var_input "Edit UI scale:" uitoolscale 0.1

        ui_tool_checkbox toolinfoactionsound [
            uit_label = "Edit info notification sound"
            uit_tip_simple = "Play a notification sound whenever edit information bar is shown"
        ]

        ui_tool_checkbox toolinfoextendedstatus [
            uit_label = "Show extended render status"
            uit_tip_simple = "Display extended renderer information in the status bar"
        ]

        ui_tool_var_input "Edit Info length (ms)" toolinfoactionlength 1000

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_checkbox autoshowblendmap [
            uit_label = "Auto-prepare blendmap painting"
            uit_tip_simple = "Automatically prepares world geometry when entering blendmap painting mode"
        ]

        uiline $ui_toolview_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        #(ui_tool_get_action_button ta_edit_bindings)
        uiclamp- 1 1

        uialign* -1 -1
    ]
]

tool_search_max_results = 10

tool_search_query = ""
tool_search_results = []
tool_search_num_actions = 0
tool_search_action_idx = 0
tool_search_has_more_results = 0
tool_search_focus = 0

tool_search_init = [
    tool_search_query = ""
    tool_search_results = []
    tool_search_num_actions = 0
    tool_search_has_more_results = 0
    tool_search_action_idx = 0
]

tool_search_proc = [
    tool_search_num_actions = 0
    tool_search_action_idx = 0
    tool_search_has_more_results = 0
    tool_search_results = []

    if (strlen $arg1) [
        loopwhile i $tool_actions_num (< $tool_search_num_actions (+ $tool_search_max_results 1)) [
            local _action
            _action = (at $tool_actions $i)

            if (tool_action_filter $_action $arg1) [
                append tool_search_results $_action
                tool_search_num_actions = (+ 1 $tool_search_num_actions)
            ]
        ]
    ]

    tool_search_has_more_results = (> $tool_search_num_actions $tool_search_max_results)
    tool_search_num_actions = (clamp $tool_search_num_actions 0 $tool_search_max_results)
]

ui_tool_search_input = [
    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uiimage "textures/icons/action" $ui_toolview_accent_colour 0 $ui_toolview_base_icon_size $ui_toolview_base_icon_size
        uiinput tool_search_query 32 [
            tool_search_proc $tool_search_query
        ] $ui_toolpanel_text_size 0 "[search for action here]" 0 "" [
            if (&& $tool_search_focus $ui_freecursor) [
                uieditorsetfocus
                tool_search_focus = 0
            ]
        ]
    ]
]

ui_tool_search_list = [
    if $toolpanel_this_isinit [
        tool_search_action_idx = -1
    ]

    loop i $tool_search_num_actions [
        local _action _action_type _cat _label _keys _bind_info
        _action = (at $tool_search_results $i)
        _action_type = @(tool_action_prop [$_action] type)
        _cat = @(tool_action_prop [$_action] category)
        _label = @(tool_action_prop [$_action] short_desc)
        _keys = $[tb_@_action]
        _bind_info = (prettybindinfo (at $_keys 0) (at $_keys 1))

        if (= $i $tool_search_action_idx) uisethighlight
        ui_tool_button [
            uit_children = [
                uihlist $ui_toolview_small_elem_space [
                    if (= $_action_type $TOOL_ACTION_SCROLL) [
                        uiimage "textures/keys/mouse5" $ui_toolview_accent_colour 0 (*f $ui_toolview_small_icon_size 0.8) $ui_toolview_small_icon_size
                        _bind_info = (concatword "+" $_bind_info)
                    ]
                    if @_bind_info [
                        uicolourtext [@_bind_info] $ui_toolview_warn_colour $ui_toolview_tiny_text_size
                        uitriangle $ui_toolview_dark_accent_colour $ui_toolview_separator_icon_size $ui_toolview_separator_icon_size -90
                    ]
                    uicolourtext @_cat $ui_toolview_dark_accent_colour $ui_toolview_tiny_text_size
                ]
                uialign- 1
            ]
            uit_label = [@_label]
            uit_label_align = -1
            uit_tip_action = $_action
            uit_on_click = [
                toolpanel_close tool_search
                tool_do_action @_action $TOOL_ACTION_CTX_SEARCH
            ]
        ]
        uiclamp- 1 1
    ]

    if $tool_search_has_more_results [
        local _txt
        _txt = (concat "Search limited to" $tool_search_max_results "results")
        uifill 0 $ui_toolview_base_elem_space
        uicolourtext $_txt $ui_toolview_dark_accent_colour $ui_toolview_small_text_size
    ]
]

// 1:<type> 2:<param>
ui_tool_search_nav = [
    case $arg1 @TOOLPANEL_NAV_V [
        tool_search_action_idx = (- $tool_search_action_idx $arg2)
        tool_search_action_idx = (clamp $tool_search_action_idx 0 (- $tool_search_num_actions 1))
    ] @TOOLPANEL_NAV_ENTER [
        toolpanel_close tool_search
        tool_do_action (at $tool_search_results $tool_search_action_idx)
    ] @TOOLPANEL_NAV_ESCAPE [
        toolpanel_close tool_search
    ]
]

ui_tool_search = [
    uivlist 0 [
        uiclamp 1 1
        ui_tool_search_input
        uifill 0 $ui_toolpanel_elem_space
        uiline $ui_toolview_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_toolpanel_elem_space
        ui_tool_search_list
    ]
]

ui_tool_main_menu = [
    uivlist $ui_padbutton [
        ui_contents_main
    ]
]
