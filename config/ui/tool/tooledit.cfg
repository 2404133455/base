ui_tool_edit_bind_text_width = 0.4
ui_tool_edit_bind_field_width = 0.2

tool_bind_search_query = ""

defvarp toolfreevaluescroll 0 0 1

// 1:<action id> 2:<query>
tool_action_filter = [
    > (strcasestr (at $$arg1 $TA_PROP_SHORT_DESC) $arg2) -1
]

// 1:<action id>
ui_tool_edit_cat_bind = [
    local _action_type _desc
    _action_type = (at $$arg1 $TA_PROP_TYPE)
    _desc = (at $$arg1 $TA_PROP_LONG_DESC)

    uihlist 0 [
        uitarget $ui_tool_edit_bind_text_width 0 [
            uitext (at $$arg1 $TA_PROP_SHORT_DESC) $ui_tool_text_size_s
            uialign- -1
            ui_tool_tip [
                p_tip_simple = [@_desc]
            ]
        ]

        if (= $_action_type $TA_TYPE_SCROLL) [
            uitarget 0 0 [
                uiimage "textures/keys/mouse5" $ui_tool_accent_colour 0 (*f $ui_tool_icon_size_m 0.8) $ui_tool_icon_size_m
                ui_tool_tip [
                    p_tip_simple = "Mouse scroll action"
                ]
            ]
        ]
    ]

    uihlist 0 [
        uiclamp 1 1
        ui_tool_keyinput [tb_@arg1] [
            p_var_update = 0
            p_bind_command = toolbind
            p_bind_action = @arg1
            p_width = $ui_tool_edit_bind_field_width
        ]
        ui_tool_button [
            p_label = "X"
            p_tip_simple = "Clear bind"
            p_on_click = [
                toolunbind @arg1
            ]
        ]
        uiclamp- 1 1
    ]
    uialign- 1

    uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
    uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
]

// 1:<category>
ui_tool_edit_cat_bindings = [
    local _num_actions _action
    _num_actions = (listlen $[tool_actions_@arg1])

    uigrid 2 $ui_tool_elem_space_l 0 [
        uiclamp 1 1
        loop i $_num_actions [
            _action = (at $[tool_actions_@arg1] $i)

            if (|| (=s tool_bind_search_query "") [tool_action_filter $_action $tool_bind_search_query]) [
                ui_tool_edit_cat_bind $_action
            ]
        ]
    ]
]

ui_tool_edit_binding_presets = [
    uivlist 0 [
        uiclamp 1 1

        ui_tool_button [
            p_label = "Legacy"
            p_tip_simple = "Provides old-style, legacy editor key binds"
            p_on_click = [
                toolpanel_close_this
                exec "config/tool/binds/legacy.cfg"
            ]
        ]

        ui_tool_button [
            p_label = "Default"
            p_tip_simple = "Provides new editor key binds, tailored for the overhauled workflow"
            p_on_click = [
                toolpanel_close_this
                exec "config/tool/binds/default.cfg"
            ]
        ]

        uiclamp* 1 1
    ]
]

ui_tool_edit_bindings = [
    local _num_cats _cat
    _num_cats = (listlen $tool_action_categories)

    uivlist $ui_tool_elem_space_l [
        ui_tool_button [
            p_label = "Presets"
            p_on_click = [
                toolpanel_open tool_edit_binding_presets popup [
                    p_position = (uicursorpos)
                ]
            ]
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        uihlist 0 [
            uitext "Search: " $ui_tool_text_size_s
            uiinput tool_bind_search_query 32 [] $ui_toolpanel_text_size 0 "[search for action here]"
        ]

        uiclamp* 1 1

        ui_tool_vscrollarea [
            uivlist 0 [
                uiclamp 1 1
                loop i $_num_cats [
                    _cat = (at $tool_action_categories $i)
                    uifill 0 $ui_tool_elem_space_l
                    ui_tool_collapsegroup [tool_bind_group_@_cat] [
                        ui_tool_edit_cat_bindings $_cat
                    ] [
                        p_label = $_cat
                        p_force_open = @(!=s $tool_bind_search_query "")
                    ]
                ]
            ]
        ] [
            p_height = 0.5
        ]
    ]
]

# ui_tool_edit_settings [
    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        ui_tool_var_input "Undo size (MB):" undomegs 8

        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_var_input "Float speed:" floatspeed 50 [
            p_immediate = 1
        ]

        ui_tool_var_input "Fast float speed:" shiftfloatspeed 50 [
            p_immediate = 1
        ]

        ui_tool_var_input "Float coast:" floatcoast 1 [
            p_immediate = 1
        ]

        ui_tool_var_input "Edit FOV:" editfov 10 [
            p_immediate = 1
        ]

        ui_tool_checkbox nompedit [
            p_label = "Lock non-multiplayer functionality"
            p_tip_simple = "Disables edit functions which cannot be synchronized in multiplayer"
        ]

        ui_tool_var_input "Entity sel. box size" entselradius 1 [
            p_immediate = 1
        ]

        ui_tool_checkbox entmoveselect [
            p_label = "Add entities to selection on grab"
            p_tip_simple = "Adds hovered entities to move selection with already having entities selected"
        ]

        ui_tool_checkbox enteditingonpanel [
            p_label = "Entity edit only with panel open"
            p_tip_simple = "Requires the entity panel to be open to allow for entity editing"
        ]

        ui_tool_checkbox passthroughentcancel [
            p_label = "Cancel entity selection in passthrough"
            p_tip_simple = "Deselects entities when engaging passthrough mode"
        ]

        ui_tool_checkbox texeditdefaultautoapply [
            p_label = "Default auto-apply during texture edit"
            p_tip_simple = "Automatically enables auto-apply for the duration of editing texture variants"
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_var_input "Edit UI scale:" uitoolscale 10 [
            p_val_format = i
            p_get = [ toint (round (*f $arg1 100)) ]
            p_set = [ divf $arg1 100 ]
            p_val_text = [ concatword $arg1 "^%" ]
        ]

        ui_tool_checkbox toolinfoactionsound [
            p_label = "Edit info notification sound"
            p_tip_simple = "Play a notification sound whenever edit information bar is shown"
        ]

        ui_tool_checkbox toolinfoextendedstatus [
            p_label = "Show extended render status"
            p_tip_simple = "Display extended renderer information in the status bar"
        ]

        ui_tool_var_input "Edit Info length" toolinfoactionlength 1 [
            p_val_format = f
            p_get = [ divf $arg1 1000 ]
            p_set = [ toint (round (*f $arg1 1000)) ]
            p_val_text = [ concat $arg1 "s" ]
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_checkbox toolfreevaluescroll [
            p_label = "Free value scrolling"
            p_tip_simple = "When enabled, allows scrolling of values on supported widgets without holding the shift key.^nCan get in the way of scrolling displayed contents, use at your discretion."
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        ui_tool_checkbox autoshowblendmap [
            p_label = "Auto-prepare blendmap painting"
            p_tip_simple = "Automatically prepares world geometry when entering blendmap painting mode"
        ]

        ui_tool_checkbox disableblendpaintonuiclose [
            p_label = "Disable blend paining when exiting painting panel"
            p_tip_simple = "Automatically disables blend painting mode when closing the blendmap painting panel"
        ]

        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]

        #(ui_tool_get_action_button ta_edit_bindings)
        uiclamp- 1 1

        uialign* -1 -1
    ]
]

tool_search_max_results = 10

tool_search_query = ""
tool_search_results = []
tool_search_num_actions = 0
tool_search_action_idx = 0
tool_search_has_more_results = 0
tool_search_focus = 0

tool_search_init = [
    tool_search_query = ""
    tool_search_results = []
    tool_search_num_actions = 0
    tool_search_has_more_results = 0
    tool_search_action_idx = 0
]

tool_search_proc = [
    tool_search_num_actions = 0
    tool_search_action_idx = 0
    tool_search_has_more_results = 0
    tool_search_results = []

    if (strlen $arg1) [
        loopwhile i $tool_num_actions (< $tool_search_num_actions (+ $tool_search_max_results 1)) [
            local _action
            _action = (at $tool_actions $i)

            if (tool_action_filter $_action $arg1) [
                append tool_search_results $_action
                tool_search_num_actions = (+ 1 $tool_search_num_actions)
            ]
        ]
    ]

    tool_search_has_more_results = (> $tool_search_num_actions $tool_search_max_results)
    tool_search_num_actions = (clamp $tool_search_num_actions 0 $tool_search_max_results)
]

ui_tool_search_input = [
    uihlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        uiimage "textures/icons/action" $ui_tool_accent_colour 0 $ui_tool_icon_size_m $ui_tool_icon_size_m
        uiinput tool_search_query 32 [
            tool_search_proc $tool_search_query
        ] $ui_toolpanel_text_size 0 "[search for action here]" 0 "" [
            if (&& $tool_search_focus $ui_freecursor) [
                uieditorsetfocus
                tool_search_focus = 0
            ]
        ]
    ]
]

ui_tool_search_list = [
    if $toolpanel_this_isinit [
        tool_search_action_idx = -1
    ]

    loop i $tool_search_num_actions [
        local _action _action_type _cat _label _keys _bind_info
        _action = (at $tool_search_results $i)
        _action_type = (at $$_action $TA_PROP_TYPE)
        _cat = (at $$_action $TA_PROP_CATEGORY)
        _label = (at $$_action $TA_PROP_SHORT_DESC)
        _keys = $[tb_@_action]
        _bind_info = (prettybindinfo (at $_keys 0) (at $_keys 1))

        if (= $i $tool_search_action_idx) uisethighlight
        ui_tool_button [
            p_children = [
                uihlist $ui_tool_elem_space_s [
                    if (= $_action_type $TA_TYPE_SCROLL) [
                        uiimage "textures/keys/mouse5" $ui_tool_accent_colour 0 (*f $ui_tool_icon_size_s 0.8) $ui_tool_icon_size_s
                        _bind_info = (concatword "+" $_bind_info)
                    ]
                    if @_bind_info [
                        uicolourtext [@_bind_info] $ui_tool_warn_colour $ui_tool_text_size_xs
                        uitriangle $ui_tool_dark_accent_colour $ui_tool_separator_size $ui_tool_separator_size -90
                    ]
                    uicolourtext @_cat $ui_tool_dark_accent_colour $ui_tool_text_size_xs
                ]
                uialign- 1
            ]
            p_label = [@_label]
            p_label_align = -1
            p_tip_action = $_action
            p_on_click = [
                toolpanel_close tool_search
                tool_do_action @_action $TA_CTX_SEARCH
            ]
        ]
        uiclamp- 1 1
    ]

    if $tool_search_has_more_results [
        local _txt
        _txt = (concat "Search limited to" $tool_search_max_results "results")
        uifill 0 $ui_tool_elem_space_l
        uicolourtext $_txt $ui_tool_dark_accent_colour $ui_tool_text_size_s
    ]
]

// 1:<type> 2:<param>
ui_tool_search_nav = [
    case $arg1 @TOOLPANEL_NAV_V [
        tool_search_action_idx = (- $tool_search_action_idx $arg2)
        tool_search_action_idx = (clamp $tool_search_action_idx 0 (- $tool_search_num_actions 1))
    ] @TOOLPANEL_NAV_ENTER [
        if (!=s $tool_search_results) [
            toolpanel_close tool_search
            tool_do_action (at $tool_search_results $tool_search_action_idx) $TA_CTX_SEARCH
        ]
    ] @TOOLPANEL_NAV_ESCAPE [
        toolpanel_close tool_search
    ]
]

ui_tool_search = [
    uivlist 0 [
        uiclamp 1 1
        ui_tool_search_input
        uifill 0 $ui_toolpanel_elem_space
        uiline $ui_tool_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_toolpanel_elem_space
        ui_tool_search_list
    ]
]

ui_tool_main_menu = [
    uivlist $ui_padbutton [
        ui_contents_main
    ]
]
