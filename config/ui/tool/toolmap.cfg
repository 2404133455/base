ui_toolview_panel_save_as_save_button_width = (uiwidth 0.07)

tool_save_as_filename = ""

ui_tool_save_as = [
    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        ui_tool_textinput tool_save_as_filename 16 [
            p_label = "File name:"
            p_prompt = "[click here to set file name]"
        ]
        ui_tool_button [
            p_label = "Save"
            p_on_click = [
                savemap $tool_save_as_filename
            ]
            p_width = $ui_toolview_panel_save_as_save_button_width
            p_disabled = (=s $tool_save_as_filename "")
        ]
        uialign- 1
    ]
]

tool_map_assetpack = ""

# ui_tool_map_settings [
    uivlist 0 [
        uiclamp 1 1
        uiimage $mapname 0xFFFFFF 0 0.24 0.18

        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        ui_tool_textinput mapname 24 [
            p_label = "Map path:"
        ]
        ui_tool_textinput maptitle 24 [
            p_label = "Title:"
        ]
        ui_tool_textinput mapauthor 24 [
            p_label = "Author:"
        ]

        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        ui_tool_switch forcemapvariant [
            p_label = "Shown map variant"
            p_label_size = $ui_tool_text_size_s
            p_options = [ "Normal" "Alternative" ]
            p_get = [
                = $arg1 2
            ]
            p_set = [
                ? (= $arg1 2) 1 2
            ]
        ]

        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        if $toolpanel_this_isinit [
            tool_map_assetpack = ""
        ]

        uihlist 0 [
            uiclamp 1 1

            uitext "Load asset pack" $ui_tool_text_size_s
            uialign- -1

            ui_tool_fileselect tool_map_assetpack [
                p_file_type = $TOOL_FILE_ASSETPACK
                p_columns = 4
                p_sel_size = 0.105
                p_show_value = 0
                p_picker_width = 0.48
                p_on_change = [
                    exec (concatword $tool_map_assetpack ".cfg")
                ]
            ]
            uialign- 1
        ]

        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        if (isonline) [
            uihlist 0 [
                ui_tool_button [
                    #(ui_tool_action_button ta_getmap)
                    p_height = $ui_tool_button_height_m_unscaled
                    p_width  = $ui_tool_button_width_l_unscaled
                ]

                ui_tool_button [
                    #(ui_tool_action_button ta_sendmap)
                    p_height = $ui_tool_button_height_m_unscaled
                    p_width  = $ui_tool_button_width_l_unscaled
                ]
            ]

            uifill 0 $ui_tool_elem_space_s
            uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
            uifill 0 $ui_tool_elem_space_s
        ]

        ui_tool_button [
            #(ui_tool_action_button ta_map_new)
            p_height = 0.05
        ]
        uiclamp- 1 1
    ]
]

tool_map_new_size = 10

ui_tool_map_new = [
    uivlist $ui_tool_elem_space_l [
        uifill 0.3
        ui_tool_numinput tool_map_new_size 10 14 1 [
            p_label = "Size"
            p_val_format = i
            p_can_edit = 0
            p_has_menu = 0
            p_val_text = [ concat (round (divf (* (<< 1 $arg1) (<< 1 $arg1)) 16000) 0.1) "km^^2" ]
            p_width = 0.2
        ]

        uihlist 0 [
            ui_tool_button [
                p_label = "Ok"
                p_width = 0.2
                p_height = 0.05
                p_on_click = [
                    toolpanel_close tool_map_new
                    newmap $tool_map_new_size
                ]
            ]
        ]
    ]
]
