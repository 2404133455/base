ui_toolview_panel_save_as_save_button_width = (uiwidth 0.07)

tool_save_as_filename = ""

ui_tool_save_as = [
    uivlist $ui_toolpanel_elem_space [
        uiclamp 1 1
        ui_tool_textinput tool_save_as_filename 16 [
            uit_label = "File name:"
            uit_prompt = "[click here to set file name]"
        ]
        ui_tool_button [
            uit_label = "Save"
            uit_on_click = [
                savemap $tool_save_as_filename
            ]
            uit_width = $ui_toolview_panel_save_as_save_button_width
            uit_disabled = (=s $tool_save_as_filename "")
        ]
        uialign- 1
    ]
]

tool_map_assetpack = ""

ui_tool_map_settings = [
    uivlist 0 [
        uiclamp 1 1
        uiimage $mapname 0xFFFFFF 0 0.24 0.18

        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        ui_tool_textinput mapname 24 [
            uit_label = "Map path:"
        ]
        ui_tool_textinput maptitle 24 [
            uit_label = "Title:"
        ]
        ui_tool_textinput mapauthor 24 [
            uit_label = "Author:"
        ]

        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        ui_tool_switch forcemapvariant [
            uit_label = "Shown map variant"
            uit_label_size = $ui_tool_text_size_s
            uit_options = [ "Normal" "Alternative" ]
            uit_get = [
                = $arg1 2
            ]
            uit_set = [
                ? (= $arg1 2) 1 2
            ]
        ]

        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        if $toolpanel_this_isinit [
            tool_map_assetpack = ""
        ]

        uihlist 0 [
            uiclamp 1 1

            uitext "Load asset pack" $ui_tool_text_size_s
            uialign- -1

            ui_tool_fileselect tool_map_assetpack [
                uit_file_type = $TOOL_FILE_ASSETPACK
                uit_columns = 4
                uit_sel_size = 0.105
                uit_show_value = 0
                uit_picker_width = 0.48
                uit_on_change = [
                    texgroup (filedir $tool_map_assetpack)
                    exec (concatword $tool_map_assetpack ".cfg")
                ]
            ]
            uialign- 1
        ]

        uifill 0 $ui_tool_elem_space_s
        uiline $ui_tool_dark_accent_colour 0 0 [ uiclamp 1 1 ]
        uifill 0 $ui_tool_elem_space_s

        ui_tool_button [
            @@@(ui_tool_action_button ta_map_new)
            uit_height = 0.05
        ]
        uiclamp- 1 1
    ]
]

tool_map_new_size = 10

ui_tool_map_new = [
    uivlist $ui_tool_elem_space_l [
        uifill 0.3
        ui_tool_numinput tool_map_new_size 10 14 1 [
            uit_label = "Size"
            uit_val_format = i
            uit_can_edit = 0
            uit_has_menu = 0
            uit_val_text = [ concat (round (divf (* (<< 1 $arg1) (<< 1 $arg1)) 16000) 0.1) "km^^2" ]
            uit_width = 0.2
        ]

        uihlist 0 [
            ui_tool_button [
                uit_label = "Ok"
                uit_width = 0.2
                uit_height = 0.05
                uit_on_click = [
                    toolpanel_close tool_map_new
                    newmap $tool_map_new_size
                ]
            ]
        ]
    ]
]
