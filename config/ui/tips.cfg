ui_tooltip_body = ""
ui_tooltip_time = 0
ui_tooltip_last = 0
ui_tooltip_skip = 0

uihidetip = [
    hideui "tooltip"
    ui_tooltip_body = ""
    ui_tooltip_time = 0
    ui_tooltip_last = 0
    ui_tooltip_skip = 0
]

uistarttip = [
    ui_tooltip_body = $arg1
    ui_tooltip_last = $totalmillis
    ui_tooltip_time = $ui_tooltip_last
    ui_tooltip_skip = $arg2
]

uiskiptip = [
    ui_tooltip_skip = $arg1
]

uitipmenu "tooltip" [
]

uitip = [
    if $arg2 [] [arg2 = 0]
    if (&& (!=s $arg1 "") (!=s (getalias [ui_tip_@arg1]) "")) [
        uihover [
            if (!=s $ui_tooltip_body $arg1) [
                if (uivisible "tooltip") [
                    case $ui_tooltip_skip 0 [
                        if (> (- $totalmillis $ui_tooltip_last $ui_tooltipdelay) $ui_tooltipblend) [uistarttip $arg1 $arg2] [uiskiptip 1]
                    ] 2 [uistarttip $arg1 3] 4 [uiskiptip $arg2]
                ] [uistarttip $arg1 $arg2]
            ] [
                if $ui_tooltip_time [
                    ui_tooltip_last = $totalmillis
                    if (! (uivisible "tooltip")) [showui "tooltip"]
                ]
            ]
        ]
    ]
]

newui "tooltip" [
    uimenu 0
    uiontop 1
    uitooltip 1
    uiallowinput 0
    uivlist 0 [
        uiclamp 1 1 1 1
        uiborderedimageclamped $skintex 0xff000000 0 $ui_texborder $ui_screenborder 0 0 [
            uiclamp 1 1 1 1
            uispace $ui_padsmall $ui_padsmaller [
                uiclamp 1 1 1 1
                uivlist 0 [[ui_tip_@ui_tooltip_body]]
                ui_tooltip_frame = (- $totalmillis $ui_tooltip_last)
                if (|| (= $ui_tooltip_time 0) (> $ui_tooltip_frame (? (> $ui_tooltip_skip 1) $ui_tooltipwait))) [
                    ui_tooltip_alpha = (-f 1 (divf (- $ui_tooltip_frame (? (> $ui_tooltip_skip 1) $ui_tooltipwait)) $ui_tooltipblend))
                    if (>f $ui_tooltip_alpha 0) [uichangeblendsroot $ui_tooltip_alpha] [
                        uichangeblendsroot 0
                        case $ui_tooltip_skip 0 [uihidetip] 1 [uiskiptip 2] 3 [uiskiptip 0] 4 [uihidetip]
                    ]
                ] [
                    ui_tooltip_alpha = (divf (- $totalmillis $ui_tooltip_time (? $ui_tooltip_skip 0 $ui_tooltipdelay)) $ui_tooltipblend)
                    if (<=f $ui_tooltip_alpha 1) [uichangeblendsroot $ui_tooltip_alpha] [
                        uichangeblendsroot 1
                        case $ui_tooltip_skip 3 [uiskiptip 4] 4 [if (> (- $totalmillis $ui_tooltip_time $ui_tooltipdelay) $ui_tooltipblend) [uiskiptip 0]]
                    ]
                ]
            ]
        ]
    ]
]
