ui_tooltip_body = ""
ui_tooltip_time = 0
ui_tooltip_last = 0

uitip = [
    if (&& (!=s $arg1 "") (!=s (getalias [ui_tip_@arg1]) "")) [
        uihover [
            if (!=s $ui_tooltip_body $arg1) [
                ui_tooltip_body = $arg1
                ui_tooltip_last = $totalmillis
                ui_tooltip_time = $ui_tooltip_last
                if (! (uivisible "tooltip")) [showui "tooltip"]
            ] [
                if $ui_tooltip_time [
                    ui_tooltip_last = $totalmillis
                ]
            ]
        ]
    ]
]

newui "tooltip" [
    uimenu 0
    uiontop 1
    uitooltip 1
    uiallowinput 0
    uivlist 0 [
        uistyle clampxy
        uiborderedimageclamped $skintex 0xff000000 0 $ui_texborder $ui_screenborder 0 0 [
            uistyle clampxy
            uispace $ui_padsmall $ui_padsmaller [
                uistyle clampxy
                uivlist 0 [[ui_tip_@ui_tooltip_body]]
                ui_tooltip_frame = (- $totalmillis $ui_tooltip_last)
                if (|| (= $ui_tooltip_time 0) (> $ui_tooltip_frame $ui_tooltipwait)) [
                    if (> $ui_tooltip_frame $ui_tooltipdelay) [
                        hideui "tooltip"
                    ] [
                        uiroot [uicolourblend (-f 1 (divf $ui_tooltip_frame $ui_tooltipdelay))]
                    ]
                ]
            ]
        ]
    ]
] [] [
    ui_tooltip_body = ""
    ui_tooltip_time = 0
    ui_tooltip_last = 0
]
