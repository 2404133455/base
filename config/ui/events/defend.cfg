event_defend_secure = [
    event_image (modetex) 0xFFFFFF
    local event_extra event_affinity
    event_extra = 0
    event_affinity = (geteventlist $event_id "args" "affinity")
    loop event_attackid $event_clients [
        if (= (geteventtag $event_id "client" $event_attackid "team") (geteventlist $event_id "args" (? (=s $event_action "secure") "owner" "enemy"))) [
            if $event_extra [
                uitext "+" $game_hud_event_text_size [
                    uistyle leftmiddle
                    uitextalign -1
                ]
            ]
            uitext (geteventname $event_id "client" $event_attackid) $game_hud_event_text_size [
                uistyle leftmiddle
                uitextalign -1
            ]
            event_extra = 1
        ]
    ]
    event_image $pointtex (getteamcolour (geteventlist $event_id "args" "oldowner"))
    event_image $arrowrighttex 0xFFFFFF
    event_image $pointtex (getteamcolour (geteventlist $event_id "args" "owner"))
    uitext (concatword (getdefendname $event_affinity) " (^fs^fy" (getprintable $event_affinity) "^fS)") $game_hud_event_text_size [
        uistyle leftmiddle
        uitextalign -1
    ]
]

event_defend_overthrow = [@event_defend_secure]
