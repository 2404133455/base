defvarp eventintime 0 250 $varidxmax
defvarp eventouttime 0 15000 $varidxmax
defvarp eventfadetime 0 500 $varidxmax
defvarp eventlines 0 10 $varidxmax
deffvarp eventblend $fvaridxnonzero 1 1
defvarp eventobitclassic 0 0 1

deftvarp claweventtex "<grey>textures/weapons/event/claw"
deftvarp pistoleventtex "<grey>textures/weapons/event/pistol"
deftvarp swordeventtex "<grey>textures/weapons/event/sword"
deftvarp shotguneventtex "<grey>textures/weapons/event/shotgun"
deftvarp smgeventtex "<grey>textures/weapons/event/smg"
deftvarp flamereventtex "<grey>textures/weapons/event/flamer"
deftvarp plasmaeventtex "<grey>textures/weapons/event/plasma"
deftvarp zappereventtex "<grey>textures/weapons/event/zapper"
deftvarp rifleeventtex "<grey>textures/weapons/event/rifle"
deftvarp corrodereventtex "<grey>textures/weapons/event/corroder"
deftvarp grenadeeventtex "<grey>textures/weapons/event/grenade"
deftvarp mineeventtex "<grey>textures/weapons/event/mine"
deftvarp rocketeventtex "<grey>textures/weapons/event/rocket"
deftvarp meleeeventtex "<grey>textures/weapons/event/melee"

exec "config/ui/events/balance.cfg"
exec "config/ui/events/bomber.cfg"
exec "config/ui/events/capture.cfg"
exec "config/ui/events/defend.cfg"
exec "config/ui/events/duel.cfg"
exec "config/ui/events/frag.cfg"
exec "config/ui/events/game.cfg"
exec "config/ui/events/item.cfg"
exec "config/ui/events/match.cfg"
exec "config/ui/events/team.cfg"

// 1:<event type> 2:<affinity id>
event_get_affinity_owner = [
    cases $arg1 "capture" [
        getcaptureteam $arg2
    ] "bomber" [
        getbomberteam $arg2
    ] "defend" [
        getdefendowner $arg2
    ]
]

event_handlers = (getalias event_handlers)

// 1:<handler>
event_register_handler = [
    if (! (listhas $event_handlers $arg1)) [
        append event_handlers $arg1
    ]
]

// 1:<event id> 2:<event type> 3:<event action>
event_propagate = [
    local event_id event_client event_team event_affinity event_affinity_owner event_is_focus event_is_focus_affinity event_is_focus_team event_is_attack
    // Fields available to all event handlers
    event_id                = $arg1
    event_client            = (geteventtag $arg1 "client" 0 "clientnum")
    event_team              = (getclientteam $event_client)
    event_affinity          = (geteventlist $arg1 "args" "affinity")
    event_affinity_owner    = (event_get_affinity_owner $arg2 $event_affinity)
    event_is_focus          = (= $ui_hud_focus $event_client)
    event_is_focus_affinity = (= $event_affinity_owner (getclientteam $ui_hud_focus))
    event_is_focus_team     = (= $event_team (getclientteam $ui_hud_focus))
    event_is_attack         = (!= $event_affinity_owner (getclientteam $event_client))

    looplist _handler $event_handlers [
        $_handler $arg1 $arg2 $arg3
    ]
]

ui_hud_event_loop = [
    loopevents (- $eventlines) 0 event_id [
        local event_time; event_time = (geteventmillis $event_id)
        local event_millis; event_millis = (- $totalmillis $event_time)
        local event_blend; event_blend = $eventblend
        local event_growth; event_growth = 1.0
        local event_dir; event_dir = 0
        local event_type; event_type = (geteventlist $event_id "args" "type")
        local event_action; event_action = (geteventlist $event_id "args" "action")
        caseif (&& $eventouttime (> $event_millis $eventouttime)) [
            local event_ctime; event_ctime = (- $event_millis $eventouttime)
            if (&& $eventfadetime (< $event_ctime $eventfadetime)) [
                event_growth = (-f 1 (divf $event_ctime $eventfadetime))
                event_blend = (clampf (*f $event_growth $eventblend) 0 1)
                event_dir = -1
            ] [
                event_blend = 0
                event_growth = 0
            ]
        ] (&& $eventintime (< $event_millis $eventintime)) [
            event_growth = (divf $event_millis $eventintime)
            event_blend = (clampf (*f $event_growth $eventblend) 0 1)
            event_dir = 1
        ]
        if (>f $event_growth 0.0) [
            local event_clients; event_clients = (geteventtaggroups $event_id "client")
            local event_colour; event_colour = 0
            loopwhile event_clidx $event_clients [! $event_colour] [
                event_clientid = (geteventtag $event_id "client" $event_clidx "clientnum")
                if (= $focusedplayer $event_clientid) [ event_colour = 0x208020 ]
            ]
            local event_concolour; event_concolour = (geteventlist $event_id "args" "colour")
            if (! $event_concolour) [ event_concolour = 0xFFFFFF ]
            local event_obj
            event_obj = [
                uiimage $infotex 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle leftmiddle; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
                uitext (geteventlist $event_id "args" "console") $ui_texttiny [
                    uistyle leftmiddle
                    uitextalign -1
                    uicolourset $event_concolour
                ]
                uitext $event_type $ui_texttiny [
                    uistyle leftmiddle
                    uitextalign -1
                    uicolourset $event_concolour
                ]
                uitext $event_action $ui_texttiny [
                    uistyle leftmiddle
                    uitextalign -1
                    uicolourset $event_concolour
                ]
            ]
            if (= $showevents 2) [
                cases $event_type "game" [
                    if (! $event_colour) [ event_colour = 0x206060 ]
                ] "match" [
                    if (! $event_colour) [ event_colour = 0x206060 ]
                ] "balance" [
                    if (! $event_colour) [ event_colour = 0x606020 ]
                ] "team" [
                    if (! $event_colour) [ event_colour = 0x606020 ]
                ] "frag" [
                    if (! $event_colour) [ event_colour = 0x802020 ]
                    event_action = (? $eventobitclassic "classic" "normal")
                ] "duel" [
                    if (! $event_colour) [ event_colour = 0x206060 ]
                ] "bomber" [
                    if (! $event_colour) [ event_colour = 0x206060 ]
                ] "capture" [
                    if (! $event_colour) [ event_colour = 0x206060 ]
                ] "defend" [
                    if (! $event_colour) [ event_colour = 0x206060 ]
                ] "item" [
                    if (! $event_colour) [ event_colour = 0x602060 ]
                ]

                local event_source; event_source = (getalias (concatword "event_" $event_type "_" $event_action))

                if (!=s $event_source "") [ event_obj = $event_source ]
            ]
            uitag (concatword "event_" $event_time "_" (geteventseqid $event_id)) [
                uipad 0 0 0 (*f $ui_padsmaller $event_growth) [
                    uistyle lefttop
                    uiborderedimageclamped $skinalphatex 0x60000000 0 $ui_texborder $ui_screenborder 0 0 [
                        uistyle lefttop
                        uiborderedimageclamped $skinshadowtex (? $event_colour $event_colour 0x404040) 0 $ui_texborder $ui_screenborder 0 0 [
                            uistyle lefttop
                            uiclip 0 0 0 0 [
                                uistyle lefttop
                                uiclipforced 1
                                uispace $ui_padsmall $ui_padsmaller [
                                    uistyle lefttop
                                    uihlist $ui_padsmall [
                                        uistyle leftmiddle
                                        @event_obj
                                    ]
                                ]
                                case $event_dir 1 [
                                    uiclipoffsetx (*f $uilastwprev (-f 1.0 $event_growth))
                                    uiclipoffsety 0
                                    uiclipsizew (*f $uilastwprev $event_growth)
                                    uiclipsizeh $uilasthprev
                                ] -1 [
                                    uiclipoffsetx 0
                                    uiclipoffsety (*f $uilasthprev (-f 1.0 $event_growth))
                                    uiclipsizew $uilastwprev
                                    uiclipsizeh (*f $uilasthprev $event_growth)
                                ] () [
                                    uiclipoffsetx 0
                                    uiclipoffsety 0
                                    uiclipsizew $uilastwprev
                                    uiclipsizeh $uilasthprev
                                ]
                            ]
                        ]
                    ]
                    uipropagate [ uicolourblend $event_blend ]
                ]
            ]
        ]

        if (= $event_millis 0) [
            event_propagate $event_id $event_type $event_action
        ]
    ]
]

ui_hud_events = [
    local _has_new_events
    _has_new_events = 0

    uifont $textfontoutline [
        uistyle lefttop
        uivlist 0 [
            uistyle lefttop
            ui_hud_event_loop
        ]
    ]
]