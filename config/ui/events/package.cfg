defvarp eventintime 0 250 $varidxmax
defvarp eventouttime 0 15000 $varidxmax
defvarp eventfadetime 0 500 $varidxmax
defvarp eventlines 0 10 $varidxmax
deffvarp eventblend $fvaridxnonzero 1 1
defvarp eventobitclassic 0 0 1

defvarp eventskin 0 2 4
deffvarp eventskinblend 0.0 0.9 1.0
deffvarp eventskinspace 0 0.001 $fvaridxmax

defsvarp claweventtex "<grey><rotate:4>textures/weapons/claw"
defsvarp pistoleventtex "<grey><rotate:4>textures/weapons/pistol"
defsvarp swordeventtex "<grey><rotate:4>textures/weapons/sword"
defsvarp shotguneventtex "<grey><rotate:4>textures/weapons/shotgun"
defsvarp smgeventtex "<grey><rotate:4>textures/weapons/smg"
defsvarp flamereventtex "<grey><rotate:4>textures/weapons/flamer"
defsvarp plasmaeventtex "<grey><rotate:4>textures/weapons/plasma"
defsvarp zappereventtex "<grey><rotate:4>textures/weapons/zapper"
defsvarp rifleeventtex "<grey><rotate:4>textures/weapons/rifle"
defsvarp grenadeeventtex "<grey><rotate:4>textures/weapons/grenade"
defsvarp mineeventtex "<grey><rotate:4>textures/weapons/mine"
defsvarp rocketeventtex "<grey><rotate:4>textures/weapons/rocket"
defsvarp meleeeventtex "<grey><rotate:4>textures/weapons/melee"

exec "config/ui/events/balance.cfg"
exec "config/ui/events/bomber.cfg"
exec "config/ui/events/capture.cfg"
exec "config/ui/events/defend.cfg"
exec "config/ui/events/duel.cfg"
exec "config/ui/events/frag.cfg"
exec "config/ui/events/game.cfg"
exec "config/ui/events/match.cfg"
exec "config/ui/events/team.cfg"

// 1:<event id> 2:<event type> 3:<event action>
game_hud_on_new_event = [
    cases $arg2 "capture" [
        on_capture_event $arg1 $arg3
    ] "bomber" [
        on_bomber_event $arg1 $arg3
    ]
]

ui_hud_events = [
    local _has_new_events
    _has_new_events = 0

    uivlist 0 [
        uistyle lefttop
        loopevents (- $eventlines) 0 event_id [
            local event_time; event_time = (geteventmillis $event_id)
            local event_millis; event_millis = (- $totalmillis $event_time)
            local event_blend; event_blend = $eventblend
            local event_growth; event_growth = 1.0
            local event_dir; event_dir = 0
            local event_type; event_type = (geteventlist $event_id "args" "type")
            local event_action; event_action = (geteventlist $event_id "args" "action")
            caseif (&& $eventouttime (> $event_millis $eventouttime)) [
                local event_ctime; event_ctime = (- $event_millis $eventouttime)
                if (&& $eventfadetime (< $event_ctime $eventfadetime)) [
                    event_growth = (-f 1 (divf $event_ctime $eventfadetime))
                    event_blend = (clampf (*f $event_growth $eventblend) 0 1)
                    event_dir = -1
                ] [
                    event_blend = 0
                    event_growth = 0
                ]
            ] (&& $eventintime (< $event_millis $eventintime)) [
                event_growth = (divf $event_millis $eventintime)
                event_blend = (clampf (*f $event_growth $eventblend) 0 1)
                event_dir = 1
            ]
            if (>f $event_growth 0.0) [
                local event_clients; event_clients = (geteventtaggroups $event_id "client")
                local event_colour; event_colour = 0
                loopwhile event_clidx $event_clients [! $event_colour] [
                    event_clientid = (geteventtag $event_id "client" $event_clidx "clientnum")
                    if (= $focusedplayer $event_clientid) [ event_colour = 0x202010 ]
                ]
                local event_concolour; event_concolour = (geteventlist $event_id "args" "colour")
                if (! $event_concolour) [ event_concolour = 0xFFFFFF ]
                local event_obj
                event_obj = [
                    uiimage $infotex 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle leftmiddle; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
                    uitext (geteventlist $event_id "args" "console") $ui_texttiny [
                        uistyle leftmiddle
                        uitextalign -1
                        uicolourset $event_concolour
                    ]
                ]
                if (= $showevents 2) [
                    cases $event_type "game" [
                        if (! $event_colour) [ event_colour = 0x202020 ]
                    ] "match" [
                        if (! $event_colour) [ event_colour = 0x202020 ]
                    ] "balance" [
                        if (! $event_colour) [ event_colour = 0x102828 ]
                    ] "team" [
                        if (! $event_colour) [ event_colour = 0x102828 ]
                    ] "frag" [
                        event_action = (? $eventobitclassic "classic" "normal")
                    ] "duel" [
                        if (! $event_colour) [ event_colour = 0x202020 ]
                    ] "bomber" [
                        if (! $event_colour) [ event_colour = 0x202020 ]
                    ] "capture" [
                        if (! $event_colour) [ event_colour = 0x202020 ]
                    ] "defend" [
                        if (! $event_colour) [ event_colour = 0x202020 ]
                    ]

                    local event_source; event_source = (getalias (concatword "event_" $event_type "_" $event_action))

                    if (!=s $event_source "") [ event_obj = $event_source ]
                ]
                uitag (concatword "event_" $event_time "_" (geteventseqid $event_id)) [
                    uipad 0 0 0 (*f $ui_padsmaller $event_growth) [
                        uistyle lefttop
                        ui_hud_skin (? $event_colour $event_colour 0x000000) 0 0 [ uistyle lefttop ] [
                            uistyle lefttop
                            uiclip 0 0 0 0 [
                                uistyle lefttop
                                uiclipforced 1
                                uispace $ui_padsmall $ui_padsmaller [
                                    uistyle lefttop
                                    uihlist $ui_padsmall [
                                        uistyle leftmiddle
                                        @event_obj
                                    ]
                                ]
                                case $event_dir 1 [
                                    uiclipoffsetx (*f $uilastwprev (-f 1.0 $event_growth))
                                    uiclipoffsety 0
                                    uiclipsizew (*f $uilastwprev $event_growth)
                                    uiclipsizeh $uilasthprev
                                ] -1 [
                                    uiclipoffsetx 0
                                    uiclipoffsety (*f $uilasthprev (-f 1.0 $event_growth))
                                    uiclipsizew $uilastwprev
                                    uiclipsizeh (*f $uilasthprev $event_growth)
                                ] () [
                                    uiclipoffsetx 0
                                    uiclipoffsety 0
                                    uiclipsizew $uilastwprev
                                    uiclipsizeh $uilasthprev
                                ]
                            ]
                        ] (*f $eventskinspace $event_growth) $eventskin $eventskinblend
                        uipropagate [ uicolourblend $event_blend ]
                    ]
                ]
            ]

            if (= $event_millis 0) [
                game_hud_on_new_event $event_id $event_type $event_action
            ]
        ]
    ]
]