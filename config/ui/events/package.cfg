defvarp eventshowtime 1000 15000 $varidxmax [event_calc_anim]
defvarp eventlines 0 10 $varidxmax
deffvarp eventblend $fvaridxnonzero 1 1
defvarp eventobitclassic 0 0 1
defvarp eventobit 0 0 1
defvarp eventfragassist 0 0 1

deftvarp claweventtex "<grey>textures/weapons/event/claw"
deftvarp pistoleventtex "<grey>textures/weapons/event/pistol"
deftvarp swordeventtex "<grey>textures/weapons/event/sword"
deftvarp shotguneventtex "<grey>textures/weapons/event/shotgun"
deftvarp smgeventtex "<grey>textures/weapons/event/smg"
deftvarp flamereventtex "<grey>textures/weapons/event/flamer"
deftvarp plasmaeventtex "<grey>textures/weapons/event/plasma"
deftvarp zappereventtex "<grey>textures/weapons/event/zapper"
deftvarp rifleeventtex "<grey>textures/weapons/event/rifle"
deftvarp corrodereventtex "<grey>textures/weapons/event/corroder"
deftvarp grenadeeventtex "<grey>textures/weapons/event/grenade"
deftvarp mineeventtex "<grey>textures/weapons/event/mine"
deftvarp rocketeventtex "<grey>textures/weapons/event/rocket"
deftvarp meleeeventtex "<grey>textures/weapons/event/melee"

exec "config/ui/events/balance.cfg"
exec "config/ui/events/bomber.cfg"
exec "config/ui/events/capture.cfg"
exec "config/ui/events/defend.cfg"
exec "config/ui/events/duel.cfg"
exec "config/ui/events/frag.cfg"
exec "config/ui/events/game.cfg"
exec "config/ui/events/item.cfg"
exec "config/ui/events/match.cfg"
exec "config/ui/events/team.cfg"

// 1:<image> 2:<colour>
event_image = [
    uiimage $arg1 $arg2 0 0 $game_hud_event_image_size [
        uiimageaspect 1
        uiimageshadow 0.001 0x20202020 1
    ]
]

event_calc_anim = [
    game_hud_def_seqstep GAME_HUD_EVENT_FLASH   200
    game_hud_def_seqstep GAME_HUD_EVENT_EXPAND  100
    game_hud_def_seqstep GAME_HUD_EVENT_FADEIN  200
    game_hud_def_seqstep GAME_HUD_EVENT_HOLD    (- $eventshowtime 200 100 200 1000)
    game_hud_def_seqstep GAME_HUD_EVENT_FADEOUT 1000

    game_hud_def_seq GAME_HUD_EVENT_SEQ [
        GAME_HUD_EVENT_FLASH
        GAME_HUD_EVENT_EXPAND
        GAME_HUD_EVENT_FADEIN
        GAME_HUD_EVENT_HOLD
        GAME_HUD_EVENT_FADEOUT
    ]
]
event_calc_anim

// 1:<affinity id>
event_get_affinity_owner = [
    cases $gamemode $G_CAPTURE [
        getcaptureteam $arg1
    ] $G_BOMBER [
        getbomberteam $arg1
    ] $G_DEFEND [
        getdefendowner $arg1
    ]
]

event_handlers = (getalias event_handlers)

// 1:<handler>
event_register_handler = [
    if (! (listhas $event_handlers $arg1)) [
        append event_handlers $arg1
    ]
]

// 1:<event id> 2:<event type> 3:<event action>
event_propagate = [
    local event_id event_team event_affinity event_affinity_owner event_is_focus event_is_focus_affinity event_is_focus_team event_is_attack
    // Fields available to all event handlers
    event_id                = $arg1
    event_client_name       = (geteventname $arg1 "client" 0)
    event_team              = (geteventtag $arg1 "client" 0 "team")
    event_affinity          = (geteventlist $arg1 "args" "affinity")
    event_affinity_owner    = (event_get_affinity_owner $event_affinity)
    event_is_focus          = (= $ui_hud_focus (geteventtag $arg1 "client" 0 "clientnum"))
    event_is_focus_affinity = (= $event_affinity_owner (getclientteam $ui_hud_focus))
    event_is_focus_team     = (= $event_team (getclientteam $ui_hud_focus))
    event_is_attack         = (!= $event_affinity_owner $event_team)

    looplist _handler $event_handlers [
        $_handler $arg1 $arg2 $arg3
    ]
]

ui_hud_event = [
    local _small_unit_size _normal_unit_size _large_unit_size _content_scalew _content_scaleh _content_blend
    local _anim _anim_flash _anim_expand _anim_fadein _anim_hold _anim_fadeout

    _small_unit_size  = (*f $game_hud_scale $ui_padsmall)
    _normal_unit_size = (*f $game_hud_scale $ui_padnormal)
    _large_unit_size  = (*f $game_hud_scale $ui_padlarge)
    _anim             = (divf $event_millis $eventshowtime)

    _anim_flash   =       @(game_hud_anim_seq $GAME_HUD_EVENT_SEQ $GAME_HUD_EVENT_FLASH   [$_anim])
    _anim_expand  =       @(game_hud_anim_seq $GAME_HUD_EVENT_SEQ $GAME_HUD_EVENT_EXPAND  [$_anim])
    _anim_fadein  =       @(game_hud_anim_seq $GAME_HUD_EVENT_SEQ $GAME_HUD_EVENT_FADEIN  [$_anim])
    _anim_hold    =       @(game_hud_anim_seq $GAME_HUD_EVENT_SEQ $GAME_HUD_EVENT_HOLD    [$_anim])
    _anim_fadeout = (-f 1 @(game_hud_anim_seq $GAME_HUD_EVENT_SEQ $GAME_HUD_EVENT_FADEOUT [$_anim]))

    _content_scalew = (smoothstep $_anim_expand)
    _content_scaleh = (smoothstep $_anim_fadeout)
    _content_blend  = (*f $_anim_fadein $_anim_fadeout)
    event_colour    = (tool_colour_lerp_int 0xffffff $event_colour $_anim_flash)

    uitag (concatword "event_" $event_time "_" (geteventseqid $event_id)) [
        uihlist 0 [
            uistyle lefttop

            uiborderedimageclamped $skinalphatex $event_colour 0 $ui_texborder $ui_screenborder $_normal_unit_size 0 [
                uiclamp 0 0 1 1
            ]

            uifill 0 0 [
                uifill (*f $_large_unit_size (lerpf $_anim_flash 1 5)) 0 [
                    uiclamp 0 0 1 1
                    uistyle lefttop

                    ui_gameui_advshadowhoriz [
                        p_colour  = $event_colour
                        p_bgblend = 0.5
                        p_phase   = 0
                        p_factor  = 1
                    ]
                ]

                uipad $_large_unit_size (*f $_large_unit_size 4) (*f $_small_unit_size $_content_scaleh) (*f $_small_unit_size $_content_scaleh) [
                    uiclip 0 0 0 0 [
                        uihlist $_small_unit_size [
                            if (>f $_content_scalew 0) [
                                @event_obj
                            ] [
                                uitext " " $game_hud_event_text_size
                            ]
                        ]

                        uiclipoffsetx (*f $uilastwprev (-f 1.0 $_content_scalew))
                        uiclipoffsety (*f $uilasthprev (-f 1.0 $_content_scaleh))
                        uiclipsizew (*f $uilastwprev $_content_scalew)
                        uiclipsizeh (*f $uilasthprev $_content_scaleh)
                    ]

                    uipropagate [ uicolourblend $_content_blend ]
                ]

                ui_gameui_advshadowhoriz [
                    p_bgblend = 0.85
                    p_phase   = 0
                    p_factor  = 1
                    p_blend   = $_anim_expand
                ]
            ]

            uipropagate [ uicolourblend $_anim_fadeout ]
        ]
    ]
]

ui_hud_event_obj_fallback = [
    event_image $infotex 0xFFFFFF
    uicolourtext (geteventlist $event_id "args" "console") $event_concolour $game_hud_event_text_size
    uicolourtext $event_type $event_concolour $game_hud_event_text_size
    uicolourtext $event_action $event_concolour $game_hud_event_text_size
]

ui_hud_event_loop = [
    loopevents (- $eventlines) 0 event_id [
        local event_time event_millis event_blend event_type event_action event_clients event_colour event_concolour event_obj event_source
        event_time   = (geteventmillis $event_id)
        event_millis = (- $totalmillis $event_time)
        event_blend  = $eventblend
        event_type   = (geteventlist $event_id "args" "type")
        event_action = (geteventlist $event_id "args" "action")

        if (< $event_millis $eventshowtime) [
            event_clients = (geteventtaggroups $event_id "client")
            event_colour = 0

            loopwhile event_clidx $event_clients [! $event_colour] [
                event_clientid = (geteventtag $event_id "client" $event_clidx "clientnum")
                if (= $focusedplayer $event_clientid) [ event_colour = 0x208020 ]
            ]

            event_concolour = (geteventlist $event_id "args" "colour")
            if (! $event_concolour) [ event_concolour = 0xFFFFFF ]

            if (= $showevents 2) [
                cases $event_type "game" [
                    if (! $event_colour) [ event_colour = 0x206060 ]
                ] "match" [
                    if (! $event_colour) [ event_colour = 0x206060 ]
                ] "balance" [
                    if (! $event_colour) [ event_colour = 0x606020 ]
                ] "team" [
                    if (! $event_colour) [ event_colour = 0x606020 ]
                ] "frag" [
                    if (! $event_colour) [ event_colour = 0x802020 ]
                    event_action = (? $eventobitclassic "classic" "normal")
                ] "duel" [
                    if (! $event_colour) [ event_colour = 0x206060 ]
                ] "bomber" [
                    if (! $event_colour) [ event_colour = 0x206060 ]
                ] "capture" [
                    if (! $event_colour) [ event_colour = 0x206060 ]
                ] "defend" [
                    if (! $event_colour) [ event_colour = 0x206060 ]
                ] "item" [
                    if (! $event_colour) [ event_colour = 0x602060 ]
                ]

                event_source = (getalias (concatword "event_" $event_type "_" $event_action))

                if (!=s $event_source "") [ event_obj = $event_source ] [
                    event_obj = ui_hud_event_obj_fallback
                ]
            ] [
                event_obj = ui_hud_event_obj_fallback
            ]

            ui_hud_event
        ]

        if (= $event_millis 0) [
            event_propagate $event_id $event_type $event_action
        ]
    ]
]

ui_hud_events = [
    local _has_new_events
    _has_new_events = 0

    uifont $textfontoutline [
        uistyle lefttop
        uivlist 0 [
            uistyle lefttop
            ui_hud_event_loop
        ]
    ]
]