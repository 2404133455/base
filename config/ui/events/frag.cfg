event_frag_classic = [
    event_image $deadtex 0xFFFFFF
    uicolourtext (geteventlist $event_id "args" "console") $event_concolour $game_hud_event_text_size
]

event_frag_assist_filter = [
    && [
        != $_event_assist 1
    ] [
        != $_event_assist 0
    ] [
        || [hasmut $G_M_FFA] [!= (geteventtag $event_id "client" 0 "team") (geteventtag $event_id "client" $_event_assist "team")]
    ] $_event_assist
]

event_frag_assists = [
    loopeventgroups $event_id "client" 0 0 _event_assist [
        if (event_frag_assist_filter) [
            uitext "+" (*f $game_hud_event_text_size 0.8)
            uioffset 0 (*f $game_hud_event_text_size 0.0012) [
                uitext (geteventname $event_id "client" $_event_assist) (*f $game_hud_event_text_size 0.8)
            ]
        ]
    ]
]

# event_frag_style_styles = [
    [ "HEADSHOT"      #PULSE_HEADSHOT    ]
    []
    [ "CARNAGE"       #PULSE_ACHIEVEMENT ]
    [ "SLAUGHTER"     #PULSE_ACHIEVEMENT ]
    [ "MASSACRE"      #PULSE_ACHIEVEMENT ]
    [ "BLOODBATH"     #PULSE_ACHIEVEMENT ]
    [ "DOUBLE-KILL"   #PULSE_MULTI       ]
    [ "TRIPLE-KILL"   #PULSE_MULTI       ]
    [ "MULTI-KILL"    #PULSE_MULTI       ]
    [ "REVENGE"       #PULSE_MULTI       ]
    [ "DOMINATING"    #PULSE_MULTI       ]
    [ "FIRST BLOOD"   #PULSE_MULTI       ]
    [ "SPREE BREAKER" #PULSE_MULTI       ]
    [ "PRIZE WINNER"  #PULSE_PRIZE       ]
]

# event_frag_style = [
    local _event_show _event_style_text _event_style_colour
    _event_show = (bitcycle $_event_style 1000)

    uifont "play/clear/bold" [
        uioffset (*f $game_hud_event_text_size 0.01) (*f $game_hud_event_text_size 0.001) [
            loop i #(listlen $event_frag_style_styles) [
                _event_style_text   = (at $event_frag_style_styles $i)
                _event_style_colour = (pulsecolour (at $_event_style_text 1))
                _event_style_text   = (at $_event_style_text 0)

                if (& $_event_style (<< 1 $i)) [
                    uicolourtext $_event_style_text $_event_style_colour (*f $game_hud_event_text_size 0.75) [
                        uicolourblend (? (& $_event_show (<< 1 $i)) 1 0)
                    ]
                ]
            ]
        ]
    ]
]

event_frag_normal = [
    event_image $deadtex 0xFFFFFF

    local _event_weap _event_style _event_context
    _event_style = 0
    _event_context  = (geteventlist $event_id "args" "context")

    if (!= (geteventtag $event_id "client" 0 "clientnum") (geteventtag $event_id "client" 1 "clientnum")) [
        _event_style    = (& (^ $FRAG_OBLITERATE) (geteventlist $event_id "args" "style"))
        _event_weap     = (geteventlist $event_id "args" "fromweap")
        if (< $_event_weap 0) [
            _event_weap = (geteventlist $event_id "args" "actweap")
        ]

        uitext (geteventname $event_id "client" 1) $game_hud_event_text_size

        if $eventfragassist [ event_frag_assists ]

        if (>= $_event_weap) [
            _event_weap = (at $W_NAMES $_event_weap)
            event_image $[@[_event_weap]eventtex] $[@[_event_weap]colour]
        ]

        if (geteventlist $event_id "args" "burnfunc") [
            event_image $burntex (pulsecolour $PULSE_BURN)
        ]
        if (geteventlist $event_id "args" "bleedfunc") [
            event_image $bleedtex (pulsecolour $PULSE_BLEED)
        ]
        if (geteventlist $event_id "args" "shockfunc") [
            event_image $shocktex (pulsecolour $PULSE_SHOCK)
        ]
        if (geteventlist $event_id "args" "corrodefunc") [
            event_image $corrodetex (pulsecolour $PULSE_CORRODE)
        ]

        if (&& [!=s $_event_context ""] $eventobit) [
            uitext $_event_context $game_hud_event_text_size
        ]

        uitext (geteventname $event_id "client" 0) $game_hud_event_text_size
    ] [
        uitext (geteventname $event_id "client" 0) $game_hud_event_text_size

        if (&& [!=s $_event_context ""] $eventobitself) [
            uitext $_event_context $game_hud_event_text_size
        ]
    ]


    if (& (geteventlist $event_id "args" "actflags") $HIT_PRIZE) [
        _event_style = (| $_event_style (<< 1 13))
    ]

    if $_event_style [ event_frag_style ]
]
