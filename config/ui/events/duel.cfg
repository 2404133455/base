event_duel_start = [
    uiimage (modetex) 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle leftmiddle; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
    if (&& (& $mutators (<< 1 $G_M_SURVIVOR)) (> $event_clients 4)) [
        uitext (concatword "Survivor with ^fs^fc" $event_clients "^fS players") $ui_texttiny [
            uistyle leftmiddle
            uitextalign -1
        ]
    ] [
        local event_extra; event_extra = 0
        loopeventgroups $event_id "client" 0 0 event_client [
            if $event_extra [
                uitext "vs." $ui_texttiny [
                    uistyle leftmiddle
                    uitextalign -1
                    uicolourset $colourgrey
                ]
            ]
            uitext (geteventname $event_id "client" $event_client) $ui_texttiny [
                uistyle leftmiddle
                uitextalign -1
            ]
            event_extra = 1
        ]
    ]
    uiimage $arrowrighttex 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle leftmiddle; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
    local event_desc; event_desc = (? (geteventlist $event_id "args" "sudden") "SUDDEN DEATH" (format "Round %1" (geteventlist $event_id "args" "round")))
    uitext $event_desc $ui_texttiny [
        uistyle leftmiddle
        uitextalign -1
    ]
]

event_duel_draw = [
    uiimage (modetex) 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle leftmiddle; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
    uitext "All players fragged" $ui_texttiny [
        uistyle leftmiddle
        uitextalign -1
    ]
    uiimage $deadtex 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle leftmiddle; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
    uitext "EPIC FAIL" $ui_texttiny [
        uistyle leftmiddle
        uitextalign -1
    ]
]

event_duel_score = [
    uiimage (modetex) 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle leftmiddle; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
    if (&& (& $mutators (<< 1 $G_M_SURVIVOR)) (! (& $mutators (<< 1 $G_M_FFA)))) [
        local event_team; event_team = (geteventlist $event_id "args" "winner")
        if (> (geteventtaggroups $event_id "alive") 4) [
            uitext (concat "Team" (getteamname $event_team)) $ui_texttiny [
                uistyle leftmiddle
                uitextalign -1
            ]
            uiimage $winnertex 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle leftmiddle; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
        ] [
            local event_extra; event_extra = 0
            loopeventgroups $event_id "alive" 0 0 event_client [
                if $event_extra [
                    uitext "+" $ui_texttiny [
                        uistyle leftmiddle
                        uitextalign -1
                        uicolourset $colourgrey
                    ]
                ]
                uitext (geteventname $event_id "alive" $event_client) $ui_texttiny [
                    uistyle leftmiddle
                    uitextalign -1
                ]
                event_extra = 1
            ]
            uiimage $winnertex 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle leftmiddle; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
        ]
    ] [
        uitext (geteventname $event_id "alive" 0) $ui_texttiny [
            uistyle leftmiddle
            uitextalign -1
        ]
        uiimage $winnertex 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle leftmiddle; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
    ]
    local event_dead; event_dead = (geteventtaggroups $event_id "dead")
    if (> $event_dead 4) [
        uitext (concatword "^fs^fc" $event_dead "^fS / ^fs^fc" $event_clients "^fS fragged") $ui_texttiny [
            uistyle leftmiddle
            uitextalign -1
        ]
    ] [
        event_extra = 0
        loopeventgroups $event_id "dead" 0 0 event_client [
            if $event_extra [
                uitext "+" $ui_texttiny [
                    uistyle leftmiddle
                    uitextalign -1
                ]
            ]
            uitext (geteventname $event_id "dead" $event_client) $ui_texttiny [
                uistyle leftmiddle
                uitextalign -1
            ]
            event_extra = 1
        ]
    ]
    if (! (& $mutators (<< 1 $G_M_INSTAGIB))) [
        local event_flawless; event_flawless = (geteventlist $event_id "args" "flawless")
        uiimage $arrowrighttex 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle leftmiddle; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
        if $event_flawless [
            uitext "Flawless victory!" $ui_texttiny [
                uistyle leftmiddle
                uitextalign -1
            ]
        ] [
            local event_health; event_health = (divf (geteventlist $event_id "args" "health") $damagedivisor)
            if $damageinteger [ event_health = (toint (ceil $event_health)) ] [ event_health = (precf $event_health 1) ]
            uitext (concatword "^fs^fc" $event_health "^fS health left") $ui_texttiny [
                uistyle leftmiddle
                uitextalign -1
            ]
        ]
    ]
    local event_wins; event_wins = (geteventlist $event_id "args" "wins")
    if (> $event_wins 1) [
        uiimage $arrowrighttex 0xFFFFFF 0 0 $ui_hud_texttiny [ uistyle leftmiddle; uiimageaspect 1; uiimageshadow 0.001 0x20202020 1 ]
        uitext (concatword "^fs^fc" $event_wins "^fS in succession") $ui_texttiny [
            uistyle leftmiddle
            uitextalign -1
        ]
    ]
]
