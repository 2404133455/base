deffvarp ui_hud_healthtop 0.0 0.104 1.0
deffvarp ui_hud_healthbottom 0.0 0.895 1.0
deffvarp ui_hud_impulsetop 0.0 0.174 1.0
deffvarp ui_hud_impulsebottom 0.0 0.898 1.0
deffvarp ui_hud_impulsepadleft 0.0 0.5 1.0
deffvarp ui_hud_impulsepadtop 0.0 0.230 1.0
deffvarp ui_hud_impulsepadbottom 0.0 0.260 1.0

ui_hud_healthbar = [
    ui_hud_health = (getclienthealth $ui_hud_focus)
    if (& $mutators (<< 1 $G_M_INSTAGIB)) [
        ui_hud_healthtext = "INSTA"
    ] [
        ui_hud_healthdiv = (divf $ui_hud_health $damagedivisor)
        if $damageinteger [ ui_hud_healthtext = (toint (ceil $ui_hud_healthdiv)) ] [ ui_hud_healthtext = (precf $ui_hud_healthdiv 1) ]
    ]
    ui_hud_spawnhealth = (getclientspawnhealth $ui_hud_focus)
    ui_hud_healthregen = (getclientregen $ui_hud_focus)
    ui_hud_healthregenlast = (- $lastmillis $ui_hud_healthregen)
    ui_hud_healthregenerating = (<= $ui_hud_healthregenlast $regentime)
    ui_hud_healthpart = (divf $ui_hud_health $ui_hud_spawnhealth)
    ui_hud_healthhud = (getclientresidualfx $ui_hud_focus 1000)
    if (< $ui_hud_healthhud 0) [
        ui_hud_healthhud = $ui_hud_colour
        ui_hud_healthcol = $colourwhite
    ] [
        ui_hud_healthcol = $ui_hud_healthhud
    ]

    ui_hud_healthimpulse = (&& $impulsemeter (getclientallowimpulse $ui_hud_focus))
    ui_hud_impulseregen = (getclientimpulselast $ui_hud_focus)
    ui_hud_impulsecount = (getclientimpulsecount $ui_hud_focus)
    ui_hud_impulsemoves = (- $impulsecount $ui_hud_impulsecount)
    caseif $ui_hud_healthimpulse [
        ui_hud_impulse = (getclientimpulsemeter $ui_hud_focus)
        if (> $ui_hud_impulse 0) [
            ui_hud_impulsepart = (clampf (-f 1 (divf $ui_hud_impulse $impulsemeter)) 0.0 1.0)
        ] [
            ui_hud_impulsepart = 1.0
        ]
        ui_hud_impulsehud = $ui_hud_colour
    ] (!= $impulsestyle 3) [
        ui_hud_healthimpulse = 2
        ui_hud_impulse = $ui_hud_impulsemoves
        ui_hud_impulsepart = 1.0
        ui_hud_impulsepart = (clampf (divf $ui_hud_impulse $impulsecount) 0.0 1.0)
        if (<= $ui_hud_impulse 1) [ ui_hud_impulsehud = (getclientrescolour $ui_hud_focus $PULSE_WARN) ] [ ui_hud_impulsehud = $ui_hud_colour ]
    ]

    uivlist 0 [
        uistyle leftbottom
        if $ui_hud_healthimpulse [
            uiimage $impulsebgtex $ui_hud_impulsehud 0 $ui_hud_mtrm $ui_hud_mtrf [
                uistyle clampxy
                if $uishadows [uiimageshadow $ui_menu_s]
                if (>f $ui_hud_impulsepart 0) [
                    ui_hud_impulsesize = $ui_hud_mtrf
                    if (<f $ui_hud_impulsepart 1) [
                        ui_hud_impulsesec = (+f (*f (-f $ui_hud_impulsebottom $ui_hud_impulsetop) $ui_hud_impulsepart) (-f 1 $ui_hud_impulsebottom))
                        ui_hud_impulsesize = (*f $ui_hud_impulsesec $ui_hud_mtrf)
                    ]
                    uiclip $ui_hud_mtrm $ui_hud_impulsesize 0 (-f $ui_hud_mtrf $ui_hud_impulsesize) [
                        uistyle centerbottom
                        uiimage $impulsetex $colourpurple 0 $ui_hud_mtrm $ui_hud_mtrf [
                            uistyle centerbottom
                            uistyle impulsebar 0 $ui_hud_impulseregen $ui_hud_impulseregen 250
                        ]
                    ]
                ]
                uipad (*f $ui_hud_mtrm $ui_hud_impulsepadleft) 0 (*f $ui_hud_mtrf $ui_hud_impulsepadtop) (*f $ui_hud_mtrf $ui_hud_impulsepadbottom) [
                    uistyle centermiddle
                    uistyle clampxy
                    uitext $ui_hud_impulsemoves $ui_textxlarge [
                        uistyle centermiddle
                        uitextlimit 1
                        if (<= $ui_hud_impulsemoves 1) [uicolourset (getclientrescolour $ui_hud_focus $PULSE_WARN)]
                    ]
                ]
            ]
        ]
        uiimage $healthbgtex $ui_hud_healthhud 0 $ui_hud_mtrm $ui_hud_mtrf [
            uistyle clampxy
            if $uishadows [uiimageshadow $ui_menu_s]
            if (>f $ui_hud_healthpart 0) [
                ui_hud_healthsize = $ui_hud_mtrf
                if (<f $ui_hud_healthpart 1) [
                    ui_hud_healthsec = (+f (*f (-f $ui_hud_healthbottom $ui_hud_healthtop) $ui_hud_healthpart) (-f 1 $ui_hud_healthbottom))
                    ui_hud_healthsize = (*f $ui_hud_healthsec $ui_hud_mtrf)
                ]
                uiclip $ui_hud_mtrm $ui_hud_healthsize 0 (-f $ui_hud_mtrf $ui_hud_healthsize) [
                    uistyle centerbottom
                    uiimage $healthtex $ui_hud_healthcol 1 $ui_hud_mtrm $ui_hud_mtrf [
                        uistyle centerbottom
                        uistyle healthbar 0 $ui_hud_healthregenerating $ui_hud_healthregenlast $regentime
                    ]
                ]
            ]
            uispace 0.016 0.0375 [
                uistyle centertop
                uitext $ui_hud_healthtext $ui_textlarge [
                    uistyle centermiddle
                    uitextlimit 1
                    uicolourset $ui_hud_healthcol
                ]
            ]
        ]
    ]
]
