defvarp darkhud 0 2 2

defvarp showfps 0 0 4
defvarp showtime 0 0 3
defvarp showstats 0 1 2
defvarp showtexstats 0 1 1
defvarp showedittoggles 0 1 1
defvarp showvelocity 0 0 1
defvarp showweight 0 0 1
defvarp showtimestyle 0 3 4
defvarp showrealtime 0 0 1
defvarp showevents 0 2 2

defvarp texpaneltime 0 3000 $varidxmax
defvarp texpanelfade 0 1000 $varidxmax
defvarp texpanellead 0 4 $varidxmax
defvarp texpanelfollow 0 4 $varidxmax
deffvarp texpanelblend 0 0.8 1

deffvarp ui_hud_blend 0 1 1
deffvarp ui_hud_sizew $fvaridxnonzero 0.1125 $fvaridxmax
deffvarp ui_hud_sizeh $fvaridxnonzero 0.225 $fvaridxmax
deffvarp ui_hud_top $fvaridxnonzero 0.045 $fvaridxmax
deffvarp ui_hud_mtrw $fvaridxnonzero 0.3 $fvaridxmax
deffvarp ui_hud_mtrf $fvaridxnonzero 0.15 $fvaridxmax
deffvarp ui_hud_mtrh $fvaridxnonzero 0.015 $fvaridxmax
deffvarp ui_hud_mtrz $fvaridxnonzero 0.09 $fvaridxmax
deffvarp ui_hud_mtrm $fvaridxnonzero 0.075 $fvaridxmax
deffvarp ui_hud_mtrq $fvaridxnonzero 0.02 $fvaridxmax
deffvarp ui_hud_mtr2 $fvaridxnonzero 0.05 $fvaridxmax
deffvarp ui_hud_mtr3 $fvaridxnonzero 0.04 $fvaridxmax
deffvarp ui_hud_tool $fvaridxnonzero 0.04 $fvaridxmax
deffvarp ui_hud_shadow $fvaridxnonzero 0.0004 $fvaridxmax
defhvarp ui_hud_shadowcol 0 0x40000000 0xFFFFFFFF

defsvarp ui_hud_statenames [ALIVE DEAD EDIT SPEC WAIT]
defsvarp ui_hud_statetexs [playertex deadtex editingtex spectatortex waitingtex]
defsvarp ui_hud_statetex2 [playerbliptex deadtex playerbliptex spectatortex deadtex]
defsvarp ui_hud_timecols [red red red red red green yellow orange orange]

exec "config/ui/hud/event.cfg"
exec "config/ui/hud/health.cfg"
exec "config/ui/hud/minimap.cfg"
exec "config/ui/hud/radar.cfg"
exec "config/ui/hud/weapon.cfg"

exec "config/ui/hud/bomber.cfg"
exec "config/ui/hud/capture.cfg"
exec "config/ui/hud/defend.cfg"
exec "config/ui/hud/race.cfg"

ui_hud_init = [
    refreshenginestats
    ui_hud_clientnum = $playercn
    ui_hud_input = (&& $ui_freecursor (= (getclientstate $ui_hud_clientnum) $stateidxediting))
    ui_hud_focus = (? $ui_hud_input $ui_hud_clientnum $focusedplayer)
    ui_hud_actor = (getclientactortype $ui_hud_focus)
    ui_hud_model = (getclientmodel $ui_hud_focus)
    ui_hud_state = (getclientstate $ui_hud_focus)
    ui_hud_team = (getclientteam $ui_hud_focus)
    ui_hud_health = (getclienthealth $ui_hud_focus)
    ui_hud_spawnhealth = (getclientspawnhealth $ui_hud_focus)
    if (& $mutators (<< 1 $G_M_INSTAGIB)) [
        ui_hud_damagelevel = 1
        ui_hud_damagecrit = (= $ui_hud_state $stateidxdead)
    ] [
        ui_hud_damagelevel = (? (= $ui_hud_state $stateidxdead) 0.0 (divf $ui_hud_health $ui_hud_spawnhealth))
        ui_hud_damagecrit = (|| (= $ui_hud_state $stateidxdead) (&& (= $ui_hud_state $stateidxalive) (<=f $ui_hud_damagelevel $damagecritical)))
    ]
    ui_hud_carryaffinity = (getclientcarryaffinity $ui_hud_focus)
    ui_hud_colour = (getclientcolour $ui_hud_focus $playerovertone $playerovertonelevel)
    ui_hud_tone = (getclientcolour $ui_hud_focus $playerundertone $playerundertonelevel)
    case $darkhud [
        ui_hud_colour = (modcolour $ui_hud_colour 0.75)
        ui_hud_tone = (modcolour $ui_hud_tone 0.75)
    ] 2 [
        ui_hud_colour = (modcolour $ui_hud_colour 0.5)
        ui_hud_tone = (modcolour $ui_hud_tone 0.5)
    ]
    ui_hud_commandmillis = (getcommandmillis)
    ui_hud_commandopen = (> $ui_hud_commandmillis 0)
    ui_hud_showhud = (|| $ui_hud_commandopen $ui_hud_input $showhud)
]

ui_hud_camera = [
    uivlist 0 [
        uistyle leftbottom
        uiimage (? (>= $currentcam 0) $cameratex $spectatortex) $ui_hud_colour 0 $ui_hud_mtrm $ui_hud_mtrm [
            uistyle leftbottom
        ]
        uitext (? (>= $currentcam 0) (concat "CAM" (+ $currentcam 1)) "SPEC") $ui_textlarge [
            uitextlimit 1
            uistyle centermiddle
        ]
    ]
]

ui_hud_dead = [
    uivlist 0 [
        uistyle leftbottom
        uiimage $deadtex $ui_hud_colour 0 $ui_hud_mtrm $ui_hud_mtrm [
            uistyle leftbottom
        ]
        uitext "DEAD" $ui_textlarge [
            uitextlimit 1
            uistyle centerbottom
        ]
    ]
]

ui_hud_leftbottom = [
    uihlist $ui_padbutton [
        uistyle leftbottom
        caseif (= $ui_hud_state $stateidxalive) [
            ui_hud_healthbar
        ] (= $ui_hud_state $stateidxdead) [
            ui_hud_dead
        ] (|| (= $ui_hud_state $stateidxwaiting) (= $ui_hud_state $stateidxspectator)) [
            ui_hud_camera
        ]
        uifill 0.0125
        ui_gameui_chat
    ]
]

ui_hud_showfps = [
    if $showfps [
        uivlist $ui_padsmall [
            doif (>= $showfps 1) [
                uitext (concat (getenginestat 11) "fps") $ui_text [uitextlimit 1]
            ] (= $showfps 2) [
                uitext (concatword "+" (getenginestat 12) "-" (getenginestat 13)) $ui_textsmall [uitextlimit 1]
            ] (= $showfps 3) [
                uitext (concatword (- (getenginestat 11) (getenginestat 13)) "-" (+ (getenginestat 11) (getenginestat 12)) ) $ui_textsmall [uitextlimit 1]
            ] (= $showfps 4) [
                uitext (concatword "Avg: " (div 1000 (getenginestat 11)) "ms" ) $ui_textsmaller
                uitext (concatword "Max: " (div 1000 (- (getenginestat 11) (getenginestat 13)) ) "ms" ) $ui_textsmaller
            ]
        ]
    ]
]

ui_hud_timers = [
    ui_hud_timers_type = (updatetimers)
    if $ui_hud_timers_type [
        uivlist 0 [
            if $frametimer [
                uispace $ui_padtiny $ui_padtiny [
                    uivlist 0 [
                        uitext "Frame Time" $ui_texttiny [uicolourset $colouryellow]
                        uitext (concat (gettimer -1 1) "ms") $ui_texttiny [uitextlimit 1]
                    ]
                ]
            ]
            if $timer [
                ui_hud_timers_count = (gettimer -1 -1)
                loop i $ui_hud_timers_count [
                    if (gettimer $i -2) [] [
                        uispace $ui_padnormal $ui_padtiny [
                            uivlist 0 [
                                uitext (gettimer $i 0) $ui_texttiny [uicolourset $colouryellow]
                                uitext (concat (precf (gettimer $i 6) 2) "ms" (? (gettimer $i 1) "(gpu)" "(cpu)")) $ui_texttiny
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_weight = [
    uivlist 0 [
        uitext "WEIGHT" $ui_text [uitextlimit 1; uicolourset $colouryellow]
        uitext (getclientweight $ui_hud_focus) $ui_textlarge [uitextlimit 1]
    ]
]

ui_hud_velocity = [
    uivlist 0 [
        uitext "SPEED" $ui_text [uitextlimit 1; uicolourset $colourgreen]
        uitext (getenginestat 20) $ui_textlarge [uitextlimit 1]
    ]
]

ui_hud_rightbottom = [
    uivlist $ui_padsmall [
        uistyle rightbottom
        uivlist $ui_padsmall [
            uistyle rightbottom
            case $gamemode $G_CAPTURE [
                ui_hud_captureinventory
            ] $G_DEFEND [
                ui_hud_defendinventory
            ] $G_BOMBER [
                ui_hud_bomberinventory
            ] $G_RACE [
                if (= $ui_hud_state $stateidxalive) [
                    ui_hud_raceinventory
                ]
            ]
        ]
        uivlist $ui_padtiny [
            uistyle rightbottom
            ui_hud_timers
        ]
        case $ui_hud_state $stateidxalive [
            uihlist $ui_padtiny [
                uistyle rightbottom
                if $showweight [uivlist 0 [ui_hud_weight]]
                if $showvelocity [uivlist 0 [ui_hud_velocity]]
            ]
        ]
        if $showminimap [ui_hud_minimap]
        uivlist $ui_padtiny [
            uistyle rightbottom
            ui_hud_rightinventory
        ]
    ]
]

ui_hud_rightinventory = [
    case $ui_hud_state $stateidxalive [
        ui_hud_weaponinventory
    ]
]

ui_hud_timestate = [
    if $showtime [
        uivlist 0 [
            uifill $ui_hud_mtrz 0
            uistyle righttop
            caseif $gamepaused [
                if (> $showtime 1) [uitext Paused $ui_textlarge [uitextlimit 1; uicolourset $colourorange; uistyle centermiddle]]
            ] (= $gamemode $G_EDITING) [
                if (> $showtime 1) [
                    uitext Editing $ui_textlarge [uitextlimit 1; uicolourset $colourgreen; uistyle centermiddle]
                    uitext (? (= $mapvariant 2) "Alternate" "Normal") $ui_text [uitextlimit 1; uicolourset $colouryellow; uistyle centermiddle]
                ]
            ] () [
                ui_hud_timeremain = $gametimeremain
                ui_hud_timeelapsed = $gametimeelapsed
                ui_hud_timecolour = (at $ui_hud_timecols $gamestate)
                if (getgametimelimit) [
                    if (> $showtime 1) [uitext (at $G_S_NAMES $gamestate) $ui_text [uitextlimit 1; uicolourset $[colour@[ui_hud_timecolour]]; uistyle centermiddle]]
                    uitext (timestr $ui_hud_timeremain $showtimestyle) $ui_textlarge [uitextlimit 1; uicolourset (? (|| (!= $gamestate 5) (< $ui_hud_timeremain 60000)) (getclientrescolour $ui_hud_focus $PULSE_WARN) $ui_default); uistyle centermiddle]
                ] [
                    if (> $showtime 1) [uitext (at $G_S_NAMES $gamestate) $ui_textlarge [uitextlimit 1; uicolourset $[colour@[ui_hud_timecolour]]; uistyle centermiddle]]
                ]
                if (> $showtime 2) [if $ui_hud_timeelapsed [uitext (timestr $ui_hud_timeelapsed $showtimestyle) $ui_textsmall [uitextlimit 1; uistyle centermiddle]]]
            ]
            if $showrealtime [
                uitext (substr (gettime) 11 5) $ui_text [uitextlimit 1; uistyle centermiddle]
            ]
        ]
    ]
]

ui_hud_lefttop = [
    uivlist $ui_padnormal [
        uistyle lefttop
        if (isediting) [
            uifill 0 $ui_toolbar_height
        ]
        if $showevents [ ui_hud_events ]
    ]
]

ui_hud_righttop = [
    uivlist $ui_padsmall [
        uistyle righttop
        ui_hud_timestate
        ui_hud_showfps
    ]
]

defvarp titleonstart 0 1 1
defvarp titlefadetime 0 2500 $varidxmax

ui_hud_headsup = [
    if (&& $isconnected (! (isediting))) [
        if $progresstype [
            local hud_title
            if (&& $titleonstart [! $uihasmenu]) [
                if (= $progresstype $PROGRESS_GAMEWAIT) [
                    if (&& (< $gamestate $G_S_INTERMISSION) (< $gametimeelapsed $titlefadetime)) [
                        hud_title = (-f 1.0 (divf $gametimeelapsed $titlefadetime))
                    ] [ hud_title = 0.0 ]
                ] [ hud_title = 1.0 ]
            ] [ hud_title = 0.0 ]
            local hud_desc; hud_desc = (at $G_S_NAMES $gamestate)
            local hud_tipstr; hud_tipstr = "Press ^fs^fw^f{=1:spectate 0}^fS to join the match."
            caseif $gametimeremain [
                hud_desc = (concatword $hud_desc ": ^fg" (timestr $gametimeremain $showtimestyle))
            ] (>= $gamestate $G_S_INTERMISSION) [ hud_tipstr = (tipshow) ]
            ui_progress $progresstype $hud_title $gametimeprogress $hud_desc $hud_tipstr [
                uispace $ui_padhudh $ui_padhudv [
                    uistyle leftbottom
                    uivlist $ui_padtiny [
                        uistyle leftbottom
                        uifill 0.0125
                        ui_gameui_chat
                    ]
                ]
            ]
        ] [
            uispace $ui_padhudh $ui_padhudv [
                uistyle righttop
                uihlist 0 [
                    uistyle righttop
                    uihlist $ui_padtiny [
                        uistyle righttop
                        ui_hud_righttop
                    ]
                ]
            ]
            uispace $ui_padhudh $ui_padhudv [
                uistyle leftbottom
                uihlist $ui_padsmall [
                    uistyle leftbottom
                    uivlist $ui_padsmall [
                        uistyle leftbottom
                        uivlist $ui_padtiny [
                            uistyle leftbottom
                            ui_hud_leftbottom
                        ]
                    ]
                ]
            ]
            uispace $ui_padhudh $ui_padhudv [
                uistyle rightbottom
                uivlist $ui_padsmall [
                    uistyle rightbottom
                    uivlist $ui_padtiny [
                        uistyle rightbottom
                        ui_hud_rightbottom
                    ]
                ]
            ]
            if (&& $showradar (= $ui_hud_state $stateidxalive)) [
                uispace $ui_padtiny $ui_padtiny [ ui_hud_midradar ]
            ]
        ]
    ]
    uispace $ui_padhudh $ui_padhudv [
        uistyle clampx
        uistyle lefttop
        uihlist 0 [
            uistyle clampx
            uistyle lefttop
            uihlist $ui_padtiny [
                uistyle clampx
                uistyle lefttop
                ui_hud_lefttop
            ]
        ]
    ]
    if (<f $ui_hud_blend 1) [ uiprop [ uicolourblend $ui_hud_blend ]  ]
]

newui "hud" [
    uistyle clampxy
    uiescrelease [ uitoggle ]
    ui_hud_init
    uiexclusive (? $ui_hud_commandopen 1 0)
    uiallowinput (? $ui_hud_input 2 0)
    uimenu 0 // not a menu
    if $ui_hud_showhud [
        ui_hud_headsup
        if (&& [isediting] [! (uivisible "main")]) [
            ui_hud_toolview
        ]
        if (!=f $hudblend 1) [ uiprop [ uicolourblend $hudblend ] ]
    ]
    if (|| $ui_hud_commandopen $showconsole) [ ui_hud_showconsole ]
]
