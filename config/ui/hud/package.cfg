defvarp darkhud 0 2 2

defvarp showfps 0 0 4
defvarp showtime 0 1 1
defvarp showtimestyle 0 3 4
defvarp showrealtime 0 0 1
defvarp showevents 0 2 2

defsvarp ui_hud_statenames [ALIVE DEAD EDIT SPEC WAIT]
defsvarp ui_hud_statetexs [playertex deadtex editingtex spectatortex waitingtex]
defsvarp ui_hud_statetex2 [playerbliptex deadtex playerbliptex spectatortex deadtex]
defsvarp ui_hud_timecols [red red red red red green yellow orange orange]

defvarp hudskin 0 1 4 // 0:none 1:colour 2:skin 3:skinshadow 4:skin+skinshadow
deffvarp hudskinblend 0.0 0.75 1.0
deffvarp hudskinspace 0 0.00175 $fvaridxmax
defvarp hudskinoverlay 0 0 4 // 0:none 1:colour 2:skin 3:skinshadow 4:skin+skinshadow
deffvarp hudskinoverlayblend 0.0 0.75 1.0
deffvarp hudskinoverlayspace 0 0.0025 $fvaridxmax
defvarp hudskinunderlay 0 2 4 // 0:none 1:colour 2:skin 3:skinshadow 4:skin+skinshadow
deffvarp hudskinunderlayblend 0.0 0.75 1.0
deffvarp hudskinunderlayspace 0 0.00175 $fvaridxmax
defvarp hudskinbar 0 2 4 // 0:none 1:colour 2:skin 3:skinshadow 4:skin+skinshadow
deffvarp hudskinbarblend 0.0 1.0 1.0

deffvarp hudskinsize $fvaridxnonzero 0.035 $fvaridxmax
deffvarp hudskinscale $fvaridxnonzero 1.5 $fvaridxmax
deffvarp hudskinpad 0 0.001 $fvaridxmax
deffvarp hudskinpadsmall 0 0.0005 $fvaridxmax
deffvarp hudskinpadtiny 0 0.00025 $fvaridxmax
deffvarp hudskintextscale $fvaridxnonzero 1.3 $fvaridxmax
deffvarp hudskintextsize $fvaridxnonzero 1.4 $fvaridxmax
deffvarp hudskintextsmall $fvaridxnonzero 1.15 $fvaridxmax
deffvarp hudskintextinfo $fvaridxnonzero 0.875 $fvaridxmax
deffvarp hudskintextmain $fvaridxnonzero 1.3 $fvaridxmax
deffvarp hudskintextsuffix $fvaridxnonzero 0.75 $fvaridxmax
deffvarp hudskintextsub $fvaridxnonzero 0.5 $fvaridxmax
deffvarp hudskinpartscale $fvaridxnonzero 7.5 $fvaridxmax
deffvarp hudskinpartoffset 0 0.01 $fvaridxmax
deffvarp hudskinpartspace $fvaridxnonzero 0.0015 $fvaridxmax
deffvarp hudskininfoscale $fvaridxnonzero 2.0 $fvaridxmax
deffvarp hudskinmainscale $fvaridxnonzero 2.5 $fvaridxmax
deffvarp hudskinmainlong $fvaridxnonzero 5 $fvaridxmax

exec "config/ui/hud/event.cfg"
exec "config/ui/hud/minimap.cfg"
exec "config/ui/hud/radar.cfg"

exec "config/ui/hud/bomber.cfg"
exec "config/ui/hud/capture.cfg"
exec "config/ui/hud/defend.cfg"
exec "config/ui/hud/race.cfg"

ui_hud_init = [
    refreshenginestats
    ui_hud_segment = (minf (divf $uiaspect 3) 0.5)
    ui_hud_segrev = (divf 1.0 $ui_hud_segment)
    ui_hud_clientnum = $playercn
    ui_hud_input = (&& $ui_freecursor (= (getclientstate $ui_hud_clientnum) $stateidxediting))
    ui_hud_focus = (? $ui_hud_input $ui_hud_clientnum $focusedplayer)
    ui_hud_actor = (getclientactortype $ui_hud_focus)
    ui_hud_model = (getclientmodel $ui_hud_focus)
    ui_hud_state = (getclientstate $ui_hud_focus)
    ui_hud_team = (getclientteam $ui_hud_focus)
    ui_hud_health = (getclienthealth $ui_hud_focus)
    ui_hud_spawnhealth = (getclientspawnhealth $ui_hud_focus)
    if (& $mutators (<< 1 $G_M_INSTAGIB)) [
        ui_hud_damagelevel = 1
        ui_hud_damagecrit = (= $ui_hud_state $stateidxdead)
    ] [
        ui_hud_damagelevel = (? (= $ui_hud_state $stateidxdead) 0.0 (divf $ui_hud_health $ui_hud_spawnhealth))
        ui_hud_damagecrit = (|| (= $ui_hud_state $stateidxdead) (&& (= $ui_hud_state $stateidxalive) (<=f $ui_hud_damagelevel $damagecritical)))
    ]
    ui_hud_carryaffinity = (getclientcarryaffinity $ui_hud_focus)
    ui_hud_colour = (? (|| $progresstype (= $ui_hud_state $stateidxspectator)) 0x404040 (getclientcolour $ui_hud_focus $playerovertone $playerovertonelevel))
    ui_hud_colour1 = (modcolour $ui_hud_colour 2.0)
    ui_hud_colour2 = (modcolour $ui_hud_colour 0.5)
    ui_hud_tone = (getclientcolour $ui_hud_focus $playerundertone $playerundertonelevel)
    case $darkhud 0 [
        ui_hud_colour = (modcolour $ui_hud_colour 0.75)
        ui_hud_colour1 = (modcolour $ui_hud_colour1 0.75)
        ui_hud_colour2 = (modcolour $ui_hud_colour2 0.75)
        ui_hud_tone = (modcolour $ui_hud_tone 0.75)
    ] 1 [
        ui_hud_colour = (modcolour $ui_hud_colour 0.5)
        ui_hud_colour1 = (modcolour $ui_hud_colour1 0.5)
        ui_hud_colour2 = (modcolour $ui_hud_colour2 0.5)
        ui_hud_tone = (modcolour $ui_hud_tone 0.5)
    ] 2 [
        ui_hud_colour = (modcolour $ui_hud_colour 0.25)
        ui_hud_colour1 = (modcolour $ui_hud_colour1 0.25)
        ui_hud_colour2 = (modcolour $ui_hud_colour2 0.25)
        ui_hud_tone = (modcolour $ui_hud_tone 0.25)
    ]
    ui_hud_showhud = (|| $ui_hud_input $showhud)
]

ui_hud_timers = [
    ui_hud_timers_type = (updatetimers)
    if $ui_hud_timers_type [
        uivlist 0 [
            if $frametimer [
                uispace $ui_padtiny $ui_padtiny [
                    uivlist 0 [
                        uitext "Frame Time" $ui_texttiny [uicolourset $colouryellow]
                        uitext (concat (gettimer -1 1) "ms") $ui_texttiny [uitextlimit 1]
                    ]
                ]
            ]
            if $timer [
                ui_hud_timers_count = (gettimer -1 -1)
                loop i $ui_hud_timers_count [
                    if (gettimer $i -2) [] [
                        uispace $ui_padnormal $ui_padtiny [
                            uivlist 0 [
                                uitext (gettimer $i 0) $ui_texttiny [uicolourset $colouryellow]
                                uitext (concat (precf (gettimer $i 6) 2) "ms" (? (gettimer $i 1) "(gpu)" "(cpu)")) $ui_texttiny
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_barcount = 0
ui_hud_barcolour = [ result (? (= (mod $ui_hud_barcount 2) (? $arg1 1 0)) $ui_hud_colour2 $ui_hud_colour) ]

// 1:colour 2:width 3:height 4:common 5:children 6:space 7:type 8:blend 9:scale
ui_hud_skin = [
    if (=s $arg6 "") [arg6 = $hudskinspace]
    if (=s $arg7 "") [arg7 = $hudskin]
    if (=s $arg8 "") [arg8 = $hudskinblend]
    if (=s $arg9 "") [arg9 = 1.0]
    case $arg7 1 [
        uicolour $arg1 $arg2 $arg3 [
            if $arg4 [doargs $arg4]
            uicolourblend $arg8
            if $arg5 [
                uigroup [
                    uistyle clampxy
                    if $arg4 [doargs $arg4]
                    if (>f $arg6 0.0) [
                        uispace $arg6 $arg6 [
                            uistyle clampxy
                            doargs $arg5
                        ]
                    ] [
                        doargs $arg5
                    ]
                ]
            ]
        ]
   ] 2 [
        uiborderedimageclamped $skintex $arg1 0 $ui_texborder (*f $ui_screenborder $arg9) $arg2 $arg3 [
            if $arg4 [doargs $arg4]
            uicolourblend $arg8
            if $arg5 [
                uigroup [
                    uistyle clampxy
                    if $arg4 [doargs $arg4]
                    if (>f $arg6 0.0) [
                        uispace $arg6 $arg6 [
                            uistyle clampxy
                            doargs $arg5
                        ]
                    ] [
                        doargs $arg5
                    ]
                ]
            ]
        ]
   ] 3 [
        uiborderedimageclamped $skinshadowtex $arg1 0 $ui_texborder (*f $ui_screenborder $arg9) $arg2 $arg3 [
            if $arg4 [doargs $arg4]
            uicolourblend $hudskinblend
            if $arg5 [
                uigroup [
                    uistyle clampxy
                    if $arg4 [doargs $arg4]
                    if (>f $arg6 0.0) [
                        uispace $arg6 $arg6 [
                            uistyle clampxy
                            doargs $arg5
                        ]
                    ] [
                        doargs $arg5
                    ]
                ]
            ]
        ]
    ] 4 [
        uiborderedimageclamped $skinalphatex $arg1 0 $ui_texborder (*f $ui_screenborder $arg9) $arg2 $arg3 [
            if $arg4 [doargs $arg4]
            uicolourblend $arg8
            if $arg5 [
                uigroup [
                    uistyle clampxy
                    if $arg4 [doargs $arg4]
                    if (>f $arg6 0.0) [
                        uispace $arg6 $arg6 [
                            uistyle clampxy
                            doargs $arg5
                        ]
                    ] [
                        doargs $arg5
                    ]
                ]
            ]
            uiborderedimageclamped $skinshadowtex (modcolour $arg1 2.0) 0 $ui_texborder (*f $ui_screenborder $arg9) $arg2 $arg3 [
                uistyle clampxy
                uicolourblend $arg8
            ]
        ]
     ] () [
        uifill $arg2 $arg3 [
            if $arg4 [doargs $arg4]
            if $arg5 [
                uigroup [
                    uistyle clampxy
                    if $arg4 [doargs $arg4]
                    if (>f $arg6 0.0) [
                        uispace $arg6 $arg6 [
                            uistyle clampxy
                            doargs $arg5
                        ]
                    ] [
                        doargs $arg5
                    ]
                ]
            ]
        ]
    ]
]

// 1:icon 2:iconcolour 3:amount 4:amtcolour 5:text 6:textcolour 7:altbar 8:segments 9:maxsegs 10:segcolour 11:dir
ui_hud_baricon = [
    if (! (mod $arg7 2)) [
        ui_hud_skin (ui_hud_barcolour) $hudskinsize $hudskinsize [] [
            uiimage $arg1 $arg2 0 (-f $hudskinsize (*f $hudskinspace 2)) (-f $hudskinsize (*f $hudskinspace 2))
        ]
    ]
]

ui_hud_barbody = [
    if (! (mod $arg7 2)) [
        local partscale
        partscale = (*f $hudskinsize $hudskinpartscale)
        if (|| (= $arg7 4) (! $arg9)) [ partscale = (+f $partscale (*f $hudskinsize $hudskintextscale)) ]
        ui_hud_skin (ui_hud_barcolour) $partscale $hudskinsize [] [
            uistyle (? $arg11 rightmiddle leftmiddle)
            local curwidth curheight
            curwidth = (-f $partscale (*f $hudskinspace 3) $hudskinpartoffset)
            curheight = (-f $hudskinsize (*f $hudskinspace 2) $hudskinpartoffset)
            uispace (*f $hudskinpartoffset 0.5) (*f $hudskinpartoffset 0.5) [
                uistyle (? $arg11 rightmiddle leftmiddle)
                uivlist $hudskinpartspace [
                    uistyle (? $arg11 rightmiddle leftmiddle)
                    local hasbar
                    hasbar = (>=f $arg3 0)
                    if $arg9 [
                        if $hasbar [ curheight = (-f (*f $curheight 0.5) $hudskinpartspace) ]
                        uihlist $hudskinpartspace [
                            uistyle (? $arg11 rightmiddle leftmiddle)
                            if $arg8 [
                                if (> $arg9 10) [
                                    ui_hud_skin (modcolour $arg10 0.5) (*f $curwidth (divf $arg8 $arg9)) $curheight [uistyle (? $arg11 rightmiddle leftmiddle)] [] 0 $hudskinbar $hudskinbarblend 0
                                ] [
                                    local segwidth segment
                                    segwidth = (+f (-f $curwidth (*f (*f $hudskinpartspace 2.0) (- $arg9 1))) $hudskinpartspace)
                                    segment = (divf $segwidth $arg9)
                                    loop i $arg8 [
                                        if $i [ uifill 0 $curheight [uistyle (? $arg11 rightmiddle leftmiddle)] ]
                                        ui_hud_skin (modcolour $arg10 0.5) $segment $curheight [uistyle (? $arg11 rightmiddle leftmiddle)] [] 0 $hudskinbar $hudskinbarblend 0
                                    ]
                                ]
                            ]  [
                                if $hasbar [ uifill $curwidth $curheight [uistyle (? $arg11 rightmiddle leftmiddle)] ]
                            ]
                        ]
                        if $hasbar [ uifill $curwidth 0 [uistyle (? $arg11 rightmiddle leftmiddle)] ]
                    ]
                    if $hasbar [
                        if (>f $arg3 0.0) [
                            local barwidth
                            barwidth = (*f (clampf $arg3 0.0 1.0) $curwidth)
                            ui_hud_skin $arg4 $barwidth $curheight [uistyle (? $arg11 rightmiddle leftmiddle)] [
                                uistyle (? $arg11 rightmiddle leftmiddle)
                                if (>f $arg3 1.0) [
                                    barwidth = (*f (modf $arg3 1.0) $curwidth)
                                    ui_hud_skin $colourwhite (-f $barwidth 0.005) (-f $curheight 0.005) [uistyle (? $arg11 rightmiddle leftmiddle)] [] 0 $hudskinbar $hudskinbarblend 0
                                ]
                            ] 0 $hudskinbar $hudskinbarblend 0
                        ] [
                            uifill $curwidth $curheight [uistyle (? $arg11 rightmiddle leftmiddle)]
                        ]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_barinfo = [
    case $arg7 0 [
        ui_hud_skin (ui_hud_barcolour) (*f $hudskinsize $hudskintextscale) $hudskinsize [] [
            if $arg9 [
                uihlist 0 [
                    uistyle clampy
                    uitext $arg8 $hudskintextsmall [
                        uistyle (? $arg11 righttop lefttop)
                        uitextlimit 1
                        uicolourset $arg6
                    ]
                    uitext "/" $hudskintextsmall [
                        uistyle (? $arg11 rightmiddle leftmiddle)
                        uitextlimit 1
                        uicolourset (modcolour $arg6 0.5)
                    ]
                    uitext $arg5 $hudskintextsmall [
                        uistyle (? $arg11 rightbottom leftbottom)
                        uitextlimit 1
                        uicolourset $arg6
                    ]
                ]
            ] [
                uitext $arg5 $hudskintextsize [
                    uitextlimit 1
                    uicolourset $arg6
                ]
            ]
        ]
    ] 1 [
        if $arg9 [
            ui_hud_skin (ui_hud_barcolour) (*f $hudskinsize $hudskintextscale 2.0) $hudskinsize [uistyle (? $arg11 rightbottom leftbottom)] [
                uitext $arg5 $hudskintextsize [
                    uitextlimit 1
                    uicolourset $arg6
                ]
            ]
        ]
    ] 2 [
        ui_hud_skin (ui_hud_barcolour) (*f $hudskinsize $hudskintextscale) $hudskinsize [uistyle (? $arg11 rightbottom leftbottom)] [
            uitext (? $arg9 $arg8 $arg5) $hudskintextsize [
                uitextlimit 1
                uicolourset $arg6
            ]
        ]
    ] 3 [
        ui_hud_skin (ui_hud_barcolour) (*f $hudskinsize $hudskintextscale 2.0) (*f $hudskinsize 2.0) [uistyle (? $arg11 rightbottom leftbottom)] [
            if $arg9 [
                uivlist 0 [
                    uifill (-f (*f $hudskinsize $hudskintextscale 2.0) $hudskinspace) 0
                    uitext $arg8 (*f $hudskintextsize 0.75) [
                        uitextlimit 1
                        uicolourset $arg6
                    ]
                    uitext $arg5 (*f $hudskintextsize 1.45) [
                        uitextlimit 1
                        uicolourset $arg6
                    ]
                ]
            ] [
                uitext $arg5 (*f $hudskintextsize 2.0) [
                    uitextlimit 1
                    uicolourset $arg6
                ]
            ]
        ]
    ]
]

ui_hud_container = [
    local align callback
    align = (? (=s $arg1 "") "centermiddle" $arg1)
    callback = $arg2
    if (|| $hudskinunderlay $hudskinoverlay) [
        uigroup [
            uistyle $align
            if $hudskinunderlay [
                ui_hud_skin $ui_hud_colour2 0 0 [] @callback (*f $hudskinunderlayspace 0.5) $hudskinunderlay $hudskinunderlayblend
            ] [
                if $hudskinoverlay [ uispace (*f $hudskinoverlayspace 0.5) (*f $hudskinoverlayspace 0.5) @callback ]
            ]
            if $hudskinoverlay [
                ui_hud_skin $ui_hud_colour 0 0 [ uistyle clampxy ] [] 0 $hudskinoverlay $hudskinoverlayblend
            ]
        ]
    ] [
        @callback
    ]
]

ui_hud_leftbottom = [
    uivlist $hudskinpadsmall [
        uistyle leftbottom
        ui_gameui_chat
        case $ui_hud_state $stateidxalive [
            ui_game_hud_player
        ]
    ]
]

ui_hud_centerside = [
    uihlist 0 [
        case $gamemode $G_CAPTURE [
            ui_hud_captureinventory
        ] $G_DEFEND [
            ui_hud_defendinventory
        ] $G_BOMBER [
            ui_hud_bomberinventory
        ] $G_RACE [
            if (= $ui_hud_state $stateidxalive) [
                ui_hud_raceinventory
            ]
        ]
    ]
]

ui_hud_centertime = [
    uihlist 0 [
        if $showtime [
            ui_hud_skin $ui_hud_colour (*f $hudskinsize $hudskinmainlong) (*f $hudskinsize 2.0) [] [
                uivlist 0 [
                    uifill (-f (*f $hudskinsize $hudskinmainlong) (*f $hudskinspace 2.0)) 0
                    caseif $gamepaused [
                        uitext "Game Paused" 1 [uitextlimit 1; uicolourset $colourorange]
                    ] (= $gamemode $G_EDITING) [
                        uitext (? (= $mapvariant 2) "Alternate" "Normal") $hudskintextmain [uitextlimit 1; uicolourset $colourgreen]
                        uitext "Editing" $hudskintextsub [uitextlimit 1]
                    ] () [
                        ui_hud_timeremain = $gametimeremain
                        ui_hud_timeelapsed = $gametimeelapsed
                        ui_hud_timecolour = (at $ui_hud_timecols $gamestate)
                        if (getgametimelimit) [
                            uitext (timestr $ui_hud_timeremain $showtimestyle) $hudskintextmain [uitextlimit 1; uicolourset (? (|| (!= $gamestate 5) (< $ui_hud_timeremain 60000)) (getclientrescolour $ui_hud_focus $PULSE_WARN) $[colour@[ui_hud_timecolour]])]
                        ] [
                            uitext (timestr $ui_hud_timeelapsed $showtimestyle) $hudskintextmain [uitextlimit 1; uicolourset $[colour@[ui_hud_timecolour]]]
                        ]
                        uitext (? (= $gamestate $G_S_PLAYING) (? (getgametimelimit) "Remaining" "Elapsed") (at $G_S_NAMES $gamestate)) $hudskintextinfo [uitextlimit 1]
                    ]
                ]
            ]
        ]
        if $showrealtime [
            ui_hud_skin $ui_hud_colour (*f $hudskinsize $hudskinmainlong) $hudskinsize [] [
                uivlist 0 [
                    uifill (-f (*f $hudskinsize $hudskinmainlong) (*f $hudskinspace 2.0)) 0
                    uitext (at (gettime) 1) $hudskintextinfo [uitextlimit 1; uicolourset $colouryellow]
                    uitext (at (gettime) 0) $hudskintextsub [uitextlimit 1]
                ]
            ]
        ]
    ]
]

ui_hud_centertop = [
    uivlist $hudskinpadsmall [
        uistyle centertop
        if (|| $showtime $showrealtime) [ ui_hud_container centertop ui_hud_centertime ]
    ]
]

ui_hud_centerbottom = [
    uivlist $hudskinpadsmall [
        uistyle centerbottom
        ui_hud_container centertop ui_hud_centerside
    ]
]

ui_hud_centermiddle = [
    if (&& $showradar (= $ui_hud_state $stateidxalive)) [
        uitag "hud_radar" [ uispace $hudskinpadtiny $hudskinpadtiny [ uigroup [ ui_hud_midradar ] ] ]
    ]
]

ui_hud_showfps = [
    uihlist 0 [
        doif (>= $showfps 1) [
            ui_hud_skin $ui_hud_colour $hudskinsize $hudskinsize [] [
                uivlist 0 [
                    uifill (-f $hudskinsize (*f $hudskinspace 2.0)) 0
                    uitext (getenginestat 11) $hudskintextinfo [uitextlimit 1; uicolourset $colouryellow]
                    uitext "FPS" $hudskintextsub [uitextlimit 1]
                ]
            ]
        ] (= $showfps 2) [
            ui_hud_skin $ui_hud_colour $hudskinsize $hudskinsize [] [
                uivlist 0 [
                    uifill (-f $hudskinsize (*f $hudskinspace 2.0)) 0
                    uitext (getenginestat 12) $hudskintextinfo [uitextlimit 1; uicolourset $colouryellow]
                    uitext "SUB" $hudskintextsub [uitextlimit 1]
                ]
            ]
            ui_hud_skin $ui_hud_colour $hudskinsize $hudskinsize [] [
                uivlist 0 [
                    uifill (-f $hudskinsize (*f $hudskinspace 2.0)) 0
                    uitext (getenginestat 13) $hudskintextinfo [uitextlimit 1; uicolourset $colouryellow]
                    uitext "ADD" $hudskintextsub [uitextlimit 1]
                ]
            ]
        ] (= $showfps 3) [
            ui_hud_skin $ui_hud_colour $hudskinsize $hudskinsize [] [
                uivlist 0 [
                    uifill (-f $hudskinsize (*f $hudskinspace 2.0)) 0
                    uitext (- (getenginestat 11) (getenginestat 13)) $hudskintextinfo [uitextlimit 1; uicolourset $colouryellow]
                    uitext "MIN" $hudskintextsub [uitextlimit 1]
                ]
            ]
            ui_hud_skin $ui_hud_colour $hudskinsize $hudskinsize [] [
                uivlist 0 [
                    uifill (-f $hudskinsize (*f $hudskinspace 2.0)) 0
                    uitext (+ (getenginestat 11) (getenginestat 12)) $hudskintextinfo [uitextlimit 1; uicolourset $colouryellow]
                    uitext "MAX" $hudskintextsub [uitextlimit 1]
                ]
            ]
        ] (= $showfps 4) [
            ui_hud_skin $ui_hud_colour $hudskinsize $hudskinsize [] [
                uivlist 0 [
                    uifill (-f $hudskinsize (*f $hudskinspace 2.0)) 0
                    uihlist 0 [
                        uitext (div 1000 (getenginestat 11)) $hudskintextinfo [uitextlimit 1; uicolourset $colouryellow]
                        uitext "ms" $hudskintextsub [uitextlimit 1]
                    ]
                    uitext "AVG" $hudskintextsub [uitextlimit 1]
                ]
            ]
            ui_hud_skin $ui_hud_colour $hudskinsize $hudskinsize [] [
                uivlist 0 [
                    uifill (-f $hudskinsize (*f $hudskinspace 2.0)) 0
                    uihlist 0 [
                        uitext (div 1000 (- (getenginestat 11) (getenginestat 13))) $hudskintextinfo [uitextlimit 1; uicolourset $colouryellow]
                        uitext "ms" $hudskintextsub [uitextlimit 1]
                    ]
                    uitext "MAX" $hudskintextsub [uitextlimit 1]
                ]
            ]
        ]
    ]
]

ui_hud_rightbottom = [
    uivlist $hudskinpadsmall [
        uistyle rightbottom
         case $ui_hud_state $stateidxalive [
            ui_game_hud_weapon
        ]
   ]
]

ui_hud_lefttop = [
    uivlist $hudskinpadsmall [
        uistyle lefttop
        if (>= $showconsole (? (isediting) 1 2)) [
            uifont $textfontoutline [
                uivlist 0 [
                    uistyle lefttop
                    ui_console_draw $conlinesamt $conouttime $confadetime $conintime $conblend $ui_hud_segment $ui_textsmaller
                ]
            ]
        ]
        if $showevents [ ui_hud_events ]
    ]
]

ui_hud_righttop = [
    uivlist $hudskinpadsmall [
        uistyle righttop
        if $showminimap [ ui_hud_container righttop ui_hud_minimap ]
        if $showfps [ ui_hud_container righttop ui_hud_showfps ]
    ]
]

ui_hud_space = [
    uifill 0 (*f $arg3 0.1)
]

ui_hud_toolcheck = [
    if (isediting) [
        uifill 0 $ui_toolbar_height
    ]
]

defvarp titleonstart 0 1 1
defvarp titlefadetime 0 2500 $varidxmax

ui_hud_pos = [
    if $arg1 [] [arg1 = "leftbottom"]
    if $arg2 [] [arg2 = $arg1]
    if $arg3 [] [arg3 = $ui_hud_segment]
    uitag (format "hud_%1" $arg1) [
        uifill $arg3 $arg3 [
            uistyle $arg1
            uispace $ui_padhud $ui_padhud [
                uistyle $arg1
                uivlist $ui_padsmall [
                    uistyle $arg1
                    if $arg4 [doargs $arg4]
                    looplist target $arg2 [[ui_hud_@target] [@@@@@arg1] [@@@@@arg2] [@@@@@arg3]]
                ]
            ]
        ]
    ]
]

ui_hud_headbody = [
    if $arg1 [
        ui_hud_pos lefttop [space lefttop]
        ui_hud_pos leftbottom [leftbottom space]

        ui_hud_pos centertop [space centertop]
        ui_hud_pos centerbottom [centerbottom space]

        ui_hud_pos righttop [space righttop]
        ui_hud_pos rightbottom [rightbottom space]
    ] [
        ui_hud_pos lefttop [lefttop toolcheck]
        ui_hud_pos leftbottom

        ui_hud_pos centertop
        ui_hud_pos centerbottom

        ui_hud_pos righttop
        ui_hud_pos rightbottom

        ui_hud_pos centermiddle
    ]
]

ui_hud_progress = [
    local hud_title
    if (&& $titleonstart [! $uihasmenu]) [
        case $progresstype $PROGRESS_GAMEWAIT [
            hud_title = (? (< $gametimeelapsed $titlefadetime) (-f 1.0 (divf $gametimeelapsed $titlefadetime)) 0.0)
        ] $PROGRESS_GAMESTATE [
            hud_title = (? (< $gamestate $G_S_INTERMISSION) 1.0 0.0)
        ] () [
            hud_title = 1.0
        ]
    ] [
        hud_title = 0.0
    ]
    local hud_desc; hud_desc = (at $G_S_NAMES $gamestate)
    local hud_tipstr; hud_tipstr = "Press ^fs^fw^f{=1:spectate 0}^fS to join the match."
    caseif $gametimeremain [
        hud_desc = (concatword $hud_desc ": ^fg" (timestr $gametimeremain $showtimestyle))
    ] (>= $gamestate $G_S_INTERMISSION) [
        hud_tipstr = (tipshow)
    ]
    ui_progress $progresstype $hud_title $gametimeprogress $hud_desc $hud_tipstr $arg1
]

newui "hud" $SURFACE_MAIN [
    uistyle clampxy
    ui_hud_init
    uiallowinput (? $ui_hud_input 2 0)
    uimenu 0 // not a menu
    if $ui_hud_showhud [
        uigroup [
            uistyle clampxy
            uiclip 0 0 0 0 [
                uiclipforced 1
                uitext "Dummy" $ui_texttiny [ ui_hud_texttiny = $uilasth ]
            ]
            if (&& $isconnected [! (isediting)]) [
                if $progresstype [
                    if (& $visorhud 4) [ ui_hud_progress [ ui_hud_headbody 1 ] ] [ ui_hud_headbody 1 ]
                ] [ ui_hud_headbody ]
            ]
            if (&& [isediting] [! (uivisible "main")]) [ ui_hud_toolview ]
        ]
    ]
]

newui "hud" $SURFACE_FOREGROUND [
    uistyle clampxy
    uiallowinput 0
    uimenu 0 // not a menu
    if $ui_hud_showhud [
        uigroup [
            uistyle clampxy
            if (&& $isconnected [! (isediting)]) [
                if (&& (! (& $visorhud 4)) $progresstype) [ ui_hud_progress ]
            ]
            uipropagate [ uicolourblend 0.75 ]
        ]
    ]
]
