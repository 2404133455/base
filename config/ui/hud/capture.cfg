ui_hud_captureinventory = [
    loopcapture 0 0 ui_hud_flag [
        ui_hud_flagteam = (getcaptureteam $ui_hud_flag)
        ui_hud_flagname = (at $teamnames $ui_hud_flagteam)
        ui_hud_flagcol = $[team@[ui_hud_flagname]colour]
        ui_hud_flaghud = $ui_hud_colour
        ui_hud_flagdrop = (getcapturedroptime $ui_hud_flag)
        ui_hud_flagtake = (getcapturetaketime $ui_hud_flag)
        ui_hud_flagowner = (getcaptureowner $ui_hud_flag)
        //ui_hud_flagpart = 1.0
        caseif (>= $ui_hud_flagowner 0) [
            ui_hud_flagoffs = (- $lastmillis $ui_hud_flagtake)
            //ui_hud_flagpart = 1.0
            ui_hud_flagownerteam = (getclientteam $ui_hud_flagowner)
            ui_hud_flagownername = (at $teamnames $ui_hud_flagownerteam)
            caseif (= $ui_hud_flagowner $ui_hud_focus) [
                ui_hud_flaghud = (getclientrescolour $ui_hud_focus $PULSE_DISCO)
            ] (&& (= $ui_hud_team $ui_hud_flagteam) (!= $ui_hud_flagownerteam $ui_hud_flagteam)) [
                ui_hud_flaghud = (getclientrescolour $ui_hud_focus $PULSE_WARN)
            ]
        ] $ui_hud_flagdrop [
            ui_hud_flagoffs = (- $lastmillis $ui_hud_flagdrop)
            //ui_hud_flagpart = (-f 1 (divf $ui_hud_flagoffs (getcapturedelay)))
        ] () [
            ui_hud_flagcol = (modcolour $ui_hud_flagcol 0.25)
            ui_hud_flaghud = (modcolour $ui_hud_flaghud 0.25)
        ]

        ui_hud_skin $ui_hud_colour (*f $hudskinsize 2.0) (*f $hudskinsize 2.0) [] [
            uiimage $flagtex $ui_hud_flagcol 0 (-f (*f $hudskinsize 2.0) (*f $hudskinspace 2)) (-f (*f $hudskinsize 2.0) (*f $hudskinspace 2)) [
                caseif (>= $ui_hud_flagowner 0) [
                    uiimage $flagtakentex $[team@[ui_hud_flagownername]colour] 0 (*f $hudskinsize 0.5) (*f $hudskinsize 0.5) [uistyle rightbottom]
                ] (> $ui_hud_flagdrop 0) [
                    uiimage $flagdroptex $colourcyan 0 (*f $hudskinsize 0.5) (*f $hudskinsize 0.5) [uistyle rightbottom]
                ] () [uiimage $pointtex $ui_hud_flagcol 0 (*f $hudskinsize 0.5) (*f $hudskinsize 0.5) [uistyle rightbottom]]
            ]
        ] $hudskinspace $hudskin $hudskinblend
    ]
]
