ui_hud_captureinventory = [
    loopcapture 0 0 ui_hud_flag [
        ui_hud_flagteam = (getcaptureteam $ui_hud_flag)
        ui_hud_flagname = (at $teamnames $ui_hud_flagteam)
        ui_hud_flagcol = $[team@[ui_hud_flagname]colour]
        ui_hud_flaghud = $ui_hud_colour
        ui_hud_flagdrop = (getcapturedroptime $ui_hud_flag)
        ui_hud_flagtake = (getcapturetaketime $ui_hud_flag)
        ui_hud_flagowner = (getcaptureowner $ui_hud_flag)
        //ui_hud_flagpart = 1.0
        caseif (>= $ui_hud_flagowner 0) [
            ui_hud_flagoffs = (- $lastmillis $ui_hud_flagtake)
            //ui_hud_flagpart = 1.0
            ui_hud_flagownerteam = (getclientteam $ui_hud_flagowner)
            ui_hud_flagownername = (at $teamnames $ui_hud_flagownerteam)
            caseif (= $ui_hud_flagowner $ui_hud_focus) [
                ui_hud_flaghud = (getclientrescolour $ui_hud_focus $pulseidxdisco)
            ] (&& (= $ui_hud_team $ui_hud_flagteam) (!= $ui_hud_flagownerteam $ui_hud_flagteam)) [
                ui_hud_flaghud = (getclientrescolour $ui_hud_focus $pulseidxwarn)
            ]
        ] $ui_hud_flagdrop [
            ui_hud_flagoffs = (- $lastmillis $ui_hud_flagdrop)
            //ui_hud_flagpart = (-f 1 (divf $ui_hud_flagoffs (getcapturedelay)))
        ] () [
            ui_hud_flagcol = (modcolour $ui_hud_flagcol 0.25)
            ui_hud_flaghud = (modcolour $ui_hud_flaghud 0.25)
        ]
        uihlist $ui_padsmall [
            uistyle rightbottom
            uiimage $inventorytex $ui_hud_flaghud 0 $ui_hud_mtrm $ui_hud_mtrm [
                uistyle rightbottom
                uispace $ui_padnormal $ui_padnormal [
                    uistyle clampxy
                    uiimage $flagtex $ui_hud_flagcol 0 $ui_hud_mtr2 $ui_hud_mtr2 [
                        uistyle centermiddle
                        uiimageshadow $ui_hud_shadow $ui_hud_shadowcol
                    ]
                    caseif (>= $ui_hud_flagowner 0) [
                        uiimage $flagtakentex $[team@[ui_hud_flagownername]colour] 0 $ui_hud_mtrq $ui_hud_mtrq [uistyle rightbottom; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]
                    ] (> $ui_hud_flagdrop 0) [
                        uiimage $flagdroptex $colourcyan 0 $ui_hud_mtrq $ui_hud_mtrq [uistyle rightbottom; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]
                    ] () [uiimage $pointtex $ui_hud_flagcol 0 $ui_hud_mtrq $ui_hud_mtrq [uistyle rightbottom; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]]
                ]
            ]
        ]
    ]
]
