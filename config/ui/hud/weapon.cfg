defvarp weaponhud 0 1 3

ui_hud_weapbar = [
    ui_hud_weapidx = (at $W_NAMES $arg1)
    ui_hud_weapstate = (getclientweapstate $ui_hud_focus $arg1)
    ui_hud_weapammo = (max (getclientweapammo $ui_hud_focus $arg1) 0)
    ui_hud_weapstore = (max (getclientweapstore $ui_hud_focus $arg1) 0)
    ui_hud_weapval = (? (> $[@[ui_hud_weapidx]ammostore] 0) $[@[ui_hud_weapidx]ammostore] 0)
    ui_hud_weapclip = $[@[ui_hud_weapidx]ammoclip]
    ui_hud_weapmax = (+ $ui_hud_weapval $ui_hud_weapclip)
    ui_hud_weapcol = $[@[ui_hud_weapidx]colour]
    ui_hud_weapammocol = $ui_hud_weapcol
    ui_hud_weapclipcol = $ui_hud_weapcol
    ui_hud_weaptext = $colourwhite
    ui_hud_weappart = (divf $ui_hud_weapstore $ui_hud_weapmax)
    if (&& (> $[@[ui_hud_weapidx]ammoitem] 1) (!= $[@[ui_hud_weapidx]ammostore] -1)) [
        if (<=f $ui_hud_weappart 0.25) [
            ui_hud_weaptext = (getclientrescolour $ui_hud_focus $PULSE_WARN)
            ui_hud_weapammocol = (getclientrescolour $ui_hud_focus $PULSE_WARN)
        ]
        if (<=f (divf $ui_hud_weapammo $ui_hud_weapclip) 0.25) [
            ui_hud_weapclipcol = (getclientrescolour $ui_hud_focus $PULSE_WARN)
        ]
    ] [
        ui_hud_weappart = -1.0
    ]
    if (!= $arg1 $arg2) [
        ui_hud_weapcol = (modcolour $ui_hud_weapcol 0.5)
        ui_hud_weapammocol = (modcolour $ui_hud_weapcol 0.5)
        ui_hud_weapclipcol = (modcolour $ui_hud_weapcol 0.5)
        ui_hud_weaptext = (modcolour $ui_hud_weaptext 0.5)
    ]
    ui_hud_bar $[@[ui_hud_weapidx]tex] $ui_hud_weapcol $ui_hud_weappart $ui_hud_weapammocol $ui_hud_weapstore $ui_hud_weaptext (? (= $arg3 1) 1 2) $ui_hud_weapammo $ui_hud_weapclip $ui_hud_weapclipcol $arg4
]

ui_hud_weaponinventory = [
    ui_hud_weapselect = (getclientweapselect $ui_hud_focus)
    case $weaponhud 3 [
        loopinventoryrev $ui_hud_focus 0 0 ui_hud_weapon [ui_hud_weapbar $ui_hud_weapon $ui_hud_weapselect $arg1]
    ] 2 [
        ui_hud_weapbar $ui_hud_weapselect $ui_hud_weapselect $arg1 [
            uistyle clampxy
            ui_hud_weapcount = 0
            loopinventory $ui_hud_focus 0 0 ui_hud_weapon [
                if (!= $ui_hud_weapon $ui_hud_weapselect) [ ui_hud_weapcount = (+ $ui_hud_weapcount 1) ]
            ]
            ui_hud_weapscale = (-f (*f $hudbarsize (divf 3 $ui_hud_weapcount) 0.75) (*f $hudbarspace 3.0))
            uigrid 3 0 0 [
                uistyle clampxy
                uistyle centermiddle
                loopinventory $ui_hud_focus 0 0 ui_hud_weapon [
                    if (!= $ui_hud_weapon $ui_hud_weapselect) [
                        ui_hud_weapidx = (at $W_NAMES $ui_hud_weapon)
                        uiimage $[@[ui_hud_weapidx]tex] $[@[ui_hud_weapidx]colour] 0 $ui_hud_weapscale $ui_hud_weapscale [uistyle centermiddle]
                    ]
                ]
            ]
        ]
    ] 1 [
        ui_hud_weapbar $ui_hud_weapselect $ui_hud_weapselect $arg1
    ]
]
