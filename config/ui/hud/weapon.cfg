defvarp weaponhud 0 2 2

ui_hud_weapbar = [
    ui_hud_weapidx = (at $W_NAMES $arg1)
    ui_hud_weapstate = (getclientweapstate $ui_hud_focus $arg1)
    ui_hud_weapammo = (max (getclientweapammo $ui_hud_focus $arg1) 0)
    ui_hud_weapstore = (max (getclientweapstore $ui_hud_focus $arg1) 0)
    ui_hud_weapval = (? (> $[@[ui_hud_weapidx]ammostore] 0) $[@[ui_hud_weapidx]ammostore] 0)
    ui_hud_weapmax = (+ $ui_hud_weapval $[@[ui_hud_weapidx]ammoclip])
    ui_hud_weapcol = $[@[ui_hud_weapidx]colour]
    ui_hud_weaphud = $ui_hud_colour
    ui_hud_weaptext = $ui_default
    ui_hud_weappart = (divf (+ $ui_hud_weapstore $ui_hud_weapammo) $ui_hud_weapmax)
    if (&& (> $[@[ui_hud_weapidx]ammoitem] 1) (<=f $ui_hud_weappart 0.25) (!= $[@[ui_hud_weapidx]ammostore] -1)) [
        ui_hud_weaphud = (modcolour (getclientrescolour $ui_hud_focus $PULSE_WARN) 0.75)
        ui_hud_weaptext = (getclientrescolour $ui_hud_focus $PULSE_WARN)
    ]
    if (!= $arg1 $arg2) [
        ui_hud_weapcol = (modcolour $ui_hud_weapcol 0.25)
        ui_hud_weaphud = (modcolour $ui_hud_weaphud 0.25)
        ui_hud_weaptext = (modcolour $ui_hud_weaptext 0.25)
    ]
    ui_hud_weaptext2 = (modcolour $ui_hud_weaptext 0.25)
    uihlist $ui_padsmall [
        uistyle rightbottom
        uivlist 0 [
            uistyle righttop
            if (&& (= $[@[ui_hud_weapidx]ammosub1] 0) (= $[@[ui_hud_weapidx]ammosub2] 0)) [
                uitext "-" $ui_textlarge [
                    uistyle righttop
                    uicolourset $ui_hud_weaptext
                ]
            ] [
                uitext $ui_hud_weapammo $ui_textlarge [
                    uistyle righttop
                    uicolourset $ui_hud_weaptext
                ]
                if (|| (= $[@[ui_hud_weapidx]ammostore] 0) (= $[@[ui_hud_weapidx]ammostore] -1)) [
                    uitext "-" $ui_textsmall [
                        uistyle righttop
                        uicolourset $ui_hud_weaptext2
                    ]
                ] [
                    uitext $ui_hud_weapstore $ui_textsmall [
                        uistyle righttop
                        uicolourset $ui_hud_weaptext2
                    ]
                ]
            ]
        ]
        uiimage $inventorytex $ui_hud_weaphud 0 $ui_hud_mtrm $ui_hud_mtrm [
            uistyle rightbottom
            uispace $ui_padnormal $ui_padnormal [
                uistyle clampxy
                uiimage $[@[ui_hud_weapidx]tex] $ui_hud_weapcol 0 $ui_hud_mtr2 $ui_hud_mtr2 [
                    uistyle centermiddle
                    uiimageshadow $ui_hud_shadow $ui_hud_shadowcol
                ]
            ]
        ]
    ]
]

ui_hud_weaponinventory = [
    ui_hud_weapselect = (getclientweapselect $ui_hud_focus)
    case $weaponhud 2 [
        loopinventoryrev $ui_hud_focus 0 0 ui_hud_weapon [ui_hud_weapbar $ui_hud_weapon $ui_hud_weapselect]
    ] 1 [
        ui_hud_weapbar $ui_hud_weapselect $ui_hud_weapselect
    ]
]
