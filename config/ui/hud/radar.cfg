defvarp showradar 0 0 1
deffvarp radardist 0 384 $fvaridxmax
deffvarp radardistblend 0 500 $fvaridxmax
deffvarp radaroffset 0 0.2 1
deffvarp radarblipsize 0 0.0165 1
deffvarp radarsmallblipsize 0 0.01 1
defvarp radarplayernames 0 0 1
deffvarp radarsize 0 0.75 1
defvarp radaraspect 0 0 1

// 1:dist 2:offset 3:border 4:w 5:h 6:blip 7:smallblip 8:playernames 9:distblend 10:compass
ui_hud_compassdirs = [0 90 180 270]
ui_hud_compassnames = [N E S W]
ui_hud_radar = [
    uiradar $arg1 $arg2 $arg3 $arg4 $arg5 [
        if $arg10 [
            loop ui_hud_compass 4 [
                ui_hud_compassdir = (cameraoffyaw (at $ui_hud_compassdirs $ui_hud_compass))
                ui_hud_compassname = (at $ui_hud_compassnames $ui_hud_compass)
                uiradarblip $bliptex 0xFF000000 $ui_hud_compassdir $ui_hud_compassdir $arg1 (*f $arg6 1.2) (*f $arg6 1.2) [
                    uivlist 0 [uitext $ui_hud_compassname $ui_textradar]
                ]
            ]
        ]
        loopclientsif 0 0 ui_hud_radarclient [getclientradarallow $ui_hud_radarclient] [
            ui_hud_radarclientdir = (getclientradardir $ui_hud_radarclient)
            ui_hud_radarclientyaw = (getclientradaryaw $ui_hud_radarclient)
            ui_hud_radarclientvel = (getclientvelocity $ui_hud_radarclient)
            ui_hud_radarclientdist = (getclientradardist $ui_hud_radarclient)
            ui_hud_radarclientstate = (getclientstate $ui_hud_radarclient)
            ui_hud_radarclientdominated = (&& (= $ui_hud_radarclientstate 0) (getclientisdominated $ui_hud_radarclient $ui_hud_focus))
            ui_hud_radarclientpcolour = (getclientcolour $ui_hud_radarclient -1 2)
            if $ui_hud_radarclientdominated [
                ui_hud_radarclientcolour = (getclientrescolour $ui_hud_radarclient $PULSE_DISCO)
                uiradarblip $playerbliptex (| 0x80000000 $ui_hud_radarclientpcolour) $ui_hud_radarclientdir $ui_hud_radarclientyaw $ui_hud_radarclientdist $arg6 $arg6 [
                    uiimage $dominatedtex $ui_hud_radarclientcolour 0 $arg6 $arg6
                    if $arg8 [uivlist 0 [uispace 0 $arg7; uitext (getclientname $ui_hud_radarclient 0 0) $ui_textradar [uicolourset $ui_hud_radarclientcolour]]]
                ]
            ] [
                ui_hud_radarclientblend = (? (= $ui_hud_radarclientstate 0) 255 160)
                if $ui_hud_radarclientblend [
                    if (>f $arg9 0) [
                        if (>f $ui_hud_radarclientdist $arg9) [
                            ui_hud_radarclientblend = 0
                        ] [
                            ui_hud_radarclientblend = (clamp (*f $ui_hud_radarclientblend (-f 1 (divf $ui_hud_radarclientdist $arg9))) 1 255)
                        ]
                    ]
                    if $ui_hud_radarclientblend [
                        ui_hud_radarclientteam = (getclientteam $ui_hud_radarclient)
                        ui_hud_radarclienttcolour = $[team@(at $teamnames $ui_hud_radarclientteam)colour]
                        ui_hud_radarclientblend2 = (clamp (*f $ui_hud_radarclientblend 0.75) 1 255)
                        ui_hud_radarclientrcolour = (getclientresidualfx $ui_hud_radarclient 3000)
                        if (>= $ui_hud_radarclientrcolour 0) [ ui_hud_radarclientpcolour = $ui_hud_radarclientrcolour ]
                        uiradarblip $[@(at $ui_hud_statetex2 $ui_hud_radarclientstate)] (? (= $ui_hud_radarclientstate 0) (| (<< $ui_hud_radarclientblend2 24) $ui_hud_radarclientpcolour) (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclienttcolour)) $ui_hud_radarclientdir (? (= $ui_hud_radarclientstate 0) $ui_hud_radarclientyaw $ui_hud_radarclientdir) $ui_hud_radarclientdist $arg6 $arg6 [
                            if (= $ui_hud_radarclientstate 0) [uiimage $bliptex (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclienttcolour) 0 $arg7 $arg7]
                            if $arg8 [uivlist 0 [uispace 0 $arg7; uitext (getclientname $ui_hud_radarclient 0 0) $ui_textradar [uicolourset (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclienttcolour)]]]
                        ]
                    ]
                ]
            ]
        ]
        case $gamemode $G_CAPTURE [
            loopcaptureif 0 0 ui_hud_radarcapture [getcaptureradarallow $ui_hud_radarcapture] [
                ui_hud_radarcapturedir = (getcaptureradardir $ui_hud_radarcapture)
                ui_hud_radarcapturedist = (getcaptureradardist $ui_hud_radarcapture)
                ui_hud_radarcaptureteam = (getcaptureteam $ui_hud_radarcapture)
                ui_hud_radarcapturetcolour = $[team@(at $teamnames $ui_hud_radarcaptureteam)colour]
                ui_hud_radarcaptureowner = (getcaptureowner $ui_hud_radarcapture)
                ui_hud_radarcapturedrop = (getcapturedroptime $ui_hud_radarcapture)
                ui_hud_radarcapturetex = ""
                ui_hud_radarcapturesize = $arg6
                doif (&& $ui_hud_carryaffinity (= $ui_hud_radarcaptureteam $ui_hud_team)) [
                    if (|| (< $ui_hud_radarcaptureowner 0) (= $ui_hud_radarcaptureowner $ui_hud_focus)) [
                        ui_hud_radarcapturedir = (getcaptureradardir $ui_hud_radarcapture 1)
                        ui_hud_radarcapturedist = (getcaptureradardist $ui_hud_radarcapture 1)
                    ]
                    ui_hud_radarcapturetcolour = (getclientrescolour $ui_hud_focus $PULSE_DISCO)
                    ui_hud_radarcapturetex = $arrowtex
                    ui_hud_radarcapturesize = (*f $arg6 1.5)
                ] (|| $ui_hud_radarcapturedrop (&& (>= $ui_hud_radarcaptureowner 0) (= $ui_hud_radarcaptureteam $ui_hud_team) (!= $ui_hud_radarcaptureowner $ui_hud_focus))) [
                    ui_hud_radarcapturetex = $flagtex
                    ui_hud_radarcapturetcolour = (getclientrescolour $ui_hud_focus $PULSE_DISCO)
                ] (!= $ui_hud_radarcaptureowner $ui_hud_focus) [
                    ui_hud_radarcapturetex = $flagtex
                ]
                if (!=s $ui_hud_radarcapturetex "") [
                    uiradarblip $ui_hud_radarcapturetex $ui_hud_radarcapturetcolour $ui_hud_radarcapturedir $ui_hud_radarcapturedir $ui_hud_radarcapturedist $ui_hud_radarcapturesize $ui_hud_radarcapturesize [
                        if (>= $ui_hud_radarcaptureowner 0) [uistyle centerbottom]
                        if $arg8 [uivlist 0 [uispace 0 (*f $arg7 1.5); uitext $[team@(at $teamnames $ui_hud_radarcaptureteam)name] $ui_textradar [uicolourset (| $ui_transor $ui_hud_radarcapturetcolour)]]]
                    ]
                ]
            ]
        ] $G_DEFEND [
            loopdefendif 0 0 ui_hud_radardefend [getdefendradarallow $ui_hud_radardefend] [
                ui_hud_radardefenddir = (getdefendradardir $ui_hud_radardefend)
                ui_hud_radardefenddist = (getdefendradardist $ui_hud_radardefend)
                ui_hud_radardefendteam = (getdefendowner $ui_hud_radardefend)
                ui_hud_radardefendtcolour = $[team@(at $teamnames $ui_hud_radardefendteam)colour]
                ui_hud_radardefendname = (getdefendname $ui_hud_radardefend)
                uiradarblip $pointtex $ui_hud_radardefendtcolour $ui_hud_radardefenddir $ui_hud_radardefenddir $ui_hud_radardefenddist $arg6 $arg6 [
                    if $arg8 [uivlist 0 [uispace 0 (*f $arg7 2); uitext $ui_hud_radardefendname $ui_textradar [uistyle centerbottom; uitextwrap 0.04; uicolourset (| $ui_transor $ui_hud_radardefendtcolour)]]]
                ]
            ]
        ] $G_BOMBER [
            loopbomberif 0 0 ui_hud_radarbomber [getbomberradarallow $ui_hud_radarbomber] [
                ui_hud_radarbomberdir = (getbomberradardir $ui_hud_radarbomber)
                ui_hud_radarbomberteam = (getbomberteam $ui_hud_radarbomber)
                ui_hud_radarbomberowner = (getbomberowner $ui_hud_radarbomber)
                ui_hud_radarbombertex = ""
                ui_hud_radarbombername = ""
                ui_hud_radarbombersize = $arg6
                doif (!= $ui_hud_radarbomberteam $teamidxneutral) [
                    if (&& $ui_hud_carryaffinity (!= $ui_hud_radarbomberteam $ui_hud_team)) [
                        ui_hud_radarbombertex = $arrowtex
                        ui_hud_radarbombername = "Destroy"
                        ui_hud_radarbombersize = (*f $arg6 1.5)
                    ]
                ] (&& (= $ui_hud_radarbomberteam $teamidxneutral) (!= $ui_hud_radarbomberowner $ui_hud_focus)) [
                    ui_hud_radarbombertex = $bombtex
                    ui_hud_radarbombername = "Bomb"
                ]
                if (!=s $ui_hud_radarbombertex "") [
                    ui_hud_radarbomberdist = (getbomberradardist $ui_hud_radarbomber)
                    uiradarblip $ui_hud_radarbombertex (getclientrescolour $ui_hud_focus $PULSE_DISCO) $ui_hud_radarbomberdir $ui_hud_radarbomberdir $ui_hud_radarbomberdist $ui_hud_radarbombersize $ui_hud_radarbombersize [
                        if (>= $ui_hud_radarbomberowner 0) [uistyle centerbottom]
                        if (&& $arg8 (!=s $ui_hud_radarbombername "")) [uivlist 0 [uispace 0 (*f $arg7 1.5); uitext $ui_hud_radarbombername $ui_textradar [uicolourset (| $ui_transor (getclientrescolour $ui_hud_focus $PULSE_DISCO))]]]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_midradar = [
    ui_hud_radar $radardist (*f (divf 1 $radarsize) (? $radaraspect (*f $radaroffset $uiaspect) $radaroffset)) 0 (? $radaraspect (*f $radarsize $uiaspect) $radarsize) $radarsize $radarblipsize $radarsmallblipsize $radarplayernames $radardistblend 0
]
