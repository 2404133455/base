ui_hud_radarflagnames = [compass players affinities]

ui_hud_radarflags = [
    local _flags _name
    _flags = 0
    loop i (listlen $ui_hud_radarflagnames) [
        _name = (at $ui_hud_radarflagnames $i)
        if $[@arg1@_name] [
            _flags = (| $_flags (<< 1 $i))
        ]
    ]
    result $_flags
]

// 1:dist 2:offset 3:border 4:w 5:h 6:blip 7:smallblip 8:names 9:distblend 10:flags 11:shape 12:arrows
ui_hud_radar = [
    uiradar $arg1 $arg2 $arg3 $arg4 $arg5 [
        uiradarshape $arg11
        if (& $arg10 1) [
            local compassrot compassdir compassname
            loop ui_hud_compass 4 [
                ui_hud_compassrot = (modf (+f $mapnorth (*f $ui_hud_compass 90)))
                ui_hud_compassdir = (cameraoffyaw $ui_hud_compassrot)
                ui_hud_compassname = (at "N E S W" $ui_hud_compass)
                uiradarblip $bliptex 0xFF000000 $ui_hud_compassdir $ui_hud_compassdir (*f $arg1 2) (*f $arg6 1.2) (*f $arg6 1.2) [
                    uivlist 0 [uitext $ui_hud_compassname $ui_textradar]
                ]
            ]
        ]

        if (& $arg10 2) [
            loopclientsif 0 0 ui_hud_radarclient [getclientradarallow $ui_hud_radarclient] [
                ui_hud_radarclientdir = (getclientradardir $ui_hud_radarclient)
                ui_hud_radarclientyaw = (getclientradaryaw $ui_hud_radarclient)
                ui_hud_radarclientvel = (getclientvelocity $ui_hud_radarclient)
                ui_hud_radarclientdist = (getclientradardist $ui_hud_radarclient)
                ui_hud_radarclientstate = (getclientstate $ui_hud_radarclient)
                ui_hud_radarclientdominator = (&& (= $ui_hud_radarclientstate 0) (getclientisdominator $ui_hud_focus $ui_hud_radarclient))
                ui_hud_radarclientpcolour = (getclientcolour $ui_hud_radarclient $playerundertone $playerundertonelevel $playerundertonemix)
                if $ui_hud_radarclientdominator [
                    ui_hud_radarclientcolour = (getclientpulsecolour $ui_hud_radarclient $PULSE_DISCO)
                    uiradarblip $playerbliptex (| 0x80000000 $ui_hud_radarclientpcolour) $ui_hud_radarclientdir $ui_hud_radarclientyaw $ui_hud_radarclientdist $arg6 $arg6 [
                        uiimage $dominatedtex $ui_hud_radarclientcolour 0 $arg6 $arg6
                        if $arg8 [uivlist 0 [uispace 0 $arg7; uitext (getclientname $ui_hud_radarclient 0 0) $ui_textradar [uicolourset $ui_hud_radarclientcolour]]]
                    ]
                ] [
                    ui_hud_radarclientblend = (? (= $ui_hud_radarclientstate 0) 255 178)
                    ui_hud_radarclientteam = (getclientteam $ui_hud_radarclient)
                    ui_hud_radarclienttcolour = $[team@(at $T_NAMES $ui_hud_radarclientteam)colour]
                    ui_hud_radarclientblend2 = (clamp (*f $ui_hud_radarclientblend 0.85) 1 255)
                    ui_hud_radarclientrcolour = (getclientresidualfx $ui_hud_radarclient 3000)
                    if (>= $ui_hud_radarclientrcolour 0) [ ui_hud_radarclientpcolour = $ui_hud_radarclientrcolour ]
                    uiradarblip $[@(at $ui_hud_statetex2 $ui_hud_radarclientstate)] (? (= $ui_hud_radarclientstate 0) (| (<< $ui_hud_radarclientblend2 24) $ui_hud_radarclienttcolour) (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclientpcolour)) $ui_hud_radarclientdir (? (= $ui_hud_radarclientstate 0) $ui_hud_radarclientyaw $ui_hud_radarclientdir) $ui_hud_radarclientdist $arg6 $arg6 [
                        if (= $ui_hud_radarclientstate 0) [uiimage $bliptex (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclientpcolour) 0 $arg7 $arg7]
                        if $arg8 [uivlist 0 [uispace 0 (*f $arg7 1.5); uitext (getclientname $ui_hud_radarclient 0 0) $ui_textradar [uicolourset (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclienttcolour)]]]
                        if $arg9 [uiradarblipmaxdist $arg9]
                    ]
                ]
            ]
        ]

        if (& $arg10 4) [
            case $gamemode $G_CAPTURE [
                loopcaptureif 0 0 ui_hud_radarcapture [getcaptureradarallow $ui_hud_radarcapture] [
                    ui_hud_radarcapturedir = (getcaptureradardir $ui_hud_radarcapture)
                    ui_hud_radarcapturedist = (getcaptureradardist $ui_hud_radarcapture)
                    ui_hud_radarcaptureteam = (getcaptureteam $ui_hud_radarcapture)
                    ui_hud_radarcapturetcolour = $[team@(at $T_NAMES $ui_hud_radarcaptureteam)colour]
                    ui_hud_radarcaptureowner = (getcaptureowner $ui_hud_radarcapture)
                    ui_hud_radarcapturedrop = (getcapturedroptime $ui_hud_radarcapture)
                    ui_hud_radarcapturetex = ""
                    doif (&& $ui_hud_hasaffinity (= $ui_hud_radarcaptureteam $ui_hud_team)) [
                        if (|| (< $ui_hud_radarcaptureowner 0) (= $ui_hud_radarcaptureowner $ui_hud_focus)) [
                            ui_hud_radarcapturedir = (getcaptureradardir $ui_hud_radarcapture 1)
                            ui_hud_radarcapturedist = (getcaptureradardist $ui_hud_radarcapture 1)
                        ]
                        ui_hud_radarcapturetcolour = (getclientpulsecolour $ui_hud_focus $PULSE_DISCO)
                        ui_hud_radarcapturetex = $arrowtex
                    ] (|| $ui_hud_radarcapturedrop (&& (>= $ui_hud_radarcaptureowner 0) (= $ui_hud_radarcaptureteam $ui_hud_team) (!= $ui_hud_radarcaptureowner $ui_hud_focus))) [
                        ui_hud_radarcapturetex = (? $arg12 $arrowtex $flagtex)
                        ui_hud_radarcapturetcolour = (getclientpulsecolour $ui_hud_focus $PULSE_DISCO)
                    ] (!= $ui_hud_radarcaptureowner $ui_hud_focus) [
                        ui_hud_radarcapturetex = (? $arg12 $arrowtex $flagtex)
                    ]
                    if (!=s $ui_hud_radarcapturetex "") [
                        uiradarblip $ui_hud_radarcapturetex $ui_hud_radarcapturetcolour $ui_hud_radarcapturedir $ui_hud_radarcapturedir $ui_hud_radarcapturedist (*f $arg6 1.5) (*f $arg6 1.5) [
                            if $arg8 [uivlist 0 [uispace 0 (*f $arg7 2.0); uitext $[team@(at $T_NAMES $ui_hud_radarcaptureteam)name] $ui_textradar [uicolourset (| $ui_transor $ui_hud_radarcapturetcolour)]]]
                            uiradarblippriority 1
                        ]
                    ]
                ]
            ] $G_DEFEND [
                loopdefendif 0 0 ui_hud_radardefend [getdefendradarallow $ui_hud_radardefend] [
                    ui_hud_radardefenddir = (getdefendradardir $ui_hud_radardefend)
                    ui_hud_radardefenddist = (getdefendradardist $ui_hud_radardefend)
                    ui_hud_radardefendteam = (getdefendowner $ui_hud_radardefend)
                    ui_hud_radardefendtcolour = $[team@(at $T_NAMES $ui_hud_radardefendteam)colour]
                    ui_hud_radardefendname = (getdefendname $ui_hud_radardefend)
                    uiradarblip (? $arg12 $arrowtex $pointtex) $ui_hud_radardefendtcolour $ui_hud_radardefenddir $ui_hud_radardefenddir $ui_hud_radardefenddist (*f $arg6 1.5) (*f $arg6 1.5) [
                        if $arg8 [uivlist 0 [uispace 0 (*f $arg7 2.0); uitext $ui_hud_radardefendname $ui_textradar [uitextwrap 0.04; uicolourset (| $ui_transor $ui_hud_radardefendtcolour)]]]
                        uiradarblippriority 1
                    ]
                ]
            ] $G_BOMBER [
                loopbomberif 0 0 ui_hud_radarbomber [getbomberradarallow $ui_hud_radarbomber] [
                    ui_hud_radarbomberdir = (getbomberradardir $ui_hud_radarbomber)
                    ui_hud_radarbomberteam = (getbomberteam $ui_hud_radarbomber)
                    ui_hud_radarbomberowner = (getbomberowner $ui_hud_radarbomber)
                    ui_hud_radarbombertex = ""
                    ui_hud_radarbombername = ""
                    doif (!= $ui_hud_radarbomberteam $T_NEUTRAL) [
                        if (&& $ui_hud_hasaffinity (!= $ui_hud_radarbomberteam $ui_hud_team)) [
                            ui_hud_radarbombertex = $arrowtex
                            ui_hud_radarbombername = "Destroy"
                        ]
                    ] (&& (= $ui_hud_radarbomberteam $T_NEUTRAL) (!= $ui_hud_radarbomberowner $ui_hud_focus)) [
                        ui_hud_radarbombertex = (? $arg12 $arrowtex $bombtex)
                        ui_hud_radarbombername = "Bomb"
                    ]
                    if (!=s $ui_hud_radarbombertex "") [
                        ui_hud_radarbomberdist = (getbomberradardist $ui_hud_radarbomber)
                        uiradarblip $ui_hud_radarbombertex (getclientpulsecolour $ui_hud_focus $PULSE_DISCO) $ui_hud_radarbomberdir $ui_hud_radarbomberdir $ui_hud_radarbomberdist (*f $arg6 1.5) (*f $arg6 1.5) [
                            if (&& $arg8 (!=s $ui_hud_radarbombername "")) [uivlist 0 [uispace 0 (*f $arg7 2.0); uitext $ui_hud_radarbombername $ui_textradar [uicolourset (| $ui_transor (getclientpulsecolour $ui_hud_focus $PULSE_DISCO))]]]
                            uiradarblippriority 1
                        ]
                    ]
                ]
            ]
        ]
    ]
]

defvarp showradar 0 0 1
deffvarp radardist 0 128 $fvaridxmax
deffvarp radardistblend 0 256 $fvaridxmax
deffvarp radaroffset 0 0.125 1
deffvarp radarblipsize 0 0.015 1
deffvarp radarsmallblipsize 0 0.0085 1
defvarp radarcompass 0 0 1
defvarp radarplayers 0 1 1
defvarp radaraffinities 0 1 1
defvarp radarnames 0 0 1
deffvarp radarsize 0 0.25 1
deffvarp radarblend 0 0.5 1
defvarp radaraspect 0 0 1
defvarp radarshape 0 0 1

ui_hud_midradar = [
    uitag "hud_radar" [
        uistyle centermiddle
        uispace $hudskinpadtiny $hudskinpadtiny [
            uistyle centermiddle
            uigroup [
                ui_hud_radar $radardist (*f (divf 1 $radarsize) (? $radaraspect (*f $radaroffset $uiaspect) $radaroffset)) 0 (? $radaraspect (*f $radarsize $uiaspect) $radarsize) $radarsize $radarblipsize $radarsmallblipsize $radarnames $radardistblend (ui_hud_radarflags radar) $radarshape
                uipropagate [ uicolourblend $radarblend ]
            ]
        ]
    ]
]

defvarp showminimap 0 0 1
deffvarp minimapscale $fvaridxnonzero 0.22 $fvaridxmax
deffvarp minimapblend $fvaridxnonzero 0.8 $fvaridxmax
deffvarp minimapdist 0 768 $fvaridxmax
deffvarp minimapdistblend 0 1024 $fvaridxmax
deffvarp minimapoffset 0 0.01 1
deffvarp minimapborder 0 0.1 1
deffvarp minimapblipsize 0 0.01 1
deffvarp minimapsmallblipsize 0 0.005 1
defvarp minimapcompass 0 1 1
defvarp minimapplayers 0 1 1
defvarp minimapaffinities 0 1 1
defvarp minimapnames 0 0 1
defvarp minimapshape 0 0 1

ui_hud_minimap = [
    local _colour _tex _scale
    _tex = (? $minimapshape $radarsquaretex $radarcircletex)
    _scale = (*f $minimapscale $game_hud_scale)

    uigroup [
        uioffset 0 0.0025 [
            uiimage $_tex 0 0 $_scale $_scale
        ]
        uiminimap "" $ui_hud_colour2 $minimapdist 0.1 $_scale $_scale [
            uiminimapshape $minimapshape
        ]
        uiimage $_tex $ui_hud_colour 0 $_scale $_scale [
            uicolourscale 0x808080
            uigroup [
                ui_hud_radar $minimapdist (*f (divf 1 $_scale) $minimapoffset) $minimapborder $_scale $_scale $minimapblipsize $minimapsmallblipsize $minimapnames $minimapdistblend (ui_hud_radarflags minimap) $minimapshape
            ]
        ]
        uipropagate [ uicolourblend $minimapblend ]
   ]
]

defvarp showbearing 0 1 1
deffvarp bearingsize $fvaridxnonzero 0.75 $fvaridxmax
deffvarp bearingheight $fvaridxnonzero 0.0225 $fvaridxmax
deffvarp bearingblend $fvaridxnonzero 1.0 $fvaridxmax
deffvarp bearingdist 0 1024 $fvaridxmax
deffvarp bearingdistblend 0 2048 $fvaridxmax
deffvarp bearingoffset -1 0 1
deffvarp bearingborder 0 0 1
deffvarp bearingblipsize 0 0.011 1
deffvarp bearingsmallblipsize 0 0.006 1
defvarp bearingcompass 0 1 1
defvarp bearingplayers 0 1 1
defvarp bearingaffinities 0 1 1
defvarp bearingnames 0 0 1

ui_hud_bearing = [
    local _colour _height _scale
    _height = (*f $bearingheight $game_hud_scale)
    _scale = (*f $bearingsize $game_hud_scale)

    uigroup [
        uifill $_scale $_height [
            ui_gameui_advshadowhoriz [
                p_colour  = 0xaa000000
                p_bgblend = 0.5
            ]
        ]
        uipad 0 0 0 $ui_padhud [
            ui_hud_radar $bearingdist $bearingoffset $bearingborder $_scale $_height $bearingblipsize $bearingsmallblipsize $bearingnames $bearingdistblend (ui_hud_radarflags bearing) 2 1
            uipropagate [ uicolourblend $bearingblend ]
        ]
    ]
]
