defvarp showevents 0 2 2
defvarp eventintime 0 250 $varidxmax
defvarp eventouttime 0 10000 $varidxmax
defvarp eventfadetime 0 500 $varidxmax
defvarp eventlines 0 5 $varidxmax
defvarp eventlinesopen 0 7 $varidxmax
deffvarp eventblend 0 1 1

ui_hud_events = [
    if $showevents [
        uivlist $ui_padtiny [
            uistyle lefttop
            loopevents (- $eventlines) 0 event_id [
                local event_time; event_time = (geteventmillis $event_id)
                local event_millis; event_millis = (- $totalmillis $event_time)
                local event_blend; event_blend = $eventblend
                local event_growth; event_growth = 1.0
                caseif (&& $eventouttime (> $event_millis $eventouttime)) [
                    local event_ctime; event_ctime = (- $event_millis $eventouttime)
                    if (&& $eventfadetime (< $event_ctime $eventfadetime)) [
                        event_growth = (-f 1 (divf $event_ctime $eventfadetime))
                        event_blend = (clampf (*f $event_growth $eventblend) 0 1)
                    ] [ event_blend = 0 ]
                ] (&& $eventintime (< $event_millis $eventintime)) [
                    event_growth = (divf $event_millis $eventintime)
                    event_blend = (clampf (*f $event_growth $eventblend) 0 1)
                ]
                if (>f $event_blend 0.0) [
                    local event_client; event_client = (geteventclient $event_id)
                    local event_self; event_self = 0
                    loopwhile event_clidx $event_client [! $event_self] [
                        event_clientid = (geteventclient $event_id $event_clidx 0)
                        if (= $focusedplayer $event_clientid) [ event_self = 1 ]
                    ]
                    local event_obj
                    case $showevents 1 [
                        event_obj = [
                            uitext (geteventstr $event_id) $ui_textxtiny [
                                uistyle leftmiddle
                                uitextalign -1
                            ]
                        ]
                    ] 2 [
                        local event_type; event_type = (geteventtype $event_id)
                        case $event_type $eventidxfrag [
                            event_obj = [
                                uihlist $ui_padsmall [
                                    uistyle leftmiddle
                                    local event_texth
                                    local event_extra; event_extra = 0
                                    loop event_attackid (- $event_client 1) [
                                        if (> $event_attackid 0) [
                                            event_extra = 1
                                            uitext "+" $ui_textxtiny [
                                                uistyle leftmiddle
                                                uitextalign -1
                                            ]
                                        ]
                                        uitext (geteventname $event_id (+ $event_attackid 1) 3 0) (? $event_extra $ui_textxtiny $ui_texttiny) [
                                            uistyle leftmiddle
                                            uitextalign -1
                                            if (! $event_extra) [ event_texth = $uilasth ]
                                        ]
                                    ]
                                    local event_weap; event_weap = (geteventinfo $event_id 0)
                                    if (>= $event_weap 0) [
                                        local event_weapon; event_weapon = (at $weapname $event_weap)
                                        uiimage $[@[event_weapon]tex] $[@[event_weapon]colour] 0 $event_texth $event_texth [ uistyle leftmiddle ]
                                    ] [
                                        if (geteventinfo $event_id 5) [
                                            uiimage $burntex (getclientrescolour $focusedplayer $pulseidxburn) 0 $event_texth $event_texth [ uistyle leftmiddle ]
                                        ]
                                        if (geteventinfo $event_id 6) [
                                            uiimage $bleedtex (getclientrescolour $focusedplayer $pulseidxbleed) 0 $event_texth $event_texth [ uistyle leftmiddle ]
                                        ]
                                        if (geteventinfo $event_id 7) [
                                            uiimage $shocktex (getclientrescolour $focusedplayer $pulseidxshock) 0 $event_texth $event_texth [ uistyle leftmiddle ]
                                        ]
                                    ]
                                    if (!= (geteventclient $event_id 0 0) (geteventclient $event_id 1 0)) [
                                        if (= (geteventclient $event_id 0 2) (geteventclient $event_id 1 2)) [
                                            uiimage $warningtex (getclientrescolour $focusedplayer $pulseidxwarn) 0 $event_texth $event_texth [ uistyle leftmiddle ]
                                        ]
                                        uitext (geteventname $event_id 0 3 0) $ui_texttiny [
                                            uistyle leftmiddle
                                            uitextalign -1
                                            event_texth = $uilasth
                                        ]
                                    ] [
                                        uiimage $deadtex 0xFFFFFF 0 $event_texth $event_texth [ uistyle leftmiddle ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                    uiborderedimageclamped $skinalphatex (? $event_self 0xaa303030 0x99000000) 0 $ui_texborder $ui_screenborder 0 0 [
                        uistyle lefttop
                        local event_height
                        uiclip 0 0 0 0 [
                            uispace $ui_padsmall $ui_padsmall [
                                uistyle lefttop
                                event_obj
                                event_height = (+f $ui_padsmall $ui_padsmall $uilasthprev)
                            ]
                            uiclipsizeh (*f $event_height $event_growth)
                        ]
                        uiprop [ uicolourblend $event_blend ]
                    ]
                ]
            ]
        ]
    ]
]