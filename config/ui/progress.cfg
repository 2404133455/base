defvarp progressinfo 0 1 1

progressui "default" [
    uistyle clampxy
    uiborderedimageclamped $skinshadowtex 0xff000000 0 $ui_texborder $ui_screenborder 0.75 0.25 [
        uiclip 0.4 0.25 0.0 0.075 [
            uiimage (modetexlist $gamemode $mutators -1) 0x80020202 0 0.4 0.4
        ]
        uispace $ui_padlarge $ui_padlarge [
            uistyle clampxy
            uivlist 0 [
                uistyle clampxy
                uivlist 0 [
                    uistyle lefttop
                    case $progresstype 3 [
                        uitext "Connecting..." $ui_textxlarge [uistyle lefttop; uitextalign -1; uitextlimit -0.7]
                    ] 2 [
                        uitext "Saving..." $ui_textxlarge [uistyle lefttop; uitextalign -1; uitextlimit -0.7]
                    ] 1 [
                        uitext (getmaptitle $mapname) $ui_textxlarge [uistyle lefttop; uitextalign -1; uicolourset $colourorange; uitextlimit -0.7]
                        if (!=s (getmapauthor $mapname) "") [
                            uispace $ui_padnormal 0 [
                                uistyle lefttop
                                uistyle clampx
                                uitext (concat "by" (getmapauthor $mapname)) $ui_text [uistyle lefttop; uitextalign -1; uicolourset $colourgrey; uitextlimit -0.7]
                            ]
                        ]
                    ] 0 [
                        uitext "Loading..." $ui_textxlarge [uistyle lefttop; uitextalign -1; uitextlimit -0.7]
                    ]
                ]
                uivlist 0 [
                    uistyle rightbottom
                    uitext (gamename $gamemode $mutators 0 32) $ui_textxlarge [uistyle rightbottom; uitextalign 1; uicolourset $colouryellow; uitextlimit -0.7]
                    uitext (modedesc $gamemode $mutators 5) $ui_text [uistyle rightbottom; uitextalign 1; uicolourset $colourgrey; uitextlimit -0.7]
                ]
            ]
        ]
    ]

    uiimage $logotex 0xFFFFFF 1 0.3 0.15 [uistyle lefttop]

    uicolour 0xFF000000 0 0 [
        uistyle clampx
        uistyle leftbottom
        uistyle fadebar
        uivlist 0 [
            uistyle clampx
            uistyle leftbottom
            uispace $ui_padnormal $ui_padnormal [
                uistyle clampx
                uihlist 0 [
                    uistyle clampx
                    if $progressinfo [
                        local ui_progress_title; ui_progress_title = (getprogresstitle)
                        if (!=s $ui_progress_title "") [
                            uispace $ui_padsmall 0 [
                                uistyle leftbottom
                                uitext $ui_progress_title $ui_text [uistyle leftmiddle; uitextalign -1]
                            ]
                        ]
                    ]
                    uitext (tipshow) $ui_text [uistyle rightmiddle; uitextalign -1]
                ]
            ]
            uiclip (*f $uiaspect $progressamt) 0.01 0.0 0.0 [
                uistyle leftbottom
                uicolour $colourdarkred $uiaspect 0.01 [
                    uistyle leftbottom
                    uicolourdir 1
                    uicolouradd $colourred
                    uicolouradd $colourpink
                ]
            ]
        ]
    ]
]
