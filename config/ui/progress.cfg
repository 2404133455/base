defvarp progressinfo 0 1 1

progressui "default" [
    uiclamp 1 1 1 1
    uifont $textfontclear [
        uiclamp 1 1 1 1

        uiborderedimageclamped "textures/hud/uiskinshadow" 0xd0ffffff 0 $ui_texborder $ui_screenborder 0.75 0.25 [
            uiclip 0.4 0.25 0.0 0.075 [
                uiimage (modetexlist $gamemode $mutators -1) 0x80020202 0 0.4 0.4
            ]
            uispace $ui_padwin $ui_padwin [
                uiclamp 1 1 1 1
                uivlist 0 [
                    uiclamp 1 1 1 1
                    uivlist 0 [
                        uialign -1 -1
                        case $getmapstate 2 [
                            uitext "Saving..." $ui_texthuge [uialign -1 -1; uitextalign -1; uitextlimit -0.7]
                        ] 1 [
                            uitext (getmaptitle $mapname) $ui_texthuge [uialign -1 -1; uitextalign -1; uitextlimit -0.7]
                            if (!=s (getmapauthor $mapname) "") [
                                uispace $ui_padnormal 0 [
                                    uialign -1 -1
                                    uiclamp 1 1 0 0
                                    uitext (concat "by" (getmapauthor $mapname)) $ui_textlarge [uialign -1 -1; uitextalign -1; uicolourset $colourgrey; uitextlimit -0.7]
                                ]
                            ]
                        ] 0 [
                            uitext "Loading..." $ui_texthuge [uialign -1 -1; uitextalign -1; uitextlimit -0.7]
                        ]
                    ]
                    uivlist 0 [
                        uialign 1 1
                        uitext (gamename $gamemode $mutators 0 32) $ui_textxlarge [uialign 1 1; uitextalign 1; uicolourset $colouryellow; uitextlimit -0.7]
                        uitext (modedesc $gamemode $mutators 5) $ui_text [uialign 1 1; uitextalign 1; uicolourset $colourgrey; uitextlimit -0.7]
                    ]
                ]
            ]
        ]

        uiimage $logotex 0xFFFFFF 1 0.3 0.15 [uialign -1 -1]

        uivlist 0 [
            uiclamp 1 1 0 0
            uialign -1 1
            uispace $ui_padnormal $ui_padnormal [
                uiclamp 1 1 0 0
                uihlist 0 [
                    uiclamp 1 1 0 0
                    if $progressinfo [
                        local ui_progress_title; ui_progress_title = (getprogresstitle)
                        if (!=s $ui_progress_title "") [
                            uispace $ui_padsmall 0 [
                                uialign -1 1
                                uitext $ui_progress_title $ui_text [uialign -1 0; uitextalign -1]
                            ]
                        ]
                    ]
                    uitext (tipshow) $ui_text [uialign 1 0; uitextalign -1]
                ]
            ]
            uiclip (*f $uiaspect $progressamt) 0.01 0.0 0.0 [
                uialign -1 1
                uicolour $colourdarkred $uiaspect 0.01 [
                    uialign -1 1
                    uicolourdir 1
                    uicolouradd $colourred
                    uicolouradd $colourpink
                ]
            ]
        ]
    ]
] [] [] 16
