defvarp progressinfo 0 1 1

ui_progress = [
    if (>f $arg2 0) [
        uiborderedimageclamped $skinshadowtex 0xff000000 0 $ui_texborder $ui_screenborder 0.75 0.25 [
            uiclip 0.4 0.25 0.0 0.075 [
                uiimage (modetex) 0x80020202 0 0.4 0.4
            ]
            uispace $ui_padlarge $ui_padlarge [
                uistyle clampxy
                uivlist 0 [
                    uistyle clampxy
                    uivlist 0 [
                        uistyle lefttop
                        if (|| (= $arg1 $PROGRESS_MAPLOAD) (= $arg1 $PROGRESS_GAMESTATE) (= $arg1 $PROGRESS_GAMEWAIT)) [
                            uitext (getmaptitle $mapname) $ui_textxlarge [uistyle lefttop; uitextalign -1; uicolourset $colourorange; uitextlimit -0.7]
                            if (!=s (getmapauthor $mapname) "") [
                                uispace $ui_padnormal 0 [
                                    uistyle lefttop
                                    uistyle clampx
                                    uitext (concat "by" (getmapauthor $mapname)) $ui_text [uistyle lefttop; uitextalign -1; uicolourset $colourgrey; uitextlimit -0.7]
                                ]
                            ]
                        ] [
                            uitext (concatword (at $PROGRESS_NAMES $arg1) "...") $ui_textxlarge [uistyle lefttop; uitextalign -1; uitextlimit -0.7]
                            uispace $ui_padnormal 0 [
                                uistyle lefttop
                                uistyle clampx
                                uitext "Please wait..." $ui_text [uistyle lefttop; uitextalign -1; uicolourset $colourgrey; uitextlimit -0.7]
                            ]
                        ]
                    ]
                    uivlist 0 [
                        uistyle rightbottom
                        uitext (gamename $gamemode $mutators 0 32) $ui_textxlarge [uistyle rightbottom; uitextalign 1; uicolourset $colouryellow; uitextlimit -0.7]
                        uitext (modedesc $gamemode $mutators 5) $ui_text [uistyle rightbottom; uitextalign 1; uicolourset $colourgrey; uitextlimit -0.7]
                    ]
                ]
            ]
            if (<f $arg2 1) [ uiprop [ uicolourblend $arg2 ] ]
        ]
    ]

    uiimage $logotex 0xFFFFFF 1 0.3 0.15 [ uistyle righttop ]

    uicolour 0xFF000000 0 0 [
        uistyle clampx
        uistyle leftbottom
        uistyle fadebar
        uivlist 0 [
            uistyle clampx
            uistyle leftbottom
            uispace $ui_padnormal $ui_padnormal [
                uistyle clampx
                uihlist 0 [
                    uistyle clampx
                    if $progressinfo [
                        if (!=s $arg4 "") [
                            uispace $ui_padsmall 0 [
                                uistyle leftbottom
                                uitext $arg4 $ui_text [uistyle leftmiddle; uitextalign -1]
                            ]
                        ]
                    ]
                    uispace $ui_padsmall 0 [
                        uistyle rightbottom
                        uitext $arg5 $ui_text [uistyle rightmiddle; uitextalign -1]
                    ]
                ]
            ]
            uiclip (*f $uiaspect $arg3) 0.01 0.0 0.0 [
                uistyle leftbottom
                uicolour $colourdarkred $uiaspect 0.01 [
                    uistyle leftbottom
                    uicolourdir 1
                    uicolouradd $colourred
                    uicolouradd $colourpink
                ]
            ]
        ]
    ]
]

progressui "default" [
    uistyle clampxy
    ui_progress $progresstype 1.0 $progressamt (getprogresstitle) (tipshow)
]
