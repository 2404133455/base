defvarp progressinfo 0 1 1
defvarp progresstips 0 1 1

ui_progress = [
    if (>f $arg2 0) [
        uiborderedimageclamped $skintex 0xA0000000 0 $ui_texborder $ui_screenborder 0.75 0.25 [
            uiborderedimageclamped $skinshadowtex 0xff000000 0 $ui_texborder $ui_screenborder 0.75 0.25 [
                uiclip 0.4 0.25 0.0 0.075 [
                    uiimage (modetex) 0x80020202 0 0.4 0.4
                ]
                uispace $ui_padlarge $ui_padlarge [
                    uistyle clampxy
                    uivlist 0 [
                        uistyle clampxy
                        uivlist 0 [
                            uistyle clampx
                            uistyle lefttop
                            if (&& (! $emptymap) (|| [= $arg1 $PROGRESS_MAPLOAD] [= $arg1 $PROGRESS_GAMESTATE] [= $arg1 $PROGRESS_GAMEWAIT])) [
                                uitext (getmaptitle $mapname) $ui_textxlarge [ uistyle leftmiddle; uitextalign -1; uicolourset $colourorange ]
                                if (!=s (getmapauthor $mapname) "") [
                                    uispace $ui_padnormal 0 [
                                        uistyle leftmiddle
                                        uistyle clampx
                                        uitext (concat "by" (getmapauthor $mapname)) $ui_text [ uistyle leftmiddle; uitextalign -1; uicolourset $colourgrey ]
                                    ]
                                ]
                            ] [
                                uitext (concatword (at $PROGRESS_NAMES $arg1) "...") $ui_textxlarge [ uistyle leftmiddle; uitextalign -1 ]
                                uispace $ui_padnormal 0 [
                                    uistyle leftmiddle
                                    uistyle clampx
                                    uitext "Please wait..." $ui_text [ uistyle leftmiddle; uitextalign -1; uicolourset $colourgrey ]
                                ]
                            ]
                        ]
                        uivlist 0 [
                            uistyle clampx
                            uistyle rightbottom
                            if $mutators [
                                uitext (mutsname $gamemode $mutators 0 32) $ui_text [ uistyle rightmiddle; uitextalign 1; uicolourset $colouryellow ]
                            ]
                            uitext (modename $gamemode $mutators) $ui_textxlarge [ uistyle rightmiddle; uitextalign 1; uicolourset $colouryellow ]
                            uitext (modedesc $gamemode $mutators 5) $ui_text [ uistyle rightmiddle; uitextalign 1; uicolourset $colourgrey ]
                        ]
                    ]
                ]
                if (<f $arg2 1) [ uipropagate [ uicolourblend $arg2 ] ]
            ]
        ]
    ]

    if $showloadinglogos [ uispace $ui_padsmall $ui_padwin [ uistyle righttop; uiimage $logocroptex 0xFFFFFF 1 0.3 0 [ uistyle righttop; uiimageaspect 1 ] ] ]

    uivlist 0 [
        uistyle clampxy
        uistyle centermiddle
        if $arg6 [ uifill 0 0 [ uistyle clampxy; doargs $arg6 ] ]
        uivlist 0 [
            uistyle clampx
            uistyle centerbottom
            uicolour 0xFF000000 $uiaspect 0 [
                uistyle clampx
                uistyle centerbottom
                uistyle fadebardark
                uicolourblend 0.75
                uispace $ui_padlarge $ui_padnormal [
                    uistyle clampx
                    uihlist 0 [
                        uistyle clampx
                        if (&& $progressinfo (!=s $arg4 "")) [
                            uispace $ui_padsmall 0 [
                                uistyle leftmiddle
                                uitext $arg4 $ui_text [ uistyle leftmiddle; uitextalign -1 ]
                            ]
                        ]
                        if (&& $progresstips (!=s $arg5 "")) [
                            uispace $ui_padsmall 0 [
                                uistyle rightmiddle
                                uitext $arg5 $ui_text [ uistyle rightmiddle; uitextalign -1 ]
                            ]
                        ]
                    ]
                ]
            ]
            if (>f $arg3 0.0) [
                uiclip (*f $uiaspect $arg3) 0.015 0.0 0.0 [
                    uistyle centerbottom
                    uicolour $colourred $uiaspect 0.015 [
                        uistyle leftbottom
                        uistyle fadebarred
                        uicolourblend 0.75
                    ]
                ]
            ] [
                uifill (*f $uiaspect $arg3) 0.015 [ uistyle leftbottom ]
            ]
        ]
    ]
]

newui "default" $SURFACE_PROGRESS [
    uistyle clampxy
    ui_progress $progresstype 1.0 $progressamt (getprogresstitle) (tipshow)
]
