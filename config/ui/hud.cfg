defvarp darkhud 0 2 2
defvarp weaponhud 0 2 2

defvarp showfps 0 0 4
defvarp showstats 0 1 2
defvarp showtexstats 0 1 1
defvarp showedittoggles 0 1 1
defvarp showvelocity 0 0 1
defvarp showweight 0 0 1
defvarp showtimestyle 0 3 4
defvarp showrealtime 0 0 1

defvarp showminimap 0 0 1
deffvarp minimapdist 0 384 $fvaridxmax
deffvarp minimapdistblend 0 500 $fvaridxmax
deffvarp minimapoffset 0 0.025 1
deffvarp minimapborder 0 0.1 1
deffvarp minimapblipsize 0 0.009 1
deffvarp minimapsmallblipsize 0 0.006 1
defvarp minimapplayernames 0 0 1
defvarp minimapcompass 0 1 1

defvarp showradar 0 1 1
deffvarp radardist 0 384 $fvaridxmax
deffvarp radardistblend 0 500 $fvaridxmax
deffvarp radaroffset 0 0.155 1
deffvarp radarblipsize 0 0.0165 1
deffvarp radarsmallblipsize 0 0.01 1
defvarp radarplayernames 0 0 1
deffvarp radarsize 0 0.35 2
defvarp radaraspect 0 0 1
defsvarp radarskintex "<grey>textures/hud/radar"

defvarp texpaneltime 0 3000 $varidxmax
defvarp texpanelfade 0 1000 $varidxmax
defvarp texpanellead 0 4 $varidxmax
defvarp texpanelfollow 0 4 $varidxmax
deffvarp texpanelblend 0 0.8 1

deffvarp ui_hud_blend 0 1 1
deffvarp ui_hud_sizew $fvaridxnonzero 0.1125 $fvaridxmax
deffvarp ui_hud_sizeh $fvaridxnonzero 0.225 $fvaridxmax
deffvarp ui_hud_top $fvaridxnonzero 0.045 $fvaridxmax
deffvarp ui_hud_mtrw $fvaridxnonzero 0.3 $fvaridxmax
deffvarp ui_hud_mtrf $fvaridxnonzero 0.15 $fvaridxmax
deffvarp ui_hud_mtrh $fvaridxnonzero 0.015 $fvaridxmax
deffvarp ui_hud_mtrz $fvaridxnonzero 0.09 $fvaridxmax
deffvarp ui_hud_mtrm $fvaridxnonzero 0.075 $fvaridxmax
deffvarp ui_hud_mtrq $fvaridxnonzero 0.02 $fvaridxmax
deffvarp ui_hud_mtr2 $fvaridxnonzero 0.05 $fvaridxmax
deffvarp ui_hud_mtr3 $fvaridxnonzero 0.04 $fvaridxmax
deffvarp ui_hud_tool $fvaridxnonzero 0.04 $fvaridxmax
deffvarp ui_hud_shadow $fvaridxnonzero 0.0004 $fvaridxmax
defhvarp ui_hud_shadowcol 0 0x40000000 0xFFFFFFFF

defsvarp ui_hud_statenames [ALIVE DEAD EDIT SPEC WAIT]
defsvarp ui_hud_statetexs [playertex deadtex editingtex spectatortex waitingtex]
defsvarp ui_hud_statetex2 [playerbliptex deadtex playerbliptex spectatortex deadtex]
defsvarp ui_hud_timecols [red red red red red green yellow orange orange]

deffvarp ui_hud_healthtop 0.0 0.104 1.0
deffvarp ui_hud_healthbottom 0.0 0.895 1.0
deffvarp ui_hud_impulsetop 0.0 0.174 1.0
deffvarp ui_hud_impulsebottom 0.0 0.898 1.0
deffvarp ui_hud_impulsepadleft 0.0 0.5 1.0
deffvarp ui_hud_impulsepadtop 0.0 0.230 1.0
deffvarp ui_hud_impulsepadbottom 0.0 0.260 1.0

ui_hud_init = [
    refreshenginestats
    ui_hud_clientnum = (getplayercn)
    ui_hud_input = (&& $ui_freecursor (= (getclientstate $ui_hud_clientnum) $stateidxediting))
    ui_hud_focus = (? $ui_hud_input $ui_hud_clientnum (getclientfocused))
    ui_hud_actor = (getclientactortype $ui_hud_focus)
    ui_hud_model = (getclientmodel $ui_hud_focus)
    ui_hud_state = (getclientstate $ui_hud_focus)
    ui_hud_team = (getclientteam $ui_hud_focus)
    ui_hud_health = (getclienthealth $ui_hud_focus)
    ui_hud_spawnhealth = (getclientspawnhealth $ui_hud_focus)
    if (& (mutators) $mutsbitinstagib) [
        ui_hud_damagelevel = 1
        ui_hud_damagecrit = (= $ui_hud_state $stateidxdead)
    ] [
        ui_hud_damagelevel = (? (= $ui_hud_state $stateidxdead) 0.0 (divf $ui_hud_health $ui_hud_spawnhealth))
        ui_hud_damagecrit = (|| (= $ui_hud_state $stateidxdead) (&& (= $ui_hud_state $stateidxalive) (<=f $ui_hud_damagelevel $damagecritical)))
    ]
    ui_hud_carryaffinity = (getclientcarryaffinity $ui_hud_focus)
    ui_hud_colour = (getclientcolour $ui_hud_focus $playerovertone $playerovertonelevel)
    ui_hud_tone = (getclientcolour $ui_hud_focus $playerundertone $playerundertonelevel)
    case $darkhud [
        ui_hud_colour = (modcolour $ui_hud_colour 0.75)
        ui_hud_tone = (modcolour $ui_hud_tone 0.75)
    ] 2 [
        ui_hud_colour = (modcolour $ui_hud_colour 0.5)
        ui_hud_tone = (modcolour $ui_hud_tone 0.5)
    ]
    ui_hud_commandmillis = (getcommandmillis)
    ui_hud_commandopen = (> $ui_hud_commandmillis 0)
    ui_hud_showhud = (|| $ui_hud_commandopen $ui_hud_input $showhud)
]

ui_hud_healthbar = [
    ui_hud_health = (getclienthealth $ui_hud_focus)
    if (& (mutators) $mutsbitinstagib) [
        ui_hud_healthtext = "INSTA"
        ui_hud_healthcol1 = $colouryellow
        ui_hud_healthcol2 = $colourred
        ui_hud_healthcol3 = $colourdarkpurple
    ] [
        ui_hud_healthdiv = (divf $ui_hud_health $damagedivisor)
        if $damageinteger [ ui_hud_healthtext = (toint (ceil $ui_hud_healthdiv)) ] [ ui_hud_healthtext = (precf $ui_hud_healthdiv 1) ]
        ui_hud_healthcol1 = $colourgreen
        ui_hud_healthcol2 = $colouryellow
        ui_hud_healthcol3 = $colourdarkred
    ]
    ui_hud_spawnhealth = (getclientspawnhealth $ui_hud_focus)
    ui_hud_healthregen = (getclientregen $ui_hud_focus)
    ui_hud_healthpart = (divf $ui_hud_health $ui_hud_spawnhealth)
    ui_hud_healthhud = (getclientresidualfx $ui_hud_focus 1000)
    if (< $ui_hud_healthhud 0) [
        ui_hud_healthhud = $ui_hud_colour
        ui_hud_healthcol = $colourwhite
    ] [
        ui_hud_healthcol = $ui_hud_healthhud
    ]

    ui_hud_healthimpulse = (&& $impulsemeter (getclientallowimpulse $ui_hud_focus))
    ui_hud_impulseregen = (getclientimpulselast $ui_hud_focus)
    ui_hud_impulsecount = (getclientimpulsecount $ui_hud_focus)
    ui_hud_impulsemoves = (- $impulsecount $ui_hud_impulsecount)
    caseif $ui_hud_healthimpulse [
        ui_hud_impulse = (getclientimpulsemeter $ui_hud_focus)
        if (> $ui_hud_impulse 0) [
            ui_hud_impulsepart = (clampf (-f 1 (divf $ui_hud_impulse $impulsemeter)) 0.0 1.0)
        ] [
            ui_hud_impulsepart = 1.0
        ]
        ui_hud_impulsehud = $ui_hud_colour
    ] (!= $impulsestyle 3) [
        ui_hud_healthimpulse = 2
        ui_hud_impulse = $ui_hud_impulsemoves
        ui_hud_impulsepart = 1.0
        ui_hud_impulsepart = (clampf (divf $ui_hud_impulse $impulsecount) 0.0 1.0)
        if (<= $ui_hud_impulse 1) [ ui_hud_impulsehud = (getclientrescolour $ui_hud_focus $pulseidxwarn) ] [ ui_hud_impulsehud = $ui_hud_colour ]
    ]

    uivlist 0 [
        uialign -1 1
        if $ui_hud_healthimpulse [
            uiimage "textures/hud/impulsebg" $ui_hud_impulsehud 0 $ui_hud_mtrm $ui_hud_mtrf [
                uiclamp 1 1 1 1
                if $uishadows [uiimageshadow $ui_menu_s]
                if (>f $ui_hud_impulsepart 0) [
                    ui_hud_impulsesize = $ui_hud_mtrf
                    if (<f $ui_hud_impulsepart 1) [
                        ui_hud_impulsesec = (+f (*f (-f $ui_hud_impulsebottom $ui_hud_impulsetop) $ui_hud_impulsepart) (-f 1 $ui_hud_impulsebottom))
                        ui_hud_impulsesize = (*f $ui_hud_impulsesec $ui_hud_mtrf)
                    ]
                    uiclip $ui_hud_mtrm $ui_hud_impulsesize 0 (-f $ui_hud_mtrf $ui_hud_impulsesize) [
                        uialign 0 1
                        uiimage "textures/hud/impulse" $colourpurple 0 $ui_hud_mtrm $ui_hud_mtrf [
                            uialign 0 1
                            looplist ui_hud_impulsecol [@colourcyan @colourdarkblue] [uicolouradd $ui_hud_impulsecol]
                            if $ui_hud_impulseregen [ uicolourrotate (divf [@@ui_hud_impulseregen] 1000) ]
                        ]
                    ]
                ]
                uipad (*f $ui_hud_mtrm $ui_hud_impulsepadleft) 0 (*f $ui_hud_mtrf $ui_hud_impulsepadtop) (*f $ui_hud_mtrf $ui_hud_impulsepadbottom) [
                    uialign 0 0
                    uiclamp 1 1 1 1
                    uitext $ui_hud_impulsemoves $ui_textextralarge [
                        uialign 0 0
                        uitextlimit 1
                        if (<= $ui_hud_impulsemoves 1) [uicolourset (getclientrescolour $ui_hud_focus $pulseidxwarn)]
                    ]
                ]
            ]
        ]
        uiimage "textures/hud/healthbg" $ui_hud_healthhud 0 $ui_hud_mtrm $ui_hud_mtrf [
            uiclamp 1 1 1 1
            if $uishadows [uiimageshadow $ui_menu_s]
            if (>f $ui_hud_healthpart 0) [
                ui_hud_healthsize = $ui_hud_mtrf
                if (<f $ui_hud_healthpart 1) [
                    ui_hud_healthsec = (+f (*f (-f $ui_hud_healthbottom $ui_hud_healthtop) $ui_hud_healthpart) (-f 1 $ui_hud_healthbottom))
                    ui_hud_healthsize = (*f $ui_hud_healthsec $ui_hud_mtrf)
                ]
                uiclip $ui_hud_mtrm $ui_hud_healthsize 0 (-f $ui_hud_mtrf $ui_hud_healthsize) [
                    uialign 0 1
                    uiimage "textures/hud/health" $ui_hud_healthcol1 0 $ui_hud_mtrm $ui_hud_mtrf [
                        uialign 0 1
                        looplist ui_hud_healthcol [@ui_hud_healthcol2 @ui_hud_healthcol3] [uicolouradd $ui_hud_healthcol]
                        if [@@ui_hud_healthregen] [
                            ui_hud_healthregenlast = (- (getmillis) [@@ui_hud_healthregen])
                            if (<= $ui_hud_healthregenlast $regentime) [ uicolourrotate (divf $ui_hud_healthregenlast $regentime) ]
                        ]
                    ]
                ]
            ]
            uispace 0.016 0.0375 [
                uialign 0 -1
                uitext $ui_hud_healthtext $ui_textlarge [
                    uialign 0 0
                    uitextlimit 1
                    uicolourset $ui_hud_healthcol
                ]
            ]
        ]
    ]
]

ui_hud_camera = [
    uivlist 0 [
        uialign -1 1
        uiimage (? (>= $currentcam 0) $cameratex $spectatortex) $ui_hud_colour 0 $ui_hud_mtrm $ui_hud_mtrm [
            uialign -1 1
        ]
        uitext (? (>= $currentcam 0) (concat "CAM" (+ $currentcam 1)) "SPEC") $ui_textlarge [
            uitextlimit 1
            uialign 0 0
        ]
    ]
]

ui_hud_dead = [
    uivlist 0 [
        uialign -1 1
        uiimage $deadtex $ui_hud_colour 0 $ui_hud_mtrm $ui_hud_mtrm [
            uialign -1 1
        ]
        uitext "DEAD" $ui_textlarge [
            uitextlimit 1
            uialign 0 1
        ]
    ]
]

ui_hud_leftbottom = [
    uihlist $ui_padbutton [
        uialign -1 1
        caseif (= $ui_hud_state $stateidxalive) [
            ui_hud_healthbar
        ] (= $ui_hud_state $stateidxdead) [
            ui_hud_dead
        ] (|| (= $ui_hud_state $stateidxwaiting) (= $ui_hud_state $stateidxspectator)) [
            ui_hud_camera
        ]
        uifill 0.025
        ui_gameui_chat
    ]
]

ui_hud_captureinventory = [
    loopcapture 0 0 ui_hud_flag [
        ui_hud_flagteam = (getcaptureteam $ui_hud_flag)
        ui_hud_flagname = (at $teamnames $ui_hud_flagteam)
        ui_hud_flagcol = $[team@[ui_hud_flagname]colour]
        ui_hud_flaghud = $ui_hud_colour
        ui_hud_flagdrop = (getcapturedroptime $ui_hud_flag)
        ui_hud_flagtake = (getcapturetaketime $ui_hud_flag)
        ui_hud_flagowner = (getcaptureowner $ui_hud_flag)
        //ui_hud_flagpart = 1.0
        caseif (>= $ui_hud_flagowner 0) [
            ui_hud_flagoffs = (- (getmillis) $ui_hud_flagtake)
            //ui_hud_flagpart = 1.0
            ui_hud_flagownerteam = (getclientteam $ui_hud_flagowner)
            ui_hud_flagownername = (at $teamnames $ui_hud_flagownerteam)
            caseif (= $ui_hud_flagowner $ui_hud_focus) [
                ui_hud_flaghud = (getclientrescolour $ui_hud_focus $pulseidxdisco)
            ] (&& (= $ui_hud_team $ui_hud_flagteam) (!= $ui_hud_flagownerteam $ui_hud_flagteam)) [
                ui_hud_flaghud = (getclientrescolour $ui_hud_focus $pulseidxwarn)
            ]
        ] $ui_hud_flagdrop [
            ui_hud_flagoffs = (- (getmillis) $ui_hud_flagdrop)
            //ui_hud_flagpart = (-f 1 (divf $ui_hud_flagoffs (getcapturedelay)))
        ] () [
            ui_hud_flagcol = (modcolour $ui_hud_flagcol 0.25)
            ui_hud_flaghud = (modcolour $ui_hud_flaghud 0.25)
        ]
        uihlist $ui_padsmall [
            uialign 1 1
            uiimage "textures/hud/inventory" $ui_hud_flaghud 0 $ui_hud_mtrm $ui_hud_mtrm [
                uialign 1 1
                uispace $ui_padnormal $ui_padnormal [
                    uiclamp 1 1 1 1
                    uiimage $flagtex $ui_hud_flagcol 0 $ui_hud_mtr2 $ui_hud_mtr2 [
                        uialign 0 0
                        uiimageshadow $ui_hud_shadow $ui_hud_shadowcol
                    ]
                    caseif (>= $ui_hud_flagowner 0) [
                        uiimage $flagtakentex $[team@[ui_hud_flagownername]colour] 0 $ui_hud_mtrq $ui_hud_mtrq [uialign 1 1; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]
                    ] (> $ui_hud_flagdrop 0) [
                        uiimage $flagdroptex $colourcyan 0 $ui_hud_mtrq $ui_hud_mtrq [uialign 1 1; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]
                    ] () [uiimage $pointtex $ui_hud_flagcol 0 $ui_hud_mtrq $ui_hud_mtrq [uialign 1 1; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]]
                ]
            ]
        ]
    ]
]

ui_hud_defendinventory = [
    loopdefend 0 0 ui_hud_point [
        //ui_hud_pointname = (getdefendname $ui_hud_point)
        ui_hud_pointowner = (getdefendowner $ui_hud_point)
        ui_hud_pointname1 = (at $teamnames $ui_hud_pointowner)
        ui_hud_pointenemy = (getdefendenemy $ui_hud_point)
        ui_hud_pointname2 = (at $teamnames $ui_hud_pointenemy)
        ui_hud_pointcol = $[team@[ui_hud_pointname1]colour]
        ui_hud_pointhud = $ui_hud_colour
        //ui_hud_pointcol1 = (modcolour $ui_hud_pointcol 0.75)
        //ui_hud_pointcol2 = (? (&& (= $ui_hud_pointowner $teamidxneutral) (!= $ui_hud_pointenemy $teamidxneutral)) $[team@[ui_hud_pointname2]colour] $[team@[ui_hud_pointname1]colour])
        //ui_hud_pointcol3 = (modcolour $ui_hud_pointcol2 0.5)
        //ui_hud_pointcol2 = (modcolour $ui_hud_pointcol 0.5)
        //ui_hud_pointowners = (getdefendowners $ui_hud_point)
        //ui_hud_pointenemies = (getdefendenemies $ui_hud_point)
        //ui_hud_pointconverted = (getdefendconverted $ui_hud_point)
        //ui_hud_pointoccupied = (getdefendoccupied $ui_hud_point)
        ui_hud_pointinside = (getdefendinside $ui_hud_point $ui_hud_focus 1)
        //ui_hud_pointpart = 1.0
        caseif $ui_hud_pointenemy [
            //ui_hud_pointpart = $ui_hud_pointoccupied
            if (&& $ui_hud_pointinside $ui_hud_pointenemy (= $ui_hud_pointenemy $ui_hud_team)) [
                ui_hud_pointhud = (getclientrescolour $ui_hud_focus $pulseidxdisco)
            ] [
                ui_hud_pointhud = (getclientrescolour $ui_hud_focus $pulseidxwarn)
            ]
        ]
        uihlist $ui_padsmall [
            uialign 1 1
            uiimage "textures/hud/inventory" $ui_hud_pointhud 0 $ui_hud_mtrm $ui_hud_mtrm [
                uialign 1 1
                uispace $ui_padnormal $ui_padnormal [
                    uiclamp 1 1 1 1
                    uiimage $pointtex $ui_hud_pointcol 0 $ui_hud_mtr3 $ui_hud_mtr3 [
                        uialign 0 0
                        uiimageshadow $ui_hud_shadow $ui_hud_shadowcol
                    ]
                    caseif $ui_hud_pointenemy [
                        uiimage $attacktex $[team@[ui_hud_pointname2]colour] 0 $ui_hud_mtrq $ui_hud_mtrq [uialign 1 1; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]
                    ] $ui_hud_pointowner [
                        uiimage $[team@[ui_hud_pointname1]tex] $[team@[ui_hud_pointname1]colour] 0 $ui_hud_mtrq $ui_hud_mtrq [uialign 1 1; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]
                    ] () [uiimage $[teamneutraltex] $[teamneutralcolour] 0 $ui_hud_mtrq $ui_hud_mtrq [uialign 1 1; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]]
                ]
            ]
        ]
    ]
]

ui_hud_bomberinventory = [
    loopbomber 0 0 ui_hud_bomb [
        if (&& (getbomberenabled $ui_hud_bomb) (= (getbomberteam $ui_hud_bomb) $teamidxneutral)) [
            //ui_hud_bombteam = (getbomberteam $ui_hud_bomb)
            ui_hud_bombcol = (getclientrescolour $ui_hud_focus $pulseidxdisco)
            ui_hud_bombhud = $ui_hud_colour
            //ui_hud_bombcol1 = (modcolour $ui_hud_bombcol 0.75)
            //ui_hud_bombcol2 = (modcolour $ui_hud_bombcol 0.5)
            ui_hud_bombdrop = (getbomberdroptime $ui_hud_bomb)
            ui_hud_bombtake = (getbombertaketime $ui_hud_bomb)
            ui_hud_bombowner = (getbomberowner $ui_hud_bomb)
            //ui_hud_bomblast = (getbomberlastowner $ui_hud_bomb)
            //ui_hud_bombtarget = (getbombertarget $ui_hud_bomb)
            //ui_hud_bombpart = 1.0
            caseif (>= $ui_hud_bombowner 0) [
                //ui_hud_bomboffs = (- (getmillis) $ui_hud_bombtake)
                //ui_hud_bombpart = (-f 1 (divf $ui_hud_bomboffs $bombercarrytime))
                ui_hud_bombownerteam = (getclientteam $ui_hud_bombowner)
                ui_hud_bombownername = (at $teamnames $ui_hud_bombownerteam)
                if (= $ui_hud_bombowner $ui_hud_focus) [
                    ui_hud_bombhud = (getclientrescolour $ui_hud_focus $pulseidxdisco)
                ] [
                    ui_hud_bombhud = (getclientrescolour $ui_hud_focus $pulseidxwarn)
                ]
            ] $ui_hud_bombdrop [
                //ui_hud_bomboffs = (- (getmillis) $ui_hud_bombdrop)
                //ui_hud_bombpart = (-f 1 (divf $ui_hud_bomboffs $bomberresetdelay))
                //ui_hud_bombhud = (getclientrescolour $ui_hud_focus $pulseidxflash)
            ] () [
                ui_hud_bombhud = (modcolour $ui_hud_bombhud 0.25)
            ]
            uihlist $ui_padsmall [
                uialign 1 1
                uiimage "textures/hud/inventory" $ui_hud_bombhud 0 $ui_hud_mtrm $ui_hud_mtrm [
                    uialign 1 1
                    uispace $ui_padnormal $ui_padnormal [
                        uiclamp 1 1 1 1
                        uiimage $bombtex $ui_hud_bombcol 0 $ui_hud_mtr3 $ui_hud_mtr3 [
                            uialign 0 0
                            uiimageshadow $ui_hud_shadow $ui_hud_shadowcol
                        ]
                        caseif (>= $ui_hud_bombowner 0) [
                            uiimage $bombtakentex $[team@[ui_hud_bombownername]colour] 0 $ui_hud_mtrq $ui_hud_mtrq [uialign 1 1; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]
                        ] (> $ui_hud_bombdrop 0) [
                            uiimage $bombdroptex $colourcyan 0 $ui_hud_mtrq $ui_hud_mtrq [uialign 1 1; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]
                        ] () [uiimage $pointtex $[teamneutralcolour] 0 $ui_hud_mtrq $ui_hud_mtrq [uialign 1 1; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_raceinventory = [
    if (|| (= $ui_hud_team $teamidxalpha) (! (& (mutators) $mutsbitgsp3))) [
        ui_hud_racetime = (getclientcptime $ui_hud_focus)
        ui_hud_racemillis = (getclientcpmillis $ui_hud_focus)
        ui_hud_racelast = (getclientcplast $ui_hud_focus)
        ui_hud_racepoints = (getclientpoints $ui_hud_focus)

        uihlist $ui_padsmall [
            uialign 1 1
            uivlist 0 [
                uialign 1 -1
                uitext (timestr $ui_hud_racetime $scoreracestyle) $ui_texttiny [
                    uialign 1 -1
                    uicolourset $colourcyan
                ]
                uitext (timestr $ui_hud_racemillis $scoreracestyle) $ui_textsmall [
                    uialign 1 -1
                    uicolourset (? $ui_hud_racemillis $colourgreen $colouryellow)
                ]
                uitext (timestr $ui_hud_racelast $scoreracestyle) $ui_texttiny [
                    uialign 1 -1
                ]
            ]
            uiimage "textures/hud/inventory" $ui_hud_colour 0 $ui_hud_mtrm $ui_hud_mtrm [
                uialign 1 1
                uispace $ui_padnormal $ui_padnormal [
                    uiclamp 1 1 1 1
                    uitext $ui_hud_racepoints $ui_textlarge [uitextlimit 1; uialign 0 0]
                    uiimage $moderacetex (? $ui_hud_racemillis $colourgreen $colouryellow) 0 $ui_hud_mtrq $ui_hud_mtrq [uialign 1 1; uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]
                ]
            ]
        ]
        //if (getclientpoints (getclientnum)) [
        //    showui challenges
        //]
    ]
]

ui_hud_showfps = [
    if $showfps [
        uivlist 0 [
            doif (>= $showfps 1) [
                uitext (concat (getenginestat 11) "fps") $ui_text [uitextlimit 1]
            ] (= $showfps 2) [
                uitext (concatword "+" (getenginestat 12) "-" (getenginestat 13)) $ui_textsmall [uitextlimit 1]
            ] (= $showfps 3) [
                uitext (concatword (- (getenginestat 11) (getenginestat 13)) "-" (+ (getenginestat 11) (getenginestat 12)) ) $ui_textsmall [uitextlimit 1]
            ] (= $showfps 4) [
                uitext (concatword "Avg: " (div 1000 (getenginestat 11)) "ms" ) $ui_textsmaller
                uitext (concatword "Max: " (div 1000 (- (getenginestat 11) (getenginestat 13)) ) "ms" ) $ui_textsmaller
            ]
        ]
    ]
]

ui_hud_timers = [
    ui_hud_timers_type = (updatetimers)
    if $ui_hud_timers_type [
        uivlist 0 [
            if $frametimer [
                uispace $ui_padtiny $ui_padtiny [
                    uivlist 0 [
                        uitext "Frame Time" $ui_texttiny [uicolourset $colouryellow]
                        uitext (concat (gettimer -1 1) "ms") $ui_texttiny [uitextlimit 1]
                    ]
                ]
            ]
            if $timer [
                ui_hud_timers_count = (gettimer -1 -1)
                loop i $ui_hud_timers_count [
                    if (gettimer $i -2) [] [
                        uispace $ui_padnormal $ui_padtiny [
                            uivlist 0 [
                                uitext (gettimer $i 0) $ui_texttiny [uicolourset $colouryellow]
                                uitext (concat (precf (gettimer $i 6) 2) "ms" (? (gettimer $i 1) "(gpu)" "(cpu)")) $ui_texttiny
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_weight = [
    uivlist 0 [
        uitext "WEIGHT" $ui_text [uitextlimit 1; uicolourset $colouryellow]
        uitext (getclientweight $ui_hud_focus) $ui_textlarge [uitextlimit 1]
    ]
]

ui_hud_velocity = [
    uivlist 0 [
        uitext "SPEED" $ui_text [uitextlimit 1; uicolourset $colourgreen]
        uitext (getenginestat 20) $ui_textlarge [uitextlimit 1]
    ]
]

ui_hud_rightbottom = [
    uivlist $ui_padsmall [
        uialign 1 1
        uivlist $ui_padsmall [
            uialign 1 1
            case (gamemode) $modeidxcapture [
                ui_hud_captureinventory
            ] $modeidxdefend [
                ui_hud_defendinventory
            ] $modeidxbomber [
                ui_hud_bomberinventory
            ] $modeidxrace [
                if (= $ui_hud_state $stateidxalive) [
                    ui_hud_raceinventory
                ]
            ]
        ]
        uivlist $ui_padtiny [
            uialign 1 1
            ui_hud_timers
        ]
        case $ui_hud_state $stateidxalive [
            uihlist $ui_padtiny [
                uialign 1 1
                if $showweight [uivlist 0 [ui_hud_weight]]
                if $showvelocity [uivlist 0 [ui_hud_velocity]]
            ]
        ]
        if $showminimap [ui_hud_minimap]
        uivlist $ui_padtiny [
            uialign 1 1
            ui_hud_rightinventory
        ]
    ]
]

ui_hud_weapbar = [
    ui_hud_weapidx = (at $weapname $arg1)
    ui_hud_weapstate = (getclientweapstate $ui_hud_focus $arg1)
    ui_hud_weapammo = (max (getclientweapammo $ui_hud_focus $arg1) 0)
    ui_hud_weapstore = (max (getclientweapstore $ui_hud_focus $arg1) 0)
    ui_hud_weapval = (? (> $[@[ui_hud_weapidx]ammostore] 0) $[@[ui_hud_weapidx]ammostore] 0)
    ui_hud_weapmax = (+ $ui_hud_weapval $[@[ui_hud_weapidx]ammoclip])
    ui_hud_weapcol = $[@[ui_hud_weapidx]colour]
    ui_hud_weaphud = $ui_hud_colour
    ui_hud_weaptext = $ui_default
    ui_hud_weappart = (divf (+ $ui_hud_weapstore $ui_hud_weapammo) $ui_hud_weapmax)
    if (&& (> $[@[ui_hud_weapidx]ammoitem] 1) (<=f $ui_hud_weappart 0.25) (!= $[@[ui_hud_weapidx]ammostore] -1)) [
        ui_hud_weaphud = (modcolour (getclientrescolour $ui_hud_focus $pulseidxwarn) 0.75)
        ui_hud_weaptext = (getclientrescolour $ui_hud_focus $pulseidxwarn)
    ]
    if (!= $arg1 $arg2) [
        ui_hud_weapcol = (modcolour $ui_hud_weapcol 0.25)
        ui_hud_weaphud = (modcolour $ui_hud_weaphud 0.25)
        ui_hud_weaptext = (modcolour $ui_hud_weaptext 0.25)
    ]
    ui_hud_weaptext2 = (modcolour $ui_hud_weaptext 0.25)
    uihlist $ui_padsmall [
        uialign 1 1
        uivlist 0 [
            uialign 1 -1
            if (&& (= $[@[ui_hud_weapidx]ammosub1] 0) (= $[@[ui_hud_weapidx]ammosub2] 0)) [
                uitext "-" $ui_textlarge [
                    uialign 1 -1
                    uicolourset $ui_hud_weaptext
                ]
            ] [
                uitext $ui_hud_weapammo $ui_textlarge [
                    uialign 1 -1
                    uicolourset $ui_hud_weaptext
                ]
                if (|| (= $[@[ui_hud_weapidx]ammostore] 0) (= $[@[ui_hud_weapidx]ammostore] -1)) [
                    uitext "-" $ui_textsmall [
                        uialign 1 -1
                        uicolourset $ui_hud_weaptext2
                    ]
                ] [
                    uitext $ui_hud_weapstore $ui_textsmall [
                        uialign 1 -1
                        uicolourset $ui_hud_weaptext2
                    ]
                ]
            ]
        ]
        uiimage "textures/hud/inventory" $ui_hud_weaphud 0 $ui_hud_mtrm $ui_hud_mtrm [
            uialign 1 1
            uispace $ui_padnormal $ui_padnormal [
                uiclamp 1 1 1 1
                uiimage $[@[ui_hud_weapidx]tex] $ui_hud_weapcol 0 $ui_hud_mtr2 $ui_hud_mtr2 [
                    uialign 0 0
                    uiimageshadow $ui_hud_shadow $ui_hud_shadowcol
                ]
            ]
        ]
    ]
]

ui_hud_weaponinventory = [
    ui_hud_weapselect = (getclientweapselect $ui_hud_focus)
    case $weaponhud 2 [
        loopinventoryrev $ui_hud_focus 0 0 ui_hud_weapon [ui_hud_weapbar $ui_hud_weapon $ui_hud_weapselect]
    ] 1 [
        ui_hud_weapbar $ui_hud_weapselect $ui_hud_weapselect
    ]
]

ui_hud_rightinventory = [
    case $ui_hud_state $stateidxalive [
        ui_hud_weaponinventory
    ]
]

ui_hud_timestate = [
    uivlist 0 [
        uifill $ui_hud_mtrz 0
        uialign 1 -1
        caseif $gamepaused [
            uitext Paused $ui_textlarge [uitextlimit 1; uicolourset $colourorange; uialign 0 0]
        ] (= (gamemode) $modeidxediting) [
            uitext Editing $ui_textlarge [uitextlimit 1; uicolourset $colourgreen; uialign 0 0]
            uitext (? (= $mapvariant 2) "Alternate" "Normal") $ui_text [uitextlimit 1; uicolourset $colouryellow; uialign 0 0]
        ] () [
            ui_hud_timeremain = (getgametimeremain)
            ui_hud_timeelapsed = (getgametimeelapsed)
            ui_hud_timecolour = (at $ui_hud_timecols (getgamestate))
            if (getgametimelimit) [
                uitext (getgamestatestr 1) $ui_text [uitextlimit 1; uicolourset $[colour@[ui_hud_timecolour]]; uialign 0 0]
                uitext (timestr $ui_hud_timeremain $showtimestyle) $ui_textlarge [uitextlimit 1; uicolourset (? (|| (!= (getgamestate) 5) (< $ui_hud_timeremain 60000)) (getclientrescolour $ui_hud_focus $pulseidxwarn) $ui_default); uialign 0 0]
            ] [
                uitext (getgamestatestr 1) $ui_textlarge [uitextlimit 1; uicolourset $[colour@[ui_hud_timecolour]]; uialign 0 0]
            ]
            if $ui_hud_timeelapsed [uitext (timestr $ui_hud_timeelapsed $showtimestyle) $ui_textsmall [uitextlimit 1; uialign 0 0]]
        ]
        if $showrealtime [
            uitext (substr (gettime) 11 5) $ui_text [uitextlimit 1; uialign 0 0]
        ]
    ]
]

// 1:dist 2:offset 3:border 4:w 5:h 6:blip 7:smallblip 8:playernames 9:distblend 10:compass
ui_hud_compassdirs = [0 90 180 270]
ui_hud_compassnames = [N E S W]
ui_hud_radar = [
    uiradar $arg1 $arg2 $arg3 $arg4 $arg5 [
        if $arg10 [
            loop ui_hud_compass 4 [
                ui_hud_compassdir = (getcameraoffyaw (at $ui_hud_compassdirs $ui_hud_compass))
                ui_hud_compassname = (at $ui_hud_compassnames $ui_hud_compass)
                uiradarblip $bliptex 0xFF000000 $ui_hud_compassdir $ui_hud_compassdir $arg1 (*f $arg6 1.2) (*f $arg6 1.2) [
                    uivlist 0 [uitext $ui_hud_compassname $ui_textradar]
                ]
            ]
        ]
        loopclientsif 0 0 ui_hud_radarclient [getclientradarallow $ui_hud_radarclient] [
            ui_hud_radarclientdir = (getclientradardir $ui_hud_radarclient)
            ui_hud_radarclientyaw = (getclientradaryaw $ui_hud_radarclient)
            ui_hud_radarclientvel = (getclientvelocity $ui_hud_radarclient)
            ui_hud_radarclientdist = (getclientradardist $ui_hud_radarclient)
            ui_hud_radarclientstate = (getclientstate $ui_hud_radarclient)
            ui_hud_radarclientdominated = (&& (= $ui_hud_radarclientstate 0) (getclientisdominated $ui_hud_radarclient $ui_hud_focus))
            ui_hud_radarclientpcolour = (getclientcolour $ui_hud_radarclient -1 2)
            if $ui_hud_radarclientdominated [
                ui_hud_radarclientcolour = (getclientrescolour $ui_hud_focus $pulseidxdisco)
                uiradarblip $playerbliptex (| 0x80000000 $ui_hud_radarclientpcolour) $ui_hud_radarclientdir $ui_hud_radarclientyaw $ui_hud_radarclientdist $arg6 $arg6 [
                    uiimage $dominatedtex $ui_hud_radarclientcolour 0 $arg6 $arg6 [uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]
                    if $arg8 [uivlist 0 [uispace 0 $arg7; uitext (getclientname $ui_hud_radarclient 0 0) $ui_textradar [uicolourset $ui_hud_radarclientcolour]]]
                ]
            ] [
                ui_hud_radarclientblend = (? (= $ui_hud_radarclientstate 0) 255 160)
                if $ui_hud_radarclientblend [
                    if (>f $arg9 0) [
                        if (>f $ui_hud_radarclientdist $arg9) [
                            ui_hud_radarclientblend = 0
                        ] [
                            ui_hud_radarclientblend = (clamp (*f $ui_hud_radarclientblend (-f 1 (divf $ui_hud_radarclientdist $arg9))) 1 255)
                        ]
                    ]
                    if $ui_hud_radarclientblend [
                        ui_hud_radarclientteam = (getclientteam $ui_hud_radarclient)
                        ui_hud_radarclienttcolour = $[team@(at $teamnames $ui_hud_radarclientteam)colour]
                        ui_hud_radarclientblend2 = (clamp (*f $ui_hud_radarclientblend 0.75) 1 255)
                        ui_hud_radarclientrcolour = (getclientresidualfx $ui_hud_radarclient 1000)
                        if (>= $ui_hud_radarclientrcolour 0) [ ui_hud_radarclientpcolour = $ui_hud_radarclientrcolour ]
                        uiradarblip $[@(at $ui_hud_statetex2 $ui_hud_radarclientstate)] (? (= $ui_hud_radarclientstate 0) (| (<< $ui_hud_radarclientblend2 24) $ui_hud_radarclientpcolour) (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclienttcolour)) $ui_hud_radarclientdir (? (= $ui_hud_radarclientstate 0) $ui_hud_radarclientyaw $ui_hud_radarclientdir) $ui_hud_radarclientdist $arg6 $arg6 [
                            if (= $ui_hud_radarclientstate 0) [uiimage $bliptex (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclienttcolour) 0 $arg7 $arg7 [uiimageshadow $ui_hud_shadow $ui_hud_shadowcol]]
                            if $arg8 [uivlist 0 [uispace 0 $arg7; uitext (getclientname $ui_hud_radarclient 0 0) $ui_textradar [uicolourset (| (<< $ui_hud_radarclientblend 24) $ui_hud_radarclienttcolour)]]]
                        ]
                    ]
                ]
            ]
        ]
        case (gamemode) $modeidxcapture [
            loopcaptureif 0 0 ui_hud_radarcapture [getcaptureradarallow $ui_hud_radarcapture] [
                ui_hud_radarcapturedir = (getcaptureradardir $ui_hud_radarcapture)
                ui_hud_radarcapturedist = (getcaptureradardist $ui_hud_radarcapture)
                ui_hud_radarcaptureteam = (getcaptureteam $ui_hud_radarcapture)
                ui_hud_radarcapturetcolour = $[team@(at $teamnames $ui_hud_radarcaptureteam)colour]
                ui_hud_radarcaptureowner = (getcaptureowner $ui_hud_radarcapture)
                ui_hud_radarcapturedrop = (getcapturedroptime $ui_hud_radarcapture)
                ui_hud_radarcapturetex = ""
                ui_hud_radarcapturesize = $arg6
                doif (&& $ui_hud_carryaffinity (= $ui_hud_radarcaptureteam $ui_hud_team)) [
                    if (|| (< $ui_hud_radarcaptureowner 0) (= $ui_hud_radarcaptureowner $ui_hud_focus)) [
                        ui_hud_radarcapturedir = (getcaptureradardir $ui_hud_radarcapture 1)
                        ui_hud_radarcapturedist = (getcaptureradardist $ui_hud_radarcapture 1)
                    ]
                    ui_hud_radarcapturetcolour = (getclientrescolour $ui_hud_focus $pulseidxdisco)
                    ui_hud_radarcapturetex = $arrowtex
                    ui_hud_radarcapturesize = (*f $arg6 1.5)
                ] (|| $ui_hud_radarcapturedrop (&& (>= $ui_hud_radarcaptureowner 0) (= $ui_hud_radarcaptureteam $ui_hud_team) (!= $ui_hud_radarcaptureowner $ui_hud_focus))) [
                    ui_hud_radarcapturetex = $flagtex
                    ui_hud_radarcapturetcolour = (getclientrescolour $ui_hud_focus $pulseidxdisco)
                ] (!= $ui_hud_radarcaptureowner $ui_hud_focus) [
                    ui_hud_radarcapturetex = $flagtex
                ]
                if (!=s $ui_hud_radarcapturetex "") [
                    uiradarblip $ui_hud_radarcapturetex $ui_hud_radarcapturetcolour $ui_hud_radarcapturedir $ui_hud_radarcapturedir $ui_hud_radarcapturedist $ui_hud_radarcapturesize $ui_hud_radarcapturesize [
                        if (>= $ui_hud_radarcaptureowner 0) [uialign 0 1]
                        uivlist 0 [uispace 0 (*f $arg7 1.5); uitext $[team@(at $teamnames $ui_hud_radarcaptureteam)name] $ui_textradar [uicolourset (| $ui_transor $ui_hud_radarcapturetcolour)]]
                    ]
                ]
            ]
        ] $modeidxdefend [
            loopdefendif 0 0 ui_hud_radardefend [getdefendradarallow $ui_hud_radardefend] [
                ui_hud_radardefenddir = (getdefendradardir $ui_hud_radardefend)
                ui_hud_radardefenddist = (getdefendradardist $ui_hud_radardefend)
                ui_hud_radardefendteam = (getdefendowner $ui_hud_radardefend)
                ui_hud_radardefendtcolour = $[team@(at $teamnames $ui_hud_radardefendteam)colour]
                ui_hud_radardefendname = (getdefendname $ui_hud_radardefend)
                uiradarblip $pointtex $ui_hud_radardefendtcolour $ui_hud_radardefenddir $ui_hud_radardefenddir $ui_hud_radardefenddist $arg6 $arg6 [
                    uivlist 0 [uispace 0 (*f $arg7 2); uitext $ui_hud_radardefendname $ui_textradar [uialign 0 1; uitextwrap 0.04; uicolourset (| $ui_transor $ui_hud_radardefendtcolour)]]
                ]
            ]
        ] $modeidxbomber [
            loopbomberif 0 0 ui_hud_radarbomber [getbomberradarallow $ui_hud_radarbomber] [
                ui_hud_radarbomberdir = (getbomberradardir $ui_hud_radarbomber)
                ui_hud_radarbomberteam = (getbomberteam $ui_hud_radarbomber)
                ui_hud_radarbomberowner = (getbomberowner $ui_hud_radarbomber)
                ui_hud_radarbombertex = ""
                ui_hud_radarbombername = ""
                ui_hud_radarbombersize = $arg6
                doif (!= $ui_hud_radarbomberteam $teamidxneutral) [
                    if (&& $ui_hud_carryaffinity (!= $ui_hud_radarbomberteam $ui_hud_team)) [
                        ui_hud_radarbombertex = $arrowtex
                        ui_hud_radarbombername = "Destroy"
                        ui_hud_radarbombersize = (*f $arg6 1.5)
                    ]
                ] (&& (= $ui_hud_radarbomberteam $teamidxneutral) (!= $ui_hud_radarbomberowner $ui_hud_focus)) [
                    ui_hud_radarbombertex = $bombtex
                    ui_hud_radarbombername = "Bomb"
                ]
                if (!=s $ui_hud_radarbombertex "") [
                    ui_hud_radarbomberdist = (getbomberradardist $ui_hud_radarbomber)
                    uiradarblip $ui_hud_radarbombertex (getclientrescolour $ui_hud_focus $pulseidxdisco) $ui_hud_radarbomberdir $ui_hud_radarbomberdir $ui_hud_radarbomberdist $ui_hud_radarbombersize $ui_hud_radarbombersize [
                        if (>= $ui_hud_radarbomberowner 0) [uialign 0 1]
                        if (!=s $ui_hud_radarbombername "") [uivlist 0 [uispace 0 (*f $arg7 1.5); uitext $ui_hud_radarbombername $ui_textradar [uicolourset (| $ui_transor (getclientrescolour $ui_hud_focus $pulseidxdisco))]]]
                    ]
                ]
            ]
        ]
    ]
]

ui_hud_minimap = [
    uiminimap $radarskintex $ui_hud_colour $minimapdist 0.1 $ui_hud_sizeh $ui_hud_sizeh [
        ui_hud_radar $minimapdist (*f (divf 1 $ui_hud_sizeh) $minimapoffset) $minimapborder $ui_hud_sizeh $ui_hud_sizeh $minimapblipsize $minimapsmallblipsize $minimapplayernames $minimapdistblend $minimapcompass
    ]
]

ui_hud_midradar = [
    ui_hud_radar $radardist (*f (divf 1 $radarsize) (? $radaraspect (*f $radaroffset (uiaspect)) $radaroffset)) 0 (? $radaraspect (*f $radarsize (uiaspect)) $radarsize) $radarsize $radarblipsize $radarsmallblipsize $radarplayernames $radardistblend 0
]

ui_hud_lefttop = [
    uivlist $ui_padnormal [
        uialign -1 -1
        if (isediting) [
            uifill 0 $ui_toolbar_height
        ]
        if (|| $ui_hud_commandopen $showconsole) [ui_hud_showconsole]
    ]
]

ui_hud_righttop = [
    uivlist $ui_padsmall [
        uialign 1 -1
        ui_hud_timestate
        ui_hud_showfps
    ]
]

ui_hud_headsup = [
    if (! (isediting)) [
        if (isconnected) [
            uispace $ui_padhudh $ui_padhudv [
                uialign 1 -1
                uihlist 0 [
                    uialign 1 -1
                    uihlist $ui_padtiny [
                        uialign 1 -1
                        ui_hud_righttop
                    ]
                ]
            ]
            uispace $ui_padhudh $ui_padhudv [
                uialign -1 1
                uihlist $ui_padsmall [
                uialign -1 1
                    uivlist $ui_padsmall [
                        uialign -1 1
                        uivlist $ui_padtiny [
                            uialign -1 1
                            ui_hud_leftbottom
                        ]
                    ]
                ]
            ]
            uispace $ui_padhudh $ui_padhudv [
                uialign 1 1
                uivlist $ui_padsmall [
                    uialign 1 1
                    uivlist $ui_padtiny [
                        uialign 1 1
                        ui_hud_rightbottom
                    ]
                ]
            ]
            if (&& $showradar (= $ui_hud_state $stateidxalive)) [
                uispace $ui_padtiny $ui_padtiny [ui_hud_midradar]
            ]
        ] [
            ui_hud_topui = (topui)
        ]
    ]
    uispace $ui_padhudh $ui_padhudv [
        uiclamp 1 1
        uialign -1 -1
        uihlist 0 [
            uiclamp 1 1
            uialign -1 -1
            uihlist $ui_padtiny [
                uiclamp 1 1
                uialign -1 -1
                ui_hud_lefttop
            ]
        ]
    ]
    if (<f $ui_hud_blend 1) [ uichangeblends $ui_hud_blend ]
]

newui "hud" [
    uiclamp 1 1 1 1
    uiescrelease [uitoggle]
    ui_hud_init
    uiexclusive (? $ui_hud_commandopen 1 0)
    uiallowinput (? $ui_hud_input 2 (? (! (isconnected)) 1 0))
    if $ui_hud_showhud [
        ui_hud_headsup
        if (&& [isediting] [! (uivisible "main")]) [
            ui_hud_toolview
        ]
        if (!=f $hudblend 1) [uichangeblends $hudblend]
    ]
] [] [] 16


newui "progress" [
    uiclamp 1 1 1 1
    uiescrelease [uitoggle]
    uiexclusive 1
    uiallowinput 0
    ui_progress_type = (getprogresstype)
    ui_progress_title = (getprogresstitle)
    if $ui_progress_type [
        if (! (getprogresswait)) [uiexclusive 1]
        uiblurskin 0.3 0 $ui_menu_a [
            uispace $ui_padwin $ui_padwin [
                uivlist 0 [
                    if (getloadedmap) [
                        uitext (concatword "^f<bold>" (getmaptitle $mapname)) $ui_textlarge
                        if (!=s (getmapauthor $mapname) "") [
                            uitext (concat "by" (getmapauthor $mapname)) $ui_textbig
                        ]
                        uispace 0 $ui_padbutton
                        uitext (format "^f<bold>^fs^fy%1^fS" (gamename (gamemode) (mutators) 0 32)) $ui_text
                        ui_loading_modetexs = (modetexlist (gamemode) (mutators))
                        looplist ui_loading_modetex $ui_loading_modetexs [uiimage $ui_loading_modetex $ui_default 0 $ui_imgbut $ui_imgbut [uiimageshadow $ui_menu_s]]
                        uitext (modedesc (gamemode) (mutators) 5) $ui_textstatus
                        if (> (mutators) 0) [
                            loop i $mutsidxnum [
                                ui_loading_mut = (<< 1 $i)
                                if (&& [& (mutators) $ui_loading_mut] [! (& (mutsimplied (gamemode)) $ui_loading_mut)]) [
                                    ui_loading_muttxt = (mutsdesc (gamemode) $ui_loading_mut 4)
                                    if (!=s $ui_loading_muttxt "") [uitext (concatword "^fa" $ui_loading_muttxt) $ui_textstatus]
                                ]
                            ]
                        ]
                    ] [uitext "^f<bold>Loading.." $ui_textlarge]
                    if (isconnected) [
                        uispace 0 $ui_padbutton
                        if (!=s $connectname "") [
                            uitext (format "^faOn: ^fw%1:[%2]" $connectname $connectport) $ui_textstatus
                            if (!=s $serverdesc "") [uitext (format "^"^fs%1^fS^"" $serverdesc) $ui_textstatus]
                        ] [uitext (format "^faPlaying Offline" $connectname $connectport) $ui_textstatus]
                    ]
                ]
            ]
        ]
        uispace $ui_padwin $ui_padwin [
            uialign 0 1
            uioffset 0 -0.4 [
                uiblurskin 0 0 $ui_menu_a [
                    uispace $ui_padwin $ui_padnormal [uitext (tipshow) $ui_text]
                ]
            ]
        ]
    ] [uiexclusive 0]
    if (!=s $ui_progress_title "") [
        uispace $ui_padwin $ui_padwin [
            uialign -1 1
            uihlist 0 [
                uialign -1 1
                uiblurskin 0 0 $ui_menu_a [
                    uispace $ui_padsmall $ui_padsmall [
                        if (>f $progressamt 0) [
                            uihmetera $progressamt $ui_buttonzs $ui_buttonzz $colourdarkred $ui_menu $ui_disa [@colourred] [] -1
                        ] [
                            uihthroba (divf (mod (getmillis -1) 1000) 1000) $ui_buttonzs $ui_buttonzz $colourdarkred $ui_menu $ui_disa [@colourred]
                        ]
                    ]
                ]
                uispace $ui_padsmall
                uitext $ui_progress_title $ui_text [uitextalign -1; uialign -1 0]
            ]
        ]
    ]
] [] [] 16
