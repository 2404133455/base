deffvarp defendaboveblend $fvaridxnonzero 1 1

dynui defendabove [
    local defend_presence
    defend_presence = (getdefendnum $uiarg1)
    if (>= $defend_presence 0) [
        uiallowinput 0
        uimenu 0
        uizindex -1 // draw behind other stuff on this surface
        uistyle centertop

        local defend_opacity
        defend_opacity = 1.0

        local defend_owner defend_owners defend_enemy defend_enemies
        defend_owner = (getdefendowner $uiarg1)
        defend_owners = (getdefendowners $uiarg1)
        defend_enemy = (getdefendenemy $uiarg1)
        defend_enemies = (getdefendenemies $uiarg1)

        local defend_converted defend_points defend_occupied defend_name
        defend_converted = (getdefendconverted $uiarg1)
        defend_points = (getdefendpoints $uiarg1)
        defend_occupied = (getdefendoccupied $uiarg1)
        defend_name = (getdefendname $uiarg1)

        local defend_colour defend_text defend_disptime

        defend_colour = (getteamcolour $defend_owner)

        if $defend_owner [
            defend_text = (getteamname $defend_owner)
            if $defend_enemy [
                local defend_ecolour
                defend_ecolour = (getteamcolour $defend_enemy)
                defend_colour = (skewcolour $defend_colour $defend_ecolour (-f 1.0 $defend_occupied))
                defend_text = (format "%1 v %2" $defend_text (getteamname $defend_enemy))
            ]
        ] [
            if $defend_enemy [
                local defend_ecolour
                defend_ecolour = (getteamcolour $defend_enemy)
                defend_colour = (skewcolour $defend_colour $defend_ecolour (-f 1.0 $defend_occupied))
                defend_text = (getteamname $defend_enemy)
            ] [
                defend_text = (getteamname $defend_owner)
            ]
        ]
        local defend_bordercolour
        defend_bordercolour = (modcolour $defend_colour 0.25)

        uivlist 0.00125 [
            uistyle centermiddle
            uihlist 0 [
                if $defend_enemy [
                    uicolour (getteamcolour $defend_owner) (*f 0.02 (-f 1.0 $defend_occupied)) 0.002 [ uicolourblend 0.65 ]
                    uicolour (getteamcolour $defend_enemy) (*f 0.02 $defend_occupied) 0.002 [ uicolourblend 0.65 ]
                ] [
                    uicolour (getteamcolour $defend_owner) (*f 0.02 $defend_occupied) 0.002 [ uicolourblend 0.65 ]
                ]
            ]
            uivlist 0 [
                uiborderedimageclamped $skinshadowtex $defend_bordercolour 0 $ui_texborder $ui_screenborder 0 0 [
                    uicolourblend 0.5
                    uispace 0.003 0.003 [
                        uifont $textfontoutline [
                            uivlist 0 [
                                uitext $defend_name 0.5
                                uitext $defend_text 0.75
                                uitext (? $defend_enemy "CONVERTING" (? $defend_owner "OCCUPIED" "UNOCCUPIED")) 0.5
                            ]
                        ]
                    ]
                ]

                uiimage $pointshadowtex $defend_bordercolour 0 0.0075 0.0075
            ]
        ]

        uipropagate [ uicolourblend (*f $defendaboveblend $defend_opacity) ]
    ] [ hideui $uiname $uisurfacetype ]
]

dynui defendoverlay []