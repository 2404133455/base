deffvarp defendaboveblend $fvaridxnonzero 1 1

dynui defendabove [
    local _presence _opacity _owner _owners _enemy _enemies _converted _points _occupied _name _seq _colour _text _disptime _ecolour _bordercolour
    _presence = (getdefendnum $uiarg1)
    if (>= $_presence 0) [
        uiallowinput 0
        uimenu 0
        uizindex -1 // draw behind other stuff on this surface
        uistyle centertop

        _opacity = 1.0

        _owner = (getdefendowner $uiarg1)
        _owners = (getdefendowners $uiarg1)
        _enemy = (getdefendenemy $uiarg1)
        _enemies = (getdefendenemies $uiarg1)

        _converted = (getdefendconverted $uiarg1)
        _points = (getdefendpoints $uiarg1)
        _occupied = (getdefendoccupied $uiarg1)
        _name = (getdefendname $uiarg1)
        _seq = (getprintable $uiarg1)


        _colour = (getteamcolour $_owner)

        if $_owner [
            _text = (getteamname $_owner)
            if $_enemy [
                _ecolour = (getteamcolour $_enemy)
                _colour = (skewcolour $_colour $_ecolour (-f 1.0 $_occupied))
                _text = (format "%1 v %2" $_text (getteamname $_enemy))
            ]
        ] [
            if $_enemy [
                _ecolour = (getteamcolour $_enemy)
                _colour = (skewcolour $_colour $_ecolour (-f 1.0 $_occupied))
                _text = (getteamname $_enemy)
            ] [
                _text = (getteamname $_owner)
            ]
        ]
        _bordercolour = (modcolour $_colour 0.25)

        uivlist 0.00125 [
            uistyle centermiddle
            uihlist 0 [
                if $_enemy [
                    uicolour (getteamcolour $_owner) (*f 0.02 (-f 1.0 $_occupied)) 0.002 [ uicolourblend 0.65 ]
                    uicolour (getteamcolour $_enemy) (*f 0.02 $_occupied) 0.002 [ uicolourblend 0.65 ]
                ] [
                    uicolour (getteamcolour $_owner) (*f 0.02 $_occupied) 0.002 [ uicolourblend 0.65 ]
                ]
            ]
            uivlist 0 [
                uiborderedimageclamped $skinshadowtex $_bordercolour 0 $ui_texborder $ui_screenborder 0 0 [
                    uicolourblend 0.5
                    uispace 0.003 0.003 [
                        uifont $textfontoutline [
                            uivlist 0 [
                                uitext (concatword $_name " (^fs^fy" $_seq "^fS)") 0.5
                                uitext $_text 0.75
                                uitext (? $_enemy "CONVERTING" (? $_owner "OCCUPIED" "UNOCCUPIED")) 0.5
                            ]
                        ]
                    ]
                ]

                uiimage $pointshadowtex $_bordercolour 0 0.0075 0.0075
            ]
        ]

        uipropagate [ uicolourblend (*f $defendaboveblend $_opacity) ]
    ] [ hideui $uiname $uisurfacetype ]
]

dynui defendoverlay []