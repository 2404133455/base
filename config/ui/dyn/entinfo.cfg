defvar entinfouilevel 0 0 2
defvar entinfouinumber 0 1 1
deffvarp entinfouiblend 0 0.5 1.0
deffvarp entinfouiselblend 0 0.75 1.0
deffvarp entinfouihoverblend 0 1.0 1.0

dynui entinfo [
    local ent_type; ent_type = (getentity $uiarg1 0)
    if $ent_type [
        local ent_name; ent_name = (getentinfo $ent_type 1)
        local ent_attrs; ent_attrs = (getentity $uiarg1 1)
        local ent_attr1; ent_attr1 = (getentity $uiarg1 1 0)
        local ent_group; ent_group = (entgrouppos $uiarg1)
        local ent_hover; ent_hover = (enthoverpos $uiarg1)

        local ent_selected ent_hovering
        uihover [
            ent_selected = 1
            ent_hovering = 1
        ] [
            ent_selected = (>= $ent_group 0)
            ent_hovering = (= $ent_hover 0)
        ]

        uistyle centertop
        uimenu 0
        uiontop $ent_selected
        uiallowinput 1
        uiallowstate 1

        uizindex (? $ent_hovering $ent_hovering -1)

        local ent_full; ent_full = (> $entinfouilevel (? (|| $ent_selected $ent_hovering) 0 1))
        uivlist $ui_padtiny [
            uiborderedimageclamped $skinshadowtex 0xff000000 0 $ui_texborder $ui_screenborder 0 0 [
                uispace $ui_padwin $ui_padsmall [
                    uivlist $ui_padsmall [
                        uifont $textfontoutline [
                            uihlist $ui_padsmall [
                                uitext $ent_name 0.75
                                if $entinfouinumber [ uitext (concatword "(" $uiarg1 ")") 0.75 [uicolourset $colouryellow] ]
                            ]
                        ]
                        if $ent_full [
                            uicolour 0x80000000 0 0 [
                                uitable 0 0 [
                                    loop i $ent_attrs [
                                        local ent_aname; ent_aname = (getentattr $ent_type $i $ent_attr1)
                                        if (!=s $ent_aname "") [
                                            uitablerow [
                                                uispace $ui_padsmall $ui_padtiny [uitext (+ $i 1) 0.25]
                                                uispace $ui_padsmall $ui_padtiny [uitext $ent_aname 0.25]
                                                uispace $ui_padsmall $ui_padtiny [uitext (getentity $uiarg1 1 $i) 0.25]
                                            ]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]

            uiimage (getenttex $uiarg1) 0xFFFFFFFF 0 0.03 0.03
        ]

        local ent_blend; ent_blend = $entinfouiblend
        if $ent_selected [ ent_blend = (maxf $ent_blend $entinfouiselblend) ]
        if $ent_hovering [ ent_blend = (maxf $ent_blend $entinfouihoverblend) ]

        uipropagate [ uicolourblend $ent_blend ]
    ] [ hideui $uiname ]
]