dynui entinfo [
    local ent_type; ent_type = (getentity $uiparam 0)
    local ent_name; ent_name = (getentinfo $ent_type 1)
    local ent_attrs; ent_attrs = (getentity $uiparam 1)
    local ent_attr1; ent_attr1 = (getentity $uiparam 1 0)
    uialign 0 -1
    uiallowinput 0
    uiskinborder 0 0 $ui_menu_a $ui_menu_a $ui_menu_a $ui_line_a $ui_line_a $ui_line_a [
        uispace $ui_padsmall $ui_padsmall [
            uitable 0 0 [
                uitableheader [] [
                    uivlist 0 [
                        uispace $ui_padsmall $ui_padtiny [uitext (concatword "Entity #" $uiparam) 1.25 [uisetcolour 0xFFFF00]]
                        uispace $ui_padsmall $ui_padtiny [uitext $ent_name 1.125 [uisetcolour 0xFF8800]]
                    ]
                ]
                loop i $ent_attrs [
                    local ent_aname; ent_aname = (getentattr $ent_type $i $ent_attr1)
                    if (!=s $ent_aname "") [
                        uitablerow [
                            uispace $ui_padsmall $ui_padtiny [uitext (+ $i 1) 1.0]
                            uispace $ui_padsmall $ui_padtiny [uitext $ent_aname 1.0]
                            uispace $ui_padsmall $ui_padtiny [uitext (getentity $uiparam 1 $i) 1.0]
                        ]
                    ]
                ]
            ]
        ]
    ]
] [] [] 32