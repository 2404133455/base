deffvarp entinfouiblend 0 0.5 1.0
deffvarp entinfouihover 0 1.0 1.0

dynui entinfo [
    local ent_type; ent_type = (getentity $uiparam 0)
    if $ent_type [
        local ent_name; ent_name = (getentinfo $ent_type 1)
        local ent_attrs; ent_attrs = (getentity $uiparam 1)
        local ent_attr1; ent_attr1 = (getentity $uiparam 1 0)
        local ent_hover; ent_hover = (enthoverpos $uiparam)

        uistyle centertop
        uimenu 0
        uiontop 1
        uiallowinput 1
        uiallowstate 1
        local ent_hovering
        uihover [ ent_hovering = 1 ] [ ent_hovering = (= $ent_hover 0) ]

        uizindex $ent_hovering

        uiborderedimageclamped $skinalphatex 0xff000000 0 $ui_texborder $ui_screenborder 0 0 [
            uispace $ui_padsmall $ui_padsmall [
                uitable 0 0 [
                    uitableheader [] [
                        uivlist 0 [
                            uispace $ui_padsmall $ui_padtiny [uitext (concatword "Entity #" $uiparam) 1.25 [uicolourset 0xFFFF00]]
                            uispace $ui_padsmall $ui_padtiny [uitext $ent_name 1.125 [uicolourset 0xFF8800]]
                        ]
                    ]
                    loop i $ent_attrs [
                        local ent_aname; ent_aname = (getentattr $ent_type $i $ent_attr1)
                        if (!=s $ent_aname "") [
                            uitablerow [
                                uispace $ui_padsmall $ui_padtiny [uitext (+ $i 1) 1.0]
                                uispace $ui_padsmall $ui_padtiny [uitext $ent_aname 1.0]
                                uispace $ui_padsmall $ui_padtiny [uitext (getentity $uiparam 1 $i) 1.0]
                            ]
                        ]
                    ]
                ]
            ]
        ]
        uipropagate [ uicolourblend (? $ent_hovering $entinfouihover $entinfouiblend) ]
    ] [ hideui $uiname ]
]