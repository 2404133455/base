defvar entityabovelevel 0 0 2
defvar entityabovenumber 0 1 1
deffvarp entityaboveblend 0 0.5 1.0
deffvarp entityaboveselblend 0 0.75 1.0
deffvarp entityabovehoverblend 0 1.0 1.0

dynui entityabove [
    uistyle centertop
    uimenu 0
    uiallowinput 0

    local ent_type; ent_type = (getentity $uiarg1 0)
    if $ent_type [
        local ent_name; ent_name = (getentinfo $ent_type 1)
        local ent_attrs; ent_attrs = (getentity $uiarg1 1)
        local ent_attr1; ent_attr1 = (getentity $uiarg1 1 0)
        local ent_group; ent_group = (entgrouppos $uiarg1)
        local ent_hover; ent_hover = (enthoverpos $uiarg1)

        local ent_selected ent_hovering
        ent_selected = (>= $ent_group 0)
        ent_hovering = (= $ent_hover 0)

        uiontop (|| $ent_hovering $ent_selected)
        uizindex (? $ent_hovering -1 -2)

        local ent_full; ent_full = (> $entityabovelevel (? (|| $ent_selected $ent_hovering) 0 1))
        uivlist $ui_padtiny [
            uiborderedimageclamped (? $ent_selected $skinalphatex $skinshadowtex) 0xff000000 0 $ui_texborder $ui_screenborder 0 0 [
                uispace $ui_padnormal $ui_padsmall [
                    uivlist $ui_padsmall [

                        uihlist $ui_padsmall [
                            uiimage (? $ent_selected "textures/icons/star" "textures/icons/dot") 0xFFFFFFFF 0 0.01 0.01
                            uifont $textfontoutline [
                                uihlist $ui_padsmall [
                                    uitext $ent_name 0.65
                                    if $entityabovenumber [ uitext (concatword "(" $uiarg1 ")") 0.65 [uicolourset $colouryellow] ]
                                ]
                            ]
                        ]

                        if $ent_full [
                            uicolour 0x80000000 0 0 [
                                uitable 0 0 [
                                    loop i $ent_attrs [
                                        local ent_aname; ent_aname = (getentattr $ent_type $i $ent_attr1)
                                        if (!=s $ent_aname "") [
                                            uitablerow [
                                                uispace $ui_padsmall $ui_padtiny [uitext (+ $i 1) 0.25]
                                                uispace $ui_padsmall $ui_padtiny [uitext $ent_aname 0.25]
                                                uispace $ui_padsmall $ui_padtiny [uitext (getentity $uiarg1 1 $i) 0.25]
                                            ]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]

        local ent_blend; ent_blend = $entityaboveblend
        if $ent_selected [ ent_blend = (maxf $ent_blend $entityaboveselblend) ]
        if $ent_hovering [ ent_blend = (maxf $ent_blend $entityabovehoverblend) ]

        uipropagate [ uicolourblend $ent_blend ]
    ] [ hideui $uiname ]
]

deffvarp entityoverlayblend 0 0.5 1.0
deffvarp entityoverlayselblend 0 1.0 1.0
deffvarp entityoverlayhoverblend 0 1.0 1.0

dynui entityoverlay [
    uistyle centermiddle
    uimenu 0
    uiallowinput 0

    local ent_type; ent_type = (getentity $uiarg1 0)
    if $ent_type [
        local ent_group; ent_group = (entgrouppos $uiarg1)
        local ent_hover; ent_hover = (enthoverpos $uiarg1)

        local ent_selected ent_hovering ent_size
        ent_selected = (>= $ent_group 0)
        ent_hovering = (= $ent_hover 0)
        ent_size = (? (|| $ent_hovering $ent_selected) 0.025 0.015)

        uiontop (|| $ent_hovering $ent_selected)
        uizindex (? $ent_hovering -1 -2)

        uiimage (getenttex $uiarg1) 0xFFFFFFFF 0 $ent_size $ent_size [
            uiimageshadow 0.0005 0x000000
        ]

        local ent_blend; ent_blend = $entityoverlayblend
        if $ent_selected [ ent_blend = (maxf $ent_blend $entityoverlayselblend) ]
        if $ent_hovering [ ent_blend = (maxf $ent_blend $entityoverlayhoverblend) ]

        uipropagate [ uicolourblend $ent_blend ]
    ] [ hideui $uiname ]
]