defvar entityitemuihint 0 0 1
deffvar entityitemuiblend 0 0.75 1
deffvar entityitemuiinactive 0 0.25 1

ui_entityitem_dynui = [
    local _name _attr1 _enter _ready _colour

    _name = (getentinfo $_type 1)
    _attr1 = (getentity $_ent 1 0 1)

    _ready = (? (>= $_idx 0) (getclientactitemready $focusedplayer $_idx) 0)
    _name = (at $W_NAMES $_attr1)
    _colour = (? (= $_type 13) $[@[_name]colour] 0xFFFFFF)
    _enter = (getclientactitementer $focusedplayer $_idx)

    if $_ready [
        _blend = $entityitemuiblend
        uiontop (= $_idx 0)
        uizindex (? (= $_idx 0) -1 -2)
    ] [
        _blend = $entityitemuiinactive
        uiontop 0
        uizindex -3
    ]

    uihint (? $entityitemuihint $_enter 0)

    uivlist $ui_padsmaller [
        if $_ready [
            uiborderedimageclamped $skinshadowtex (modcolour $_colour 0.5) 0 $ui_texborder $ui_screenborder 0 0 [
                uispace $ui_padtiny $ui_padtiny [
                    uivlist $ui_padtiny [
                        if (getclientcanuse $focusedplayer $_ent) [
                            uitext "^f{=use}" 0.35
                        ]
                        uitext $[@[_name]longname] 0.15
                    ]
                ]
                uipropagate [ uicolourblend 0.5 ]
            ]
        ]
        uiimage (getenttex $_ent) $_colour 0 0.0125 0.0125
        uiimage $pointshadowtex $_colour 0 0.0075 0.0075
    ]
]

dynui entityitem [
    local _ent _idx _type _blend

    uistyle centertop
    uimenu 0
    uiallowinput 0

    _ent = $uiarg1
    _type = (getentity $_ent 0)
    _idx = (getclientactitemidx $focusedplayer $ACTITEM_ENT $_ent)

    ui_entityitem_dynui

    uipropagate [ uicolourblend $_blend ]
] [] [] [
    result (= [getclientactitemidx $focusedplayer $ACTITEM_ENT $uiarg1] 0)
]

dynui entityproj [
    local _ent _idx _type _blend

    uistyle centertop
    uimenu 0
    uiallowinput 0

    _ent = (getprojent $uiarg1)
    _type = (getentity $_ent 0)
    _idx = (getclientactitemidx $focusedplayer $ACTITEM_PROJ -1 $uiarg1)

    ui_entityitem_dynui

    uipropagate [ uicolourblend $_blend ]
] [] [] [
    result (= [getclientactitemidx $focusedplayer $ACTITEM_PROJ -1 $uiarg1] 0)
]

defvar entityedituilevel 0 0 2
defvar entityedituinumber 0 1 1
deffvarp entityedituiblend 0 0.5 1.0
deffvarp entityedituiselblend 0 0.75 1.0
deffvarp entityedituihoverblend 0 1.0 1.0

ui_entityedit_dynui_full = [
    uicolour 0x80000000 0 0 [
        uitable 0 0 [
            loop i (getentity $uiarg1 1) [
                _aname = (getentattr $_type $i $_attr1)
                if (!=s $_aname "") [
                    uitablerow [
                        uispace $ui_padsmall $ui_padtiny [uitext (+ $i 1) 0.25]
                        uispace $ui_padsmall $ui_padtiny [uitext $_aname 0.25]
                        uispace $ui_padsmall $ui_padtiny [uitext (getentity $uiarg1 1 $i) 0.25]
                    ]
                ]
            ]
        ]
    ]
]

ui_entityedit_dynui = [
    local _name _attr1 _group_hover _selected _hovering _full _aname

    _name = (getentinfo $_type 1)
    _attr1 = (getentity $uiarg1 1 0)
    _blend = $entityedituiblend

    _group = (entgrouppos $uiarg1)
    _hover = (enthoverpos $uiarg1)

    _selected = (>= $_group 0)
    _hovering = (= $_hover 0)

    uiontop (|| $_hovering $_selected)
    uizindex (? $_selected -1 (? $_hovering -2 -3))

    _full = (> $entityedituilevel (? (|| $_selected $_hovering) 0 1))
    uivlist $ui_padtiny [
        uiborderedimageclamped (? $_selected $skinalphatex $skinshadowtex) 0xff000000 0 $ui_texborder $ui_screenborder 0 0 [
            uispace $ui_padnormal $ui_padsmall [
                uivlist $ui_padsmall [

                    uihlist $ui_padsmall [
                        uiimage (? $_selected "<grey>textures/icons/star" "<grey>textures/icons/dot") 0xFFFFFFFF 0 0.0075 0.0075
                        uifont $textfontoutline [
                            uihlist $ui_padsmall [
                                uitext $_name 0.5
                                if $entityedituinumber [ uitext (concatword "(" $uiarg1 ")") 0.5 [uicolourset $colouryellow] ]
                            ]
                        ]
                    ]

                    if $_full [
                        ui_entityedit_dynui_full
                    ]
                ]
            ]
        ]
    ]

    if $_selected [ _blend = (maxf $_blend $entityedituiselblend) ]
    if $_hovering [ _blend = (maxf $_blend $entityedituihoverblend) ]
]

dynui entityedit [
    local _type _blend

    _type = (getentity $uiarg1 0)

    if (&& $_type [isediting] [! $editinhibit]) [
        uistyle centertop
        uimenu 0
        uiallowinput 0

        ui_entity_dynui

        uipropagate [ uicolourblend $_blend ]
    ] [
        uivisible 0
    ]
] [] [] [
    result (&& (getentity $uiarg1 0) [isediting] [! $editinhibit])
]
