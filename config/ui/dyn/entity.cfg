defvar entityuilevel 0 0 2
defvar entityuinumber 0 1 1
deffvarp entityuiblend 0 0.5 1.0
deffvarp entityuiselblend 0 0.75 1.0
deffvarp entityuihoverblend 0 1.0 1.0

ui_entity_dynui_full = [
    uicolour 0x80000000 0 0 [
        uitable 0 0 [
            loop i (getentity $uiarg1 1) [
                _aname = (getentattr $_type $i $_attr1)
                if (!=s $_aname "") [
                    uitablerow [
                        uispace $ui_padsmall $ui_padtiny [uitext (+ $i 1) 0.25]
                        uispace $ui_padsmall $ui_padtiny [uitext $_aname 0.25]
                        uispace $ui_padsmall $ui_padtiny [uitext (getentity $uiarg1 1 $i) 0.25]
                    ]
                ]
            ]
        ]
    ]
]

ui_entity_dynui = [
    local _name _attr1 _blend _group_hover _selected _hovering _full _aname

    uistyle centertop
    uimenu 0
    uiallowinput 0

    _name = (getentinfo $_type 1)
    _attr1 = (getentity $uiarg1 1 0)
    _blend = $entityuiblend

    _group = (entgrouppos $uiarg1)
    _hover = (enthoverpos $uiarg1)

    _selected = (>= $_group 0)
    _hovering = (= $_hover 0)

    uiontop (|| $_hovering $_selected)
    uizindex (? $_selected -1 (? $_hovering -2 -3))

    _full = (> $entityuilevel (? (|| $_selected $_hovering) 0 1))
    uivlist $ui_padtiny [
        uiborderedimageclamped (? $_selected $skinalphatex $skinshadowtex) 0xff000000 0 $ui_texborder $ui_screenborder 0 0 [
            uispace $ui_padnormal $ui_padsmall [
                uivlist $ui_padsmall [

                    uihlist $ui_padsmall [
                        uiimage (? $_selected "<grey>textures/icons/star" "<grey>textures/icons/dot") 0xFFFFFFFF 0 0.0075 0.0075
                        uifont $textfontoutline [
                            uihlist $ui_padsmall [
                                uitext $_name 0.5
                                if $entityuinumber [ uitext (concatword "(" $uiarg1 ")") 0.5 [uicolourset $colouryellow] ]
                            ]
                        ]
                    ]

                    if $_full [
                        ui_entity_dynui_full
                    ]
                ]
            ]
        ]
    ]

    if $_selected [ _blend = (maxf $_blend $entityuiselblend) ]
    if $_hovering [ _blend = (maxf $_blend $entityuihoverblend) ]

    uipropagate [ uicolourblend $_blend ]
]

dynui entity [
    local _type

    _type = (getentity $uiarg1 0)
    if (&& $_type [isediting]) [
        ui_entity_dynui
    ] [ uivisible 0 ]
]
