defvar entityabovelevel 0 0 2
defvar entityabovenumber 0 1 1
deffvarp entityaboveblend 0 0.5 1.0
deffvarp entityaboveselblend 0 0.75 1.0
deffvarp entityabovehoverblend 0 1.0 1.0

dynui entityabove [
    local _type _name _attr1 _blend _group_hover _selected _hovering _full _aname

    uistyle centertop
    uimenu 0
    uiallowinput 0

    _type = (getentity $uiarg1 0)
    if $_type [
        _name = (getentinfo $_type 1)
        _attr1 = (getentity $uiarg1 1 0)
        _blend = $entityaboveblend

        if (isediting) [
            _group = (entgrouppos $uiarg1)
            _hover = (enthoverpos $uiarg1)

            _selected = (>= $_group 0)
            _hovering = (= $_hover 0)

            uiontop (|| $_hovering $_selected)
            uizindex (? $_selected -1 (? $_hovering -2 -3))

            _full = (> $entityabovelevel (? (|| $_selected $_hovering) 0 1))
            uivlist $ui_padtiny [
                uiborderedimageclamped (? $_selected $skinalphatex $skinshadowtex) 0xff000000 0 $ui_texborder $ui_screenborder 0 0 [
                    uispace $ui_padnormal $ui_padsmall [
                        uivlist $ui_padsmall [

                            uihlist $ui_padsmall [
                                uiimage (? $_selected "<grey>textures/icons/star" "<grey>textures/icons/dot") 0xFFFFFFFF 0 0.0075 0.0075
                                uifont $textfontoutline [
                                    uihlist $ui_padsmall [
                                        uitext $_name 0.5
                                        if $entityabovenumber [ uitext (concatword "(" $uiarg1 ")") 0.5 [uicolourset $colouryellow] ]
                                    ]
                                ]
                            ]

                            if $_full [
                                uicolour 0x80000000 0 0 [
                                    uitable 0 0 [
                                        loop i (getentity $uiarg1 1) [
                                            _aname = (getentattr $_type $i $_attr1)
                                            if (!=s $_aname "") [
                                                uitablerow [
                                                    uispace $ui_padsmall $ui_padtiny [uitext (+ $i 1) 0.25]
                                                    uispace $ui_padsmall $ui_padtiny [uitext $_aname 0.25]
                                                    uispace $ui_padsmall $ui_padtiny [uitext (getentity $uiarg1 1 $i) 0.25]
                                                ]
                                            ]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]

            if $_selected [ _blend = (maxf $_blend $entityaboveselblend) ]
            if $_hovering [ _blend = (maxf $_blend $entityabovehoverblend) ]
        ] [
            uiontop 0
            uizindex -1
        ]

        uipropagate [ uicolourblend $_blend ]
    ] [ hideui $uiname $uisurfacetype ]
]

deffvarp entityoverlayblend 0 0.5 1.0
deffvarp entityoverlayselblend 0 1.0 1.0
deffvarp entityoverlayhoverblend 0 1.0 1.0

dynui entityoverlay [
    local _type _blend _group _hover _selected _hovering _size

    uistyle centermiddle
    uimenu 0
    uiallowinput 0

    _type = (getentity $uiarg1 0)
    if $_type [
        _blend = $entityoverlayblend

        if (isediting) [
            _group = (entgrouppos $uiarg1)
            _hover = (enthoverpos $uiarg1)

            _selected = (>= $_group 0)
            _hovering = (= $_hover 0)
            _size = (? (|| $_hovering $_selected) 0.0125 0.0075)

            uiontop (|| $_hovering $_selected)
            uizindex (? $_hovering -1 -2)

            uiimage (getenttex $uiarg1) 0xFFFFFFFF 0 $_size $_size [
                uiimageshadow 0.0005 0x000000
            ]

            if $_selected [ _blend = (maxf $_blend $entityoverlayselblend) ]
            if $_hovering [ _blend = (maxf $_blend $entityoverlayhoverblend) ]
        ] [
            uiontop 0
            uizindex -1
        ]

        uipropagate [ uicolourblend $_blend ]
    ] [ hideui $uiname $uisurfacetype ]
]