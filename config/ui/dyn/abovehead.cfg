defvarp aboveheadhealth 0 1 1
defvarp aboveheadimpulse 0 0 1
deffvarp aboveheadbarwidth $fvaridxnonzero 0.15 $fvaridxmax
deffvarp aboveheadbarheight $fvaridxnonzero 0.01 $fvaridxmax
deffvarp aboveheadbarborder $fvaridxnonzero 0.002 $fvaridxmax

dynui abovehead [
    local abovehead_presence; abovehead_presence = (getclientpresence $uiparam)
    if $abovehead_presence [
        uistyle centertop
        uiallowinput 0

        local abovehead_state abovehead_team abovehead_colour abovehead_opacity
        abovehead_state = (getclientstate $uiparam)
        abovehead_team = (getclientteam $uiparam)
        abovehead_colour = (getclientcolour $uiparam $playerovertone $playerovertonelevel)
        abovehead_opacity = (getclientopacity $uiparam)

        uivlist $ui_padtiny [
            uistyle centermiddle
            if (|| (= $abovehead_state $stateidxdead) (= $abovehead_state $stateidxwaiting)) [
                uiimage $deadtex $abovehead_colour 0 0.1 0.1 [ uistyle centermiddle ]
            ]
            uitext (getclientname $uiparam 0 0) 2.5 [ uicolourset $abovehead_colour ]

            if (= $abovehead_state $stateidxalive) [
                if (&& $aboveheadimpulse (!= $impulsestyle 3) (getclientallowimpulse $uiparam)) [
                    local abovehead_impulseregen abovehead_impulsecount abovehead_impulsemoves abovehead_impulsepart

                    abovehead_impulseregen = (getclientimpulselast $uiparam)
                    abovehead_impulsecount = (getclientimpulsecount $uiparam)
                    abovehead_impulsemoves = (- $impulsecount $abovehead_impulsecount)

                    if $impulsemeter [
                        abovehead_impulse = (getclientimpulsemeter $uiparam)
                        abovehead_impulsepart = (clampf (-f 1 (divf $abovehead_impulse $impulsemeter)) 0.0 1.0)
                    ] [
                        abovehead_impulse = $abovehead_impulsemoves
                        abovehead_impulsepart = (clampf (divf $abovehead_impulse $impulsecount) 0.0 1.0)
                    ]

                    local abovehead_bordercolour
                    caseif (<=f $abovehead_impulsepart 0.25) [
                        abovehead_bordercolour = (modcolour (getclientrescolour $uiparam $PULSE_WARN) 0.25)
                    ] $abovehead_impulseregen [
                        abovehead_bordercolour = (modcolour (getclientrescolour $uiparam $PULSE_REGEN) 0.25)
                    ] () [
                        abovehead_bordercolour = 0x000000
                    ]

                    uicolour $abovehead_bordercolour (+f $aboveheadbarwidth (*f $aboveheadbarborder 2)) (+f $aboveheadbarheight (*f $aboveheadbarborder 2)) [
                        uispace $aboveheadbarborder $aboveheadbarborder [
                            uistyle leftmiddle
                            uiclip (*f $aboveheadbarwidth $abovehead_impulsepart) $aboveheadbarheight 0.0 0.0 [
                                uistyle leftmiddle
                                uicolour $colourcyan $aboveheadbarwidth $aboveheadbarheight [
                                    uistyle leftmiddle
                                    uistyle impulsebar 1 $abovehead_impulseregen $abovehead_impulseregen 250
                                ]
                            ]
                        ]
                    ]
                ]

                if (&& $aboveheadhealth (! (& $mutators (<< 1 $G_M_INSTAGIB)))) [
                    local abovehead_health abovehead_spawnhealth abovehead_healthregen abovehead_healthregenlast abovehead_healthregenerating abovehead_damagelevel abovehead_damagecrit
                    abovehead_health = (getclienthealth $uiparam)
                    abovehead_spawnhealth = (getclientspawnhealth $uiparam)
                    abovehead_healthregen = (getclientregen $uiparam)
                    abovehead_healthregenlast = (- $lastmillis $abovehead_healthregen)
                    abovehead_healthregenerating = (<= $abovehead_healthregenlast $regentime)
                    abovehead_damagelevel = (? (= $abovehead_state $stateidxdead) 0.0 (divf $abovehead_health $abovehead_spawnhealth))
                    abovehead_damagecrit = (|| (= $abovehead_state $stateidxdead) (&& (= $abovehead_state $stateidxalive) (<=f $abovehead_damagelevel $damagecritical)))

                    local abovehead_bordercolour
                    caseif $abovehead_damagecrit [
                        abovehead_bordercolour = (modcolour (getclientrescolour $uiparam $PULSE_WARN) 0.25)
                    ] $abovehead_healthregenerating [
                        abovehead_bordercolour = (modcolour (getclientrescolour $uiparam $PULSE_REGEN) 0.25)
                    ] () [
                        abovehead_bordercolour = 0x000000
                    ]

                    uicolour $abovehead_bordercolour (+f $aboveheadbarwidth (*f $aboveheadbarborder 2)) (+f $aboveheadbarheight (*f $aboveheadbarborder 2)) [
                        uispace $aboveheadbarborder $aboveheadbarborder [
                            uistyle leftmiddle
                            uiclip (*f $aboveheadbarwidth $abovehead_damagelevel) $aboveheadbarheight 0.0 0.0 [
                                uistyle leftmiddle
                                uicolour $colourgreen $aboveheadbarwidth $aboveheadbarheight [
                                    uistyle leftmiddle
                                    uistyle healthbar 1 $abovehead_healthregenerating $abovehead_healthregenlast $regentime
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
        uiprop [uicolourblend (*f $aboveheadblend $abovehead_opacity)]
    ] [ hideui $uiname ]
]