defvarp aboveheadhealth 0 1 1
defvarp aboveheadimpulse 0 0 1
deffvarp aboveheadblend $fvaridxnonzero 1 1
deffvarp aboveheadbarwidth $fvaridxnonzero 0.035 $fvaridxmax
deffvarp aboveheadbarheight $fvaridxnonzero 0.002 $fvaridxmax
deffvarp aboveheadbarborder $fvaridxnonzero 0.0005 $fvaridxmax

dynui abovehead [
    local abovehead_presence
    abovehead_presence = (&& (!= $uiarg1 $focusedplayer) (getclientpresence $uiarg1))
    if $abovehead_presence [
        uistyle centertop
        uiallowinput 0

        local abovehead_state abovehead_team abovehead_colour abovehead_opacity
        abovehead_state = (getclientstate $uiarg1)
        abovehead_team = (getclientteam $uiarg1)
        abovehead_colour = (getclientcolour $uiarg1 $playerovertone $playerovertonelevel)
        abovehead_opacity = (getclientopacity $uiarg1)

        uivlist $ui_padtiny [
            uistyle centermiddle
            if (|| (= $abovehead_state $stateidxdead) (= $abovehead_state $stateidxwaiting)) [
                uiimage $deadtex $abovehead_colour 0 0.025 0.025 [ uistyle centermiddle ]
            ]
            uifont $textfontoutline [ uitext (getclientname $uiarg1 0 0) 0.75 [ uicolourset $abovehead_colour ] ]

            if (= $abovehead_state $stateidxalive) [
                if (&& $aboveheadimpulse (getclientallowimpulse $uiarg1)) [
                    local abovehead_impulseregen abovehead_impulsecount abovehead_impulsemoves abovehead_impulsepart

                    abovehead_impulseregen = (getclientimpulselastmeter $uiarg1)
                    abovehead_impulsecount = (getclientimpulsecount $uiarg1)
                    abovehead_impulsemoves = (- $impulsecount $abovehead_impulsecount)

                    if $impulsecostmeter [
                        abovehead_impulse = (getclientimpulsecostmeter $uiarg1)
                        abovehead_impulsepart = (clampf (-f 1 (divf $abovehead_impulse $impulsecostmeter)) 0.0 1.0)
                    ] [
                        abovehead_impulse = $abovehead_impulsemoves
                        abovehead_impulsepart = (clampf (divf $abovehead_impulse $impulsecount) 0.0 1.0)
                    ]

                    local abovehead_bordercolour
                    caseif (<=f $abovehead_impulsepart 0.25) [
                        abovehead_bordercolour = (modcolour (getclientrescolour $uiarg1 $PULSE_WARN) 0.2)
                    ] $abovehead_impulseregen [
                        abovehead_bordercolour = (modcolour (getclientrescolour $uiarg1 $PULSE_REGEN) 0.2)
                    ] () [
                        abovehead_bordercolour = 0x000000
                    ]

                    uiborderedimageclamped $skinalphatex $abovehead_bordercolour 0 $ui_texborder $ui_screenborder (+f $aboveheadbarwidth (*f $aboveheadbarborder 2)) (+f $aboveheadbarheight (*f $aboveheadbarborder 2)) [
                        uispace $aboveheadbarborder $aboveheadbarborder [
                            uistyle leftmiddle
                            uiclip (*f $aboveheadbarwidth $abovehead_impulsepart) $aboveheadbarheight 0.0 0.0 [
                                uistyle leftmiddle
                                uiborderedimageclamped $skinalphatex $colourcyan 0 $ui_texborder $ui_screenborder $aboveheadbarwidth $aboveheadbarheight [
                                    uistyle leftmiddle
                                    uistyle impulsebar 1 $abovehead_impulseregen $abovehead_impulseregen 250
                                ]
                            ]
                        ]
                    ]
                ]

                if (&& $aboveheadhealth (! (& $mutators (<< 1 $G_M_INSTAGIB)))) [
                    local abovehead_health abovehead_spawnhealth abovehead_healthregen abovehead_healthregenlast abovehead_healthregenerating abovehead_damagelevel abovehead_damagecrit
                    abovehead_health = (getclienthealth $uiarg1)
                    abovehead_spawnhealth = (getclientspawnhealth $uiarg1)
                    abovehead_healthregen = (getclientregen $uiarg1)
                    abovehead_healthregenlast = (- $lastmillis $abovehead_healthregen)
                    abovehead_healthregenerating = (<= $abovehead_healthregenlast $regentime)
                    abovehead_damagelevel = (? (= $abovehead_state $stateidxdead) 0.0 (divf $abovehead_health $abovehead_spawnhealth))
                    abovehead_damagecrit = (|| (= $abovehead_state $stateidxdead) (&& (= $abovehead_state $stateidxalive) (<=f $abovehead_damagelevel $damagecritical)))

                    local abovehead_bordercolour
                    caseif $abovehead_damagecrit [
                        abovehead_bordercolour = (modcolour (getclientrescolour $uiarg1 $PULSE_WARN) 0.2)
                    ] $abovehead_healthregenerating [
                        abovehead_bordercolour = (modcolour (getclientrescolour $uiarg1 $PULSE_REGEN) 0.2)
                    ] () [
                        abovehead_bordercolour = 0x000000
                    ]

                    uiborderedimageclamped $skinalphatex $abovehead_bordercolour 0 $ui_texborder $ui_screenborder (+f $aboveheadbarwidth (*f $aboveheadbarborder 2)) (+f $aboveheadbarheight (*f $aboveheadbarborder 2)) [
                        uispace $aboveheadbarborder $aboveheadbarborder [
                            uistyle leftmiddle
                            uiclip (*f $aboveheadbarwidth $abovehead_damagelevel) $aboveheadbarheight 0.0 0.0 [
                                uistyle leftmiddle
                                uiborderedimageclamped $skinalphatex $colourgreen 0 $ui_texborder $ui_screenborder $aboveheadbarwidth $aboveheadbarheight [
                                    uistyle leftmiddle
                                    uistyle healthbar 1 $abovehead_healthregenerating $abovehead_healthregenlast $regentime
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
        uipropagate [ uicolourblend (*f $aboveheadblend $abovehead_opacity) ]
    ] [ hideui $uiname ]
]
