dynui abovehead [
    local abovehead_presence; abovehead_presence = (getclientpresence $uiparam)
    if $abovehead_presence [
        uialign 0 -1
        uiallowinput 0

        local abovehead_state abovehead_team abovehead_colour abovehead_opacity
        abovehead_state = (getclientstate $uiparam)
        abovehead_team = (getclientteam $uiparam)
        abovehead_colour = (getclientcolour $uiparam $playerovertone $playerovertonelevel)
        abovehead_opacity = (getclientopacity $uiparam)

        uivlist 0 [
            uialign 0 0
            if (|| (= $abovehead_state $stateidxdead) (= $abovehead_state $stateidxwaiting)) [
                uiimage $deadtex $abovehead_colour 0 0.1 0.1 [ uialign 0 0 ]
            ]
            uitext (getclientname $uiparam 0 0) 2.5 [ uisetcolour $abovehead_colour ]

            if (! (& $mutators $mutsbitinstagib)) [
                local abovehead_health abovehead_spawnhealth abovehead_healthregen abovehead_healthregenlast abovehead_regenerating abovehead_damagelevel abovehead_damagecrit abovehead_bordercolour
                abovehead_health = (getclienthealth $uiparam)
                abovehead_spawnhealth = (getclientspawnhealth $uiparam)
                abovehead_healthregen = (getclientregen $uiparam)
                abovehead_healthregenlast = (- $lastmillis $abovehead_healthregen)
                abovehead_regenerating = (<= $abovehead_healthregenlast $regentime)
                abovehead_damagelevel = (? (= $abovehead_state $stateidxdead) 0.0 (divf $abovehead_health $abovehead_spawnhealth))
                abovehead_damagecrit = (|| (= $abovehead_state $stateidxdead) (&& (= $abovehead_state $stateidxalive) (<=f $abovehead_damagelevel $damagecritical)))
                caseif $abovehead_damagecrit [
                    abovehead_bordercolour = (modcolour (getclientrescolour $uiparam $pulseidxwarn) 0.5)
                ] $abovehead_regenerating [
                    abovehead_bordercolour = (modcolour (getclientrescolour $uiparam $pulseidxregen) 0.5)
                ] () [
                    abovehead_bordercolour = (modcolour $abovehead_colour 0.5)
                ]

                uicolour $abovehead_bordercolour 0.158 0.013 [
                    if (= $abovehead_state $stateidxalive) [
                        uispace 0.002 0.002 [
                            uicolour $colourblack 0.154 0.009 [
                                uispace 0.002 0.002 [
                                    uialign -1 0
                                    uiclip (*f 0.15 $abovehead_damagelevel) 0.005 0.0 0.0 [
                                        uialign -1 0
                                        uicolour $colourdarkred 0.15 0.005 [
                                            uialign -1 0
                                            uicolourdir 1
                                            uicolouradd $colouryellow
                                            uicolouradd $colourgreen
                                            if $abovehead_regenerating [ uicolourrotate (divf $abovehead_healthregenlast $regentime) ]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
        uichangeblends (*f $aboveheadblend $abovehead_opacity)
    ] [ hideui $uiname ]
]