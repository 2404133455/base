defvarp playerabovehealth 0 1 1
defvarp playerabovedamage 0 1 1
deffvarp playeraboveblend $fvaridxnonzero 1 1

dynui playerabove [
    uiallowinput 0
    uimenu 0
    uizindex -1 // draw behind other stuff on this surface
    uistyle centertop

    local player_presence
    player_presence = (&& (!= $uiarg1 $focusedplayer) (getclientpresence $uiarg1))
    if $player_presence [
        local player_state player_team player_colour player_opacity
        if (&& (!= $player_state $CS_SPECTATOR) (|| (&& (!= $player_state $CS_DEAD) (!= $player_state $CS_WAITING)) (getclientlastdeath $uiarg1))) [
            player_state = (getclientstate $uiarg1)
            player_team = (getclientteam $uiarg1)
            player_colour = (getclientcolour $uiarg1 $playerdisplaytone $playerdisplaytonelevel $playerdisplaytonemix)
            player_opacity = (*f (getclientopacity $uiarg1) (getdarkness $DARK_HALO))

            uivlist 0.00125 [
                uistyle centermiddle

                local player_health player_spawnhealth player_healthregen player_healthregenlast player_healthregenerating player_damagelevel player_damagecrit
                player_health = (getclienthealth $uiarg1)
                player_spawnhealth = (getclientspawnhealth $uiarg1)
                player_healthregen = (getclientregen $uiarg1)
                player_healthregenlast = (- $lastmillis $player_healthregen)
                player_healthregenerating = (<= $player_healthregenlast $regentime)
                player_buffing = (getclientbuffing $uiarg1)
                player_healthlevel = (clampf (? (= $player_state $CS_DEAD) 0.0 (divf $player_health $player_spawnhealth)) 0.0 2.0)
                player_healthcrit = (|| (= $player_state $CS_DEAD) (&& (= $player_state $CS_ALIVE) (<=f $player_healthlevel $damagecritical)))

                local player_bordercolour player_modtime
                player_bordercolour = (modcolour $player_colour 0.25)
                player_modtime = (divf (mod $lastmillis 1000) 500)
                if (>f $player_modtime 1.0) [ player_modtime = (-f 2.0 $player_modtime) ]

                if (= $player_state $CS_ALIVE) [
                    caseif $player_healthcrit [
                        player_bordercolour = (skewcolour (modcolour (getclientrescolour $uiarg1 $PULSE_WARN) 0.75) $player_bordercolour $player_modtime)
                    ] $player_buffing [
                        player_bordercolour = (skewcolour (modcolour (getclientrescolour $uiarg1 $PULSE_BUFF) 0.5) $player_bordercolour $player_modtime)
                    ]
                ]

                uihlist 0 [
                    caseif (|| (= $player_state $CS_DEAD) (= $player_state $CS_WAITING)) [
                        uiimage $deadtex $player_colour 0 0.025 0.025 [
                            uistyle centermiddle
                            uiimageshadow 0.001 0x88010101
                            uicolourblend 0.75
                        ]
                    ] (! (& $mutators (<< 1 $G_M_INSTAGIB))) [
                        if $playerabovehealth [
                            local player_healthcolour
                            player_healthcolour = (? (>f $player_healthlevel 1.0) (skewcolour $colourgreen $colourcyan (-f $player_healthlevel 1.0)) (skewcolour $colourred $colourgreen $player_healthlevel))
                            if $player_healthregenerating [
                                player_healthcolour = (skewcolour (getclientrescolour $uiarg1 $PULSE_REGEN) $player_healthcolour $player_modtime)
                            ]

                            uicolour $player_healthcolour (*f 0.02 $player_healthlevel) 0.002 [ uicolourblend 0.65 ]
                        ]
                    ]

                    if $playerabovedamage [
                        loopdamagerevif 0 0 player_curdamage [&& (= (getdamageclient $player_curdamage) $uiarg1) (= (getdamagefrom $player_curdamage) $focusedplayer)] [
                            local player_totaldamage player_damagecolour player_damagelevel player_damageblend player_damageready
                            player_totaldamage = (getdamageamt $player_curdamage)
                            player_damagelevel = (clampf (divf $player_totaldamage $player_spawnhealth) 0.0 2.0)

                            case (getdamagetype $player_curdamage) 0 [
                                player_damagecolour = (skewcolour $colourdarkred (getclientrescolour $uiarg1 $PULSE_WARN) 0.5)
                            ] 1 [
                                player_damagecolour = (skewcolour $colourdarkred (getclientrescolour $uiarg1 $PULSE_BURN) 0.5)
                            ] 2 [
                                player_damagecolour = (skewcolour $colourdarkred (getclientrescolour $uiarg1 $PULSE_BLEED) 0.5)
                            ] 3 [
                                player_damagecolour = (skewcolour $colourdarkred (getclientrescolour $uiarg1 $PULSE_SHOCK) 0.5)
                            ]
                            player_damageready = (getdamageready $player_curdamage)

                            if $player_damageready [
                                local player_damagetime player_damagelength
                                player_damagetime = (- $totalmillis $player_damageready)
                                player_damagelength = (div (getdamagelength $player_curdamage) 2)
                                if (> $player_damagetime $player_damagelength) [
                                    local player_damageoffset
                                    player_damageoffset = (- $player_damagetime $player_damagelength)
                                    player_damageblend = (-f 1.0 (divf $player_damageoffset $player_damagelength))
                                ] [
                                    player_damageblend = 1.0
                                ]
                            ] [
                                player_damageblend = 1.0
                            ]

                            uicolour $player_damagecolour (*f 0.02 $player_damagelevel $player_damageblend) 0.002 [ uicolourblend (*f 0.65 $player_damageblend) ]
                        ]
                    ]
                ]

                uivlist 0 [
                    uiborderedimageclamped $skinshadowtex $player_bordercolour 0 $ui_texborder $ui_screenborder 0 0 [
                        uicolourblend 0.5
                        uispace 0.003 0.0 [
                            uifont $textfontoutline [ uitext (getclientname $uiarg1 0 0) 0.75 [ uicolourset $player_colour ] ]
                        ]
                    ]

                    uiimage $pointshadowtex $player_bordercolour 0 0.0075 0.0075
                ]
            ]
            uipropagate [ uicolourblend (*f $playeraboveblend $player_opacity) ]
        ] [ hideui $uiname $uisurfacetype ]
    ] [ hideui $uiname $uisurfacetype ]
]

dynui playeroverlay [
    uiallowinput 0
    uimenu 0
    uizindex -1 // draw behind other stuff on this surface
    uistyle centermiddle

    local player_presence
    player_presence = (&& (!= $uiarg1 $focusedplayer) (getclientpresence $uiarg1))
    if $player_presence [
        local player_state
        player_state = (getclientstate $uiarg1)
        if (&& (!= $player_state $CS_SPECTATOR) (|| (&& (!= $player_state $CS_DEAD) (!= $player_state $CS_WAITING)) (getclientlastdeath $uiarg1))) [
            local player_opacity
            player_opacity = (*f (getclientopacity $uiarg1) (getdarkness 5))

            if (&& (= $gamemode $G_BOMBER) (= $uiarg1 (bombercurtarget))) [
                local player_effect player_offset
                player_effect = (absf (-f 1 (divf (mod $lastmillis 500) 250)))
                player_offset = (+f 0.085 (*f 0.015 $player_effect))
                uiimage "<grey>textures/hud/indicator" (skewcolour 0xFFFFFF (pulsecolour $PULSE_WARN) $player_effect) 0 $player_offset $player_offset [
                    uistyle centermiddle
                    uiimageshadow 0.001 0x88010101
                    uiimage "<grey>textures/icons/bomb" (pulsecolour $PULSE_DISCO) 0 0.02 0.02 [
                        uistyle lefttop
                        uiimageshadow 0.001 (modcolour (pulsecolour $PULSE_DISCO) 0.25)
                    ]
                ]
            ]
            uipropagate [ uicolourblend (*f $playeraboveblend $player_opacity) ]
        ] [ hideui $uiname $uisurfacetype ]
    ] [ hideui $uiname $uisurfacetype ]
]