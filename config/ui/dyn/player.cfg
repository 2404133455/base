defvarp playerabovehealth 0 1 1
defvarp playeraboveimpulse 0 0 1
deffvarp playeraboveblend $fvaridxnonzero 1 1
deffvarp playerabovebarwidth $fvaridxnonzero 0.035 $fvaridxmax
deffvarp playerabovebarheight $fvaridxnonzero 0.002 $fvaridxmax
deffvarp playerabovebarborder $fvaridxnonzero 0.0005 $fvaridxmax

dynui playerabove [
    uiallowinput 0
    uimenu 0
    uizindex -1 // draw behind other stuff on this surface

    local player_presence
    player_presence = (&& (!= $uiarg1 $focusedplayer) (getclientpresence $uiarg1))
    if $player_presence [
        uistyle centertop

        local player_state player_team player_colour player_opacity
        player_state = (getclientstate $uiarg1)
        player_team = (getclientteam $uiarg1)
        player_colour = (getclientcolour $uiarg1 $playerovertone $playerovertonelevel)
        player_opacity = (*f (getclientopacity $uiarg1) (getdarkness 5))

        uivlist $ui_padtiny [
            uistyle centermiddle
            if (|| (= $player_state $stateidxdead) (= $player_state $stateidxwaiting)) [
                uiimage $deadtex $player_colour 0 0.025 0.025 [
                    uistyle centermiddle
                    uiimageshadow 0.002 0x88010101
                ]
            ]
            uifont $textfontoutline [ uitext (getclientname $uiarg1 0 0) 0.75 [ uicolourset $player_colour ] ]

            if (= $player_state $stateidxalive) [
                if (&& $playeraboveimpulse (getclientallowimpulse $uiarg1)) [
                    local player_impulseregen player_impulsecount player_impulsemoves player_impulsepart

                    player_impulseregen = (getclientimpulselastmeter $uiarg1)
                    player_impulsecount = (getclientimpulsecount $uiarg1)
                    player_impulsemoves = (- $impulsecount $player_impulsecount)

                    if $impulsecostmeter [
                        player_impulse = (getclientimpulsecostmeter $uiarg1)
                        player_impulsepart = (clampf (-f 1 (divf $player_impulse $impulsecostmeter)) 0.0 1.0)
                    ] [
                        player_impulse = $player_impulsemoves
                        player_impulsepart = (clampf (divf $player_impulse $impulsecount) 0.0 1.0)
                    ]

                    local player_bordercolour
                    caseif (<=f $player_impulsepart 0.25) [
                        player_bordercolour = (modcolour (getclientrescolour $uiarg1 $PULSE_WARN) 0.2)
                    ] $player_impulseregen [
                        player_bordercolour = (modcolour (getclientrescolour $uiarg1 $PULSE_REGEN) 0.2)
                    ] () [
                        player_bordercolour = 0x000000
                    ]

                    uiborderedimageclamped $skinalphatex $player_bordercolour 0 $ui_texborder $ui_screenborder (+f $playerabovebarwidth (*f $playerabovebarborder 2)) (+f $playerabovebarheight (*f $playerabovebarborder 2)) [
                        uispace $playerabovebarborder $playerabovebarborder [
                            uistyle leftmiddle
                            uiclip (*f $playerabovebarwidth $player_impulsepart) $playerabovebarheight 0.0 0.0 [
                                uistyle leftmiddle
                                uiborderedimageclamped $skinalphatex $colourcyan 0 $ui_texborder $ui_screenborder $playerabovebarwidth $playerabovebarheight [
                                    uistyle leftmiddle
                                    uistyle impulsebar 1 $player_impulseregen $player_impulseregen 250
                                ]
                            ]
                        ]
                    ]
                ]

                if (&& $playerabovehealth (! (& $mutators (<< 1 $G_M_INSTAGIB)))) [
                    local player_health player_spawnhealth player_healthregen player_healthregenlast player_healthregenerating player_damagelevel player_damagecrit
                    player_health = (getclienthealth $uiarg1)
                    player_spawnhealth = (getclientspawnhealth $uiarg1)
                    player_healthregen = (getclientregen $uiarg1)
                    player_healthregenlast = (- $lastmillis $player_healthregen)
                    player_healthregenerating = (<= $player_healthregenlast $regentime)
                    player_damagelevel = (? (= $player_state $stateidxdead) 0.0 (divf $player_health $player_spawnhealth))
                    player_damagecrit = (|| (= $player_state $stateidxdead) (&& (= $player_state $stateidxalive) (<=f $player_damagelevel $damagecritical)))

                    local player_bordercolour
                    caseif $player_damagecrit [
                        player_bordercolour = (modcolour (getclientrescolour $uiarg1 $PULSE_WARN) 0.2)
                    ] $player_healthregenerating [
                        player_bordercolour = (modcolour (getclientrescolour $uiarg1 $PULSE_REGEN) 0.2)
                    ] () [
                        player_bordercolour = 0x000000
                    ]

                    uiborderedimageclamped $skinalphatex $player_bordercolour 0 $ui_texborder $ui_screenborder (+f $playerabovebarwidth (*f $playerabovebarborder 2)) (+f $playerabovebarheight (*f $playerabovebarborder 2)) [
                        uispace $playerabovebarborder $playerabovebarborder [
                            uistyle leftmiddle
                            uiclip (*f $playerabovebarwidth $player_damagelevel) $playerabovebarheight 0.0 0.0 [
                                uistyle leftmiddle
                                uiborderedimageclamped $skinalphatex $colourgreen 0 $ui_texborder $ui_screenborder $playerabovebarwidth $playerabovebarheight [
                                    uistyle leftmiddle
                                    uistyle healthbar 1 $player_healthregenerating $player_healthregenlast $regentime
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
        uipropagate [ uicolourblend (*f $playeraboveblend $player_opacity) ]
    ] [ hideui $uiname ]
]

dynui playeroverlay [
    uiallowinput 0
    uimenu 0
    uizindex -1 // draw behind other stuff on this surface

    local player_presence
    player_presence = (&& (!= $uiarg1 $focusedplayer) (getclientpresence $uiarg1))
    if $player_presence [
        uistyle centermiddle

        local player_state player_team player_colour player_opacity
        player_state = (getclientstate $uiarg1)
        player_team = (getclientteam $uiarg1)
        player_colour = (getclientcolour $uiarg1 $playerovertone $playerovertonelevel)
        player_opacity = (*f (getclientopacity $uiarg1) (getdarkness 5))

        uivlist $ui_padtiny [
            uistyle centermiddle
            uiimage "textures/icons/player" $player_colour 0 0.025 0.025 [
                uistyle centermiddle
                uiimageshadow 0.002 0x88010101
            ]
        ]
        uipropagate [ uicolourblend (*f $playeraboveblend $player_opacity) ]
    ] [ hideui $uiname ]
]