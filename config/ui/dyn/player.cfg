defvarp playeruihealth 0 1 1
defvarp playeruidamage 0 1 1
deffvarp playeruiblend $fvaridxnonzero 1 1

ui_player_dynui_flash = [
    _modtime = (divf (mod $lastmillis 1000) 500)
    if (>f $_modtime 1.0) [ _modtime = (-f 2.0 $_modtime) ]

    caseif $_healthcrit [
        _bordercolour = (skewcolour (modcolour (getclientrescolour $uiarg1 $PULSE_WARN) 0.75) $_bordercolour $_modtime)
    ] $_buffing [
        _bordercolour = (skewcolour (modcolour (getclientrescolour $uiarg1 $PULSE_BUFF) 0.5) $_bordercolour $_modtime)
    ]
]

ui_player_dynui_health = [
    caseif (&& [= $gamemode $G_BOMBER] [= $uiarg1 (bombercurtarget)]) [
        _bordercolour = (modcolour (getclientrescolour $uiarg1 $PULSE_DISCO) 0.75)
        uiimage $bombtex $_bordercolour 0 0.025 0.025 [
            uistyle centermiddle
            uiimageshadow 0.001 0x88010101
            uicolourblend 0.75
        ]
    ] (getclientconopen $uiarg1) [
        uiimage $chattex $_colour 0 0.025 0.025 [
            uistyle centermiddle
            uiimageshadow 0.001 0x88010101
            uicolourblend 0.75
        ]
    ]

    if $playeruihealth [
        _healthcolour = (? (>f $_healthlevel 1.0) (skewcolour $colourgreen $colourcyan (-f $_healthlevel 1.0)) (skewcolour $colourred $colourgreen $_healthlevel))
        if $_healthregenerating [
            _healthcolour = (skewcolour (getclientrescolour $uiarg1 $PULSE_REGEN) $_healthcolour $_modtime)
        ]

        uicolour $_healthcolour (*f 0.02 $_healthlevel) 0.002 [ uicolourblend 0.65 ]
    ]
]

# ui_player_dynui_damage [
    loopdamagerevif 0 0 _curdamage [&& (= (getdamageclient $_curdamage) $uiarg1) (= (getdamagefrom $_curdamage) $focusedplayer)] [
        _totaldamage = (getdamageamt $_curdamage)
        _damagelevel = (clampf (divf $_totaldamage $_spawnhealth) 0.0 2.0)

        _damagecolour = (skewcolour $colourdarkred (getclientrescolour $uiarg1 (at [
            #PULSE_WARN
            #PULSE_BURN
            #PULSE_BLEED
            #PULSE_SHOCK
            #PULSE_CORRODE
        ] (getdamagetype $_curdamage))) 0.5)

        _damageready = (getdamageready $_curdamage)

        if $_damageready [
            _damagetime = (- $totalmillis $_damageready)
            _damagelength = (div (getdamagelength $_curdamage) 2)
            if (> $_damagetime $_damagelength) [
                _damageoffset = (- $_damagetime $_damagelength)
                _damageblend = (-f 1.0 (divf $_damageoffset $_damagelength))
            ] [
                _damageblend = 1.0
            ]
        ] [
            _damageblend = 1.0
        ]

        uicolour $_damagecolour (*f 0.02 $_damagelevel $_damageblend) 0.002 [ uicolourblend (*f 0.65 $_damageblend) ]
    ]
]

ui_player_dynui = [
    local _team _colour _opacity _health _spawnhealth _healthregen _healthregenlast _healthregenerating _damagelevel _damagecrit _bordercolour _modtime _healthcolour _totaldamage _damagecolour _damagelevel _damageblend _damageready _damagetime _damagelength _damageoffset

    uiallowinput 0
    uimenu 0
    uizindex -2 // draw behind other stuff on this surface
    uistyle centertop

    _team = (getclientteam $uiarg1)
    _colour = (getclientcolour $uiarg1 $playerdisplaytone $playerdisplaytonelevel $playerdisplaytonemix)
    _opacity = (*f (getclientopacity $uiarg1) (getdarkness $DARK_HALO))

    uivlist 0.00125 [
        uistyle centermiddle

        _health = (getclienthealth $uiarg1)
        _spawnhealth = (getclientspawnhealth $uiarg1)
        _healthregen = (getclientregen $uiarg1)
        _healthregenlast = (- $lastmillis $_healthregen)
        _healthregenerating = (<= $_healthregenlast $regentime)
        _buffing = (getclientbuffing $uiarg1)
        _healthlevel = (clampf (? (= $_state $CS_DEAD) 0.0 (divf $_health $_spawnhealth)) 0.0 2.0)
        _healthcrit = (|| [= $_state $CS_DEAD] [&& [= $_state $CS_ALIVE] [<=f $_healthlevel $damagecritical]])

        _bordercolour = (modcolour $_colour 0.25)
        if (= $_state $CS_ALIVE) [
            ui_player_dynui_flash
        ]

        uihlist 0 [
            caseif (|| (= $_state $CS_DEAD) (= $_state $CS_WAITING)) [
                uiimage $deadtex $_colour 0 0.025 0.025 [
                    uistyle centermiddle
                    uiimageshadow 0.001 0x88010101
                    uicolourblend 0.75
                ]
            ] (! (& $mutators (<< 1 $G_M_INSTAGIB))) [
                ui_player_dynui_health
            ]

            if $playeruidamage [
                ui_player_dynui_damage
            ]
        ]

        uivlist 0 [
            uiborderedimageclamped $skinshadowtex $_bordercolour 0 $ui_texborder $ui_screenborder 0 0 [
                uicolourblend 0.5
                uispace 0.003 0.0 [
                    uifont $textfontoutline [ uitext (getclientname $uiarg1 0 0) 0.5 [ uicolourset $_colour ] ]
                ]
            ]
           uiimage $pointshadowtex $_bordercolour 0 0.0075 0.0075
        ]
    ]

    uipropagate [ uicolourblend (*f $playeruiblend $_opacity) ]
]

dynui player [
    local _presence _state

    _presence = (&& [!= $uiarg1 $focusedplayer] [getclientpresence $uiarg1])
    _state = (? $_presence (getclientstate $uiarg1) $CS_SPECTATOR)

    if (&& [!= $_state $CS_SPECTATOR] [|| [&& [!= $_state $CS_DEAD] [!= $_state $CS_WAITING]] [getclientlastdeath $uiarg1]]) [
        ui_player_dynui
    ] [ uivisible 0 ]
]