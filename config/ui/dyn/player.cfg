defvarp playerabovehealth 0 1 1
defvarp playerabovedamage 0 1 1
deffvarp playeraboveblend $fvaridxnonzero 1 1

dynui playerabove [
    local _presence _state _team _colour _opacity _health _spawnhealth _healthregen _healthregenlast _healthregenerating _damagelevel _damagecrit _bordercolour _modtime _healthcolour _totaldamage _damagecolour _damagelevel _damageblend _damageready _damagetime _damagelength _damageoffset

    uiallowinput 0
    uimenu 0
    uizindex -2 // draw behind other stuff on this surface
    uistyle centertop

    _presence = (&& (!= $uiarg1 $focusedplayer) (getclientpresence $uiarg1))
    if $_presence [
        if (&& (!= $_state $CS_SPECTATOR) (|| (&& (!= $_state $CS_DEAD) (!= $_state $CS_WAITING)) (getclientlastdeath $uiarg1))) [
            _state = (getclientstate $uiarg1)
            _team = (getclientteam $uiarg1)
            _colour = (getclientcolour $uiarg1 $playerdisplaytone $playerdisplaytonelevel $playerdisplaytonemix)
            _opacity = (*f (getclientopacity $uiarg1) (getdarkness $DARK_HALO))

            uivlist 0.00125 [
                uistyle centermiddle

                _health = (getclienthealth $uiarg1)
                _spawnhealth = (getclientspawnhealth $uiarg1)
                _healthregen = (getclientregen $uiarg1)
                _healthregenlast = (- $lastmillis $_healthregen)
                _healthregenerating = (<= $_healthregenlast $regentime)
                _buffing = (getclientbuffing $uiarg1)
                _healthlevel = (clampf (? (= $_state $CS_DEAD) 0.0 (divf $_health $_spawnhealth)) 0.0 2.0)
                _healthcrit = (|| (= $_state $CS_DEAD) (&& (= $_state $CS_ALIVE) (<=f $_healthlevel $damagecritical)))

                _bordercolour = (modcolour $_colour 0.25)
                _modtime = (divf (mod $lastmillis 1000) 500)
                if (>f $_modtime 1.0) [ _modtime = (-f 2.0 $_modtime) ]

                if (= $_state $CS_ALIVE) [
                    caseif $_healthcrit [
                        _bordercolour = (skewcolour (modcolour (getclientrescolour $uiarg1 $PULSE_WARN) 0.75) $_bordercolour $_modtime)
                    ] $_buffing [
                        _bordercolour = (skewcolour (modcolour (getclientrescolour $uiarg1 $PULSE_BUFF) 0.5) $_bordercolour $_modtime)
                    ]
                ]

                uihlist 0 [
                    caseif (|| (= $_state $CS_DEAD) (= $_state $CS_WAITING)) [
                        uiimage $deadtex $_colour 0 0.025 0.025 [
                            uistyle centermiddle
                            uiimageshadow 0.001 0x88010101
                            uicolourblend 0.75
                        ]
                    ] (! (& $mutators (<< 1 $G_M_INSTAGIB))) [
                        if (getclientconopen $uiarg1) [
                            uiimage $chattex $_colour 0 0.025 0.025 [
                                uistyle centermiddle
                                uiimageshadow 0.001 0x88010101
                                uicolourblend 0.75
                            ]
                        ]
                        if $playerabovehealth [
                            _healthcolour = (? (>f $_healthlevel 1.0) (skewcolour $colourgreen $colourcyan (-f $_healthlevel 1.0)) (skewcolour $colourred $colourgreen $_healthlevel))
                            if $_healthregenerating [
                                _healthcolour = (skewcolour (getclientrescolour $uiarg1 $PULSE_REGEN) $_healthcolour $_modtime)
                            ]

                            uicolour $_healthcolour (*f 0.02 $_healthlevel) 0.002 [ uicolourblend 0.65 ]
                        ]
                    ]

                    if $playerabovedamage [
                        loopdamagerevif 0 0 _curdamage [&& (= (getdamageclient $_curdamage) $uiarg1) (= (getdamagefrom $_curdamage) $focusedplayer)] [
                            _totaldamage = (getdamageamt $_curdamage)
                            _damagelevel = (clampf (divf $_totaldamage $_spawnhealth) 0.0 2.0)

                            case (getdamagetype $_curdamage) 0 [
                                _damagecolour = (skewcolour $colourdarkred (getclientrescolour $uiarg1 $PULSE_WARN) 0.5)
                            ] 1 [
                                _damagecolour = (skewcolour $colourdarkred (getclientrescolour $uiarg1 $PULSE_BURN) 0.5)
                            ] 2 [
                                _damagecolour = (skewcolour $colourdarkred (getclientrescolour $uiarg1 $PULSE_BLEED) 0.5)
                            ] 3 [
                                _damagecolour = (skewcolour $colourdarkred (getclientrescolour $uiarg1 $PULSE_SHOCK) 0.5)
                            ]
                            _damageready = (getdamageready $_curdamage)

                            if $_damageready [

                                _damagetime = (- $totalmillis $_damageready)
                                _damagelength = (div (getdamagelength $_curdamage) 2)
                                if (> $_damagetime $_damagelength) [
                                    _damageoffset = (- $_damagetime $_damagelength)
                                    _damageblend = (-f 1.0 (divf $_damageoffset $_damagelength))
                                ] [
                                    _damageblend = 1.0
                                ]
                            ] [
                                _damageblend = 1.0
                            ]

                            uicolour $_damagecolour (*f 0.02 $_damagelevel $_damageblend) 0.002 [ uicolourblend (*f 0.65 $_damageblend) ]
                        ]
                    ]
                ]

                uivlist 0 [
                    uiborderedimageclamped $skinshadowtex $_bordercolour 0 $ui_texborder $ui_screenborder 0 0 [
                        uicolourblend 0.5
                        uispace 0.003 0.0 [
                            uifont $textfontoutline [ uitext (getclientname $uiarg1 0 0) 0.75 [ uicolourset $_colour ] ]
                        ]
                    ]

                    uiimage $pointshadowtex $_bordercolour 0 0.0075 0.0075
                ]
            ]
            uipropagate [ uicolourblend (*f $playeraboveblend $_opacity) ]
        ] [ hideui $uiname $uisurfacetype ]
    ] [ hideui $uiname $uisurfacetype ]
]

dynui playeroverlay [
    local _presence _state _opacity _effect _offset

    uiallowinput 0
    uimenu 0
    uizindex -2 // draw behind other stuff on this surface
    uistyle centermiddle

    _presence = (&& (!= $uiarg1 $focusedplayer) (getclientpresence $uiarg1))
    if $_presence [

        _state = (getclientstate $uiarg1)
        if (&& (!= $_state $CS_SPECTATOR) (|| (&& (!= $_state $CS_DEAD) (!= $_state $CS_WAITING)) (getclientlastdeath $uiarg1))) [
            _opacity = (*f (getclientopacity $uiarg1) (getdarkness 5))

            if (&& (= $gamemode $G_BOMBER) (= $uiarg1 (bombercurtarget))) [
                _effect = (absf (-f 1 (divf (mod $lastmillis 500) 250)))
                _offset = (+f 0.085 (*f 0.015 $_effect))
                uiimage "<grey>textures/hud/indicator" (skewcolour 0xFFFFFF (pulsecolour $PULSE_WARN) $_effect) 0 $_offset $_offset [
                    uistyle centermiddle
                    uiimageshadow 0.001 0x88010101
                    uiimage "<grey>textures/icons/bomb" (pulsecolour $PULSE_DISCO) 0 0.02 0.02 [
                        uistyle lefttop
                        uiimageshadow 0.001 (modcolour (pulsecolour $PULSE_DISCO) 0.25)
                    ]
                ]
            ]
            uipropagate [ uicolourblend (*f $playeraboveblend $_opacity) ]
        ] [ hideui $uiname $uisurfacetype ]
    ] [ hideui $uiname $uisurfacetype ]
]