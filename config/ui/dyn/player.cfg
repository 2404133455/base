defvarp playerabovehealth 0 1 1
deffvarp playeraboveblend $fvaridxnonzero 1 1
deffvarp playerabovebarwidth $fvaridxnonzero 0.035 $fvaridxmax
deffvarp playerabovebarheight $fvaridxnonzero 0.002 $fvaridxmax
deffvarp playerabovebarborder $fvaridxnonzero 0.00075 $fvaridxmax

dynui playerabove [
    uiallowinput 0
    uimenu 0
    uizindex -1 // draw behind other stuff on this surface

    local player_presence
    player_presence = (&& (!= $uiarg1 $focusedplayer) (getclientpresence $uiarg1))
    if $player_presence [
        uistyle centertop

        local player_state player_team player_colour player_opacity
        player_state = (getclientstate $uiarg1)
        player_team = (getclientteam $uiarg1)
        player_colour = (getclientcolour $uiarg1 $playerovertone $playerovertonelevel)
        player_opacity = (*f (getclientopacity $uiarg1) (getdarkness 5))

        uivlist 0.0025 [
            uistyle centermiddle

            local player_health player_spawnhealth player_healthregen player_healthregenlast player_healthregenerating player_damagelevel player_damagecrit
            player_health = (getclienthealth $uiarg1)
            player_spawnhealth = (getclientspawnhealth $uiarg1)
            player_healthregen = (getclientregen $uiarg1)
            player_healthregenlast = (- $lastmillis $player_healthregen)
            player_healthregenerating = (<= $player_healthregenlast $regentime)
            player_damagelevel = (? (= $player_state $stateidxdead) 0.0 (divf $player_health $player_spawnhealth))
            player_damagecrit = (|| (= $player_state $stateidxdead) (&& (= $player_state $stateidxalive) (<=f $player_damagelevel $damagecritical)))

            local player_bordercolour
            caseif $player_damagecrit [
                player_bordercolour = (modcolour (getclientrescolour $uiarg1 $PULSE_WARN) 0.2)
            ] $player_healthregenerating [
                player_bordercolour = (modcolour (getclientrescolour $uiarg1 $PULSE_REGEN) 0.2)
            ] () [
                player_bordercolour = 0x000000
            ]

            if (|| (= $player_state $stateidxdead) (= $player_state $stateidxwaiting)) [
                uiimage $deadtex $player_colour 0 0.025 0.025 [
                    uistyle centermiddle
                    uiimageshadow 0.001 0x88010101
                ]
            ] [
                if (&& $playerabovehealth (! (& $mutators (<< 1 $G_M_INSTAGIB)))) [
                    uiborderedimageclamped $skinshadowtex $player_bordercolour 0 $ui_texborder $ui_screenborder (+f $playerabovebarwidth (*f $playerabovebarborder 2)) (+f $playerabovebarheight (*f $playerabovebarborder 2)) [
                        uicolourblend 0.5
                        uispace $playerabovebarborder $playerabovebarborder [
                            uistyle leftmiddle
                            uiclip (*f $playerabovebarwidth $player_damagelevel) $playerabovebarheight 0.0 0.0 [
                                uistyle leftmiddle
                                uiborderedimageclamped $skinalphatex (skewcolour $colourred $colourgreen $player_damagelevel) 0 $ui_texborder $ui_screenborder $playerabovebarwidth $playerabovebarheight [
                                    uistyle leftmiddle
                                ]
                            ]
                        ]
                    ]
                ]
            ]

            uiborderedimageclamped $skinshadowtex $player_bordercolour 0 $ui_texborder $ui_screenborder 0 0 [
                uicolourblend 0.5
                uispace 0.005 0.002 [
                    uivlist 0.001 [
                        uifont $textfontoutline [ uitext (getclientname $uiarg1 0 0) 0.65 [ uicolourset $player_colour ] ]
                    ]
                ]
            ]
        ]
        uipropagate [ uicolourblend (*f $playeraboveblend $player_opacity) ]
    ] [ hideui $uiname ]
]

dynui playeroverlay [
    uiallowinput 0
    uimenu 0
    uizindex -1 // draw behind other stuff on this surface

    local player_presence
    player_presence = (&& (!= $uiarg1 $focusedplayer) (getclientpresence $uiarg1))
    if $player_presence [
        local player_overlay player_param
        caseif (= $gamemode $G_BOMBER) [
            if (= $uiarg1 (bombercurtarget)) [ player_overlay = "bomber" ]
        ] [
            player_overlay = ""
        ]

        if (!=s $player_overlay "") [
            uistyle centermiddle

            local player_opacity
            player_opacity = (*f (getclientopacity $uiarg1) (getdarkness 5))

            uivlist $ui_padtiny [
                uistyle centermiddle
                local player_effect player_offset
                player_effect = (absf (-f 1 (divf (mod $lastmillis 500) 250)))
                player_offset = (+f 0.085 (*f 0.015 $player_effect))
                uiimage "textures/hud/indicator" (skewcolour 0xFFFFFF (pulsecolour $PULSE_WARN) $player_effect) 0 $player_offset $player_offset [
                    uistyle centermiddle
                    uiimageshadow 0.001 0x88010101
                    uiimage "textures/icons/bomb" (pulsecolour $PULSE_DISCO) 0 0.02 0.02 [
                        uistyle lefttop
                        uiimageshadow 0.001 (modcolour (pulsecolour $PULSE_DISCO) 0.25)
                    ]
                ]
            ]
            uipropagate [ uicolourblend (*f $playeraboveblend $player_opacity) ]
        ]
    ] [ hideui $uiname ]
]