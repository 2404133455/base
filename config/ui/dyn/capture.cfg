deffvarp captureaboveblend $fvaridxnonzero 1 1

dynui captureabove [
    local capture_presence
    capture_presence = (getcapturenum $uiarg1)
    if (>= $capture_presence 0) [
        uiallowinput 0
        uimenu 0
        uizindex -1 // draw behind other stuff on this surface
        uistyle centertop

        local capture_droptime capture_owner capture_taken
        capture_droptime = (getcapturedroptime $uiarg1)
        capture_owner = (getcaptureowner $uiarg1)
        capture_taken = (|| $capture_droptime (>= $capture_owner 0))

        local capture_team capture_colour capture_text capture_disptime

        capture_team = (getcaptureteam $uiarg1)
        capture_colour = (getteamcolour $capture_team)
        capture_text = (getteamname $capture_team)

        local capture_bordercolour capture_size capture_hasaffinity
        capture_bordercolour = (modcolour $capture_colour 0.25)
        capture_size = 1.0
        capture_hasaffinity = (&& (getclienthasaffinity $focusedplayer) (= (getclientteam $focusedplayer) $capture_team))

        local capture_show
        capture_show = 0

        if $capture_hasaffinity [
            local capture_curtime
            capture_curtime = 0

            loopcaptureif 0 0 capture_iter [= (getcaptureowner $capture_iter) $focusedplayer] [
                capture_curtime = (max $capture_curtime (getcapturetaketime $capture_iter))
            ]

            if $capture_curtime [
                uihint $capture_curtime

                local capture_offset capture_skew
                capture_offset = (- $lastmillis $capture_curtime)
                capture_show = 1

                if (< $capture_offset $uihintintime) [
                    capture_skew = (divf $capture_offset $uihintintime)
                ] [
                    capture_offset = (- $capture_offset $uihintintime)

                    if (> $capture_offset $uihintholdtime) [
                        capture_offset = (- $capture_offset $uihintholdtime)

                        if (< $capture_offset $uihintouttime) [
                            capture_skew = (-f 1.0 (divf $capture_offset $uihintouttime))
                        ] [
                            capture_show = 0
                        ]
                    ] [
                        capture_skew = 1.0
                    ]
                ]

                capture_size = (+f $capture_size $capture_skew)
            ]
        ]

        uiradar 1 0 0 0 0 [
            uiradarshape 1

            uispace (*f 0.005 $capture_size) (*f 0.005 $capture_size) [
                uivlist 0 [
                    uiborderedimageclamped $skinshadowtex $capture_bordercolour 0 $ui_texborder $ui_screenborder 0 0 [
                        uicolourblend 0.5
                        uispace 0.003 0.003 [
                            uifont $textfontoutline [
                                uivlist 0 [
                                    uitext $capture_text (*f 0.75 $capture_size)
                                    uitext (? $capture_hasaffinity "GOAL" (? $capture_taken "TAKEN" "SAFE")) (*f 0.5 $capture_size) [
                                        if $capture_hasaffinity [ uicolourset (pulsecolour $PULSE_DISCO) ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]

            if $capture_show [
                local capture_yaw capture_dir
                capture_dir = (getcaptureradardir $uiarg1 1)
                capture_yaw = (clampf (modf (-f 360 (+f $capture_dir 180)) 360) 90 270)

                uiradarblip $arrowtex (pulsecolour $PULSE_DISCO) $capture_yaw $capture_yaw 2 (*f 0.01 $capture_size) (*f 0.01 $capture_size)
            ]
        ]

        uipropagate [ uicolourblend $captureaboveblend ]
    ] [ hideui $uiname $uisurfacetype ]
]

dynui captureoverlay []

deffvarp captureflagaboveblend $fvaridxnonzero 1 1

dynui captureflagabove [
    local capture_presence
    capture_presence = (getcapturenum $uiarg1)
    if (>= $capture_presence 0) [
        uiallowinput 0
        uimenu 0
        uizindex -1 // draw behind other stuff on this surface
        uistyle centermiddle

        local capture_droptime capture_owner capture_taken

        capture_droptime = (getcapturedroptime $uiarg1)
        capture_owner = (getcaptureowner $uiarg1)
        capture_taken = (|| $capture_droptime (>= $capture_owner 0))

        if $capture_taken [
            local capture_team capture_colour capture_text capture_disptime capture_opacity

            capture_team = (getcaptureteam $uiarg1)
            capture_colour = (getteamcolour $capture_team)
            capture_text = (getteamname $capture_team)
            capture_opacity = 1.0

            local capture_bordercolour
            capture_bordercolour = (modcolour $capture_colour 0.25)

            capture_disptime = (getcapturedisptime $uiarg1)
            if $capture_disptime [
                local capture_offtime
                capture_offtime = (- $lastmillis $capture_disptime)
                if (< $capture_offtime 1000) [
                    capture_opacity = (divf $capture_offtime 1000)
                ]
            ]

            uivlist 0 [
                uiborderedimageclamped $skinshadowtex $capture_colour 0 $ui_texborder $ui_screenborder 0 0 [
                    uicolourblend 0.5
                    uispace 0.003 0.003 [
                        uifont $textfontoutline [
                            uivlist 0 [
                                uitext $capture_text 0.75
                                uitext "FLAG" 0.5
                            ]
                        ]
                    ]
                ]

                uiimage $pointshadowtex $capture_colour 0 0.0075 0.0075
            ]
            uipropagate [ uicolourblend (*f $captureaboveblend $capture_opacity) ]
        ] [ hideui $uiname $uisurfacetype ]
    ] [ hideui $uiname $uisurfacetype ]
]

dynui captureflagoverlay []