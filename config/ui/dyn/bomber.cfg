deffvarp bomberaboveblend $fvaridxnonzero 1 1

dynui bomberabove [
    local bomber_presence
    bomber_presence = (getbombernum $uiarg1)
    if (>= $bomber_presence 0) [
        uiallowinput 0
        uimenu 0
        uizindex -1 // draw behind other stuff on this surface
        uistyle centertop

        local bomber_enabled bomber_opacity

        bomber_enabled = (getbomberenabled $uiarg1)
        bomber_opacity = 1.0

        if $bomber_enabled [
            local bomber_team bomber_colour bomber_text bomber_disptime

            bomber_team = (getbomberteam $uiarg1)
            if $bomber_team [
                bomber_colour = (getteamcolour $bomber_team)
                bomber_text = (getteamname $bomber_team)
            ] [
                bomber_colour = (pulsecolour $PULSE_DISCO)
                bomber_text = "BOMB"
            ]

            local bomber_bordercolour
            bomber_bordercolour = (modcolour $bomber_colour 0.25)

            bomber_disptime = (getbomberdisptime $uiarg1)
            if $bomber_disptime [
                local bomber_offtime
                bomber_offtime = (- $lastmillis $bomber_disptime)
                if (< $bomber_offtime 1000) [
                    bomber_opacity = (divf $bomber_offtime 1000)
                ]
            ]

            // bomber_droptime = (getbomberdroptime $uiarg1)
            // bomber_owner = (getbomberowner $uiarg1)

            uivlist 0 [
                uiborderedimageclamped $skinshadowtex $bomber_bordercolour 0 $ui_texborder $ui_screenborder 0 0 [
                    uicolourblend 0.5
                    uispace 0.003 0.003 [
                        uifont $textfontoutline [
                            uivlist 0 [
                                uitext $bomber_text 0.75 [ if (! $bomber_team) [ uicolourset $bomber_colour ] ]
                                if $bomber_team [ uitext "GOAL" 0.5 ]
                            ]
                        ]
                    ]
                ]

                uiimage $pointshadowtex $bomber_colour 0 0.0075 0.0075
            ]
        ]

        uipropagate [ uicolourblend (*f $bomberaboveblend $bomber_opacity) ]
    ] [ hideui $uiname $uisurfacetype ]
]

dynui bomberoverlay []