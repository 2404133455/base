deffvarp bomberaboveblend $fvaridxnonzero 1 1

dynui bomberabove [
    local bomber_presence
    bomber_presence = (getbombernum $uiarg1)
    if (>= $bomber_presence 0) [
        uiallowinput 0
        uimenu 0
        uizindex -1 // draw behind other stuff on this surface
        uistyle centertop

        local bomber_enabled bomber_opacity

        bomber_enabled = (getbomberenabled $uiarg1)
        bomber_opacity = 1.0

        if $bomber_enabled [
            local bomber_team bomber_colour bomber_text bomber_disptime

            bomber_team = (getbomberteam $uiarg1)
            if $bomber_team [
                bomber_colour = (getteamcolour $bomber_team)
                bomber_text = (getteamname $bomber_team)
            ] [
                bomber_colour = (pulsecolour $PULSE_DISCO)
                bomber_text = "BOMB"
            ]

            local bomber_bordercolour bomber_hasaffinity bomber_size
            bomber_bordercolour = (modcolour $bomber_colour 0.25)
            bomber_size = 1.0
            bomber_hasaffinity = (&& (getclienthasaffinity $focusedplayer) $bomber_team (!= (getclientteam $focusedplayer) $bomber_team))

            bomber_disptime = (getbomberdisptime $uiarg1)
            if $bomber_disptime [
                local bomber_offtime
                bomber_offtime = (- $lastmillis $bomber_disptime)
                if (< $bomber_offtime 1000) [
                    bomber_opacity = (divf $bomber_offtime 1000)
                ]
            ]

            local bomber_show
            bomber_show = 0

            if $bomber_hasaffinity [
                local bomber_curtime
                bomber_curtime = 0

                loopbomberif 0 0 bomber_iter [= (getbomberowner $bomber_iter) $focusedplayer] [
                    bomber_curtime = (max $bomber_curtime (getbombertaketime $bomber_iter))
                ]

                if $bomber_curtime [
                    uihint $bomber_curtime
                    bomber_show = 1

                    local bomber_offset bomber_skew
                    bomber_offset = (- $lastmillis $bomber_curtime)

                    if (< $bomber_offset $uihintintime) [
                        bomber_skew = (divf $bomber_offset $uihintintime)
                    ] [
                        bomber_offset = (- $bomber_offset $uihintintime)

                        if (> $bomber_offset $uihintholdtime) [
                            bomber_offset = (- $bomber_offset $uihintholdtime)

                            if (< $bomber_offset $uihintouttime) [
                                bomber_skew = (-f 1.0 (divf $bomber_offset $uihintouttime))
                            ] [
                                bomber_show = 0
                            ]
                        ] [
                            bomber_skew = 1.0
                        ]
                    ]

                    bomber_size = (+f $bomber_size $bomber_skew)
                ]
            ]

            uiradar 1 0 0 0 0 [
                uiradarshape 1

                uispace (*f 0.005 $bomber_size) (*f 0.005 $bomber_size) [
                    uivlist 0 [
                        uiborderedimageclamped $skinshadowtex $bomber_bordercolour 0 $ui_texborder $ui_screenborder 0 0 [
                            uicolourblend 0.5
                            uispace 0.003 0.003 [
                                uifont $textfontoutline [
                                    uivlist 0 [
                                        uitext $bomber_text (*f 0.75 $bomber_size) [ if (! $bomber_team) [ uicolourset $bomber_colour ] ]
                                        if $bomber_team [
                                            uitext (? (!= (getclientteam $focusedplayer) $bomber_team) "GOAL" "BASE") (*f 0.5 $bomber_size) [
                                                if $bomber_hasaffinity [ uicolourset (pulsecolour $PULSE_DISCO) ]
                                            ]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]

                if $bomber_show [
                    local bomber_yaw bomber_dir
                    bomber_dir = (getbomberradardir $uiarg1 1)
                    bomber_yaw = (clampf (modf (-f 360 (+f $bomber_dir 180)) 360) 90 270)

                    uiradarblip $arrowtex (pulsecolour $PULSE_DISCO) $bomber_yaw $bomber_yaw 2 (*f 0.01 $bomber_size) (*f 0.01 $bomber_size)
                ]
            ]
        ]

        uipropagate [ uicolourblend (*f $bomberaboveblend $bomber_opacity) ]
    ] [ hideui $uiname $uisurfacetype ]
]

dynui bomberoverlay []