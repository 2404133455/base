defvarp selectionuitype 0 2 2
deffvarp selectionuiblend 0 0.5 1.0
deffvarp selectionuihover 0 1.0 1.0

dynui selection [
    local align_x; align_x = 0
    case $uiarg1 0 [
        align_x = (? (&& (>f $uicuryaw 135) (<f $uicuryaw 315)) -1 1)
    ] 1 [
        align_x = (? (&& (>f $uicuryaw 135) (<f $uicuryaw 315)) -1 1)
    ] 2 [
        align_x = (? (&& (>f $uicuryaw 45) (<f $uicuryaw 225)) -1 1)
    ] 3 [
        align_x = (? (&& (>f $uicuryaw 45) (<f $uicuryaw 225)) -1 1)
    ] 4 [
        align_x = (? (&& (>f $uicuryaw 45) (<f $uicuryaw 225)) 1 -1)
    ] 5 [
        align_x = (? (&& (>f $uicuryaw 45) (<f $uicuryaw 225)) 1 -1)
    ] 6 [
        align_x = (? (&& (<f $uicuryaw 315) (>f $uicuryaw 135)) 1 -1)
    ] 7 [
        align_x = (? (&& (<f $uicuryaw 315) (>f $uicuryaw 135)) 1 -1)
    ]
    uialign $align_x (? (<f $uicurpitch 0) 1 -1)
    uiallowinput 0
    uiontop 1
    uimenu 0
    uizindex -1 // draw behind other stuff on this surface
    uispace 0.01 0 [ uiborderedimageclamped $skinalphatex 0xff000000 0 $ui_texborder $ui_screenborder 0 0 [
        uispace $ui_padsmall $ui_padsmall [ uihlist 0 [
            local selection_texs; selection_texs = (getseltexs)
            case $selectionuitype 1 [
                uitext (format "%1 x %2 x %3" (getenginestat 30) (getenginestat 31) (getenginestat 32)) 3.0
            ] 2 [
                local selection_matf; selection_matf = $selchildmat
                local selection_mats; selection_mats = (listlen $selection_matf)
                uitable 0 0 [
                    uitablerow [
                        uispace $ui_padsmall $ui_padtiny [uistyle righttop; uitext "Sel Size:" 2.0 [uicolourset $colouryellow]]
                        uispace $ui_padsmall $ui_padtiny [uistyle lefttop; uitext (format "%1 x %2 x %3" (getenginestat 30) (getenginestat 31) (getenginestat 32)) 2.0]
                    ]
                    uitablerow [
                        uispace $ui_padsmall $ui_padtiny [uistyle righttop; uitext "Position:" 2.0 [uicolourset $colouryellow]]
                        uispace $ui_padsmall $ui_padtiny [uistyle lefttop; uitext (format "%1, %2, %3" (getenginestat 27) (getenginestat 28) (getenginestat 29)) 2.0]
                    ]
                    uitablerow [
                        uispace $ui_padsmall $ui_padtiny [uistyle righttop; uitext "Grid Size:" 2.0 [uicolourset $colouryellow]]
                        uispace $ui_padsmall $ui_padtiny [
                            uistyle lefttop
                            if (< (getenginestat 41) 16) [
                                uitext (format "%1 (%2cm)" (getenginestat 41) (divf (getenginestat 41) 0.16)) 2.0
                            ] [
                                uitext (format "%1 (%2m)" (getenginestat 41) (divf (getenginestat 41) 16)) 2.0
                            ]
                        ]
                    ]
                    if $selection_texs [
                        uitablerow [
                            uispace $ui_padsmall $ui_padtiny [uistyle righttop; uitext (? (= $selection_texs 1) "Texture:" "Textures:") 2.0 [uicolourset $colouryellow]]
                            uispace $ui_padsmall $ui_padtiny [
                                uistyle lefttop
                                uivlist 0 [
                                    uistyle lefttop
                                    loop i $selection_texs [
                                        uitext (concat "^fo-^fS" (gettexname (getseltexs $i))) 2.0 [uistyle lefttop]
                                    ]
                                ]
                            ]
                        ]
                    ]
                    if $selection_mats [
                        uitablerow [
                            uispace $ui_padsmall $ui_padtiny [uistyle righttop; uitext (? (= $selection_mats 1) "Material:" "Materials:") 2.0 [uicolourset $colouryellow]]
                            uispace $ui_padsmall $ui_padtiny [
                                uistyle lefttop
                                uivlist 0 [
                                    uistyle lefttop
                                    loop i $selection_mats [
                                        uitext (concat "^fo-^fS" (at $selection_matf $i)) 2.0 [uistyle lefttop]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ] ]
    ] ]
    uihover [ uipropagate [ uicolourblend $selectionuihover ] ] [ uipropagate [ uicolourblend $selectionuiblend ] ]
]
