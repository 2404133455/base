defvar showconsole 0 0 1
toggleconsole = [showconsole (! $showconsole)]

defvarp conintime 0 250 $varidxmax
defvarp conouttime 0 10000 $varidxmax
defvarp confadetime 0 500 $varidxmax
defvarp conlinesamt 0 5 $varidxmax
defvarp conlinesopen 0 15 $varidxmax
deffvarp conblend 0 1 1

// 1:count 2:outtime 3:fadetime 4:intime 5:blend 6:size 7:textsize
ui_hud_console = [
    loopconlinesrev (- $arg1) $arg9 console_line [
        local console_blend; console_blend = (ceil (*f 255 $arg5))
        local console_colour; console_colour = (getconlinecolour $console_line)
        local console_time; console_time = (getconlinereftime $console_line)
        local console_millis; console_millis = (- $totalmillis $console_time)
        local console_growth; console_growth = 1.0
        caseif (&& $arg2 (> $console_millis $arg4)) [
            local console_ctime; console_ctime = (- $console_millis $arg2)
            if (&& $arg3 (< $console_ctime $arg3)) [
                local console_growth; console_growth = (-f 1 (divf $console_ctime $arg3))
                local console_blend; console_blend = (clamp (*f $console_growth $arg5 255) 0 255)
            ] [ console_blend = 0 ]
        ] (&& $arg4 (< $console_millis $arg4)) [
            local console_growth; console_growth = (divf $console_millis $arg4)
            local console_blend; console_blend = (clamp (*f $console_growth $arg5 255) 0 255)
        ]
        if (>f $console_blend 0.0) [
            local console_height
            uiclip 0 0 0 0 [
                uistyle lefttop
                uistyle clampx
                uitext (getconlinecref $console_line) $arg7 [
                    uistyle lefttop
                    uistyle clampx
                    uitextalign -1
                    uitextwrap $arg6
                    uicolourset (| (<< $console_blend 24) $console_colour)
                    console_height = $uilasth
                ]
                uiclipsizeh (*f $console_height $console_growth)
            ]
        ]
    ]
]

ui_console_colour_var = [
    result (format "%1 (%2, %3, %4)" (tohex $arg1 6) (& (>> $arg1 16) 0xFF) (& (>> $arg1 8) 0xFF) (& $arg1 0xFF))
]

ui_console_current_idname = [
    if $ui_hud_commandopen [
        local current_command; current_command = (getcommandbuf)
        if (=s (substr $current_command 0 1) "/") [
            current_command = (substr $current_command 1)

            // Get start of last id into idname_idx
            local idname_idx; idname_idx = 0
            local chrlist; chrlist = ";()[]^"$"
            loop i (strlen chrlist) [
                // strrchr-like functionality
                local new_idx; new_idx = 0
                local idx_add; idx_add = 0
                while [ (!= $new_idx -1) ] [
                    new_idx = (strstr (substr $current_command $idx_add) (substr $chrlist $i 1))
                    idx_add = (+ $idx_add $new_idx 1)
                    // If character found and after last found, use this index
                    if (&& (!= $new_idx -1) (> $idx_add $idname_idx ))[
                        idname_idx = $idx_add
                    ]
                ]
            ]

            // Skip any leading whitespace
            while [ ( =s (substr (substr $current_command $idname_idx) 0 1) " ") ] [
                idname_idx = (+ $idname_idx 1)
            ]

            // Get single id into idname, stopping at next space/tab.
            local idname; idname = (substr $current_command $idname_idx)
            local idname_end; idname_end = (strstr $idname " ")
            if ( = $idname_end -1 ) [
                idname_end = (strstr $idname "^t")
            ]
            if ( = $idname_end -1 ) [
                idname_end = (strlen $idname)
            ]
            idname = (substr $idname 0 $idname_end)

            if (&& (strlen $idname) ( >= (getvartype $idname) 0)) [
                result $idname
            ] [
                result ""
            ]
        ] [
            result ""
        ]
    ] [
        result ""
    ]
]

ui_console_usage_text = [
    local textstyle; textstyle = [ uistyle lefttop; uitextalign -1; uitextwrap $arg2 ]
    local idname; idname = $arg1
    local idtype; idtype = (getvartype $idname)
    local idflags; idflags = (getvarflags $idname)
    local idtype_s; idtype_s = ""
    // IDF_CLIENT || IDF_SERVER
    if (|| (& $idflags $idfbitclient) (& $idflags $idfbitserver)) [
        // Describe IDF_CLIENT var as "Game" to clarify that changing it will affect the entire game, not just the user's client.
        // IDF_SERVER can be obviously described as "Server"
        idtype_s = (concatif $idtype_s (? (& $idflags $idfbitclient) "Game" "Server"))
    ]
    // not ID_COMMAND
    if (!= $idtype $ididxcommand) [
        // IDF_INIT
        if (& $idflags $idfbitinit) [ idtype_s = (concatif $idtype_s "Initialiser")]
        // IDF_PERSIST
        if (& $idflags $idfbitpersist) [ idtype_s = (concatif $idtype_s "Persistent")]
        // IDF_READONLY
        if (& $idflags $idfbitreadonly) [ idtype_s = (concatif $idtype_s "Read-only")]
        // IDF_MAP
        if (& $idflags $idfbitmap) [ idtype_s = (concatif $idtype_s "Map")]
    ]

    idtype_s = (concatif $idtype_s $[idname@(at $ididxname $idtype)])

    uitext (concatword "^fAType: ^fw" $idtype_s) $ui_textsmall $textstyle

    case $idtype $ididxvar [
        if (& $idflags $idfbithex) [
            if (= (getvarmax $idname) 0xFFFFFF) [
                uitext (format "^fAMin: ^fw%1^fA, Max: ^fw%2" (ui_console_colour_var (getvarmin $idname)) (ui_console_colour_var (getvarmax $idname))) $ui_textsmall $textstyle
                if (= (getvardef $idname) $$idname) [
                    uitext (format "^fACurrent: ^fw%1^fA [^fs^f[%2]#^fS] ^fA(default)" (ui_console_colour_var (getvardef $idname)) $$idname) $ui_textsmall $textstyle
                ] [
                    uitext (format "^fADefault: ^fw%1^fA, Current: ^fy%2^fA [^fs^f[%3]#^fS]" (ui_console_colour_var (getvardef $idname)) (ui_console_colour_var $$idname) $$idname) $ui_textsmall $textstyle
                ]
            ] [
                uitext (format "^fAMin: ^fw%1^fA, Max: ^fw%2" (tohex (getvarmin $idname)) (tohex (getvarmax $idname))) $ui_textsmall $textstyle
                if (= (getvardef $idname) $$idname) [
                    uitext (format "^fACurrent: ^fw%1 ^fA(default)" (tohex (getvardef $idname))) $ui_textsmall $textstyle
                ] [
                    uitext (format "^fADefault: ^fw%1^fA, Current: ^fy%2" (tohex (getvardef $idname)) (tohex $$idname)) $ui_textsmall $textstyle
                ]
            ]
        ] [
            uitext (format "^fAMin: ^fw%1^fA, Max: ^fw%2" (getvarmin $idname) (getvarmax $idname)) $ui_textsmall $textstyle
            if (= (getvardef $idname) $$idname) [
                uitext (format "^fACurrent: ^fw%1 ^fA(default)" (getvardef $idname)) $ui_textsmall $textstyle
            ] [
                uitext (format "^fADefault: ^fw%1^fA, Current: ^fy%2" (getvardef $idname) $$idname) $ui_textsmall $textstyle
            ]
        ]
    ] $ididxfvar [
        uitext (format "^fAMin: ^fw%1^fA, Max: ^fw%2" (getfvarmin $idname) (getfvarmax $idname)) $ui_textsmall $textstyle
        if (=f (getfvardef $idname) $$idname) [
            uitext (format "^fACurrent: ^fw%1 ^fA(default)" (getfvardef $idname)) $ui_textsmall $textstyle
        ] [
            uitext (format "^fADefault: ^fw%1^fA, Current: ^fy%2" (getfvardef $idname) $$idname) $ui_textsmall $textstyle
        ]
    ] $ididxsvar [
        if (=s (getsvardef $idname) $$idname) [
            uitext (format "^fACurrent: ^fw%1 ^fA(default)" (getsvardef $idname)) $ui_textsmall $textstyle
        ] [
            uitext (format "^fADefault: ^fw%1^fA, Current: ^fy%2" (getsvardef $idname) $$idname) $ui_textsmall $textstyle
        ]
    ] $ididxcommand [
        if (strlen (getvarargs $idname)) [
            uitext (format "^fAArguments: ^fw%1 ^fA(^fw%2^fA)" (strlen (getvarargs $idname)) (getvarargs $idname)) $ui_textsmall $textstyle
        ] [
            uitext "^fAArguments: ^fwnone" $ui_textsmall $textstyle
        ]
    ]

    local idusage_s; idusage_s = (concatword "^fAUsage: ^fw/" $idname)

    // ID_VAR Bitfield
    if (&& (= $idtype $ididxvar) (> (getvarfields $idname) 1)) [
        idusage_s = (concat $idusage_s "<bitfield>")
        uitext $idusage_s $ui_textsmall $textstyle
        loop i (getvarfields $idname) [
            uitext (format "^t^fA%1 = %2" (<< 1 $i) (getvarfields $idname $i)) $ui_textsmall $textstyle
        ]
    ] [
        if (getvarfields $idname) [
            loop i (getvarfields $idname) [
                if (strlen (getvarfields $idname $i)) [
                    idusage_s = (concat $idusage_s (concatword "<" (getvarfields $idname $i) ">"))
                ]
            ]
        ] [
            case $idtype $ididxalias [
                idusage_s = (concat $idusage_s "<arguments>")
            ] $ididxvar [
                idusage_s = (concat $idusage_s "<integer>")
            ] $ididxfvar [
                idusage_s = (concat $idusage_s "<float>")
            ] $ididxsvar [
                idusage_s = (concat $idusage_s "<string>")
            ] $ididxcommand [
                loop i (strlen (getvarargs $idname)) [
                    local c; c = (substr (getvarargs $idname) $i 1)
                    cases $c s [n = "<string>"
                    ] i [n = (? (& $idflags $idfbithex) "<bitfield>" "<integer>")
                    ] b [n = (? (& $idflags $idfbithex) "<bitfield>" "<integer>")
                    ] n [n = (? (& $idflags $idfbithex) "<bitfield>" "<integer>")
                    ] f [n = "<float>"
                    ] g [n = "<float>"
                    ] t [n = "<null>"
                    ] e [n = "<command>"
                    ] r [n = "<ident>"
                    ] "^$" [n = "<ident>"
                    ] () [n = "<?>"]
                    idusage_s = (concat $idusage_s $n)
                ]
            ]
        ]
        uitext $idusage_s $ui_textsmall $textstyle
    ]

    if (strlen (getvardesc $idname)) [
        uitext (concatword "^fADescription: ^fw" (getvardesc $idname)) $ui_textsmall $textstyle
    ]

    if (= $idtype $ididxalias) [
        idusage_alias = (getalias $idname)
        if (>= (strlen $idusage_alias) 256) [
            idusage_alias = (concat (substr $idusage_alias 0 252) "..")
        ]
        uitext (concatword "^fAContents: ^fw" $idusage_alias) $ui_textsmall $textstyle
    ]
]

ui_hud_showconsole = [
    uivlist 0 [
        uistyle clampx
        uistyle lefttop
        if $ui_hud_commandopen [
            uicolour $ui_menu $uiaspect 0 [
                uistyle lefttop
                uispace $ui_padsmall $ui_padsmall [
                    uistyle clampx
                    uistyle lefttop
                    uivlist 0 [
                        uistyle clampx
                        uistyle lefttop
                        if $conlines [
                            uiskinborder 0 0 $ui_menu $ui_menu $ui_menu $ui_menu_t $ui_menu_t $ui_menu_t [
                                uistyle clampx
                                uistyle lefttop
                                uispace $ui_padnormal $ui_padsmall [
                                    uistyle clampx
                                    uistyle lefttop
                                    uivlist 0 [
                                        uistyle clampx
                                        uistyle lefttop
                                        local console_width; console_width = $uilastw
                                        ui_hud_console $conlinesopen 0 0 0 1 $console_width $ui_textsmall
                                    ]
                                ]
                            ] [
                                uistyle clampx
                                uistyle lefttop
                            ] 0 1
                            uispace 0 $ui_padtiny
                        ]
                        local console_flash; console_flash = (skewcolour $ui_menu (getclientrescolour $ui_hud_focus $PULSE_FLASH) 0.5)
                        uiskinborder 0 0 $ui_menu $ui_menu $ui_menu $console_flash $console_flash $console_flash [
                            uistyle clampx
                            uistyle lefttop
                            uispace $ui_padnormal $ui_padsmall [
                                uistyle clampx
                                uistyle lefttop
                                uihlist 0 [
                                    uistyle clampx
                                    uistyle lefttop
                                    local console_width; console_width = $uilastw
                                    uitext (getcommandbuf) $ui_text [
                                        uistyle lefttop
                                        uitextwrap $console_width
                                        uitextpos (getcommandpos)
                                        uitextalign -1
                                    ]
                                ]
                            ]
                        ] [
                            uistyle clampx
                            uistyle lefttop
                        ] 0 1
                        local idname; idname = (ui_console_current_idname)
                        if $idname [
                            uispace 0 $ui_padtiny
                            uihlist 0 [
                                uistyle clampx
                                uistyle lefttop
                                uiskinborder (*f $uiaspect 0.9) 0 $ui_menu $ui_menu $ui_menu $ui_menu_t $ui_menu_t $ui_menu_t [
                                    uistyle clampx
                                    uistyle lefttop
                                    uispace $ui_padnormal $ui_padsmall [
                                        uistyle clampx
                                        uistyle lefttop
                                        uivlist 0 [
                                            uistyle clampx
                                            uistyle lefttop
                                            ui_console_usage_text $idname $uilastw
                                        ]
                                    ]
                                ] [
                                    uistyle clampx
                                    uistyle lefttop
                                ] 0 1
                            ]
                        ]
                    ]
                ]
            ] [
                uistyle clampx
                uistyle lefttop
            ]
        ] [
            if (|| (isediting) $showconsole) [
                ui_hud_console $conlinesamt (? $showconsole 0 $conouttime) (? $showconsole 0 $confadetime) (? $showconsole 0 $conintime) (? $showconsole 1 $conblend) (*f $uiaspect 0.75) $ui_textsmall
            ]
        ]
    ]
]
