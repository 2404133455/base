gameui_begin_ids

gameui_maps_mode           = -1
gameui_maps_mode_hover     = -1
gameui_maps_mode_pos       = 0
gameui_maps_mode_hover_pos = 0

gameui_maps_muts           = 0
gameui_maps_mut_hover      = 0

gameui_maps_map            = -1

gameui_maps_num_rot        = 0
gameui_maps_num_other      = 0
gameui_maps_sorted_rot     = []
gameui_maps_sorted_other   = []

gameui_maps_mode_tip_pos   = 0

gameui_maps_confirm = [
    gameui_close_all

    if $gameui_maps_mode [
        local _map_idx
        _map_idx = $gameui_maps_map

        if (< $_map_idx 0) [
            // 0th index is the random selector
            _map_idx = (at $gameui_maps_sorted_rot (rnd $gameui_maps_num_rot 1))
        ]

        start (filename (mapcinfo $_map_idx 0)) $gameui_maps_mode $gameui_maps_muts
    ] [
        demo (filenoext (filename (demoinfo $gameui_maps_map 5)))
    ]
]

gameui_scan_maps = [
    mapcreset 1
    demoreset 1

    looplist curmap (listfiles maps mpz) [
        mapcscan (concatword "maps/" $curmap)
    ]

    looplist curdemo (listfiles demos dmo) [
        demoscan (concatword "demos/" $curdemo ".dmo")
    ]

    mapcsort
    demosort
]
gameui_scan_maps

gameui_sort_maps = [
    local _is_fav _is_rot _rot_fav_list _rot_list _other_fav_list _other_list _list
    _is_fav         = 0
    _is_rot         = 0
    _rot_fav_list   = []
    _rot_list       = []
    _other_fav_list = []
    _other_list     = []
    _list           = []

    // Include the random map selector
    gameui_maps_num_rot   = 1
    gameui_maps_num_other = 0

    loop i (mapcinfo -1) [
        _is_fav = (listhas $mapfavs (mapcinfo $i 0))
        _is_rot = (listhas (getmaplist $gameui_maps_mode $gameui_maps_muts) (filename (mapcinfo $i 0)))

        if $_is_rot [
            _list = (? $_is_fav _rot_fav_list _rot_list)
            gameui_maps_num_rot = (+ $gameui_maps_num_rot 1)
        ] [
            _list = (? $_is_fav _other_fav_list _other_list)
            gameui_maps_num_other = (+ $gameui_maps_num_other 1)
        ]

        $_list = (listsortinsert $$_list $i)
    ]

    // Include the random map selector
    gameui_maps_sorted_rot   = (concat -1 $_rot_fav_list $_rot_list)
    gameui_maps_sorted_other = (concat $_other_fav_list $_other_list)
]

// 1:<index> 2:<id>
ui_gameui_maps_mode = [
    if (ismodelocked $arg1) [] [
        uitarget (uiwidth 0.04) (uiwidth 0.04) [
            local _anim_state_var _is_sel _size
            _anim_state_var = (concatword "gameui_widget_anim_" $arg2)
            _anim_state     = (getalias $_anim_state_var)
            _is_sel         = (= $arg1 $gameui_maps_mode)

            if $_is_sel [
                _anim_state = (minf 1 (+f $_anim_state (*f 0.015 $getupdatemillis)))
            ] [
                _anim_state = (maxf 0 (-f $_anim_state (*f 0.015 $getupdatemillis)))
            ]

            _size = (lerpf (uiwidth 0.03) (uiwidth 0.04) $_anim_state)

            uiimage (modetexlist $arg1) 0xffffff 0 $_size $_size [
                uichangeblends (? $_is_sel 1 0.33)
            ]

            uihover [
                gameui_maps_mode_hover     = $arg1
                gameui_maps_mode_hover_pos = (+f (uigetlastsx) (*f (uigetlastw) 0.5))
            ]

            uirelease [
                // Select first map/demo when switching between demo and normal modes
                caseif (&& [! $gameui_maps_mode] $arg1) [
                    gameui_maps_map = -1
                ] (&& $gameui_maps_mode [! $arg1]) [
                    gameui_maps_map = 0
                ]

                gameui_maps_mode     = $arg1
                gameui_maps_mode_pos = (+f (uigetlastsx) (*f (uigetlastw) 0.5))
                gameui_sort_maps
            ]

            $_anim_state_var = $_anim_state
        ]
    ]
]

// 1:<mutator> 2:<locked>
ui_gameui_maps_mut = [
    uitarget (uiwidth 0.02) (uiwidth 0.02) [
        local _is_sel _size _colour _icon
        _is_sel = (& $arg1 $gameui_maps_muts)
        _size   = (? $_is_sel (uiwidth 0.02) (uiwidth 0.015))
        _colour = (? $arg2 0x000000 0xffffff)
        _icon = (at (modetexlist $gameui_maps_mode $arg1 1) -1)

        if (=s $_icon (modetexlist $gameui_maps_mode)) [
            _icon = (at (modetexlist 2 $arg1 1) -1)
        ]

        uiimage $_icon $_colour 0 $_size $_size [
            uichangeblends (? (&& $_is_sel (! $arg2)) 1 0.33)
        ]

        if $arg2 [] [
            uihover [
                gameui_maps_mut_hover = $arg1
            ]

            uirelease [
                gameui_maps_muts = (^ $gameui_maps_muts $arg1)
            ]

        ]
    ]
]

// 1:<index>
ui_gameui_maps_map = [
    uicolour 0 (uiwidth 0.08) (uiwidth 0.08) [
        if (uidrawn) [
            local _is_fav _is_sel _can_sel _thumbnail _title

            if $gameui_maps_mode [
                _thumbnail = (? (< $arg1 0) "textures/icons/question" (mapcinfo $arg1 0))
                _is_fav    = (listhas $mapfavs (mapcinfo $arg1 0))
                _title     = (? (< $arg1 0) "Random" (mapcinfo @arg1 2))
            ] [
                _thumbnail = (concatword "maps/" (demoinfo $arg1 1))
                _title     = (filenoext (filename (demoinfo $arg1 5)))
            ]

            uithumbnail $_thumbnail (uiwidth 0.04) (uiwidth 0.04)
            if (|| [! $gameui_maps_mode] [>= $arg1 0]) [
                uiclamp- 1 1 1 1
            ]

            _is_sel  = (= $gameui_maps_map $arg1)
            _can_sel = 1

            uioffset 0 (uiwidth 0.05) [
                uiclamp 1 1

                uicolour 0x66010101 0 0.025 [
                    uiclamp 1 1

                    ui_gameui_autoscroll [
                        // Slightly smaller text, to ensure default maps fit in space
                        uitext [@@_title] 0.98
                    ] [
                        p_width = (uiwidth 0.08)
                    ]
                ]
            ]

            if $_is_sel [
                local _pulse
                _pulse = (*f (+f (sin (*f (getmillis) 0.5)) 1) 0.5)

                uispace 0.001 0.001 [
                    uiclamp 1 1 1 1

                    uiborderedimageclamped $skinbordertex (tool_colour_lerp_int 0xff0000 0xffffff $_pulse) 0 $ui_texborder $ui_screenborder
                    uiclamp- 1 1 1 1
                ]
            ]

            if (&& $gameui_maps_mode [>= $arg1 0]) [
                uiimage "textures/icons/action" (? $_is_fav 0xffff55 0xffffff) 0 (uiwidth 0.014) (uiwidth 0.014) [
                    uihover [
                        uichangeblends 0.66
                    ] [
                        uichangeblends 0.33
                    ]

                    uirelease [
                        local _favs
                        _favs = $mapfavs

                        if $_is_fav [
                            _favs = (listdel $_favs (mapcinfo $arg1 0))
                        ] [
                            append _favs (mapcinfo $arg1 0)
                        ]
                        mapfavs $_favs

                        _can_sel = 0
                    ]
                ]
                uialign- 1 -1
            ]

            uirelease [
                if $_can_sel [
                    gameui_maps_map = $arg1
                ]
            ]
        ]
    ]
]

ui_gameui_mappicker = [
    uicolour 0x33010101 (uiwidth $gameui_panel_new_size) 0 [
        uispace (uiwidth 0.05) 0 [
            uiclamp 1 1

            uihlist 0 [
                uiclamp 1 1
                uialign -1 -1

                ui_gameui_vscrollarea [
                    uivlist 0 [
                        uialign -1

                        uigrid 4 0 0 [
                            uialign -1

                            if $gameui_maps_mode [
                                loop i $gameui_maps_num_rot [
                                    ui_gameui_maps_map (at $gameui_maps_sorted_rot $i)
                                ]
                            ] [
                                loop i (demoinfo -1) [
                                    ui_gameui_maps_map $i
                                ]
                            ]
                        ]
                        if (&& $gameui_maps_num_other $gameui_maps_mode) [
                            uicolour 0x33010101 0 0.08 [
                                uiclamp 1 1

                                uispace 0.01 0.01 [
                                    uiclamp 1 1 1 1

                                    uicolourtext "Other maps" 0xcccccc 1.4
                                    uialign- -1 1
                                ]
                            ]
                            uigrid 4 0 0 [
                                uialign -1

                                loop i $gameui_maps_num_other [
                                    ui_gameui_maps_map (at $gameui_maps_sorted_other $i)
                                ]
                            ]
                        ]
                    ]
                ] [
                    p_height = (*f (uiwidth 0.08) 3) // fit for 3 rows
                    p_width  = (*f (uiwidth 0.08) 4) // fit for 4 columns
                ]
                uialign- -1 -1

                uivlist 0.005 [
                    local _image _title _subtext

                    if $gameui_maps_mode [
                        _image   = (mapcinfo $gameui_maps_map 0)
                        _title   = (mapcinfo $gameui_maps_map 2)
                        _subtext = (mapcinfo $gameui_maps_map 3)

                        if (< $gameui_maps_map 0) [
                            _image = "textures/icons/question"
                            _title = "Random"
                        ]
                    ] [
                        _image   = (concatword "maps/" (demoinfo $gameui_maps_map 1))
                        _title   = (filenoext (filename (demoinfo $gameui_maps_map 5)))
                        _subtext = (gamename (demoinfo $gameui_maps_map 2) (demoinfo $gameui_maps_map 3))
                    ]

                    uiclamp 1 1
                    uialign 1 -1

                    uifill (uiwidth 0.2)

                    uiimage $_image 0xffffff 0 (uiwidth 0.12) (uiwidth 0.12)
                    uiborderedimageclamped $skintex 0x55010101 0 $ui_texborder $ui_screenborder 0 0 [
                        uiclamp 1 1

                        uispace 0 0.005 [
                            uiclamp 1 1

                            uitext $_title 1.4
                        ]

                        ui_gameui_shadowhoriz
                    ]
                    if (>= $gameui_maps_map 0) [
                        ui_gameui_autoscroll [
                            uicolourtext $_subtext 0xaaaaaa 1
                        ] [
                            p_width = (uiwidth 0.2)
                        ]
                        if $gameui_maps_mode [
                            uispace 0.01 0 [
                                uiclamp 1 1

                                uiline 0xaaaaaa
                                uiclamp- 1 1
                            ]
                            uicolourtext (mapcinfo $gameui_maps_map 4) 0xcccccc 1 [
                                uitextwrap (uiwidth 0.2)
                            ]
                        ]
                    ]
                ]
            ]
        ]

        ui_gameui_shadowhoriz
    ]
]

# ui_gameui_modepicker [
    uicolour 0x33010101 (uiwidth $gameui_panel_new_size) 0 [
        uivlist 0 [
            uiclamp 1 1

            uihlist 0 [
                #(loopconcat i $modeidxnum [
                    result [
                        ui_gameui_maps_mode @i @(gameui_get_id)
                    ]
                ])
            ]

            local _mode_name _mode_desc _mode_tip_pos _tip_anim_speed
            _mode_desc = ""
            caseif (> $gameui_maps_mode_hover -1) [
                _mode_name    = (modedesc $gameui_maps_mode_hover 0 1)
                _mode_desc    = (modedesc $gameui_maps_mode_hover 0 2)
                _mode_tip_pos = $gameui_maps_mode_hover_pos
            ] (> $gameui_maps_mode -1) [
                _mode_name    = (modedesc $gameui_maps_mode 0 1)
                _mode_desc    = (modedesc $gameui_maps_mode 0 2)
                _mode_tip_pos = $gameui_maps_mode_pos
            ]

            _mode_tip_pos = (-f $_mode_tip_pos (uiwidth $gameui_panel_new_pos))
            _mode_tip_pos = (-f $_mode_tip_pos (*f (uiwidth 0.3) 0.5))

            if (&& $_mode_tip_pos [<=f $gameui_maps_mode_tip_pos 0]) [
                gameui_maps_mode_tip_pos = $_mode_tip_pos
            ] [
                _tip_anim_speed          = (*f 0.015 $getupdatemillis)
                gameui_maps_mode_tip_pos = (-f $gameui_maps_mode_tip_pos (*f (-f $gameui_maps_mode_tip_pos $_mode_tip_pos) $_tip_anim_speed))
            ]

            if (!=s $_mode_desc "") [
                uioffset $gameui_maps_mode_tip_pos 0 [
                    uialign -1 -2
                    uifill (uiwidth 0.3) 0.01 [
                        uivlist 0 [
                            uicolourtext $_mode_name 0xffffff 1.4
                            uicolourtext $_mode_desc 0x88ffffff 1.1
                        ]
                    ]
                ]
            ] [
                // reserve text space
                uivlist 0 [
                    uitext "" 1.4
                    uitext "" 1.1
                ]
            ]
        ]

        ui_gameui_shadowhoriz
    ]
]

ui_gameui_maps_on_open = [
    gameui_sort_maps

    gameui_maps_mode = -1
    gameui_maps_muts = 0
    gameui_maps_map  = -1
]

# ui_gameui_maps [
    gameui_maps_mode_hover = -1
    gameui_maps_mut_hover  = 0

    uivlist 0.025 [
        uialign -1 -1
        uiclamp 1 1

        uifill 0 0.05

        uicolour 0x55010101 (uiwidth $gameui_panel_new_size) 0 [
            ui_gameui_decortext "Map and game mode"
            ui_gameui_shadowhoriz
        ]

        ui_gameui_modepicker
        ui_gameui_mappicker

        uicolour 0x33010101 (uiwidth $gameui_panel_new_size) 0 [
            uispace (uiwidth 0.05) 0 [
                uiclamp 1 1

                uihlist 0 [
                    uiclamp 1 1
                    uialign -1 -1

                    uivlist 0 [
                        uicolourtext "Mutators" 0xcccccc 1.2

                        uiline 0xcccccc (uiwidth 0.16)

                        uigrid 8 0 0 [
                            local _mut _has_mut
                            loop i $mutsidxnum [
                                _mut      = (<< 1 $i)
                                _disabled = 0

                                if (|| [
                                    & (mutsimplied $gameui_maps_mode $gameui_maps_muts) $_mut
                                ] [
                                    ismodelocked $gameui_maps_mode (| $gameui_maps_muts $_mut) $_mut
                                ] [
                                    & $mutslockforce $_mut
                                ]) [
                                    _disabled = 1
                                ]

                                ui_gameui_maps_mut $_mut $_disabled
                            ]
                        ]

                        uihlist 0.005 [
                            if $gameui_maps_mut_hover [
                                uitext (mutsdesc $gameui_maps_mode $gameui_maps_mut_hover 1) 1.2
                                uicolourtext (mutsdesc $gameui_maps_mode $gameui_maps_mut_hover 2) 0x88ffffff 1
                                uialign- -2 1
                            ] [
                                uitext "" 1.2
                            ]
                        ]

                        uialign* -1
                    ]

                    ui_gameui_button [
                        p_label       = (? (isonline) "Vote" "Begin")
                        p_label_size  = 1.5
                        p_width       = 0.3
                        p_on_click    = [
                            gameui_maps_confirm
                        ]
                        p_colour      = #(hsvtohex [8 0.3 1])
                        p_highlight   = 1
                        p_disabled    = (< $gameui_maps_mode 0)
                        p_id          = #(gameui_get_id)
                    ]
                ]
            ]

            ui_gameui_shadowhoriz
        ]
    ]
]

gameui_finish_ids
