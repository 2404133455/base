game_hud_status_bottom_event_show          = 0
game_hud_status_bottom_event_text          = ""
game_hud_status_bottom_event_icon          = ""
game_hud_status_bottom_event_affinity      = 0
game_hud_status_bottom_event_anim          = 0
game_hud_status_bottom_event_affinity_take = 0

GAME_HUD_STATUS_BOTTOM_FADE_TIME = 0.1

// 1:<icon>
ui_game_hud_status_bottom_event = [
    local  _team_colour _affinity_colour _flash_anim _layout_anim _aff_anim _fade_anim _text_size _image_size _glow_size
    _team_colour     = (getteamcolour (getclientteam $ui_hud_focus))
    _flash_anim      = (gameui_anim_segment $game_hud_status_bottom_event_anim 0.85 1)
    _layout_anim     = (smoothstep (gameui_anim_segment $game_hud_status_bottom_event_anim 0.6 0.8))
    _aff_anim        = (gameui_anim_segment $_layout_anim 0.75 1)
    _fade_anim       = (gameui_anim_segment $game_hud_status_bottom_event_anim 0 $GAME_HUD_STATUS_BOTTOM_FADE_TIME)
    _text_size       = (lerpf $game_hud_status_text_size (*f $game_hud_status_text_size 1.5) $_layout_anim)
    _glow_size       = (*f $game_hud_largepanel_width 0.3 $_layout_anim)
    _team_colour     = (tool_colour_hsvmod_int $_team_colour s (-f 1 $_flash_anim))
    _affinity_colour = (game_hud_status_bottom_event_affinity_colour)

    if $game_hud_status_bottom_event_affinity_take [
        _image_size = (*f $game_hud_largepanel_width 0.3)
    ] [
        _image_size = (lerpf 0 (*f $game_hud_largepanel_width 0.3) $_layout_anim)
    ]

    uigroup [
        uialign 0 1

        uivlist 0 [
            uiimage $glowtex $_affinity_colour 0 (*f $_glow_size 1.5) $_glow_size [
                uicolourblend $_flash_anim
            ]

            uifill 0 (lerpf (*f $_glow_size 0.5) 0 $_flash_anim)
        ]

        uivlist (*f $game_hud_largepanel_width 0.01) [
            uiimage $arg1 $_affinity_colour 0 $_image_size $_image_size [
                local _affinity_blend _sub_icon_blend _sub_icon_size _shadow_offset
                _sub_icon_blend = (*f $_aff_anim 0.67 (-f 1 $_flash_anim))
                _sub_icon_size  = (*f $_image_size 0.5)
                _shadow_offset  = (*f $_image_size 0.035)

                if $game_hud_status_bottom_event_affinity_take [
                    if (=f $_aff_anim 0) [
                        game_hud_show_dyn_layout game_hud_player_affinity game_hud_player_affinity_target 750 [
                            uiimage @arg1 (game_hud_status_bottom_event_affinity_colour) 0 $_w $_h [
                                uicolourblend 0.67
                            ]
                        ]
                    ]

                    _affinity_blend = (*f (ceilf $_aff_anim) 0.67 (-f 1 $_flash_anim))
                ] [
                    _affinity_blend = $_sub_icon_blend
                ]

                uicolourblend $_affinity_blend

                uigroup [
                    uialign 1 1

                    uioffset $_shadow_offset $_shadow_offset [
                        uiimage $game_hud_status_bottom_event_icon 0 0 $_sub_icon_size $_sub_icon_size
                    ]

                    uiimage $game_hud_status_bottom_event_icon 0x888888 0 $_sub_icon_size $_sub_icon_size

                    uipropagate [ uicolourblend $_sub_icon_blend ]
                ]
            ]

            uifill 0 (lerpf 0 (*f $game_hud_largepanel_width 0.02) (smoothstep (gameui_anim_dual_edge $_flash_anim 0.45 0.55)))

            uifill 0 0 [
                ui_gameui_advshadowhoriz [
                    p_colour  = $_team_colour
                    p_bgblend = (*f (gameui_anim_pulse 1 0.5 1) $_layout_anim)
                ]

                uispace (*f $game_hud_largepanel_width 0.2) (*f $game_hud_largepanel_width 0.005) [
                    uifont "play/clear" [
                        uitext $game_hud_status_bottom_event_text $_text_size
                    ]
                ]
            ]

            uifill 0 (lerpf 0 0.05 $_layout_anim)
        ]

        uipropagate [
            uicolourblend $_fade_anim
        ]
    ]
]

// Capture the flag
GAME_HUD_STATUS_EVENT_FLAG_SECURE = 0
GAME_HUD_STATUS_EVENT_FLAG_DROP   = 1
GAME_HUD_STATUS_EVENT_FLAG_SCORE  = 2
GAME_HUD_STATUS_EVENT_FLAG_RETURN = 3
// Bomber
GAME_HUD_STATUS_EVENT_BOMB_SECURE = 4
GAME_HUD_STATUS_EVENT_BOMB_DROP   = 5
GAME_HUD_STATUS_EVENT_BOMB_SCORE  = 6

game_hud_status_bottom_event_affinity_colour = [
    case $gamemode $G_CAPTURE [
        getteamcolour (getcaptureteam $game_hud_status_bottom_event_affinity)
    ] $G_BOMBER [
        pulsecolour $PULSE_DISCO
    ]
]

// 1:<event id> 2:<affinity>
game_hud_status_bottom_event = [
    game_hud_status_bottom_event_show     = 1
    game_hud_status_bottom_event_anim     = 1
    game_hud_status_bottom_event_affinity = $arg2

    game_hud_status_bottom_event_affinity_take = 0

    case $arg1 $GAME_HUD_STATUS_EVENT_FLAG_SECURE [
        if (= (getcaptureteam $arg2) (getclientteam $ui_hud_focus)) [
            game_hud_status_bottom_event_text = "You've picked up your flag!"
        ] [
            game_hud_status_bottom_event_text = "You've picked up the enemy flag!"
        ]
        game_hud_status_bottom_event_affinity_take = 1
        game_hud_status_bottom_event_icon = $flagtakentex
    ] $GAME_HUD_STATUS_EVENT_FLAG_DROP [
        if (= (getcaptureteam $arg2) (getclientteam $ui_hud_focus)) [
            game_hud_status_bottom_event_text = "You've dropped your flag!"
        ] [
            game_hud_status_bottom_event_text = "You've dropped the enemy flag!"
        ]
        game_hud_status_bottom_event_icon = $flagdroptex
    ] $GAME_HUD_STATUS_EVENT_FLAG_SCORE [
        game_hud_status_bottom_event_text = "You've scored!"
        game_hud_status_bottom_event_icon = ""
    ] $GAME_HUD_STATUS_EVENT_FLAG_RETURN [
        game_hud_status_bottom_event_text = "You've returned your flag!"
        game_hud_status_bottom_event_icon = ""
    ] $GAME_HUD_STATUS_EVENT_BOMB_SECURE [
        game_hud_status_bottom_event_text = "You've picked up the bomb!"
        game_hud_status_bottom_event_affinity_take = 1
        game_hud_status_bottom_event_icon = $bombtakentex
    ] $GAME_HUD_STATUS_EVENT_BOMB_DROP [
        game_hud_status_bottom_event_text = "You've dropped the bomb!"
        game_hud_status_bottom_event_icon = $bombdroptex
    ] $GAME_HUD_STATUS_EVENT_BOMB_SCORE [
        game_hud_status_bottom_event_text = "You've scored!"
        game_hud_status_bottom_event_icon = ""
    ]
]

ui_game_hud_status_bottom = [
    if $game_hud_status_bottom_event_show [
        local _maintain
        _maintain = 0

        case $gamemode $G_CAPTURE [
            ui_game_hud_status_bottom_event $flagtex

            // Maintain the event if the player is still carrying the flag
            if (= (getcaptureowner $game_hud_status_bottom_event_affinity) $ui_hud_focus) [
                _maintain = 1
            ]
        ] $G_BOMBER [
            ui_game_hud_status_bottom_event $bombtex

            if (= (getbomberowner $game_hud_status_bottom_event_affinity) $ui_hud_focus) [
                _maintain = 1
            ]
        ]

        game_hud_status_bottom_event_anim = (animstep $game_hud_status_bottom_event_anim 3000 -1)

        if $_maintain [
            game_hud_status_bottom_event_anim = (maxf $GAME_HUD_STATUS_BOTTOM_FADE_TIME $game_hud_status_bottom_event_anim)
        ]

        if $game_hud_status_bottom_event_anim [] [
            game_hud_status_bottom_event_show = 0
        ]
    ]
]
