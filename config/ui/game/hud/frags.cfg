game_hud_def_seqstep GAME_HUD_FRAGS_APPEAR 200
game_hud_def_seqstep GAME_HUD_FRAGS_HOLD   4600
game_hud_def_seqstep GAME_HUD_FRAGS_FADE   200

game_hud_def_seq GAME_HUD_FRAGS_SEQ [
    GAME_HUD_FRAGS_APPEAR
    GAME_HUD_FRAGS_HOLD
    GAME_HUD_FRAGS_FADE
]

game_hud_frags_last_fragged      = -1
game_hud_frags_last_fragged_anim = 0

game_hud_frags_last_fragged_by      = -1
game_hud_frags_last_fragged_by_anim = 0

// 1:<client>
game_hud_frag = [
    game_hud_frags_last_fragged      = $arg1
    game_hud_frags_last_fragged_anim = 1
]

// 1:<client>
game_hud_frag_by = [
    game_hud_frags_last_fragged_by      = $arg1
    game_hud_frags_last_fragged_by_anim = 1
]

// 1:<client> 2:<anim> 3:<fragged by?>
ui_game_hud_frag_message = [
    local _font_size _anim_appear _anim_hold _anim_fade _blend _text _shadow_offset
    _font_size = (*f $game_hud_status_text_size 1.0)
    _blend     = 1.0

    _anim_appear = @(game_hud_anim_seq $GAME_HUD_FRAGS_SEQ $GAME_HUD_FRAGS_APPEAR [$arg2])
    _anim_hold   = @(game_hud_anim_seq $GAME_HUD_FRAGS_SEQ $GAME_HUD_FRAGS_HOLD   [$arg2])
    _anim_fade   = @(game_hud_anim_seq $GAME_HUD_FRAGS_SEQ $GAME_HUD_FRAGS_FADE   [$arg2])

    _font_size = (*f $_font_size (lerpf 1.0 0.00001 $_anim_appear))
    _font_size = (*f $_font_size (lerpf 1.2 1.0     $_anim_hold))
    _font_size = (*f $_font_size (lerpf 1.5 1.2     $_anim_fade))

    _blend = (*f $_blend (lerpf 1.0 0.0 $_anim_appear))
    _blend = (*f $_blend (lerpf 0.0 1.0 $_anim_fade))

    if $arg3 [
        _text = (concatword "You were fragged by " (getclientname $arg1) "!")
    ] [
        _text = (concatword "You fragged " (? (= $arg1 $ui_hud_focus) "yourself" (getclientname $arg1)) "!")
    ]

    _shadow_offset = (*f $_font_size 0.0025)

    uigroup [
        uioffset $_shadow_offset $_shadow_offset [
            uicolourtext (stripcolors $_text) 0 $_font_size [
                uicolourblend $_blend
            ]
        ]

        uitext $_text $_font_size [
            uicolourblend $_blend
        ]
    ]
]

ui_game_hud_frags = [
    local _seq_len
    _seq_len = (game_hud_anim_seq_len $GAME_HUD_FRAGS_SEQ)

    uifont "play/clear/bold" [
        uialign 0 -1

        uivlist 0 [
            uifill 0 0.1

            uifill 0 (*f $game_hud_status_width 0.08) [
                if (>f $game_hud_frags_last_fragged_anim 0) [
                    ui_game_hud_frag_message $game_hud_frags_last_fragged $game_hud_frags_last_fragged_anim

                    game_hud_frags_last_fragged_anim = (animstep $game_hud_frags_last_fragged_anim $_seq_len -1)
                ]
            ]

            uifill 0 (*f $game_hud_status_width 0.08) [
                if (>f $game_hud_frags_last_fragged_by_anim 0) [
                    ui_game_hud_frag_message $game_hud_frags_last_fragged_by $game_hud_frags_last_fragged_by_anim 1

                    game_hud_frags_last_fragged_by_anim = (animstep $game_hud_frags_last_fragged_by_anim $_seq_len -1)
                ]
            ]
        ]
    ]
]
