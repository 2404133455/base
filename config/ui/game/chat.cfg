gameui_chat_msg    = ""
gameui_chat_focus  = 0
gameui_chat_typing = 0
gameui_chat_scroll = 0

// 1:<focused>
ui_gameui_chat_history = [
    local _timeout _lines _max_skip _ratio_displayed _scrollbar_length _scrollbar_maxoffset _scrollbar_offset
    _timeout = (? $arg1 0 $chatouttime)
    _lines   = (? $arg1 10 $chatlines)

    uitarget 0 0 [
        uiclamp 1 1

        uivlist 0 [
            uialign -1 -1

            ui_hud_displayconsole 3 (- $_lines) $_timeout $chatfadetime $chatintime $chatblend 1 $ui_text [] $gameui_chat_scroll
            uialign- -1 -1
        ]

        if $arg1 [
            _max_skip            = (max 0 (- (getconlines 3) $_lines))
            _ratio_displayed     = (minf 1 (divf $_lines (getconlines 3)))
            _scrollbar_length    = (maxf (*f $uigetlasth $_ratio_displayed) 0.01)
            _scrollbar_maxoffset = (-f $uigetlasth $_scrollbar_length)
            _scrollbar_offset    = (*f $_scrollbar_maxoffset (divf $gameui_chat_scroll $_max_skip))

            uiscrollup [
                gameui_chat_scroll = (clamp (+ $gameui_chat_scroll 1) 0 $_max_skip)
            ]

            uiscrolldown [
                gameui_chat_scroll = (clamp (- $gameui_chat_scroll 1) 0 $_max_skip)
            ]

            uivlist 0 [
                uiborderedimageclamped $skintex 0x55ffffff 0 $ui_texborder $ui_screenborder 0.005 $_scrollbar_length
                uifill 0 $_scrollbar_offset
            ]
            uialign- 1 1
        ]
    ]
]

ui_gameui_chat = [
    if $gameui_chat_typing [
        local _team_colour
        _team_colour = (getplayerteamcolour)

        uiallowinput 1

        ui_gameui_group [
            ui_gameui_chat_history 1

            uifill 0 0.01

            uihlist 0.005 [
                uivlist 0 [
                    uiimage "textures/icons/chat" 0xffffff 1 0.03 0.03
                    if (= $gameui_chat_typing 2) [
                        uitext "Team:" 0.8
                    ]
                    uifill 0.04
                ]
                uialign- -2 -1

                ui_gameui_textinput gameui_chat_msg 60 [
                    p_prompt    = ""
                    p_focus     = $gameui_chat_focus
                    p_text_size = 1.2
                    p_lines     = 2
                    p_limit     = 100
                    p_on_change = [
                        if (!=s $gameui_chat_msg "") [
                            if (= $gameui_chat_typing 2) [
                                sayteam $gameui_chat_msg
                            ] [
                                say $gameui_chat_msg
                            ]
                        ]
                        gameui_chat_typing = 0
                    ]
                ]
            ]
        ] [
            p_space_x = 0.01
            p_space_y = 0.01
            p_clamp   = 0

            @(? (= $gameui_chat_typing 2) [
                p_colour = (tool_colour_add_alpha (modcolour $_team_colour 0.33) 0x44)
            ] [
                p_colour = 0x44242424
            ])
        ]

        if $uianyfocus [] [
            gameui_chat_typing = 0
        ]
    ] [
        ui_gameui_chat_history
    ]
    uialign- -1 1

    if $gameui_chat_focus [
        gameui_chat_focus = 0
    ]
]

gameui_chat = [
    gameui_chat_scroll = 0
    gameui_chat_msg    = ""
    gameui_chat_focus  = 1
    gameui_chat_typing = 1
]

gameui_chat_team = [
    gameui_chat
    gameui_chat_typing = 2
]
