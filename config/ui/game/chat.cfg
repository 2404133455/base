defvarp chatintime 0 250 $varidxmax
defvarp chatouttime 0 15000 $varidxmax
defvarp chatfadetime 0 500 $varidxmax
defvarp chatlines 0 6 $varidxmax
deffvarp chatblend 0 1 1

gameui_chat_msg    = ""
gameui_chat_focus  = 0
gameui_chat_typing = 0
gameui_chat_scroll = 0

// 1:<focused>
ui_gameui_chat_history = [
    local _timeout _lines _max_skip _ratio_displayed _scrollbar_length _scrollbar_maxoffset _scrollbar_offset
    _timeout = (? $arg1 0 $chatouttime)
    _lines   = (? $arg1 50 $chatlines)

    uitarget 0 0 [
        uistyle clampx

        uivlist 0 [
            uistyle lefttop
            // ui_console_draw (- $_lines) $_timeout $chatfadetime $chatintime $chatblend 1 $ui_text $gameui_chat_scroll
            local message_texth
            uiclip 0 0 0 0 [ uiclipforced 1; uitext "Dummy" $ui_texttiny [ message_texth = $uilasth ] ]
            uivlist 0 [
                uistyle lefttop
                loopmessages (- $_lines) 0 message_id [
                    local message_time; message_time = (getmessagemillis $message_id)
                    local message_millis; message_millis = (- $totalmillis $message_time)
                    local message_blend; message_blend = $chatblend
                    local message_growth; message_growth = 1.0
                    caseif (&& $_timeout (> $message_millis $_timeout)) [
                        local message_ctime; message_ctime = (- $message_millis $_timeout)
                        if (&& $chatfadetime (< $message_ctime $chatfadetime)) [
                            message_growth = (-f 1 (divf $message_ctime $chatfadetime))
                            message_blend = (clampf (*f $message_growth $chatblend) 0 1)
                        ] [ message_blend = 0 ]
                    ] (&& $chatintime (< $message_millis $chatintime)) [
                        message_growth = (divf $message_millis $chatintime)
                        message_blend = (clampf (*f $message_growth $chatblend) 0 1)
                    ]
                    if (>f $message_blend 0.0) [
                        local message_clients; message_clients = (getmessagetaggroups $message_id "client")
                        local message_colour; message_colour = 0
                        loopwhile message_clidx $message_clients [! $message_colour] [
                            message_clientid = (getmessagetag $message_id "client" $message_clidx "clientnum")
                            if (= $focusedplayer $message_clientid) [ message_colour = 0xFF202010 ]
                        ]
                        local message_obj
                        message_obj = [
                            uiimage $infotex 0xFFFFFF 0 0 $message_texth [ uistyle leftmiddle; uiimageaspect 1 ]
                            uitext (getmessagelist $message_id "args" "console") $ui_texttiny [
                                uistyle leftmiddle
                                uitextalign -1
                                uicolourset (getmessagelist $message_id "args" "colour")
                            ]
                        ]
                        local message_type; message_type = (getmessagelist $message_id "args" "type")
                        local message_action; message_action = (getmessagelist $message_id "args" "action")
                        cases $message_type "chat" [
                            message_obj = [
                                uiimage $chattex 0xFFFFFF 0 0 $message_texth [ uistyle leftmiddle; uiimageaspect 1 ]
                                local message_action; message_action = (getmessagelist $message_id "args" "action")
                                if (& $message_action $SAY_ACTION) [
                                    uitext (concat "*" (getmessagename $message_id "client" 0 3 0)) $ui_texttiny [
                                        uistyle leftmiddle
                                        uitextalign -1
                                        uicolourset $colourmagenta
                                    ]
                                    uitext (getmessagelist $message_id "args" "text") $ui_texttiny [
                                        uistyle leftmiddle
                                        uitextalign -1
                                        uicolourset $colourmagenta
                                    ]
                                ] [
                                    uitext (concatword (getmessagename $message_id "client" 0 3 0) ":") $ui_texttiny [
                                        uistyle leftmiddle
                                        uitextalign -1
                                    ]
                                    uitext (getmessagelist $message_id "args" "text") $ui_texttiny [
                                        uistyle leftmiddle
                                        uitextalign -1
                                    ]
                                ]
                            ]
                        ]
                        uiclip 0 0 0 0 [
                            uistyle lefttop
                            uispace 0 $ui_padtiny [
                                uistyle lefttop
                                uiborderedimageclamped $skinalphatex (? $message_colour $message_colour 0xDD000000) 0 $ui_texborder $ui_screenborder 0 0 [
                                    uistyle lefttop
                                    local message_height
                                    uispace $ui_padsmall $ui_padsmaller [
                                        uistyle lefttop
                                        uihlist $ui_padbutton [
                                            uistyle leftmiddle
                                            message_obj
                                        ]
                                    ]
                                    uipropagate [ uicolourblend $message_blend ]
                                ]
                            ]
                            message_height = (+f $ui_padsmaller $ui_padsmaller $uilasthprev)
                            uiclipsizeh (*f $message_height $message_growth)
                        ]
                    ]
                ]
            ]
        ]
    ]
]

ui_gameui_chat = [
    if $gameui_chat_typing [
        local _team_colour
        _team_colour = $playerteamcolour

        uiallowinput 1

        ui_gameui_group [
            ui_gameui_vscrollarea [
                ui_gameui_chat_history 1
            ] [
                p_width  = 0.98
                p_height = 0.15
                p_inverted = 1
            ]
            uiprev [uialign -1 1]

            uifill 0 0.01

            uihlist 0.005 [
                uivlist 0 [
                    uiimage "textures/icons/chat" 0xffffff 1 0.03 0.03
                    if (= $gameui_chat_typing 2) [
                        uitext "Team:" 0.8
                    ]
                    uifill 0.04
                ]
                uiprev [uialign -2 -1]

                ui_gameui_textinput gameui_chat_msg 74 [
                    p_prompt    = ""
                    p_focus     = $gameui_chat_focus
                    p_text_size = 1.2
                    p_lines     = 2
                    p_limit     = 100
                    p_on_change = [
                        if (!=s $gameui_chat_msg "") [
                            local say_action; say_action = (=s (at $gameui_chat_msg 0) "/me")
                            local say_type; say_type = (? $say_action "me" "say")
                            local say_text; say_text = (? $say_action (substr $gameui_chat_msg 4) $gameui_chat_msg)
                            if (= $gameui_chat_typing 2) [
                                [@say_type]team $say_text
                            ] [
                                [@say_type] $say_text
                            ]
                        ]
                        gameui_chat_typing = 0
                    ]
                ]
            ]
        ] [
            p_space_x = 0.01
            p_space_y = 0.01
            p_clamp   = 0

            @(? (= $gameui_chat_typing 2) [
                p_colour = (tool_colour_add_alpha (modcolour $_team_colour 0.33) 0x44)
            ] [
                p_colour = 0x44242424
            ])
        ]

        if $uianyfocus [] [
            gameui_chat_typing = 0
        ]
    ] [
        ui_gameui_chat_history
    ]
    uiprev [uialign -1 1]

    if $gameui_chat_focus [
        gameui_chat_focus = 0
    ]
]

gameui_chat = [
    gameui_chat_scroll = 0
    gameui_chat_msg    = ""
    gameui_chat_focus  = 1
    gameui_chat_typing = 1
]

gameui_chat_team = [
    gameui_chat
    gameui_chat_typing = 2
]
