gameui_begin_ids

gameui_vote_entry_counter = 0

ui_gameui_vote_entry_mapshot = [
    uipad 0.02 0 0 0 [
        uialign -1
        uicolour 0 0.2 0.1 [
            uitext "?" 4
            uicroppedimage (concatword "maps/" $_mapname) 0xffffff 0 0.2 0.1 0 0.25 1 0.75 [
                uifill 0.2 0.1 [
                    uioffset 0 0.06 [
                        uiclamp 1 1 1 1

                        uicolour 0x66010101 0 0.025 [
                            ui_gameui_shadowhoriz
                        ]
                        uiclamp- 1 1
                    ]
                    uispace 0.01 0.01 [
                        uialign 1 1

                        ui_gameui_autoscrollh [
                            uitext $_maptitle
                        ] [
                            p_width = 0.18
                        ]
                    ]
                ]
            ]
        ]
    ]
]

# ui_gameui_vote_entry_info [
    uispace 0.01 0.01 [
        uiclamp 1 1 1 1

        uialign -1 -1

        uifill 0 0 [
            uiclamp 1 1 1 1

            uivlist 0 [
                uialign -1 -1

                ui_gameui_autoscrollh [
                    uitext $_gamemode 1.3
                ] [
                    p_width = (uiwidth 0.3)
                ]

                uialign* -1
            ]

            uihlist 0 [
                uialign 1 1

                uicolourtext $_votes 0xcccccc 1.2
                uioffset 0 0.003 [
                    uicolourtext " votes" 0xaaaaaa
                ]
            ]
        ]
    ]
]

# ui_gameui_vote_entry [
    local _bg

    _bg = (? (& $gameui_vote_entry_counter 1) 0x555555 0x888888)

    uiborderedimageclamped $skintex $_bg 0 $ui_texborder $ui_screenborder (uiwidth 0.5) 0.1 [
        if (uidrawn) [
            local _mapname _maptitle _gamemode _votes
            _mapname  = (getvote $arg1 3)
            _maptitle = (getmaptitle $_mapname)
            _gamemode = (gamename (getvote $arg1 1) (getvote $arg1 2) 0 32)
            _votes    = (getvote $arg1 0 -1)

            uihover [
                uichangeblends 0.66
            ] [
                uichangeblends 0.33
            ]

            uidoublepress $arg1 [
                gameui_close_all
                start $_mapname (getvote $arg1 1) (getvote $arg1 2)
            ]

            ui_gameui_vote_entry_mapshot

            uifill (-f (uiwidth 0.5) 0.2 0.02) 0 [
                uiclamp 0 0 1 1
                uialign 1

                ui_gameui_vote_entry_info
            ]
        ]

        ui_gameui_shadow
    ]

    gameui_vote_entry_counter = (+ $gameui_vote_entry_counter 1)
]

ui_gameui_vote_dims = 0.66

# ui_gameui_vote [
    gameui_vote_entry_counter = 0

    uivlist 0.025 [
        uialign -1 -1
        uiclamp 1 1

        uifill 0 0.05

        uicolour 0x55010101 (uiwidth $gameui_panel_new_size) 0 [
            ui_gameui_decortext "Vote"
            ui_gameui_shadowhoriz
        ]

        uihlist 0.02 [
            uialign -1 -2

            uispace 0.02 0 [
                uialign -1 -1

                uivlist 0.005 [
                    ui_gameui_button [
                        p_label       = "New vote"
                        p_label_size  = 1.5
                        p_width       = 0.2
                        p_on_click    = [
                            gameui_open ui_gameui_maps
                        ]
                        p_highlight   = 1
                        p_id          = #(gameui_get_id button)
                    ]

                    ui_gameui_button [
                        p_label       = "Remove vote"
                        p_label_size  = 1.5
                        p_width       = 0.2
                        p_on_click    = [
                            clearvote
                        ]
                        p_id          = #(gameui_get_id button)
                    ]
                ]
            ]

            ui_gameui_vscrollarea [
                uiborderedimageclamped $skintex 0x44010101 0 $ui_texborder $ui_screenborder (uiwidth 0.5) 0.7 [
                    if (= (getvote -1)) [
                        uitext "No votes yet"
                    ] [
                        uivlist 0 [
                            uialign -1 -1

                            loopvotes i [
                                ui_gameui_vote_entry $i
                            ]
                        ]
                    ]
                ]
            ] [
                p_width  = (uiwidth 0.5)
                p_height = 0.7
            ]
        ]
    ]
]

gameui_finish_ids

gameui_vote_show = [
    gameui_open ui_gameui_vote
]
onevent $CMD_EVENT_GAME_VOTE gameui_vote_show
