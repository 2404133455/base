gameui_online_entry_counter = 0
gameui_online_private_desc  = ""
gameui_online_private_addr  = ""
gameui_online_private_port  = 0
gameui_online_private_pswd = ""

gameui_online_update = [
    if (>=f $gameui_panel_anim 1) [
        updateservers
    ]
]

ui_gameui_online_connect_on_close = [
    if (> $connectstatus 0) [
        disconnect
    ]
]

ui_gameui_online_connect_dims = 0.4

# ui_gameui_online_connect [
    #(gameui_begin_ids)

    if (= $connectstatus 0) [
        gameui_close_all
    ]

    local _text
    _text = ""

    caseif (< $connectstatus 0) [
        _text = "Connection failed"
    ] (= $connectstatus 1) [
        _text = "Connecting..."
    ] (> $connectstatus 1) [
        _text = (format "Connection attempt %1 of %2..." $connectstatus (+ $connectattempts 1))
    ]

    uivlist 0.025 [
        uiclamp 1 1

        uicolour 0x55010101 (uiwidth $gameui_panel_new_size) 0 [
            uivlist 0 [
                uihlist 0 [
                    if (> $connectstatus 0) [
                        uiimage $uiloadtex 0xffffff 0 0.05 0.05
                    ]
                    uitext $_text 2
                ]

                ui_gameui_autoscrollh [
                    uicolourtext $gameui_online_private_desc 0xcccccc
                ] [
                    p_width = (uiwidth $gameui_panel_new_size)
                ]
            ]
        ]

        ui_gameui_prettybutton [
            p_label       = (? (> $connectstatus 0) "Cancel" "Ok")
            p_label_align = 0
            p_width       = 0.2
            p_pad_h_shift = 0
            p_on_click    = [
                gameui_close
            ]
            p_id          = #(gameui_get_id prettybutton)
        ]
    ]
]

ui_gameui_online_private_dims = 0.4

# ui_gameui_online_private [
    #(gameui_begin_ids)

    gameui_online_update

    local _anim_smooth _img_size _password
    _anim_smooth = (-f 1 (*f (-f 1 $gameui_panel_anim) (-f 1 $gameui_panel_anim)))
    _img_size    = (*f (uiwidth $gameui_panel_new_size) $_anim_smooth)

    uispace 0 0 [
        uiimage "<grey>textures/servers/locked" #(hsvtohex [0 0.66 1]) 0 $_img_size $_img_size [
            uichangeblends 0.1
        ]
    ]

    uivlist 0.025 [
        uiclamp 1 1

        uicolour 0x55010101 (uiwidth $gameui_panel_new_size) 0 [
            uivlist 0 [
                uitext "Server password required" 2
                ui_gameui_autoscrollh [
                    uicolourtext $gameui_online_private_desc 0xcccccc
                ] [
                    p_width = (uiwidth $gameui_panel_new_size)
                ]
            ]
        ]

        ui_gameui_textinput gameui_online_private_pswd 24 [
            p_prompt = "[Enter password here]"
        ]

        ui_gameui_button [
            p_label       = "Confirm"
            p_width       = 0.2
            p_on_click    = [
                gameui_close
                gameui_open ui_gameui_online_connect
                connect $gameui_online_private_addr $gameui_online_private_port
            ]
            p_id          = #(gameui_get_id button)
        ]
    ]
]

ui_gameui_online_entry_mapshot = [
    uipad 0.02 0 0 0 [
        uialign -1
        uicolour 0 0.2 0.1 [
            if (>= $_lastping 0) [
                uitext "?" 4
                uicroppedimage (concatword "maps/" $_mapname) 0xffffff 0 0.2 0.1 0 0.25 1 0.75 [
                    uifill 0.2 0.1 [
                        uioffset 0 0.06 [
                            uiclamp 1 1 1 1

                            uicolour 0x66010101 0 0.025 [
                                ui_gameui_shadowhoriz
                            ]
                            uiclamp- 1 1
                        ]
                        uispace 0.01 0.01 [
                            uialign 1 1

                            ui_gameui_autoscrollh [
                                uitext $_maptitle
                            ] [
                                p_width = 0.18
                            ]
                        ]
                    ]
                ]
            ] [
                uiimage $uiloadtex 0xffffff 0 0.05 0.05
            ]
        ]
    ]
]

# ui_gameui_online_entry_info [
    uispace 0.01 0.01 [
        uiclamp 1 1 1 1

        uialign -1 -1

        uifill 0 0 [
            uiclamp 1 1 1 1

            uivlist 0 [
                uialign -1 -1

                ui_gameui_autoscrollh [
                    uitext $_desc 1.3
                ] [
                    p_width = (uiwidth (? (<f $uiaspect 1.5) 0.25 0.3))
                ]

                uialign* -1
            ]

            if (>= $_lastping 0) [
                uicolourtext $_gamemode 0xbbbbbb 1.2
                uialign- -1 1

                uivlist 0 [
                    uialign 1

                    uihlist 0 [
                        uialign 1 -1

                        uicolourtext $_nump (? (>= $_nump $_maxp) #(hsvtohex [0 0.8 1]) 0x999999) 1.2
                        uialign- -2 -1
                        uicolourtext "/" 0xcccccc 1.5
                        uicolourtext $_maxp 0x999999 1.2
                        uialign- -2 1
                    ]

                    uihlist 0 [
                        uialign 1 1

                        uicolourtext $_ping $_ping_color 1.2
                        uioffset 0 0.003 [
                            uicolourtext " ms" 0xcccccc
                        ]
                    ]
                ]
            ]
        ]
    ]
]

# ui_gameui_online_entry [
    local _bg

    _bg = (? (& $gameui_online_entry_counter 1) 0x555555 0x888888)

    uiborderedimageclamped $skintex $_bg 0 $ui_texborder $ui_screenborder (uiwidth 0.5) 0.1 [
        if (uidrawn) [
            local _desc _ping _ping_color _mapname _maptitle _gamemode _maxp _nump _lastping _stat _stat_icon _stat_color
            _desc     = (getserver $arg1 0 3)
            _ping     = (getserver $arg1 0 6)
            _mapname  = (getserver $arg1 0 4)
            _maptitle = (getmaptitle $_mapname)
            _gamemode = (gamename (getserver $arg1 1 1) (getserver $arg1 1 2) 0 32)
            _maxp     = (getserver $arg1 1 4)
            _nump     = (getserver $arg1 0 5)
            _lastping = (getserver $arg1 0 7)
            _stat     = (getserver $arg1 0 0)

            uihover [
                uichangeblends 0.66
            ] [
                uichangeblends 0.33
            ]

            uidoublepress $arg1 [
                local _addr _port
                _addr = (getserver $arg1 0 1)
                _port = (getserver $arg1 0 2)

                if (= $_stat 2) [
                    gameui_online_private_desc = $_desc
                    gameui_online_private_addr = $_addr
                    gameui_online_private_port = $_port
                    gameui_online_private_pswd = ""
                    gameui_open ui_gameui_online_private
                ] [
                    gameui_open ui_gameui_online_connect
                    connect $_addr $_port
                ]
            ]

            caseif (< $_ping 150) [
                _ping_color = #(hsvtohex [120 0.66 1])
            ] (< $_ping 300) [
                _ping_color = #(hsvtohex [60 0.66 1])
            ] (1) [
                _ping_color = #(hsvtohex [0 0.66 1])
            ]

            case $_stat 1 [
                _stat_icon  = "<grey>textures/servers/locked"
                _stat_color = #(hsvtohex [60 0.66 1])
            ] 2 [
                _stat_icon  = "<grey>textures/servers/locked"
                _stat_color = #(hsvtohex [0 0.66 1])
            ]

            ui_gameui_online_entry_mapshot

            uifill (-f (uiwidth 0.5) 0.2 0.02) 0 [
                uiclamp 0 0 1 1
                uialign 1

                ui_gameui_online_entry_info

                if $_stat_icon [
                    uipad (uiwidth 0.15) 0 0 0 [
                        uicroppedimage $_stat_icon $_stat_color 0 0.2 0.1 0 0.25 1 0.5 [
                            uichangeblends 0.25
                        ]
                    ]
                ]
            ]
        ]

        ui_gameui_shadow
    ]

    gameui_online_entry_counter = (+ $gameui_online_entry_counter 1)
]

ui_gameui_online_dims = 0.66

# ui_gameui_online [
    #(gameui_begin_ids)

    gameui_online_entry_counter = 0
    gameui_online_update

    local _total_players _total_servers
    _total_players = (getserverplayersif i [= (getserver $i 1 0) (getversion 1)])
    _total_servers = (getservercountif   i [= (getserver $i 1 0) (getversion 1)])

    uivlist 0.025 [
        uialign -1 -1
        uiclamp 1 1

        uifill 0 0.05

        uicolour 0x55010101 (uiwidth $gameui_panel_new_size) 0 [
            ui_gameui_decortext "Server browser"
            ui_gameui_shadowhoriz
        ]

        uihlist 0.02 [
            uialign -1 -2

            uispace 0.02 0 [
                uialign -1 -1

                uivlist 0.005 [
                    uihlist 0.005 [
                        uiimage "textures/icons/player" 0xffffff 0 0.025 0.025
                        uitext $_total_players 1.5

                        uifill 0.01

                        uiimage "textures/servers/list" 0xffffff 0 0.025 0.025
                        uitext $_total_servers 1.5
                    ]

                    ui_gameui_button [
                        p_label       = "Refresh list"
                        p_label_size  = (uiwidth 0.83)
                        p_width       = (uiwidth 0.11)
                        p_on_click    = [
                            updatefrommaster
                        ]
                        p_id          = #(gameui_get_id button)
                    ]

                    ui_gameui_button [
                        p_label       = "LAN Connect"
                        p_label_size  = (uiwidth 0.83)
                        p_width       = (uiwidth 0.11)
                        p_on_click    = [
                            gameui_online_private_desc = "LAN"
                            gameui_open ui_gameui_online_connect
                            lanconnect
                        ]
                        p_id          = #(gameui_get_id button)
                    ]
                ]
            ]

            ui_gameui_vscrollarea [
                uiborderedimageclamped $skintex 0x44010101 0 $ui_texborder $ui_screenborder (uiwidth 0.5) 0.7 [
                    if (= $_total_servers 0) [
                        uihlist 0 [
                            uiimage $uiloadtex 0xffffff 0 0.05 0.05
                            uitext "Looking for servers..."
                        ]
                    ] [
                        uivlist 0 [
                            uialign -1 -1

                            loopserversif i [= (getserver $i 1 0) (getversion 1)] [
                                ui_gameui_online_entry $i
                            ]
                        ]
                    ]

                    ui_gameui_shadow
                ]
            ] [
                p_width  = (uiwidth 0.5)
                p_height = 0.7
            ]
        ]
    ]

    #(gameui_finish_ids)
]
