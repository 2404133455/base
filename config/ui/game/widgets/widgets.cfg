exec "config/ui/game/widgets/button.cfg"

gameui_autoscroll_props = [
    [ p_width   0.5 ]
]

// 1:<content> 2:<props>
ui_gameui_autoscroll = [
    @(props $gameui_autoscroll_props arg2)

    uiclip $p_width 0 0 0 [
        arg1
        uiclipoffsetx (*f $gameui_autoscroll (-f (uigetclipvirtw) $p_width))
    ]
]

gameui_scrollarea_props = [
    [ p_slider_size        0.01  ]
    [ p_scrollbar_spacing  0.005 ]
    [ p_scrollbar_autohide 1     ]
    [ p_scroll_speed       1     ]
    [ p_width              0.5   ]
    [ p_height             0.1   ]
]

// 1:<content> 2:<props>
ui_gameui_vscrollarea = [
    @(props $gameui_scrollarea_props arg2)

    local _has_scrollbar
    _has_scrollbar = 0

    uihlist $p_scrollbar_spacing [
        uiscroll 0 $p_height [
            uialign -2 -1

            uifill $p_width
            arg1

            if (|| (! $p_scrollbar_autohide) [>f (uigetclipvirth) $p_height]) [
                _has_scrollbar = 1
            ]
        ]
        if $_has_scrollbar [
            uigroup [
                uiscrollarrow (*f $p_scroll_speed -1)
                uiscrollarrow $p_scroll_speed
                uivscrollbar [
                    uifill $p_slider_size $p_height
                    uiscrollbutton [
                        uiborderedimageclamped $skintex 0x55ffffff 0 $ui_texborder $ui_screenborder $p_slider_size 0 [
                            uiclamp 1 1 1 1
                        ]
                    ]
                ]
            ]
        ] [
            uifill $p_slider_size $p_height
        ]
    ]
]

// 1:<content> 2:<props>
ui_gameui_hscrollarea = [
    @(props $gameui_scrollarea_props arg2)

    local _has_scrollbar
    _has_scrollbar = 0

    uivlist $p_scrollbar_spacing [
        uiscroll $p_width 0 [
            uialign -2 -1

            uifill $p_height
            arg1

            if (|| (! $p_scrollbar_autohide) [>f (uigetclipvirtw) $p_width]) [
                _has_scrollbar = 1
            ]
        ]
        if $_has_scrollbar [
            uigroup [
                uiscrollarrow (*f $p_scroll_speed -1)
                uiscrollarrow $p_scroll_speed
                uihscrollbar [
                    uifill $p_width $p_slider_size
                    uiscrollbutton [
                        uiborderedimageclamped $skintex 0x55ffffff 0 $ui_texborder $ui_screenborder 0 $p_slider_size [
                            uiclamp 1 1 1 1
                        ]
                    ]
                ]
            ]
        ] [
            uifill $p_width $p_slider_size
        ]
    ]
]

// 1:<var>
ui_gameui_textinput_prompt = [
    if (&& [=s $$arg1 ""] [! (uifocus?)]) [
        uicolourtext $p_prompt 0x555555 1
    ]
]

gameui_textinput_props = [
    [ p_prompt    ["[Enter text here"] ]
    [ p_width     0                    ]
    [ p_immediate 0                    ]
    [ p_on_change []                   ]
]

// 1:<var> 2:<length> 3:<props>
ui_gameui_textinput = [
    @(props $gameui_textinput_props arg3)

    uiborderedimageclamped $skintex 0x222222 0 $ui_texborder $ui_screenborder $p_width 0 [
        uichangeblends 0.66
        uifield $arg1 $arg2 [
            p_on_change
        ] 1.5 $p_immediate [
            ui_gameui_textinput_prompt $arg1
        ]

        ui_gameui_shadow
    ]
]

gameui_decortext_props = [
    [ p_label_size      2                 ]
    [ p_colour          0xffffff          ]
    [ p_light_intensity 0.15              ]
    [ p_pad_h           [(uiwidth 0.027)] ]
    [ p_pad_v           0.01              ]
    [ p_width           [(uiwidth 0.5)]   ]
]

// 1:<text> 2:<props>
# ui_gameui_decortext [
    @(props $gameui_decortext_props arg2)

    local _anim_state _light_blend _light_blend_anim _light_len
    _anim_state       = (*f (+f (sin (*f (getmillis) 0.1)) 1) 0.5)
    _light_blend_anim = (lerpf 0.25 1 $_anim_state)
    _light_blend      = (*f $_light_blend_anim (lerpf 1 $p_light_intensity $_light_blend_anim))
    _light_len        = (*f $p_width (lerpf 0.66 1 $_anim_state))

    uifill $p_width 0 [
        uiclip $p_width 0 0 0 [
            uioffset (*f (sin (*f (getmillis) 0.04)) 0.3 $p_width) (*f (cos (*f (getmillis) 0.04)) 0.01) [
                uiimage "particles/entity" #(hsvtohex [8 0.2 1]) 0 0.5 0.05 [
                    uichangeblends $_light_blend
                ]
            ]
        ]

        uiimage "particles/stain" #(hsvtohex [8 0.8 1]) 0 $_light_len 0.02 [
            uichangeblends $_light_blend
        ]

        uiimage "particles/stain" #(hsvtohex [8 0.1 1]) 0 $_light_len 0.01 [
            uichangeblends $_light_blend
        ]

        uiimage "particles/entity" #(hsvtohex [8 0.2 1]) 0 $_light_len 0.04 [
            uichangeblends (*f $_light_blend 0.75)
        ]

        uispace $p_pad_h $p_pad_v [
            uiimage "<invert>textures/blob" #(hsvtohex [8 0.5 0.5]) 0 0 0 [
                uichangeblends (*f $_light_blend 0.33)
            ]
            uiclamp- 1 1 1 1

            uicolourtext $arg1 $p_colour $p_label_size

            uialign $p_label_align
        ]
    ]
]

gameui_slider_drag_id              = -1
gameui_slider_drag_cursor_pivot    = 0
gameui_slider_drag_offset          = -1
gameui_slider_drag_init_offset     = -1

gameui_slider_props = [
    [ p_width        [(uiwidth 0.2)]  ]
    [ p_slider_width [(uiwidth 0.05)] ]
    [ p_val_text     [[result $arg1]] ]
    [ p_min          []               ]
    [ p_max          []               ]
    [ p_val_format   []               ]
    [ p_on_change    []               ]
    [ p_tip          ""               ]
    [ p_id           []               ]
]

// 1:<var> 2:<props>
# ui_gameui_slider [
    @(props $gameui_slider_props arg2)

    local _min _max _range _max_slider_offset _slider_offset _drag _factor _line_offset _new_val
    _max_slider_offset = (-f $p_width $p_slider_width)
    _drag              = (=s $gameui_slider_drag_id $p_id)

    if (=s $p_val_format []) [
        p_val_format = (? (= (getvartype $arg1) $ididxvar) i f)
    ]

    cases $p_val_format i [
        _min   = (? (!=s $p_min) $p_min (getvarmin $arg1))
        _max   = (? (!=s $p_max) $p_max (getvarmax $arg1))
        _range = (- $_max $_min)
    ] f [
        _min   = (? (!=s $p_min) $p_min (getfvarmin $arg1))
        _max   = (? (!=s $p_max) $p_max (getfvarmax $arg1))
        _range = (-f $_max $_min)

        if (=s $p_val_text [result $arg1]) [
            p_val_text = [round $arg1 0.01]
        ]
    ]

    if $_drag [
        gameui_slider_drag_offset = (-f (uicursorx) $gameui_slider_drag_cursor_pivot)
        gameui_slider_drag_offset = (+f $gameui_slider_drag_offset $gameui_slider_drag_init_offset)
        gameui_slider_drag_offset = (clampf $gameui_slider_drag_offset 0 $_max_slider_offset)

        _slider_offset = $gameui_slider_drag_offset

        // Calculate new value from the current offset
        _factor = (divf $_slider_offset $_max_slider_offset)

        if (=s $p_val_format i) [
            _new_val = (toint (+f $_min (*f $_range $_factor)))

            if (!=s $$arg1 $_new_val) [
                set $arg1 $_new_val
                p_on_change
            ]
        ] [
            _new_val = (+f $_min (*f $_range $_factor))

            if (!=f $$arg1 $_new_val) [
                set $arg1 $_new_val
                p_on_change
            ]
        ]
    ] [
        _factor        = (divf (-f $$arg1 $_min) $_range)
        _slider_offset = (clampf (*f $_factor $_max_slider_offset) 0 $_max_slider_offset)
    ]

    uitarget $p_width 0 [
        uihold [] [
            if $_drag [
                _drag                 = 0
                gameui_slider_drag_id = -1
            ]
        ]

        uihover [
            if (gameui_can_interact) [
                gameui_hover
            ]
        ]

        // 17 horizontal lines marking regular intervals
        loop i 17 [
            _line_offset = (*f $p_width (divf $i 16))

            // Don't draw the line behind the slider
            if (|| [<f $_line_offset $_slider_offset] [>f $_line_offset (+f $_slider_offset $p_slider_width)]) [
                uioffset $_line_offset 0 [
                    uialign -1
                    uiline 0x888888 0 (? (modf $i 4) 0.005 0.008)
                ]
            ]
        ]

        if (>f $_slider_offset 0) [
            uiline 0x888888 $_slider_offset
            uialign- -1
        ]

        uiline 0x888888 (-f $p_width $_slider_offset $p_slider_width)
        uialign- 1

        uioffset $_slider_offset 0 [
            uialign -1

            ui_gameui_button [
                p_label      = @(p_val_text $$arg1 $_factor)
                p_label_size = 1
                p_width      = @p_slider_width
                p_height     = 0.025
                p_colour     = (? @_drag #(hsvtohex [8 0.3 1]) 0xcccccc)
                p_on_press   = [
                    _drag                           = 1
                    gameui_slider_drag_id           = @@p_id
                    gameui_slider_drag_offset       = 0
                    gameui_slider_drag_init_offset  = @@_slider_offset
                    gameui_slider_drag_cursor_pivot = (uicursorx)
                ]
                p_tip        = [@@p_tip]
                p_id         = @p_id
            ]
        ]
    ]
]

ui_gameui_shadow = [
    uiborderedimageclamped "textures/hud/uiskinshadow" 0x66ffffff 0 $ui_texborder $ui_screenborder 0 0 [
        uiclamp 1 1 1 1
    ]
]

ui_gameui_shadowhoriz = [
    uiborderedimageclamped "textures/hud/uiskinshadowhoriz" 0x66ffffff 0 $ui_texborder $ui_screenborder 0 0 [
        uiclamp 1 1 1 1
    ]
]

# gameui_group_props [
    [ p_label        ""                      ]
    [ p_width        0                       ]
    [ p_height       0                       ]
    [ p_space        0.02                    ]
    [ p_colour       #(hsvtohex [8 0.1 0.5]) ]
    [ p_border_scale 0.5                     ]
    [ p_clamp        1                       ]
]

// 1:<children> 2:<props>
ui_gameui_group = [
    @(props $gameui_group_props arg2)

    uiborderedimageclamped $skintex 0x88222222 0 $ui_texborder $ui_screenborder $p_width $p_height [
        uiclamp $p_clamp $p_clamp

        uispace $p_space $p_space [
            uiclamp 1 1

            uivlist 0 [
                if (!=s $p_label "") [
                    uitext $p_label 1.5
                    uialign- -1 -1

                    uifill 0 0.01
                ]

                arg1
            ]
        ]

        ui_gameui_shadow
    ]
]

// 1:<children> 2:<props>
ui_gameui_horizgroup = [
    @(props $gameui_group_props arg2)

    uiborderedimageclamped $skintex 0x88222222 0 $ui_texborder $ui_screenborder 0 $p_height [
        uiclamp 1 1

        uispace $p_space $p_space [
            uiclamp 1 1

            uivlist 0 [
                uifill $p_width

                if (!=s $p_label "") [
                    uitext $p_label 1.5
                    uialign- -1 -1

                    uifill 0 0.01
                ]

                arg1
            ]
        ]

        ui_gameui_shadowhoriz
    ]
]

gameui_switch_var       = []
gameui_switch_get       = []
gameui_switch_set       = []
gameui_switch_on_change = []

gameui_switch_update = [
    local _new_val
    _new_val = (+ (gameui_switch_get $$gameui_switch_var) $arg1)
    set $gameui_switch_var (gameui_switch_set $_new_val)

    gameui_switch_on_change
]

gameui_switch_props = [
    [ p_width     [(uiwidth 0.16)] ]
    [ p_options   [["Off" "On"]]   ]
    [ p_custom    [[result ""]]    ]
    [ p_tip       ""               ]
    [ p_get       [[result $arg1]] ]
    [ p_set       [[result $arg1]] ]
    [ p_on_change []               ]
    [ p_max_opts  22               ]
    [ p_disabled  0                ]
    [ p_id        []               ]
]

// 1:<var> 2:<props>
ui_gameui_switch = [
    @(props $gameui_switch_props arg2)

    local _num_options _val _circle_colour _circle_size _mid_size _has_dots _text
    _num_options = (listlen $p_options)
    _mid_size    = (-f $p_width 0.06)

    if $p_disabled [
        _val      = -1
        _text     = (p_custom)
        _has_dots = 0
    ] [
        _val      = (p_get (get $arg1))
        _text     = (? (&& (>= $_val 0) (< $_val $_num_options)) (at $p_options $_val) (p_custom))
        _has_dots = (<= $_num_options $p_max_opts)
    ]

    uitarget $p_width 0 [
        if $p_disabled [] [
            uirelease [
                gameui_switch_var       = $arg1
                gameui_switch_get       = $p_get
                gameui_switch_set       = $p_set
                gameui_switch_on_change = $p_on_change
            ]

        ]

        uihover [
            if (gameui_can_interact) [
                gameui_hover
            ]
        ]

        uihlist 0 [
            uiclamp 1 1

            ui_gameui_button [
                p_width     = 0.03
                p_height    = 0.03
                p_on_click  = [
                    gameui_switch_update -1
                ]
                p_disabled  = @(|| $p_disabled [<= $_val 0])
                p_icon_size = 0.01
                p_children  = [
                    uitriangle $_colour $_icon_size $_icon_size 90
                ]
                p_tip       = [@@p_tip]
                p_id        = @p_id
            ]

            uiborderedimageclamped $skintex (? $p_disabled 0x44010101 0x22010101) 0 $ui_texborder $ui_screenborder $_mid_size 0.03 [
                uivlist 0 [
                    uicolourtext $_text (? $p_disabled 0x888888 0xffffff) 0.95

                    if $_has_dots [
                        uihlist 0.005 [
                            loop i $_num_options [
                                if (= $i $_val) [
                                    _circle_colour = 0xcccccc
                                    _circle_size   = 0.006
                                ] [
                                    _circle_colour = 0x88888888
                                    _circle_size   = 0.004
                                ]

                                uiimage "textures/hud/blip" $_circle_colour 0 $_circle_size $_circle_size
                            ]
                        ]

                        uifill 0 0.002
                    ]
                ]

                ui_gameui_shadow
            ]

            ui_gameui_button [
                p_width     = 0.03
                p_height    = 0.03
                p_on_click  = [
                    gameui_switch_update 1
                ]
                p_disabled  = @(|| $p_disabled [>= $_val (- $_num_options 1)])
                p_icon_size = 0.01
                p_children  = [
                    uitriangle $_colour $_icon_size $_icon_size -90
                ]
                p_tip       = [@@p_tip]
                p_id        = @p_id
            ]
        ]
    ]
]

gameui_compactswitch_update = [
    local _new_val
    _new_val = (! (gameui_switch_get $$gameui_switch_var))
    set $gameui_switch_var (gameui_switch_set $_new_val)

    gameui_switch_on_change
]

gameui_compactswitch_props = [
    [ p_label_size 1                ]
    [ p_size       0.025            ]
    [ p_tip        ""               ]
    [ p_get        [[result $arg1]] ]
    [ p_set        [[result $arg1]] ]
    [ p_on_change  []               ]
    [ p_id         []               ]
]

// 1:<var> 2:<props>
ui_gameui_compactswitch = [
    @(props $gameui_compactswitch_props arg2)

    local _val
    _val = (p_get $$arg1)

    uitarget 0 0 [
        uirelease [
            gameui_switch_var       = $arg1
            gameui_switch_get       = $p_get
            gameui_switch_set       = $p_set
            gameui_switch_on_change = $p_on_change
        ]

        ui_gameui_button [
            p_label_size = @p_label_size
            p_width      = @p_size
            p_height     = @p_size
            p_on_click   = [
                gameui_compactswitch_update
            ]
            p_children   = [
                local _size _val _colour
                _val  = @@_val
                _size = @@p_size

                if $_val [
                    _size = (*f $_size (lerpf 0.5 0.3 $_anim_state_activate))
                ] [
                    _size = (*f $_size (lerpf 0.3 0.5 $_anim_state_activate))
                ]

                _colour = (? $_val 0xcccccc 0x88888888)
                uiborderedimageclamped $skintex $_colour 0 $ui_texborder $ui_screenborder $_size $_size
            ]
            p_tip        = [@@p_tip]
            p_id         = @p_id
        ]
    ]
]
