exec "config/ui/game/widgets/button.cfg"

gameui_autoscroll_props = [
    [ p_width   0.5 ]
]

// 1:<content> 2:<props>
ui_gameui_autoscroll = [
    @(props $gameui_autoscroll_props arg2)

    uiclip $p_width 0 0 0 [
        arg1
        uiclipoffsetx (*f $gameui_autoscroll (-f (uigetclipvirtw) $p_width))
    ]
]

gameui_vscrollarea_props = [
    [ p_slider_size        0.01  ]
    [ p_scrollbar_spacing  0.005 ]
    [ p_scrollbar_autohide 1     ]
    [ p_scroll_speed       1     ]
    [ p_width              0.5   ]
    [ p_height             0.1   ]
]

// 1:<content> 2:<props>
ui_gameui_vscrollarea = [
    @(props $gameui_vscrollarea_props arg2)

    local _has_scrollbar
    _has_scrollbar = 0

    uihlist $p_scrollbar_spacing [
        uiscroll 0 $p_height [
            uialign -2 -1

            uifill $p_width
            arg1

            if (|| (! $p_scrollbar_autohide) [>f (uigetclipvirth) $p_height]) [
                _has_scrollbar = 1
            ]
        ]
        if $_has_scrollbar [
            uigroup [
                uiscrollarrow (*f $p_scroll_speed -1)
                uiscrollarrow $p_scroll_speed
                uivscrollbar [
                    uifill $p_slider_size $p_height
                    uiscrollbutton [
                        uiborderedimageclamped $skintex 0x55ffffff 0 $ui_texborder $ui_screenborder $p_slider_size 0 [
                            uiclamp 1 1 1 1
                        ]
                    ]
                ]
            ]
        ] [
            uifill $p_slider_size $p_height
        ]
    ]
]

// 1:<var>
ui_gameui_textinput_prompt = [
    if (&& [=s $$arg1 ""] [! (uifocus?)]) [
        uicolourtext $p_prompt 0x555555 1
    ]
]

gameui_textinput_props = [
    [ p_prompt ["[Enter text here"] ]
]

// 1:<var> 2:<length> 3:<props>
ui_gameui_textinput = [
    @(props $gameui_textinput_props arg3)

    uiborderedimageclamped $skintex 0x222222 0 $ui_texborder $ui_screenborder 0 0 [
        uichangeblends 0.66
        uifield $arg1 $arg2 [] 1.5 0 [
            ui_gameui_textinput_prompt $arg1
        ]
    ]
]

gameui_decortext_props = [
    [ p_label_size      2                 ]
    [ p_colour          0xffffff          ]
    [ p_light_intensity 0.15              ]
    [ p_pad_h           [(uiwidth 0.027)] ]
    [ p_pad_v           0.01              ]
    [ p_width           [(uiwidth 0.5)]   ]
]

// 1:<text> 2:<props>
# ui_gameui_decortext [
    @(props $gameui_decortext_props arg2)

    local _anim_state _light_blend _light_blend_anim _light_len
    _anim_state       = (*f (+f (sin (*f (getmillis) 0.1)) 1) 0.5)
    _light_blend_anim = (lerp 0.25 1 $_anim_state)
    _light_blend      = (*f $_light_blend_anim (lerp 1 $p_light_intensity $_light_blend_anim))
    _light_len        = (*f $p_width (lerp 0.66 1 $_anim_state))

    uifill $p_width 0 [
        uioffset (*f (sin (*f (getmillis) 0.04)) 0.3) (*f (cos (*f (getmillis) 0.04)) 0.01) [
            uiimage "particles/entity" #(hsvtohex [13 0.2 1]) 0 0.5 0.05 [
                uichangeblends $_light_blend
            ]
        ]

        uiimage "particles/stain" #(hsvtohex [13 0.8 1]) 0 $_light_len 0.02 [
            uichangeblends $_light_blend
        ]

        uiimage "particles/stain" #(hsvtohex [13 0.1 1]) 0 $_light_len 0.01 [
            uichangeblends $_light_blend
        ]

        uiimage "particles/entity" #(hsvtohex [13 0.2 1]) 0 $_light_len 0.04 [
            uichangeblends (*f $_light_blend 0.75)
        ]

        uispace $p_pad_h $p_pad_v [
            uiimage "<invert>textures/blob" #(hsvtohex [13 0.5 0.5]) 0 0 0 [
                uichangeblends (*f $_light_blend 0.33)
            ]
            uiclamp- 1 1 1 1

            uicolourtext $arg1 $p_colour $p_label_size

            uialign $p_label_align
        ]
    ]
]
