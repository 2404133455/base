gameui_button_props = [
    [ p_label            ""                ]
    [ p_label_size       2                 ]
    [ p_tip              ""                ]
    [ p_label_align      1                 ]
    [ p_colour           0x99884444        ]
    [ p_sel_colour       0xffffffff        ]
    [ p_light_intensity  0.33              ]
    [ p_pad_h            [(uiwidth 0.027)] ]
    [ p_pad_h_shift      [(uiwidth 0.014)] ]
    [ p_pad_v            0.0075            ]
    [ p_id               []                ]
    [ p_anim_on_speed    0.01              ]
    [ p_anim_off_speed   0.005             ]
    [ p_width            [(uiwidth 0.25)]  ]
    [ p_on_click         []                ]
    [ p_disabled         0                 ]
]

// 1:<props>
# ui_gameui_button [
    @(props $gameui_button_props arg1)

    local _colour _anim_state_var _anim_state _anim_state_hover _anim_state_activate _flash _light_blend _light_len
    _colour              = (? $p_disabled 0x55010101 $p_colour)
    _anim_state_var      = (concatword "gameui_widget_anim_" $p_id)
    _anim_state          = (getalias $_anim_state_var)
    _anim_state_hover    = (at $_anim_state 0)
    _anim_state_activate = (at $_anim_state 1)
    _flash               = 0

    _anim_state_activate = (minf 1 (+f $_anim_state_activate (*f $p_anim_off_speed $getupdatemillis)))
    p_light_intensity    = (lerp $p_light_intensity 1 (gameui_anim_flash $_anim_state_activate 1 1))

    uitarget $p_width 0 [
        if $p_disabled [] [
            uihover [
                if (gameui_can_interact) [
                    gameui_hover

                    _anim_state_hover  = (minf 1 (+f $_anim_state_hover (*f $p_anim_on_speed $getupdatemillis)))
                    _light_blend       = (*f $_anim_state_hover (lerp 1 $p_light_intensity $_anim_state_hover))
                    _flash             = 1
                    _colour            = $p_sel_colour
                ]
            ] [
                _anim_state_hover  = (maxf 0 (-f $_anim_state_hover (*f $p_anim_off_speed $getupdatemillis)))
                _light_blend = (*f $_anim_state_hover $p_light_intensity)
            ]

            uirelease [
                _anim_state_activate = 0
                sleep 0 [
                    @p_on_click
                ]
            ]
        ]

        _light_len = (*f $p_width $_anim_state_hover)

        uiimage "particles/plasma" #(hsvtohex [13 0.8 1]) 0 $_light_len 0.01 [
            uichangeblends $_light_blend
        ]

        uiimage "particles/plasma" #(hsvtohex [13 0.1 1]) 0 $_light_len 0.005 [
            uichangeblends $_light_blend
        ]

        uiimage "particles/entity" #(hsvtohex [13 0.2 1]) 0 $_light_len 0.04 [
            uichangeblends (*f $_light_blend 0.75)
        ]

        if $_flash [
            uiimage "<invert>textures/blob" 0xffffff 0 $_light_len (*f $_anim_state_hover 0.04) [
                uichangeblends (gameui_anim_flash $_anim_state_hover 1 1)
            ]
        ]

        uispace (+f $p_pad_h (*f $_anim_state_hover $p_pad_h_shift)) $p_pad_v [
            uiimage "<invert>textures/blob" #(hsvtohex [13 0.5 1]) 0 0 0 [
                uichangeblends (*f $_anim_state_hover $_anim_state_hover $_light_blend)
            ]
            uiclamp- 1 1 1 1

            uicolourtext $p_label $_colour $p_label_size

            uialign $p_label_align
        ]
    ]

    $_anim_state_var = [@_anim_state_hover @_anim_state_activate]
]
