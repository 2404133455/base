ui_gameui_scoreboard_matchinfo = [
    local _mapname _time_colours
    _mapname     = (? (!=s $maptitle "") $maptitle (filename $mapname))
    _time_colour = (at ["^fr" "^fr" "^fr" "^fr" "^fr" "^fg" "^fzoy" "^fg"] $gamestate)

    ui_gameui_group [
        uistyle clampx

        uigroup [
            uistyle clampx

            uioffset 0.05 0 [
                uialign -1

                uiclip 0 0.15 0.075 0.075 [
                    uiimage (modetexlist $gamemode $mutators -1) 0x44010101 0 0.3 0.3
                ]
            ]

            uispace 0.025 0.025 [
                uialign -1

                uivlist 0 [
                    uistyle clampx

                    uihlist 0.01 [
                        uitext $_mapname 2

                        if (!=s $mapauthor "") [
                            uipad 0 0 0 0.005 [
                                uicolourtext (concat "by" $mapauthor) 0xaaaaaa
                            ]
                        ]

                        uiall [uialign -2 1]
                    ]
                    uicolourtext (gamename $gamemode $mutators 0 32) 0xaaaaaa 1.5

                    uiall [uialign -1]
                ]
            ]

            if (!= $gamemode $G_EDITING) [
                uispace 0.025 0.025 [
                    uistyle righttop
                    uihlist 0.005 [
                        if (!= $gamestate 5) [
                            uitext (at $G_S_NAMES $gamestate)
                        ]
                        if $gametimeremain [
                            uitext (timestr $gametimeremain $showtimestyle)
                            uicolourtext "remaining" 0xaaaaaa
                        ]
                    ]
                ]
            ]
        ]
    ] [
        p_space_x = 0
        p_space_y = 0
    ]
]

ui_gameui_scoreboard_header_standard = [
    // Frags
    uiimage "textures/modes/classic" 0xffffff 0 0.04 0.04 [
        uiimageshadow 0.002 0x88010101
    ]

    // Deaths
    uiimage "textures/icons/dead" 0xffffff 0 0.03 0.03 [
        uiimageshadow 0.002 0x88010101
    ]

    // Points
    uiimage "textures/icons/action" 0xffffff 0 0.03 0.03 [
        uiimageshadow 0.002 0x88010101
    ]
]

ui_gameui_scoreboard_player_standard = [
    // Frags
    uitext (getclientfrags $_player)

    // Deaths
    uitext (getclientdeaths $_player)

    // Points
    uitext (getclientpoints $_player)
]

ui_gameui_scoreboard_header_race = [
    // Laps
    uiimage "textures/modes/racelapped" 0xffffff 0 0.03 0.03 [
        uiimageshadow 0.002 0x88010101
    ]

    // Best lap
    uiimage "textures/modes/race" 0xffffff 0 0.03 0.03 [
        uiimageshadow 0.002 0x88010101
    ]
]

ui_gameui_scoreboard_player_race = [
    local _time
    _time = (getclientcptime $_player)

    // Laps
    uitext (getclientpoints $_player)

    // Best lap
    uitext (? (> $_time 0) (timestr $_time $scoreracestyle) "-")
]

gameui_scoreboard_team_score_format_standard = [
    result $arg1
]

gameui_scoreboard_team_score_format_race_timed = [
    ? (> $arg1 0) (timestr $arg1 $scoreracestyle) "-"
]

gameui_scoreboard_mode_specific = [
    caseif (&& [= $gamemode $G_RACE] [& $mutators (<< 1 $G_M_GSP1)]) [
        // Lapped race
        _header_row        = $ui_gameui_scoreboard_header_race
        _player_row        = $ui_gameui_scoreboard_player_race
        _team_score_format = gameui_scoreboard_team_score_format_standard
    ] (= $gamemode $G_RACE) [
        // Timed race
        _header_row        = $ui_gameui_scoreboard_header_race
        _player_row        = $ui_gameui_scoreboard_player_race
        _team_score_format = gameui_scoreboard_team_score_format_race_timed
    ] (= $gamemode $G_EDITING) [
        _header_row = []
        _player_row = []
    ] 1 [
        _header_row        = $ui_gameui_scoreboard_header_standard
        _player_row        = $ui_gameui_scoreboard_player_standard
        _team_score_format = gameui_scoreboard_team_score_format_standard
    ]
]

// 1:<player id>
ui_gameui_scoreboard_player = [
    local _colour
    _colour = (getclientcolour $arg1 -1 2)

    uispace 0 0.005 [
        uihlist 0.005 [
            uiimage (getclientprivtex $arg1) $_colour 0 0.03 0.03
            uitext (getclientname $arg1 0 0)
        ]
    ]
]

// 1:<group id>
# ui_gameui_scoreboard_group [
    _team        = (getscoreteam $arg1)
    _team_name   = (at $teamnames $_team)
    _team_colour = (getteamcolour $_team)

    uivlist 0 [
        if $_team [
            ui_gameui_group [
                uistyle clampx

                uigroup [
                    uistyle clampx

                    uiclip 0 0.1 0 0.05 [
                        uiimage $glowtex $_team_colour 0 0.2 0.2 [
                            uiprop [uicolourblend 0.25]
                        ]

                        uiimage $(concatword team $_team_name tex) $_team_colour 0 0.2 0.2 [
                            uiprop [uicolourblend 0.33]
                        ]
                    ]

                    uioffset 0.0025 0.0025 [
                        uicolourtext $_team_name 0 2.5
                    ]
                    uicolourtext $_team_name 0xcccccc 2.5

                    uispace 0.01 0.01 [
                        uistyle rightbottom

                        local _is_best _colour
                        _is_best = (= #1 arg1 0)
                        _colour  = (? $_is_best 0xcccc44 0xaaaaaa)

                        uiimage $glowtex $_colour 0 0.03 0 [
                            uistyle clampxy
                            uiprop [uicolourblend 0.25]

                            uicolourtext ($_team_score_format (getscoretotal #1 arg1)) $_colour 1.5
                        ]
                    ]
                ]
            ] [
                p_space_x = 0
                p_space_y = 0
                p_colour = @(tool_colour_add_alpha (modcolour $_team_colour 0.33) 0x44)
            ]
        ]
        ui_gameui_group [
            uitable 0.05 0 [
                uifill 0.5

                uitableheader [
                    // Player badge/name
                    uiimage "textures/icons/player" 0xffffff 0 0.03 0.03 [
                        uiimageshadow 0.002 0x88010101
                    ]

                    _header_row

                    // Hide ping when offline
                    if $isonline [
                        uiimage "textures/icons/voices" 0xffffff 0 0.03 0.03 [
                        uiimageshadow 0.002 0x88010101
                    ]
                    ]
                ] [
                    ui_gameui_group [] [
                        p_colour = @(tool_colour_add_alpha (modcolour $_team_colour 0.5) 0x88)
                    ]
                    uiprev [uistyle clampxy]
                ]

                _row_count = 0

                loopscoreboard _player @@arg1 [
                    uitablerow [
                        // Player badge/name
                        ui_gameui_scoreboard_player $_player

                        _player_row

                        // Hide ping when offline
                        if $isonline [
                            uitext (getclientping $_player)
                        ]
                    ] [
                        ui_gameui_group [] [
                            p_colour = (? (mod $_row_count 2) 0x44010101 0x44444444)
                        ]
                        uiprev [uistyle clampxy]

                        if (= $_player $playercn) [
                            uiborderedimageclamped $skinshadowtex $_team_colour 0 $ui_texborder $ui_screenborder 0 0
                            uiprev [uistyle clampxy]
                        ]
                    ]

                    _row_count = (+ $_row_count 1)
                ]
            ]
        ] [
            p_space_x = 0
            p_space_y = 0
            p_colour  = 0x44010101
        ]
    ]
]

ui_gameui_scoreboard_spectators = [
    uivlist 0 [
        ui_gameui_group [
            uifill 0.5

            uiimage "textures/icons/spectator" 0xffffff 0 0.03 0.03 [
                uiimageshadow 0.002 0x88010101
            ]
        ] [
            p_space_x = 0
            p_space_y = 0
        ]

        ui_gameui_group [
            uifill 0.5

            ui_gameui_autoscrollv [
                uigrid 2 0.025 0 [
                    loopscoreboard _spectator -1 [
                        ui_gameui_scoreboard_player $_spectator
                    ]
                ]
            ] [
                p_height = 0.15
            ]
        ] [
            p_space_x = 0
            p_space_y = 0
            p_colour  = 0x44010101
        ]
    ]
]

ui_gameui_scoreboard_music = [
    local _music_artist _music_title
    _music_artist = (getmusic 1)
    _music_title  = (getmusic 2)

    uivlist 0 [
        ui_gameui_group [
            uifill 0.5

            uiimage "textures/icons/edit/sound" 0xffffff 0 0.03 0.03 [
                uiimageshadow 0.002 0x88010101
            ]
        ] [
            p_space_x = 0
            p_space_y = 0
        ]

        ui_gameui_group [
            uifill 0.5

            ui_gameui_autoscrollh [
                if (&& [!=s $_music_artist ""] [!=s $_music_title ""]) [
                    uihlist 0.005 [
                        uicolourtext $_music_artist 0xcccccc
                        uicolourtext "-" 0x888888
                        uicolourtext $_music_title 0xcccccc
                    ]
                ] [
                    uicolourtext (getmusic 0) 0xcccccc
                ]
            ] [
                p_width = 0.5
            ]
        ] [
            p_space_x = 0
            p_space_y = 0
            p_colour  = 0x44010101
        ]
    ]
]

gameui_scoreboard_groups_sorted = []

// Sorts groups based on their team index
gameui_scoreboard_groups_sort = [
    gameui_scoreboard_groups_sorted = []

    loop _group $numscoregroups [
        append gameui_scoreboard_groups_sorted $_group
    ]

    gameui_scoreboard_groups_sorted = (sortlist $gameui_scoreboard_groups_sorted a b [
        < (getscoreteam $a) (getscoreteam $b)
    ])
]

ui_menu_inhibit_scoreboard = 1
newui "scoreboard" [
    ui_menu_current          = "scoreboard"
    ui_menu_close_scoreboard = 0

    uiallowinput 0
    uimenu 0
    uiontop 1
    hidecrosshair 1
    refreshscoreboard

    // Steal autoscroll from edit UI
    gameui_autoscroll = (tool_autoscroll (divf $lastmillis 1000))

    local _header_row _player_row _team _team_name _team_colour _row_count _team_score_format

    gameui_scoreboard_groups_sort
    gameui_scoreboard_mode_specific

    uifont $textfontclear [
        uicolour 0 0 1 [
            uiprop [uicolourblend 0]

            uispace 0.05 0.05 [
                uivlist 0.025 [
                    ui_gameui_scoreboard_matchinfo

                    if $numscoregroups [
                        uigrid 2 0.025 0 [
                            looplist _group $gameui_scoreboard_groups_sorted [
                                ui_gameui_scoreboard_group $_group
                            ]
                        ]
                    ]

                    if $numspectators [
                        ui_gameui_scoreboard_spectators
                    ]

                    if (getmusic 8) [
                        ui_gameui_scoreboard_music
                    ]
                ]
            ]
        ]
    ]

    ui_menu_current = ""
]
