shader $SHADER_DEFAULT compositeplayermixer [
    attribute vec4 vvertex, vcolor;
    attribute vec2 vtexcoord0;
    uniform mat4 hudmatrix;
    uniform float millis;
    varying vec2 texcoord0;
    varying vec4 colorscale;
    varying float noise;

    float rand(vec2 co) { return fract(sin(dot(co.xy, vec2(12.9898, 78.233)))*43758.5453); }

    void main(void)
    {
        gl_Position = hudmatrix * vvertex;
        texcoord0 = vtexcoord0;
        colorscale = vcolor;
        noise = rand(vec2(millis, millis * 3.33));
    }
] [
    uniform vec4 params; // regen dist invdist scale
    uniform vec3 regencolor;
    uniform float millis;
    varying vec2 texcoord0;
    varying vec4 colorscale;
    varying float noise;

    fragdata(0) vec4 fragcolor;

    float rand(vec2 co) { return fract(sin(dot(co.xy, vec2(12.9898, 78.233)))*43758.5453); }

    void main(void)
    {
        vec4 diffuse = colorscale * params.w;

        if(params.x > 0.0)
        {
            float dist = abs(params.x - texcoord0.y);
            if(dist < params.y)
            {
                float val = smoothstep(0.0, 1.0, 1.0 - (dist * params.z)),
                      mixlevel = clamp(rand(vec2(texcoord0.x + noise, texcoord0.y + val)), 0.0, 1.0);
                diffuse = mix(diffuse, vec4(regencolor * params.w * mixlevel, mixlevel * val), val);
            }
        }
        if(diffuse.a <= 0.0) discard;
        fragcolor = diffuse;
    }
] 1

newui playermixer $SURFACE_COMPOSITE [
    @(localargs cn -1)
    execid uiarg1

    local _colour _residual _blend _regen _lastregen _amt regencol1 regencol2 regencol3
    _colour = 0
    _residual = (getclientresidualfx $cn 3000)
    _blend = 0.0

    if (>= $_residual 0) [
        _amt = (divf (mod $uilastmillis 500) 250)

        if (>f $_amt 1.0) [ _amt = (-f 2.0 $_amt) ]

        _colour = (modcolour (skewcolour $_colour $_residual $_amt) 0.25)
        _blend = (+f $_blend (*f 0.05 $_amt))
    ]

    _regen = 0
    _lastregen = (getclientregen $cn)
    if (> $_lastregen 0) [
        _amt = (- $uilastmillis $_lastregen)

        if (<= $_amt 500) [
            _regen = (clampf (divf $_amt 500) 0.0 1.0)

            if (> (getclientregenamt $cn) 0) [
                setargs regencol (veccolour (getclientpulsecolour $cn $PULSE_HEALTH 50))
                _regen = (-f 1.0 $_regen)
            ] [
                setargs regencol (veccolour (skewcolour (getclientpulsecolour $cn $PULSE_DECAY 33) (getclientpulsecolour $cn $PULSE_HEALTH 50) 0.25))
            ]
        ]
    ]

    uirender compositeplayermixer 1 1 [
        uicolourblend $_blend
        uirenderparam params $_regen 0.125 8.0 1.0
        uirenderparam regencolor $regencol1 $regencol2 $regencol3
    ]
]
