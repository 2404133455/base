shader 0 compositeactor [
    attribute vec4 vvertex, vcolor;
    attribute vec2 vtexcoord0;
    uniform vec3 params; // regen dist invdist
    uniform mat4 hudmatrix;
    varying vec2 texcoord0;
    varying vec4 colorscale;
    uniform float millis;

    void main(void)
    {
        gl_Position = hudmatrix * vvertex;
        texcoord0 = vtexcoord0;
        colorscale = vcolor;
    }
] [
    uniform sampler2D tex0;
    uniform vec3 params; // regen dist invdist
    varying vec2 texcoord0;
    varying vec4 colorscale;
    uniform float millis;

    fragdata(0) vec4 fragcolor;

    void main(void)
    {
        vec4 diffuse = colorscale;

        if(params.x > 0.0)
        {
            float dist = distance(params.x, texcoord0.y);
            if(dist <= params.y)
            {
                float val = dist * params.z;
                vec4 glimmer = texture2D(tex0, texcoord0 + sin(millis) * 0.25);
                glimmer *= vec4(0.0, 1.0 + (10 * glimmer.a), 0.0, 1.0) * val;
                diffuse.rgb = mix(diffuse.rgb, glimmer.rgb, glimmer.a);
                diffuse.a = max(diffuse.a, glimmer.a * glimmer.a);
            }
        }
        if(diffuse.a <= 0.0) discard;
        fragcolor = diffuse;
    }
] 1

newui actor $SURFACE_COMPOSITE [
    @(localargs cn -1)
    execid uiarg1

    local colour; colour = (getclientcolour $cn $playerovertone $playerovertonelevel)
    local residual; residual = (getclientresidualfx $cn 3000)
    local blend; blend = 0.0

    if (>= $residual 0) [
        local amt; amt = (magcolour $residual 1)
        colour = (skewcolour $colour $residual $amt)
        blend = (+f $blend (*f 0.125 $amt))
    ]

    local regen; regen = 0
    local lastregen; lastregen = (getclientregen $cn)
    if (> $lastregen 0) [
        local offset; offset = (- $lastmillis $lastregen)
        if (<= $offset 500) [
            regen = (divf $offset 500)
        ]
    ]

    uirender compositeactor 1 1 [
        uicolourset $colour
        uicolourblend $blend
        uirenderparam params $regen 0.125 8
        uirendertex "particles/glimmer" 768
    ]
]
