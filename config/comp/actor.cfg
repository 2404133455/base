shader 0 compositeactor [
    attribute vec4 vvertex, vcolor;
    attribute vec2 vtexcoord0;
    uniform vec4 params; // scale regen dist invdist
    uniform mat4 hudmatrix;
    varying vec2 texcoord0, texcoord1;
    varying vec4 colorscale;
    uniform float millis;

    void main(void)
    {
        gl_Position = hudmatrix * vvertex;
        texcoord0 = vtexcoord0;
        texcoord1 = texcoord0 * params.x;
        colorscale = vcolor;
    }
] [
    uniform sampler2D tex0, tex1;
    uniform vec4 params; // scale regen dist invdist
    varying vec2 texcoord0, texcoord1;
    varying vec4 colorscale;
    uniform float millis;

    fragdata(0) vec4 fragcolor;

    void main(void)
    {
        vec4 source = texture2D(tex0, texcoord1);
        vec4 diffuse = vec4(1.0 - source.r) * colorscale;

        if(params.y > 0.0)
        {
            float dist = distance(params.y, texcoord0.y);
            if(dist <= params.z)
            {
                float val = dist * params.w;
                vec4 glimmer = texture2D(tex1, texcoord0 + sin(millis) * 0.25) * vec4(0.5, 5.0, 0.5, 1.0) * val;
                diffuse.rgb = mix(diffuse.rgb, glimmer.rgb, glimmer.a);
                diffuse.a = max(diffuse.a, glimmer.a);
            }
        }

        fragcolor = diffuse;
    }
] 1

newui actor $SURFACE_COMPOSITE [
    @(localargs cn -1)
    execid uiarg1

    local colour; colour = (getclientcolour $cn $playerovertone $playerovertonelevel)
    local residual; residual = (getclientresidualfx $cn 3000)
    local blend; blend = 0.1

    if (>= $residual 0) [
        local amt; amt = (magcolour $residual 1)
        colour = (skewcolour $colour $residual $amt)
        blend = (+f $blend (*f 0.1 $amt))
    ]

    local regen; regen = 0
    local lastregen; lastregen = (getclientregen $cn)
    if (> $lastregen 0) [
        local offset; offset = (- $lastmillis $lastregen)
        if (<= $offset 500) [
            regen = (divf $offset 500)
        ]
    ]

    local patname; patname = (getclientpattern $cn)
    if (=s $patname "") [ patname = "<comp>blank" ]
    local pattex; pattex = (getpattern $patname 0)
    local patclamp; patclamp = (getpattern $patname 3)
    local patscale; patscale = (getpattern $patname 4)
    if (<=f $patscale 0.0) [ patscale = 1.0 ]

    //echo $cn $colour $blend $regen $patname $pattex $patclamp $patscale

    uirender compositeactor 1 1 [
        uicolourset $colour
        uicolourblend $blend
        uirenderparam params $patscale $regen 0.125 8
        uirendertex $pattex $patclamp
        uirendertex "particles/glimmer" 768
    ]
]
