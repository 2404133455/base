shader 0 compositeactor [
    attribute vec4 vvertex, vcolor;
    attribute vec2 vtexcoord0;
    uniform mat4 hudmatrix;
    uniform float millis;
    varying vec2 texcoord0;
    varying vec4 colorscale;
    varying float noise;

    float rand(vec2 co) { return fract(sin(dot(co.xy, vec2(12.9898, 78.233)))*43758.5453); }

    void main(void)
    {
        gl_Position = hudmatrix * vvertex;
        texcoord0 = vtexcoord0;
        colorscale = vcolor;
        noise = rand(vec2(millis));
    }
] [
    uniform vec4 params; // regen dist invdist scale
    uniform float millis;
    varying vec2 texcoord0;
    varying vec4 colorscale;
    varying float noise;

    fragdata(0) vec4 fragcolor;

    float rand(vec2 co) { return fract(sin(dot(co.xy, vec2(12.9898, 78.233)))*43758.5453); }

    void main(void)
    {
        vec4 diffuse = colorscale;

        if(params.x > 0.0)
        {
            float dist = abs(params.x - texcoord0.y);
            if(dist < params.y)
            {
                float val = 1.0 - (dist * params.z);
                diffuse = mix(diffuse, vec4(0.0, rand(texcoord0 + noise) * params.w, 0.0, 1.0), val);
                diffuse.a *= rand(texcoord0 * noise) * val;
            }
        }
        if(diffuse.a <= 0.0) discard;
        fragcolor = diffuse;
    }
] 1

newui actor $SURFACE_COMPOSITE [
    @(localargs cn -1)
    execid uiarg1

    local colour; colour = 0
    local residual; residual = (getclientresidualfx $cn 3000)
    local blend; blend = 0.0

    if (>= $residual 0) [
        local amt; amt = (divf (mod $lastmillis 500) 250)
        if (>f $amt 1.0) [ amt = (-f 2.0 $amt) ]
        colour = (modcolour (skewcolour $colour $residual $amt) 0.25)
        blend = (+f $blend (*f 0.05 $amt))
    ]

    local regen; regen = 0
    local lastregen; lastregen = (getclientregen $cn)
    if (> $lastregen 0) [
        local offset; offset = (- $lastmillis $lastregen)
        if (<= $offset 500) [
            regen = (divf $offset 250)
            if (>f $regen 1.0) [ regen = (-f 2.0 $regen) ]
        ]
    ]

    uirender compositeactor 1 1 [
        uicolourblend $blend
        uirenderparam params $regen 0.0625 16.0 1.5
    ]
]
