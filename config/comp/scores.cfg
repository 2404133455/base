newcompui scoreboard [
    @(deflocal teamid 0)
    @(deflocal fontname "dotmatrix")
    @(setlocal params [4.0 8.0 10.0])
    execid uiargs
    if $teamid [
        params1 = (*f $params1 2.0)
        params2 = (*f $params2 2.0)
        params3 = (*f $params3 2.0)
    ]
    uistyle clampxy
    refreshscoreboard
    uifont $fontname [ uivlist 0 [
        uistyle clampxy
        uistyle centermiddle
        if (! $teamid) [ uifill 1.0 0.5 [
            uistyle centermiddle
            uivlist 0 [
                uistyle clampxy
                uistyle centermiddle
                uitext (gamename $gamemode $mutators 0 32) $params2 [uistyle centermiddle; uitextlimit 0.9]
                uitext (concat "[" (timestr (? $gametimeremain $gametimeremain $gametimeelapsed) $showtimestyle) "]") $params2 [uistyle centermiddle; uitextlimit 0.9]
            ]
        ] ]
        uihlist 0 [
            uistyle clampxy
            uistyle centermiddle
            local scoreboard_race; scoreboard_race = (= $gamemode $G_RACE)
            local scoreboard_racetime; scoreboard_racetime = (&& $scoreboard_race (! (& $mutators (<< 1 $G_M_GSP1))))
            if (& $mutators (<< 1 $G_M_FFA)) [
                if $teamid [
                    uiimage $icontex 0xFFFFFF 0 1.0 1.0 [
                        uistyle centermiddle
                        uicolouradd 0x888888
                        uicolourblend 0.75
                    ]
                ] [
                    local scoreboard_count; scoreboard_count = (numscoregroups)
                    local scoreboard_output; scoreboard_output = 0
                    loopscoreboard scoreboard_player 0 [
                        if (< $scoreboard_output 2) [
                            local scoreboard_team; scoreboard_team = (getclientteam $scoreboard_player)
                            local scoreboard_colour1; scoreboard_colour1 = (modcolour (getclientcolour $scoreboard_player -1 2) 0.4)
                            local scoreboard_colour2; scoreboard_colour2 = (modcolour (getteamcolour $scoreboard_team) 0.2)
                            uiimage $playertex $scoreboard_colour1 0 0.5 0.5 [
                                uistyle centermiddle
                                uivlist 0 [
                                    uistyle clampxy
                                    uistyle centermiddle
                                    uitext (getclientname $scoreboard_player) $params1 [uistyle centermiddle; uitextlimit 0.9]
                                    if $scoreboard_racetime [
                                        local scoreboard_cptime; scoreboard_cptime = (getclientcptime $scoreboard_player)
                                        uitext (? (> $scoreboard_cptime 0) (timestr $scoreboard_cptime $scoreracestyle) "-") $params3 [uistyle centermiddle; uitextlimit 0.9]
                                    ] [uitext (getclientpoints $scoreboard_player) $params3 [uistyle centermiddle; uitextlimit 0.9]]
                                ]
                                uicolouradd $scoreboard_colour2
                                uicolourblend 0.75
                            ]
                            scoreboard_output = (+ $scoreboard_output 1)
                        ]
                    ]
                ]
            ] [
                loop scoreboard_id 2 [
                    scoreboard_check = (+ $scoreboard_id $teamidxalpha)
                    if (|| (! $teamid) (= $teamid $scoreboard_check)) [
                        loopscoregroups scoreboard_curgroup [] [] [
                            local scoreboard_team; scoreboard_team = (getscoreteam $scoreboard_curgroup)
                            if (= $scoreboard_check $scoreboard_team) [
                                local scoreboard_teamid; scoreboard_teamid = (at $teamnames $scoreboard_team)
                                local scoreboard_colour; scoreboard_colour = (getteamcolour $scoreboard_team)
                                local scoreboard_colour1; scoreboard_colour1 = (modcolour $scoreboard_colour 0.2)
                                local scoreboard_colour2; scoreboard_colour2 = (modcolour $scoreboard_colour 0.4)
                                uiimage $[team@[scoreboard_teamid]tex] $scoreboard_colour1 0 (? $teamid 1.0 0.5) (? $teamid 1.0 0.5) [
                                    uistyle centermiddle
                                    uivlist 0 [
                                        uistyle clampxy
                                        uistyle centermiddle
                                        uitext $[team@[scoreboard_teamid]name] $params1 [uistyle centermiddle; uitextlimit 0.9]
                                        local scoreboard_total; scoreboard_total = (getscoretotal $scoreboard_curgroup)
                                        if $scoreboard_racetime [
                                            uitext (? (> $scoreboard_total 0) (timestr $scoreboard_total $scoreracestyle) "-") $params3 [uistyle centermiddle; uitextlimit 0.9]
                                        ] [uitext $scoreboard_total $params3 [uistyle centermiddle; uitextlimit 0.9]]
                                    ]
                                    uicolouradd $scoreboard_colour2
                                    uicolourblend 0.75
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ] ]
]
