newui comp_scoreboard [
    uiclamp 1 1 1 1
    refreshscoreboard
    local scoreboard_textsize; scoreboard_textsize = 8.0
    local scoreboard_textsmall; scoreboard_textsmall = 4.0
    local scoreboard_textlarge; scoreboard_textlarge = 10.0
    local scoreboard_isalive; scoreboard_isalive = [if (> (getclientlastspawn $scoreboard_player) (getclientlastdeath $scoreboard_player) ) [result 1] [result 0]]
    local scoreboard_alivecolour; scoreboard_alivecolour = [if (scoreboard_isalive) [result "^f7"] [result "^f4"]]
    uivlist 0 [
        uiclamp 1 1 1 1
        uialign 0 0
        uifill 1.0 0.5 [
            uiclamp 1 1 1 1
            uialign 0 0
            uivlist 0 [
                uiclamp 1 1 1 1
                uialign 0 0
                uitext (gamename (gamemode) (mutators) 0 32) $scoreboard_textsize [uialign 0 0; uitextlimit 1]
                if (!= (gamemode) $modeidxediting) [
                    uitext (concat "[" (timestr (getgametimeremain) $showtimestyle) "]") $scoreboard_textsize [uialign 0 0; uitextlimit 1]
                ]
            ]
        ]
        uihlist 0 [
            uiclamp 1 1 1 1
            uialign 0 0
            local scoreboard_count; scoreboard_count = (numscoregroups)
            local scoreboard_race; scoreboard_race = (= (gamemode) $modeidxrace)
            local scoreboard_racetime; scoreboard_racetime = (&& $scoreboard_race (! (& (mutators) $mutsbitgsp1)))
            local scoreboard_colour; scoreboard_colour = 0xFFFFFF
            local scoreboard_colour1; scoreboard_colour1 = 0x111111
            local scoreboard_colour2; scoreboard_colour2 = 0x888888
            local scoreboard_output; scoreboard_output = 0
            loopscoregroups scoreboard_curgroup [] [] [
                if (< $scoreboard_output 2) [
                    local scoreboard_team; scoreboard_team = (getscoreteam $scoreboard_curgroup)
                    local scoreboard_teamid; scoreboard_teamid = (at $teamnames $scoreboard_team)
                    local scoreboard_colour; scoreboard_colour = (getteamcolour $scoreboard_team)
                    local scoreboard_colour1; scoreboard_colour1 = (modcolour $scoreboard_colour 0.25)
                    local scoreboard_colour2; scoreboard_colour2 = (modcolour $scoreboard_colour 0.5)
                    if $scoreboard_team [
                        uiimage $[team@[scoreboard_teamid]tex] $scoreboard_colour1 0 0.5 0.5 [
                            uialign 0 0
                            uivlist 0 [
                                uiclamp 1 1 1 1
                                uialign 0 0
                                uitext $[team@[scoreboard_teamid]name] $scoreboard_textsmall [uialign 0 0]
                                local scoreboard_total; scoreboard_total = (getscoretotal $scoreboard_curgroup)
                                if $scoreboard_racetime [
                                    uitext (? (> $scoreboard_total 0) (timestr $scoreboard_total $scoreracestyle) "-") $scoreboard_textlarge [uialign 0 0]
                                ] [uitext $scoreboard_total $scoreboard_textlarge [uialign 0 0]]
                            ]
                            uichangeblend 0.75
                        ]
                    ] [
                        loopscoreboard scoreboard_player $scoreboard_curgroup [
                            if (< $scoreboard_output 2) [
                                uiimage $[team@[scoreboard_teamid]tex] (modcolour (getclientcolour $scoreboard_player -1 2) 0.25) 0 0.5 0.5 [
                                    uialign 0 0
                                    uivlist 0 [
                                        uiclamp 1 1 1 1
                                        uialign 0 0
                                        uitext (getclientname $scoreboard_player 0 0) $scoreboard_textsmall [uialign 0 0]
                                        if $scoreboard_racetime [
                                            local scoreboard_cptime; scoreboard_cptime = (getclientcptime $scoreboard_player)
                                            uitext (? (> $scoreboard_cptime 0) (timestr $scoreboard_cptime $scoreracestyle) "-") $scoreboard_textlarge [uialign 0 0]
                                        ] [uitext (getclientpoints $scoreboard_player) $scoreboard_textlarge [uialign 0 0]]
                                    ]
                                    uicolouradd $scoreboard_colour1
                                    uichangeblend 0.75
                                ]
                                scoreboard_output = (+ $scoreboard_output 1)
                            ]
                        ]
                    ]
                    scoreboard_output = (+ $scoreboard_output 1)
                ]
            ]
        ]
    ]
]