newcompui scoreboard [
    @(deflocal teamid 0)
    @(setlocal params [4.0 8.0 10.0])
    execid uicompargs
    if $teamid [
        params1 = (*f $params1 2.0)
        params2 = (*f $params2 2.0)
        params3 = (*f $params3 2.0)
    ]
    uiclamp 1 1 1 1
    refreshscoreboard
    uifont "dotmatrix" [
        uivlist 0 [
            uiclamp 1 1 1 1
            uialign 0 0
            if (! $teamid) [
                uifill 1.0 0.5 [
                    uialign 0 0
                    uivlist 0 [
                        uiclamp 1 1 1 1
                        uialign 0 0
                        uitext (gamename (gamemode) (mutators) 0 32) $params2 [uialign 0 0; uitextlimit 0.9]
                        uitext (concat "[" (timestr (? (getgametimeremain) (getgametimeremain) (getgametimeelapsed)) $showtimestyle) "]") $params2 [uialign 0 0; uitextlimit 0.9]
                    ]
                ]
            ]
            uihlist 0 [
                uiclamp 1 1 1 1
                uialign 0 0
                local scoreboard_race; scoreboard_race = (= (gamemode) $modeidxrace)
                local scoreboard_racetime; scoreboard_racetime = (&& $scoreboard_race (! (& (mutators) $mutsbitgsp1)))
                if (& (mutators) $mutsbitffa) [
                    if $teamid [
                        uiimage $icontex 0xFFFFFF 0 1.0 1.0 [
                            uialign 0 0
                            uicolouradd 0x888888
                            uichangeblend 0.75
                        ]
                    ] [
                        local scoreboard_count; scoreboard_count = (numscoregroups)
                        local scoreboard_output; scoreboard_output = 0
                        loopscoregroups scoreboard_curgroup [] [] [
                            if (< $scoreboard_output 2) [
                                loopscoreboard scoreboard_player $scoreboard_curgroup [
                                    if (< $scoreboard_output 2) [
                                        local scoreboard_team; scoreboard_team = (getscoreteam $scoreboard_curgroup)
                                        local scoreboard_colour1; scoreboard_colour1 = (modcolour (getclientcolour $scoreboard_player -1 2) 0.4)
                                        local scoreboard_colour2; scoreboard_colour2 = (modcolour (getteamcolour $scoreboard_team) 0.2)
                                        uiimage $playertex $scoreboard_colour1 0 0.5 0.5 [
                                            uialign 0 0
                                            uivlist 0 [
                                                uiclamp 1 1 1 1
                                                uialign 0 0
                                                uitext (getclientname $scoreboard_player 0 0) $params1 [uialign 0 0; uitextlimit 0.9]
                                                if $scoreboard_racetime [
                                                    local scoreboard_cptime; scoreboard_cptime = (getclientcptime $scoreboard_player)
                                                    uitext (? (> $scoreboard_cptime 0) (timestr $scoreboard_cptime $scoreracestyle) "-") $params3 [uialign 0 0; uitextlimit 0.9]
                                                ] [uitext (getclientpoints $scoreboard_player) $params3 [uialign 0 0; uitextlimit 0.9]]
                                            ]
                                            uicolouradd $scoreboard_colour2
                                            uichangeblend 0.75
                                        ]
                                        scoreboard_output = (+ $scoreboard_output 1)
                                    ]
                                ]
                            ]
                        ]
                    ]
                ] [
                    loop scoreboard_id 2 [
                        scoreboard_check = (+ $scoreboard_id $teamidxalpha)
                        if (|| (! $teamid) (= $teamid $scoreboard_check)) [
                            loopscoregroups scoreboard_curgroup [] [] [
                                local scoreboard_team; scoreboard_team = (getscoreteam $scoreboard_curgroup)
                                if (= $scoreboard_check $scoreboard_team) [
                                    local scoreboard_teamid; scoreboard_teamid = (at $teamnames $scoreboard_team)
                                    local scoreboard_colour; scoreboard_colour = (getteamcolour $scoreboard_team)
                                    local scoreboard_colour1; scoreboard_colour1 = (modcolour $scoreboard_colour 0.2)
                                    local scoreboard_colour2; scoreboard_colour2 = (modcolour $scoreboard_colour 0.4)
                                    uiimage $[team@[scoreboard_teamid]tex] $scoreboard_colour1 0 (? $teamid 1.0 0.5) (? $teamid 1.0 0.5) [
                                        uialign 0 0
                                        uivlist 0 [
                                            uiclamp 1 1 1 1
                                            uialign 0 0
                                            uitext $[team@[scoreboard_teamid]name] $params1 [uialign 0 0; uitextlimit 0.9]
                                            local scoreboard_total; scoreboard_total = (getscoretotal $scoreboard_curgroup)
                                            if $scoreboard_racetime [
                                                uitext (? (> $scoreboard_total 0) (timestr $scoreboard_total $scoreracestyle) "-") $params3 [uialign 0 0; uitextlimit 0.9]
                                            ] [uitext $scoreboard_total $params3 [uialign 0 0; uitextlimit 0.9]]
                                        ]
                                        uicolouradd $scoreboard_colour2
                                        uichangeblend 0.75
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
]

newcompui ticker [
    @(deflocal speed 2000)
    execid uicompargs
    local ticker_mod; ticker_mod = (-f 2.0 (divf (mod (getmillis) (mul $speed 4)) $speed))
    local ticker_idx; ticker_idx = (floor (divf (mod (getmillis) (mul $speed 16)) (mul $speed 4)))
    uifill 1.0 1.0 [
        uitext (concatword "^fs^fy" (gamename (gamemode) (mutators) 0 32) "^fS on ^fs^fo" (? (!=s $maptitle "") $maptitle (filename $mapname)) "^fS") 8.0 [uisetpos $ticker_mod (+f 0.05 (mulf $ticker_idx 0.25)); uitextalign -1]
        uitext (concatword "You are playing ^fs^fr" $versionname "^fS") 8.0 [uisetpos $ticker_mod (+f 0.05 (mulf (mod (+ $ticker_idx 1) 4) 0.25)); uitextalign -1]
        uitext (? (getgametimeremain) (concatword "Time remaining: ^fs^fg" (timestr (getgametimeremain) $showtimestyle) "^fS") (concatword "Time elapsed: ^fs^fg" (timestr (getgametimeelapsed) $showtimestyle) "^fS")) 8.0 [uisetpos $ticker_mod (+f 0.05 (mulf (mod (+ $ticker_idx 2) 4) 0.25)); uitextalign -1]
        uitext "Quin is the best" 8.0 [uisetpos $ticker_mod (+f 0.05 (mulf (mod (+ $ticker_idx 3) 4) 0.25)); uitextalign -1]
    ]
]
