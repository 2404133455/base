newcompui ticker [
    @(deflocal speed 10000)
    @(deflocal fontname "domatrix")
    execid uiargs

    local ticker_mod; ticker_mod = (divf (mod (getmillis) $speed) $speed)
    local ticker_idx; ticker_idx = (floor (divf (mod (getmillis) (mul $speed 2)) $speed))

    refreshscoreboard
    local ticker_race; ticker_race = (= (gamemode) $modeidxrace)
    local ticker_racetime; ticker_racetime = (&& $ticker_race (! (& (mutators) $mutsbitgsp1)))

    uifont $fontname [
        uispace 0.01 0.01 [
            uiclip 0.99 0.99 0 0 [
                uivlist 0 [
                    loop i 4 [
                        local j; j = (mod (+ $i $ticker_idx) 4)
                        uifill 8.0 0.2475 [
                            uihlist 0 [
                                uispace 0.495
                                case $j 0 [
                                    uitext (concatword "^fs^fy" (gamename (gamemode) (mutators) 0 32) "^fS on ^fs^fo" (? (!=s $maptitle "") $maptitle (filename $mapname)) "^fS") 10.0
                                ] 1 [
                                    uitext (? (getgametimeremain) (concatword "Time remaining: ^fs^fg" (timestr (getgametimeremain) $showtimestyle) "^fS") (concatword "Time elapsed: ^fs^fg" (timestr (getgametimeelapsed) $showtimestyle) "^fS")) 10.0
                                ] 2 [
                                    local ticker_first; ticker_first = -1
                                    local ticker_second; ticker_second = -1

                                    if (& (mutators) $mutsbitffa) [
                                        loopscoreboard ticker_player 0 [
                                            caseif (< $ticker_first 0) [
                                                ticker_first = $ticker_player
                                            ] (< $ticker_second 0) [
                                                ticker_second = $ticker_player
                                            ]
                                        ]

                                        caseif (< $ticker_first 0) [
                                            uitext "Waiting for players..." 10.0
                                        ] (< $ticker_second 0) [
                                            if $ticker_racetime [
                                                local ticker_cptime; ticker_cptime = (getclientcptime $ticker_first)
                                                if (> $ticker_cptime 0) [
                                                    uitext (concatword "Best lap for " (getclientname $ticker_first) " is ^fs^fc" (timestr $ticker_cptime $scoreracestyle) "^fS") 10.0
                                                ] [
                                                    uitext (concatword "Current lap for " (getclientname $ticker_player) " is ^fs^fc" (timestr (getclientcpmillis $ticker_first) $scoreracestyle) "^fS") 10.0
                                                ]
                                            ] [
                                                uitext (concatword (getclientname $ticker_first) " has a score of ^fs^fc" (getclientpoints $ticker_first) "^fS") 10.0
                                            ]
                                        ] (>= $ticker_second 0) [
                                            local ticker_teamid; ticker_teamid = (at $teamnames $ticker_team)
                                            if $ticker_racetime [
                                                local ticker_cptime; ticker_cptime = (getclientcptime $ticker_first)
                                                if (> $ticker_cptime 0) [
                                                    local ticker_cptime2; ticker_cptime2 = (getclientcptime $ticker_second)
                                                    if (> $ticker_cptime2 0) [
                                                        uitext (concatword (getclientname $ticker_first) " is ahead of " (getclientname $ticker_second) " by ^fs^fc" (timestr (- $ticker_cptime2 $ticker_cptime) $scoreracestyle) "^fS") 10.0
                                                    ] [
                                                        uitext "On your marks.. get set.. GO!" 10.0
                                                    ]
                                                ] [
                                                    uitext (concatword "Current lap for " (getclientname $ticker_first) " is ^fs^fc" (timestr (getclientcpmillis $ticker_first) $scoreracestyle) "^fS") 10.0
                                                ]
                                            ] [
                                                uitext (concatword (getclientname $ticker_first) " is ahead of " (getclientname $ticker_second) " by ^fs^fc" (- (getclientpoints $ticker_first) (getclientpoints $ticker_second)) "^fS points") 10.0
                                            ]
                                        ]
                                    ] [
                                        loopscoregroups ticker_curgroup [] [] [
                                            caseif (< $ticker_first 0) [
                                                ticker_first = (getscoreteam $ticker_curgroup)
                                            ] (< $ticker_second 0) [
                                                ticker_second = (getscoreteam $ticker_curgroup)
                                            ]
                                        ]

                                        caseif (< $ticker_first 0) [
                                            uitext "Waiting for players..." 10.0
                                        ] (< $ticker_second 0) [
                                            local ticker_team; ticker_team = (getscoreteam $ticker_first)
                                            local ticker_total; ticker_total = (getscoretotal $ticker_first)
                                            if $ticker_racetime [
                                                uitext (concatword "Best lap for " (getteamname $ticker_first) " is ^fs^fc" (timestr $ticker_total $scoreracestyle) "^fS") 10.0
                                            ] [
                                                uitext (concatword (getteamname $ticker_first) " has a score of ^fs^fc" $ticker_total "^fS") 10.0
                                            ]
                                        ] (>= $ticker_second 0) [
                                            local ticker_team; ticker_team = (getscoreteam $ticker_first)
                                            local ticker_total; ticker_total = (getscoretotal $ticker_first)
                                            local ticker_team2; ticker_team2 = (getscoreteam $ticker_second)
                                            local ticker_total2; ticker_total2 = (getscoretotal $ticker_second)

                                            if $ticker_racetime [
                                                if (> $ticker_total 0) [
                                                    if (> $ticker_total2 0) [
                                                        uitext (concatword (getteamname $ticker_team) " is ahead of " (getteamname $ticker_team2) " by ^fs^fc" (timestr (- $ticker_total2 $ticker_total) $scoreracestyle) "^fS") 10.0
                                                    ] [
                                                        uitext (concatword "Best lap for " (getteamname $ticker_team) " is ^fs^fc" (timestr $ticker_total $scoreracestyle) "^fS") 10.0
                                                    ]
                                                ] [
                                                    uitext "On your marks.. get set.. GO!" 10.0
                                                ]
                                            ] [
                                                uitext (concatword (getteamname $ticker_first) " is ahead of " (getteamname $ticker_team2) " by ^fs^fc" (- (getclientpoints $ticker_team) (getclientpoints $ticker_team2)) "^fS points") 10.0
                                            ]
                                        ]
                                    ]
                                ] 3 [
                                    uitext (tipshow) 10.0
                                ]
                                uispace 0.495
                            ]
                        ]
                    ]
                ]
                uiclipoffsetx (*f $ticker_mod $uiclipvirtw)
            ]
        ]
    ]
]