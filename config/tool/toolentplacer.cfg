tool_ent_placer_random              = 1
tool_ent_placer_template_focus      = -1
tool_ent_placer_template_list_focus = -1
tool_ent_placer_mod_focus           = 0
tool_ent_placer_templates           = []
tool_ent_placer_mods                = []

TOOL_ENT_PLACER_MOD_ZOFFSET          = 0
TOOL_ENT_PLACER_MOD_ZOFFSET_VARIANCE = 1
TOOL_ENT_PLACER_MOD_ATTR_BEGIN       = 2

TOOL_ENT_PLACER_ATTR_MOD_NONE     = 0
TOOL_ENT_PLACER_ATTR_MOD_VARIANCE = 1
TOOL_ENT_PLACER_ATTR_MOD_CAMYAW   = (<< 1 1)
TOOL_ENT_PLACER_ATTR_MOD_CAMPITCH = (<< 1 2)

tool_ent_placer_mod_mode = 0
tool_ent_placer_mod_param = 0
tool_ent_placer_mod_zoffset = 0

tool_ent_placer_convert_modifiers_v1 = [
    local _mods _mod _mod_val
    _mods = []
    _mod = []
    _mod_val = []

    loop i (getalias tool_ent_placer_num_template_lists) [
        _mods = (getalias (concatword tool_ent_placer_mods_ $i))

        loop j (listlen $_mods) [
            _mod = (at $_mods $j)

            loop k (listlen $_mod) [
                if (>= $k $TOOL_ENT_PLACER_MOD_ATTR_BEGIN) [
                    _mod_val = (at $_mod $k)

                    if $_mod_val [
                        _mod = (listsplice $_mod [[@@TOOL_ENT_PLACER_ATTR_MOD_VARIANCE @@_mod_val]] $k 1)
                    ] [
                        _mod = (listsplice $_mod $TOOL_ENT_PLACER_ATTR_MOD_NONE $k 1)
                    ]
                ]
            ]

            _mods = (listsplice $_mods [[@@_mod]] $j 1)
        ]

        if (!=s $_mods) [
            worldmeta (concatword tool_ent_placer_mods_ $i) $_mods
        ]
    ]
]

// Backwards compat conversion
tool_ent_placer_convert_template_lists = [
    case $tool_saved_version 1 [
        tool_ent_placer_convert_modifiers_v1
    ]
]

tool_ent_placer_reset_focus = [
    tool_ent_placer_templates = []
    tool_ent_placer_mods = []
    tool_ent_placer_template_focus = -1
    tool_ent_placer_template_list_focus = -1
    tool_ent_placer_mod_param = 0
    tool_ent_placer_mod_zoffset = 0
]

tool_ent_placer_on_mapload = [
    tool_ent_placer_reset_focus
    tool_ent_placer_convert_template_lists
]
tool_on_mapload tool_ent_placer_on_mapload

// 1:<name>
tool_ent_placer_find_template_list_slot = [
    local _list_slot _cur_list_name
    _list_slot = -1

    loopwhile i (getalias tool_ent_placer_num_template_lists) [< $_list_slot 0] [
        _cur_list_name = (at (getalias tool_ent_placer_template_list_names) $i)
        if (=s $arg1 $_cur_list_name) [
            _list_slot = $i
        ]
    ]

    result $_list_slot
]

// 1:<name>
tool_ent_placer_set_template_list = [
    local _list_slot
    _list_slot = (tool_ent_placer_find_template_list_slot $arg1)

    if (>= $_list_slot 0) [
        tool_ent_placer_templates = (concatword tool_ent_placer_templates_ $_list_slot)
        tool_ent_placer_template_names = (concatword tool_ent_placer_template_names_ $_list_slot)
        tool_ent_placer_mods = (concatword tool_ent_placer_mods_ $_list_slot)
        tool_ent_placer_template_list_focus = $_list_slot
    ] [
        tool_ent_placer_templates = []
        tool_ent_placer_template_names = []
        tool_ent_placer_mods = []
        tool_ent_placer_template_list_focus = -1
    ]

    tool_ent_placer_template_focus = -1
]

// 1:<requested name>
tool_ent_placer_get_template_list_new_name = [
    local _base_name _new_name _new_name_len _number _idx_start _idx _has_idx _chr
    _base_name = $arg1
    _base_name_len = (strlen $arg1)
    _new_name = $arg1
    _number = 1
    _idx_start = -1
    _idx = -1
    _has_idx = 0

    // Find base name if requested name contains numeric index at the end
    _idx_start = (strstr $_base_name " #")
    _idx = (+ $_idx_start 2)
    if (> $_idx -1) [
        _has_idx = 1

        while [&& [$_has_idx] [< $_idx $_base_name_len]] [
            _chr = (strcode $_base_name $_idx)
            if (|| [< $_chr (strcode "0")] [> $_chr (strcode "9")]) [
                _has_idx = 0
            ]

            _idx = (+ $_idx 1)
        ]
    ]

    if $_has_idx [
        _base_name = (substr $_base_name 0 $_idx_start)
    ]

    while [listhas (getalias tool_ent_placer_template_list_names) $_new_name] [
        _number = (+ $_number 1)

        if (> $_number 1) [
            _new_name = (concatword $_base_name " #" $_number)
        ]
    ]

    result $_new_name
]

// 1:<requested name> 2:<copy from index>
tool_ent_placer_add_template_list = [
    local _new_name _num_lists _list_names _list_slot _init_templates _init_names _init_mods
    _new_name = (tool_ent_placer_get_template_list_new_name $arg1)
    _num_lists = (+ (getalias tool_ent_placer_num_template_lists) 0)
    _list_names = (getalias tool_ent_placer_template_list_names)
    _list_slot = -1

    // Find free list slot
    loopwhile i $_num_lists [< $_list_slot 0] [
        if (=s (at $_list_names $i) "") [
            _list_slot = $i
        ]
    ]

    // Copy data from the requested list to duplicate it
    if (&& [!=s $arg2] [>= $arg2 0] [< $arg2 $_num_lists] [!=s (at $_list_names $arg2)]) [
        _init_templates = $(concatword tool_ent_placer_templates_ $arg2)
        _init_names = $(concatword tool_ent_placer_template_names_ $arg2)
        _init_mods = $(concatword tool_ent_placer_mods_ $arg2)
    ] [
        _init_templates = []
        _init_names = []
        _init_mods = []
    ]

    if (< $_list_slot 0) [
        _list_slot = $_num_lists

        append _list_names [[@@@_new_name]]
        worldmeta tool_ent_placer_template_list_names $_list_names
        worldmeta tool_ent_placer_num_template_lists (+ $_num_lists 1)
        worldmeta (concatword tool_ent_placer_templates_ $_list_slot) $_init_templates
        worldmeta (concatword tool_ent_placer_template_names_ $_list_slot) $_init_names
        worldmeta (concatword tool_ent_placer_mods_ $_list_slot) $_init_mods
    ] [
        _list_names = (listsplice $_list_names [[@@@_new_name]] $_list_slot 1)
        worldmeta tool_ent_placer_template_list_names $_list_names
        worldmeta (concatword tool_ent_placer_templates_ $_list_slot) $_init_templates
        worldmeta (concatword tool_ent_placer_template_names_ $_list_slot) $_init_names
        worldmeta (concatword tool_ent_placer_mods_ $_list_slot) $_init_mods
    ]

    result $_new_name
]

// 1:<name>
tool_ent_placer_rem_template_list = [
    local _template_list_names _list_slot _needs_reset

    _list_slot = (tool_ent_placer_find_template_list_slot $arg1)
    _needs_reset = (= $_list_slot $tool_ent_placer_template_list_focus)

    _template_list_names = (getalias tool_ent_placer_template_list_names)
    _template_list_names = (listsplice $_template_list_names [[]] $_list_slot 1)

    worldmeta tool_ent_placer_template_list_names $_template_list_names
    worldmeta (concatword tool_ent_placer_templates_ $_list_slot) []
    worldmeta (concatword tool_ent_placer_template_names_ $_list_slot) []
    worldmeta (concatword tool_ent_placer_mods_ $_list_slot) []

    if $_needs_reset tool_ent_placer_reset_focus
]

// 1:<old name> 2:<new name>
tool_ent_placer_rename_template_list = [
    local _list_slot _new_name _list_names
    _list_slot = (tool_ent_placer_find_template_list_slot $arg1)
    _new_name = (tool_ent_placer_get_template_list_new_name $arg2)
    _list_names = (getalias tool_ent_placer_template_list_names)
    _list_names = (listsplice $_list_names [[@@_new_name]] $_list_slot 1)

    worldmeta tool_ent_placer_template_list_names $_list_names
]

// 1:<attr idx> 2:<value>
tool_ent_placer_template_update = [
    if (>= $tool_ent_placer_template_focus 0) [
        local _template _templates
        _templates = (getalias $tool_ent_placer_templates)

        _template = (at $_templates $tool_ent_placer_template_focus)
        _template = (listsplice $_template $arg2 (+ $arg1 1) 1)

        worldmeta $tool_ent_placer_templates (listsplice $_templates [[@@_template]] $tool_ent_placer_template_focus 1)
    ] [
        error "tool_ent_placer_template_update error: invalid state!"
    ]
]

// 1:<name>
tool_ent_placer_template_name_update = [
    if (>= $tool_ent_placer_template_focus 0) [
        local _names
        _names = (getalias $tool_ent_placer_template_names)
        worldmeta $tool_ent_placer_template_names (listsplice $_names [[@@arg1]] $tool_ent_placer_template_focus 1)
    ] [
        error "tool_ent_placer_template_name_update error: invalid state!"
    ]
]

// 1:<type> 2:<value> 3:<mod index (optional)>
tool_ent_placer_mod_update = [
    if (< $numargs 3) [
        arg3 = $tool_ent_placer_mod_focus
    ]

    if (>= $tool_ent_placer_template_focus 0) [
        local _mod _mods _mod_val _mod_val_mode _mod_val_param _has_param
        _mods = (getalias $tool_ent_placer_mods)
        _mod = (at $_mods $tool_ent_placer_template_focus)
        _mod_val = (at $_mod $arg3)
        _mod_val_mode = (at $_mod_val 0)
        _mod_val_param = (at $_mod_val 1)
        _has_param = 0

        // Has param if:
        if (|| [
            // Setting a variance modifier to a non-zero value
            && [= $arg1 $TOOL_ENT_PLACER_ATTR_MOD_VARIANCE] [$arg2]
        ] [
            // Setting a different modifier that takes no param, while already having a variance
            // modifer set
            && [!= $arg1 $TOOL_ENT_PLACER_ATTR_MOD_VARIANCE] [& $_mod_val_mode $TOOL_ENT_PLACER_ATTR_MOD_VARIANCE]
        ]) [
            _has_param = 1
        ]

        if $arg2 [
            // Enable a modifier mode
            _mod_val_mode = (| $_mod_val_mode $arg1)
        ] [
            // Disable a modifier mode
            _mod_val_mode = (& $_mod_val_mode (~ $arg1))
        ]


        if $_has_param [
            if (= $arg1 $TOOL_ENT_PLACER_ATTR_MOD_VARIANCE) [
                _mod_val_param = $arg2
            ]
            _mod_val = [[@@_mod_val_mode @@_mod_val_param]]
        ] [
            _mod_val = $_mod_val_mode
        ]

        _mod = (listsplice $_mod $_mod_val $arg3 1)

        worldmeta $tool_ent_placer_mods (listsplice $_mods [[@@_mod]] $tool_ent_placer_template_focus 1)
    ] [
        error "tool_ent_placer_mod_update error: invalid state!"
    ]
]

// 1:<index>
tool_ent_placer_set_template_focus = [
    local _old_mod_focus
    _old_mod_focus = $tool_ent_placer_mod_focus

    tool_ent_placer_template_focus = $arg1

    tool_ent_placer_mod_focus = $TOOL_ENT_PLACER_MOD_ZOFFSET
    tool_ent_placer_mod_zoffset = (tool_ent_placer_mod_get)
    tool_ent_placer_mod_focus = $_old_mod_focus
]

// 1:<index>
tool_ent_placer_template_isfocus = [
    = $arg1 $tool_ent_placer_template_focus
]

tool_ent_placer_template_hasfocus = [
    >= $tool_ent_placer_template_focus 0
]

tool_ent_placer_template_get = [
    if (tool_ent_placer_template_hasfocus) [
        at (getalias $tool_ent_placer_templates) $tool_ent_placer_template_focus
    ]
]

tool_ent_placer_template_name_get = [
    if (tool_ent_placer_template_hasfocus) [
        at (getalias $tool_ent_placer_template_names) $tool_ent_placer_template_focus
    ]
]

tool_ent_placer_mods_get = [
    if (tool_ent_placer_template_hasfocus) [
        at (getalias $tool_ent_placer_mods) $tool_ent_placer_template_focus
    ]
]

tool_ent_placer_mod_get = [
    at (tool_ent_placer_mods_get) $tool_ent_placer_mod_focus
]

tool_ent_placer_mod_get_mode = [
    at (tool_ent_placer_mod_get) 0
]

tool_ent_placer_mod_get_param = [
    + (at (tool_ent_placer_mod_get) 1) 0
]

// 1:<type>
tool_ent_placer_template_add = [
    local _type_idx _num_attrs _template _templates _template_names _mod _mods
    _type_idx = (indexof $tool_ent_types $arg1)
    _num_attrs = (entityattrs $_type_idx 1)

    _template = $arg1
    _mod = [0 0]

    loop i $_num_attrs [
        append _template 0
        append _mod 0
    ]

    _templates = (getalias $tool_ent_placer_templates)
    _template_names = (getalias $tool_ent_placer_template_names)
    _mods = (getalias $tool_ent_placer_mods)

    append _templates [[@@_template]]
    append _template_names [[]]
    append _mods [[@@_mod]]

    worldmeta $tool_ent_placer_templates $_templates
    worldmeta $tool_ent_placer_template_names $_template_names
    worldmeta $tool_ent_placer_mods $_mods

    tool_ent_placer_set_template_focus (- (listlen $$tool_ent_placer_templates) 1)
]

tool_ent_placer_template_froment = [
    local _templates _template_names _mod _mods _type_idx _num_attrs
    _type_idx = (indexof $tool_ent_types (enttype))
    _num_attrs = (entityattrs $_type_idx 1)
    _templates = (getalias $tool_ent_placer_templates)
    _template_names = (getalias $tool_ent_placer_template_names)
    _mods = (getalias $tool_ent_placer_mods)

    _mod = [0 0]
    loop i $_num_attrs [
        append _mod 0
    ]

    append _templates [[@@(entget)]]
    append _template_names [[]]
    append _mods [[@@_mod]]

    worldmeta $tool_ent_placer_templates $_templates
    worldmeta $tool_ent_placer_template_names $_template_names
    worldmeta $tool_ent_placer_mods $_mods

    tool_ent_placer_set_template_focus (- (listlen $$tool_ent_placer_templates) 1)
]

tool_ent_placer_template_duplicate = [
    if (tool_ent_placer_template_hasfocus) [
        local _templates _template_names _mods
        _templates = (getalias $tool_ent_placer_templates)
        _template_names = (getalias $tool_ent_placer_template_names)
        _mods = (getalias $tool_ent_placer_mods)

        append _templates [[@@(tool_ent_placer_template_get)]]
        append _template_names [[@@(tool_ent_placer_template_name_get)]]
        append _mods [[@@(tool_ent_placer_mods_get)]]

        worldmeta $tool_ent_placer_templates $_templates
        worldmeta $tool_ent_placer_template_names $_template_names
        worldmeta $tool_ent_placer_mods $_mods

        tool_ent_placer_set_template_focus (- (listlen $$tool_ent_placer_templates) 1)
    ]
]

// 1:<index>
tool_ent_placer_template_remove = [
    worldmeta $tool_ent_placer_templates (listsplice $$tool_ent_placer_templates "" $arg1 1)
    worldmeta $tool_ent_placer_template_names (listsplice $$tool_ent_placer_template_names "" $arg1 1)
    worldmeta $tool_ent_placer_mods (listsplice $$tool_ent_placer_mods "" $arg1 1)

    if (>= $tool_ent_placer_template_focus (listlen $$tool_ent_placer_templates)) [
        tool_ent_placer_template_focus = -1
    ]
]

// 1:<index>
tool_ent_placer_search = [
    local _ent _mod _proc
    _ent = (at (getalias $tool_ent_placer_templates) $arg1)
    _mod = (at (getalias $tool_ent_placer_mods) $arg1)

    _proc = [
        local _result _val _variance _min _max _mod_val _mod_val_mode _mod_val_param
        _result = (=s (enttype) (at $_ent 0))

        loopwhile i (- (listlen $_ent) 1) [$_result] [
            _mod_val = (at $_mod (+ $i $TOOL_ENT_PLACER_MOD_ATTR_BEGIN))
            _mod_val_mode = (at $_mod_val 0)
            _mod_val_param = (at $_mod_val 1)

            if (& $_mod_val_mode (| $TOOL_ENT_PLACER_ATTR_MOD_CAMYAW $TOOL_ENT_PLACER_ATTR_MOD_CAMPITCH)) [] [
                if (& $_mod_val_mode $TOOL_ENT_PLACER_ATTR_MOD_VARIANCE) [
                    _variance = $_mod_val_param
                ] [
                    _variance = 0
                ]

                _val = (at $_ent (+ $i 1))
                _min = (- $_val $_variance)
                _max = (+ $_val $_variance)

                if (|| [< (entattr $i) $_min] [> (entattr $i) $_max]) [
                    _result = 0
                ]
            ]
        ]

        result $_result
    ]

    entselect _proc
]

tool_ent_placer_canplace = [
    && (= $tool_ent_ui_mode @T_ENT_UI_PLACER) [!=s (getalias $tool_ent_placer_templates) ""] [toolpanel_isopen tool_ent]
]

// 1:<template> 2:<mod> 3:<attribute index>
tool_ent_placer_get_attr = [
    local _value _mod_val _mod_mode _mod_param
    _value = (at $arg1 (+ $arg3 1))
    _mod_val = (at $arg2 (+ $arg3 $TOOL_ENT_PLACER_MOD_ATTR_BEGIN))
    _mod_mode = (at $_mod_val 0)
    _mod_param = (at $_mod_val 1)


    if (& $_mod_mode $TOOL_ENT_PLACER_ATTR_MOD_VARIANCE) [
        _value = (+ $_value (rnd $_mod_param (* $_mod_param -1)))
    ]

    if (& $_mod_mode $TOOL_ENT_PLACER_ATTR_MOD_CAMYAW) [
        _value = (+ $_value (toint (round (getcamerayaw))))
    ]

    if (& $_mod_mode $TOOL_ENT_PLACER_ATTR_MOD_CAMPITCH) [
        _value = (+ $_value (toint (round (getcamerapitch))))
    ]

    result $_value
]

tool_ent_placer_place = [
    if (tool_ent_placer_canplace) [
        local _temp_idx _mod _ent _final_ent _type_idx _num_attrs _rnd _variance _old_zoffset
        if $tool_ent_placer_random [
            _temp_idx = (rnd (listlen (getalias $tool_ent_placer_templates)))
        ] [
            _temp_idx = $tool_ent_placer_template_focus
        ]

        _final_ent = []

        _mod = (at (getalias $tool_ent_placer_mods) $_temp_idx)
        _ent = (at (getalias $tool_ent_placer_templates) $_temp_idx)

        _type_idx = (indexof $tool_ent_types (at $_ent 0))
        _num_attrs = (entityattrs $_type_idx 1)

        _final_ent = (at $_ent 0)

        loop i $_num_attrs [
            append _final_ent (tool_ent_placer_get_attr $_ent $_mod $i)
        ]

        _old_zoffset = $entdropzoffset

        _variance = (at $_mod $TOOL_ENT_PLACER_MOD_ZOFFSET_VARIANCE)
        _rnd = (rnd $_variance (* $_variance -1))
        entdropzoffset (+f (at $_mod $TOOL_ENT_PLACER_MOD_ZOFFSET) $_rnd)

        doargs [
            newent @_final_ent
        ]

        entdropzoffset $_old_zoffset
    ]
]
