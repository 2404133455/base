T_ENT_UI_EDIT = 0
T_ENT_UI_UNIFY = 1
T_ENT_UI_SEARCH = 2
T_ENT_UI_PLACER = 3

exec "config/tool/toolentparam.cfg"

tool_ent_types_env = [
    "light"
    "lightfx"
    "mapmodel"
    "envmap"
    "particles"
    "sound"
    "decal"
    "wind"
    "rail"
    "camera"
]

tool_ent_types_game = [
    "playerstart"
    "weapon"
    "teleport"
    "actor"
    "trigger"
    "pusher"
    "affinity"
    "checkpoint"
    "route"
]

defvarp enteditingonpanel 0 0 1 [
    entediting (? (toolpanel_isopen tool_ent) 1 0)
]

tool_ent_ui_mode = $T_ENT_UI_EDIT

tool_ent_placer_random = 1
tool_ent_placer_template_focus = -1
tool_ent_placer_template_list_focus = -1
tool_ent_placer_templates = []

tool_ent_placer_parse_template_lists = [
    tool_ent_placer_valid_template_lists = []
    tool_ent_placer_num_valid_template_lists = 0

    looplist list (getalias tool_ent_placer_template_list_names) [
        if (!=s $list "") [
            append tool_ent_placer_valid_template_lists [[@@list]]
            tool_ent_placer_num_valid_template_lists = (+ $tool_ent_placer_num_valid_template_lists 1)
        ]
    ]
]
tool_ent_placer_parse_template_lists

tool_ent_placer_reset_focus = [
    tool_ent_placer_templates = []
    tool_ent_placer_template_focus = -1
    tool_ent_placer_template_list_focus = -1
]

tool_ent_placer_on_mapload = [
    tool_ent_placer_reset_focus
    tool_ent_placer_parse_template_lists
]

onevent 0 tool_ent_placer_on_mapload

// 1:<name>
tool_ent_placer_find_template_list_slot = [
    local _list_slot _cur_list_name
    _list_slot = -1

    loopwhile i (getalias tool_ent_placer_num_template_lists) [< $_list_slot 0] [
        _cur_list_name = (at (getalias tool_ent_placer_template_list_names) $i)
        if (=s $arg1 $_cur_list_name) [
            _list_slot = $i
        ]
    ]

    result $_list_slot
]

// 1:<name>
tool_ent_placer_set_template_list = [
    local _list_slot
    _list_slot = (tool_ent_placer_find_template_list_slot $arg1)

    if (>= $_list_slot 0) [
        tool_ent_placer_templates = (concatword tool_ent_placer_templates_ $_list_slot)
    ] [
        tool_ent_placer_templates = []
    ]

    tool_ent_placer_template_focus = -1
]

// 1:<requested name>
tool_ent_placer_get_template_list_new_name = [
    local _new_name _number
    _new_name = $arg1
    _number = 1

    while [listhas $tool_ent_placer_valid_template_lists $_new_name] [
        _number = (+ $_number 1)

        if (> $_number 1) [
            _new_name = (concatword $arg1 " #" $_number)
        ]
    ]

    result $_new_name
]

// 1:<requested name>
tool_ent_placer_add_template_list = [
    local _new_name _num_lists _list_names _list_slot
    _new_name = (tool_ent_placer_get_template_list_new_name $arg1)
    _num_lists = (+ (getalias tool_ent_placer_num_template_lists) 0)
    _list_names = (getalias tool_ent_placer_template_list_names)
    _list_slot = -1

    // Find free list slot
    loopwhile i $_num_lists [< $_list_slot 0] [
        if (=s (at $_list_names $i) "") [
            _list_slot = $i
        ]
    ]

    if (< $_list_slot 0) [
        _list_slot = $_num_lists

        append _list_names [[@@@_new_name]]
        quietworldalias tool_ent_placer_template_list_names $_list_names
        quietworldalias tool_ent_placer_num_template_lists (+ $_num_lists 1)
        quietworldalias (concatword tool_ent_placer_templates_ $_list_slot) []
    ] [
        _list_names = (listsplice $_list_names [[@@@_new_name]] $_list_slot 1)
        quietworldalias tool_ent_placer_template_list_names $_list_names
        quietworldalias (concatword tool_ent_placer_templates_ $_list_slot) []
    ]

    tool_ent_placer_parse_template_lists

    result $_new_name
]

// 1:<name>
tool_ent_placer_rem_template_list = [
    local _template_list_names _list_slot
    _list_slot = (tool_ent_placer_find_template_list_slot $arg1)
    _template_list_names = (getalias tool_ent_placer_template_list_names)
    _template_list_names = (listsplice $_template_list_names [[]] $_list_slot 1)
    quietworldalias tool_ent_placer_template_list_names $_template_list_names
    quietworldalias (concatword tool_ent_placer_templates_ $_list_slot) []

    tool_ent_placer_parse_template_lists
]

// 1:<attr idx> 2:<value>
tool_ent_placer_template_update = [
    if (>= $tool_ent_placer_template_focus 0) [
        local _template _templates
        _templates = (getalias $tool_ent_placer_templates)

        _template = (at $_templates $tool_ent_placer_template_focus)
        _template = (listsplice $_template $arg2 (+ $arg1 1) 1)

        quietworldalias $tool_ent_placer_templates (listsplice $_templates [[@@_template]] $tool_ent_placer_template_focus 1)
    ] [
        error "tool_ent_placer_template_update error: invalid state!"
    ]
]

// 1:<index>
tool_ent_placer_template_isfocus = [
    = $arg1 $tool_ent_placer_template_focus
]

tool_ent_placer_template_hasfocus = [
    >= $tool_ent_placer_template_focus 0
]

tool_ent_placer_template_get = [
    if (tool_ent_placer_template_hasfocus) [
        at (getalias $tool_ent_placer_templates) $tool_ent_placer_template_focus
    ]
]

// 1:<type>
tool_ent_placer_template_add = [
    local _type_idx _num_attrs _template _templates
    _type_idx = (indexof $tool_ent_types $arg1)
    _num_attrs = (entityattrs $_type_idx 1)

    _template = $arg1
    loop i $_num_attrs [
        append _template 0
    ]

    _templates = (getalias $tool_ent_placer_templates)

    append _templates [[@@_template]]
    quietworldalias $tool_ent_placer_templates $_templates

    tool_ent_placer_template_focus = (- (listlen $$tool_ent_placer_templates) 1)
]

tool_ent_placer_template_froment = [
    local _templates
    _templates = (getalias $tool_ent_placer_templates)

    append _templates [[@@(entget)]]
    quietworldalias $tool_ent_placer_templates $_templates

    tool_ent_placer_template_focus = (- (listlen $$tool_ent_placer_templates) 1)
]

tool_ent_placer_template_duplicate = [
    if (tool_ent_placer_template_hasfocus) [
        local _templates
        _templates = (getalias $tool_ent_placer_templates)

        append _templates [[@@(tool_ent_placer_template_get)]]
        quietworldalias $tool_ent_placer_templates $_templates

        tool_ent_placer_template_focus = (- (listlen $$tool_ent_placer_templates) 1)
    ]
]

// 1:<index>
tool_ent_placer_template_remove = [
    quietworldalias $tool_ent_placer_templates (listsplice $$tool_ent_placer_templates "" $arg1 1)

    if (>= $tool_ent_placer_template_focus (listlen $$tool_ent_placer_templates)) [
        tool_ent_placer_template_focus = -1
    ]
]

tool_ent_placer_canplace = [
    && (= $tool_ent_ui_mode @T_ENT_UI_PLACER) [!=s (getalias $tool_ent_placer_templates) ""]
]

tool_ent_placer_place = [
    if (tool_ent_placer_canplace) [
        local _temp_idx
        if $tool_ent_placer_random [
            _temp_idx = (rnd (listlen (getalias $tool_ent_placer_templates)))
        ] [
            _temp_idx = $tool_ent_placer_template_focus
        ]

        doargs [
            newent @(at (getalias $tool_ent_placer_templates) $_temp_idx)
        ]
    ]
]

tool_ent_unify_type = 0
tool_ent_unify_mask = 0

tool_ent_unify_perform = [
    local _ent _ent_type _type_idx _num_attrs
    _ent = (entget)
    _ent_type = (at $_ent 0)
    _type_idx = (indexof $tool_ent_types $_ent_type)
    _num_attrs = (entityattrs $_type_idx)

    entloop [
        if $tool_ent_unify_type [
            enttype $_ent_type
        ]

        if (=s (enttype) $_ent_type) [
            loop attridx $_num_attrs [
                if (& $tool_ent_unify_mask (<< 1 $attridx)) [
                    entattr $attridx (at $_ent (+ $attridx 1))
                ]
            ]
        ]
    ]
]

tool_ent_search_operators = [
    *
    =
    !=
    >
    <
    >=
    <=
    &
]

tool_ent_search_reset_filters = [
    tool_ent_search_mask = 0

    loop attridx 23 [
        [tool_ent_search_attr_@attridx] = 0
        [tool_ent_search_attr_op_@attridx] = 0
    ]
]

tool_ent_search_get_type = [
    if (tool_ent_has_sel) [
        local _type_idx
        _type_idx = (indexof $tool_ent_types (enttype))

        tool_ent_search_type = $_type_idx
    ]
]

// 1:<attr idx>
tool_ent_search_get_attr = [
    if (tool_ent_has_sel) [
        [tool_ent_search_attr_@arg1] = (entattr $arg1)
    ]
]

tool_ent_search_get_filters = [
    if (tool_ent_has_sel) [
        local _type_idx _num_attrs
        _type_idx = (indexof $tool_ent_types (enttype))
        _num_attrs = (entityattrs $_type_idx 1)

        tool_ent_search_type = $_type_idx

        loop attridx $_num_attrs [
            [tool_ent_search_attr_@attridx] = (entattr $attridx)
            [tool_ent_search_attr_op_@attridx] = 1
        ]
    ] [
        tool_ent_search_reset_filters
    ]
]

tool_ent_search_type = 0
tool_ent_search_insel = 0
tool_ent_search_reset_filters

tool_ent_search_filter_proc = [
    local _res _type_idx _num_attrs _op
    _res = (=s (enttype) (at $tool_ent_types $tool_ent_search_type))

    _type_idx = (indexof $tool_ent_types (enttype))
    _num_attrs = (entityattrs $_type_idx 1)

    loopwhile attridx $_num_attrs _res [
        _op = (at $tool_ent_search_operators $[tool_ent_search_attr_op_@attridx])
        if (&& [!=s $_op "*"] [! ($_op (entattr $attridx) $tool_ent_search_attr_@attridx)]) [
            _res = 0
        ]
    ]

    result $_res
]

tool_ent_drag_last_snap_mod = 0
tool_ent_drag_last_entselsnap = $entselsnap
tool_ent_drag_last_entmoving = $entmoving

// Handles CTRL modifier when dragging, to toggle entselsnap
tool_ent_drag_proc = [
    // Init entmove
    if (= $entmoving 1) [
        tool_ent_drag_last_entselsnap = $entselsnap
        tool_ent_drag_last_snap_mod = 0
    ]

    // Called every frame during entmove
    if (entmoving) [
        local _key_mods _snap_mod
        _key_mods = (getkeymodifiers)
        _snap_mod = (? (& $_key_mods $KMOD_CTRL) 1 0)

        if (!= $_snap_mod $tool_ent_drag_last_snap_mod) [
            entselsnap (! $entselsnap)
        ]

        tool_ent_drag_last_snap_mod = $_snap_mod
    ]

    // Called at the end of entmove
    if (&& [! $entmoving] [$tool_ent_drag_last_entmoving]) [
        entselsnap $tool_ent_drag_last_entselsnap
    ]

    tool_ent_drag_last_entmoving = $entmoving
]

tool_proc_add tool_ent_drag_proc

tool_ent_search_find = [
    if $tool_ent_search_insel [
        entselect [&& insel tool_ent_search_filter_proc]
    ] [
        entselect tool_ent_search_filter_proc
    ]
]

// 1:<mode> 2:<init>
tool_ent_toggle_ui_mode = [
    if (&& [toolpanel_isopen tool_ent] [= $tool_ent_ui_mode $arg1]) [
        tool_ent_ui_mode = $T_ENT_UI_EDIT
    ] [
        do $arg2
        tool_ent_ui_mode = $arg1
    ]
]

tool_action_ents = [
    toolpanel_toggle tool_ent right [
        uit_title = "Entities"
    ]
]

tool_action_ent_unify = [
    toolpanel_open tool_ent right [
        uit_title = "Entities"
    ]
    tool_ent_toggle_ui_mode $T_ENT_UI_UNIFY [
        tool_ent_unify_mask = 0
    ]
]

tool_action_ent_search = [
    tool_ent_search_get_filters
    tool_ent_toggle_ui_mode $T_ENT_UI_SEARCH
    toolpanel_open tool_ent right [
        uit_title = "Entities"
    ]
]

tool_action_ent_placer = [
    tool_ent_toggle_ui_mode $T_ENT_UI_PLACER
    toolpanel_open tool_ent right [
        uit_title = "Entities"
    ]
]

tool_action_ent_delete = [
    local _num_ents _ent_type
    _num_ents = (getenginestat 15)

    if (> $_num_ents 0) [
        if (= $_num_ents 1) [
            _ent_type = (enttype)
        ]

        delent

        if (= $_num_ents 1) [
            tool_info_show_action (concat "Removed entity:" $_ent_type) ta_ent_delete
        ] [
            tool_info_show_action (concat "Removed" $_num_ents "entities") ta_ent_delete
        ]
    ] [
        tool_info_show_action "No entities selected" ta_ent_delete
    ]
]

newent_oncamera = [
    local _entdrop_old
    _entdrop_old = $entdrop

    entdrop 0
    newent $arg1
    entdrop $_entdrop_old
]

TOOL_NEWENT_DEFAULT = 0
TOOL_NEWENT_ONCAMERA = 1

tool_newent_mode = $TOOL_NEWENT_DEFAULT

tool_newent = [
    local _cmd
    _cmd = newent

    if (= $tool_newent_mode $TOOL_NEWENT_ONCAMERA) [
        _cmd = newent_oncamera
    ]

    toolpanel_open tool_ent right [
        uit_title = "Entities"
    ]
    $_cmd $arg1

    tool_newent_mode = $TOOL_NEWENT_DEFAULT
]

// 1:<popup type> 2:<on pick>
# tool_ent_pick_env [
    local _text
    _text = "Add environment entity"

    if (= $tool_newent_mode $TOOL_NEWENT_ONCAMERA) [
        append _text "^n^fAOn camera"
    ]

    $arg1 [
        uit_text = [#1 _text]
        uit_item_names = $tool_ent_types_env
        uit_size = 0.25
        uit_nav_enable = 1
        uit_on_select = [
            local _on_pick
            _on_pick = [#1 arg2]
            _on_pick (at $tool_ent_types_env $arg1)
        ]
    ]
]

// 1:<menu type> 2:<on pick>
# tool_ent_pick_game [
    local _text
    _text = "Add game entity"

    if (= $tool_newent_mode $TOOL_NEWENT_ONCAMERA) [
        append _text "^n^fAOn camera"
    ]

    $arg1 [
        uit_text = [#1 _text]
        uit_item_names = $tool_ent_types_game
        uit_size = 0.25
        uit_nav_enable = 1
        uit_on_select = [
            local _on_pick
            _on_pick = [#1 arg2]
            _on_pick (at $tool_ent_types_game $arg1)
        ]
    ]
]

// 1:<on pick> 2:<action context>
# tool_ent_pick [
    local _menutype _text
    _menutype = (? (= $arg2 $TOOL_ACTION_CTX_KEY) toolpanel_open_pie toolpanel_open_menu)
    _text = "New entity"

    if (= $tool_newent_mode $TOOL_NEWENT_ONCAMERA) [
        append _text "^n^fAOn camera"
    ]

    $_menutype [
        uit_text = [#1 _text]
        uit_item_names = [
            "Environment entities"
            "Game entities"
        ]
        uit_size = 0.25
        uit_nav_enable = 1
        uit_on_select = [
            if $arg1 [
                tool_ent_pick_game #1 _menutype [#1 arg1]
            ] [
                tool_ent_pick_env #1 _menutype [#1 arg1]
            ]
        ]
    ]
]

tool_add_ent = [
    tool_ent_pick [
        tool_newent $arg1
    ] $tool_action_ctx
]

tool_action_ent_add = [
    tool_newent_mode = $TOOL_NEWENT_DEFAULT

    if (tool_ent_placer_canplace) [
        tool_ent_placer_place
    ] [
        tool_add_ent
    ]
]

tool_action_ent_add_on_camera = [
    tool_newent_mode = $TOOL_NEWENT_ONCAMERA
    tool_add_ent
]

tool_action_ent_findinsel = [
    entselect insel
]

tool_action ta_ents [
    short_desc "Entities panel"
    long_desc "Open the entities panel"
    icon "textures/icons/edit/ent"
    category "Entities"
    action [
        tool_action_ents
    ]
]

tool_action ta_ent_sel_links [
    short_desc "Select linked entities"
    long_desc "Select linked entities"
    icon "textures/icons/shock"
    category "Entities"
    action [
        selentlinks
    ]
]

tool_action ta_ent_unify [
    short_desc "Unify entities"
    long_desc "Unify properties of all selected entities"
    icon "textures/icons/editing"
    category "Entities"
    action [
        tool_action_ent_unify
    ]
]

tool_action ta_ent_search [
    short_desc "Search entities"
    long_desc "Search entities by their properties"
    icon "textures/icons/edit/find"
    category "Entities"
    action [
        tool_action_ent_search
    ]
]

tool_action ta_ent_placer [
    short_desc "Entity placer"
    long_desc "Open entity placer"
    icon "textures/icons/attack"
    category "Entities"
    action [
        tool_action_ent_placer
    ]
]

tool_action ta_ent_delete [
    short_desc "Delete entities"
    long_desc "Delete selected entities"
    icon "textures/icons/action"
    category "Entities"
    action [
        tool_action_ent_delete
    ]
]

tool_action ta_ent_add [
    short_desc "Add entity"
    long_desc "Add a new entity. Places templates when using entity placer."
    icon "textures/icons/action"
    category "Entities"
    action [
        tool_action_ent_add
    ]
]

tool_action ta_ent_add_on_camera [
    short_desc "Add entity on camera"
    long_desc "Add a new entity at the current camera position"
    icon "textures/icons/action"
    category "Entities"
    action [
        tool_action_ent_add_on_camera
    ]
]

tool_action ta_ent_chain_link [
    short_desc "Chain-link entities"
    long_desc "Links selected entities in an ordered chain"
    icon "textures/icons/shock"
    category "Entities"
    action [
        entlink
    ]
]

tool_action ta_ent_star_link [
    short_desc "Star-link entities"
    long_desc "Links selected entities in a star pattern"
    icon "textures/icons/shock"
    category "Entities"
    action [
        entlink 1
    ]
]

tool_action ta_ent_cyclefocus [
    short_desc "Cycle entity focus"
    long_desc "Cycles focus between selected entities"
    icon "textures/icons/edit/ent"
    category "Entities"
    type @TOOL_ACTION_SCROLL
    action [
        domodifier 10; onrelease entautoview
    ]
]

tool_action ta_ent_deselect [
    short_desc "Deselect entities"
    long_desc "Deselect entities"
    icon "textures/icons/edit/ent"
    category "Entities"
    action [
        entcancel
    ]
]

tool_action ta_ent_selinsel [
    short_desc "Select entities in selection"
    long_desc "Select entities inside of world selection"
    icon "textures/icons/edit/cube"
    category "Entities"
    action [
        entselect insel
    ]
]
