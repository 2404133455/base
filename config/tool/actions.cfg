tool_actions = []
tool_actions_categories = []
tool_actions_num = 0

TOOL_ACTION_CTX_DEFAULT = 0
TOOL_ACTION_CTX_KEY = 1
TOOL_ACTION_CTX_SEARCH = 2

tool_action_ctx = $TOOL_ACTION_CTX_DEFAULT

// 1:<action name> 2:<context>
tool_do_action = [
    tool_action_ctx = $arg2
    doargs (listassoc=s $$arg1 action)
]

TBI_KEY = 0
TBI_KMOD = 1

TOOL_ACTION_PRESS = 0
TOOL_ACTION_SCROLL = 1

// 1:<name> 2:<key> 3:<modifiers>
tool_set_bind_info = [
    [tb_@arg1] [ [@@arg2] @arg3 ]
]

// 1:<name>
tool_get_bind_info = [
    result $[tb_@arg1]
]

// 1:<name>
tool_has_bind_info = [
    result (identexists [tb_@arg1])
]

// 1:<name>
tool_str_bind_info = [
    local _bind_info _key _modifiers
    _bind_info = (tool_get_bind_info $arg1)
    _key = (at $_bind_info @TBI_KEY)
    _modifiers = (at $_bind_info @TBI_KMOD)

    result (prettybindinfo $_key $_modifiers)
]

// 1:<name>
toolunbind = [
    local _bind_info _key _modifiers
    _bind_info = (tool_get_bind_info $arg1)
    _key = (at $_bind_info @TBI_KEY)
    _modifiers = (at $_bind_info @TBI_KMOD)

    if $_key [
        editbind $_key [] $_modifiers
        tool_set_bind_info $arg1 "" 0
    ]
]

// 1:<key> 2:<key modifier mask>
gettoolbind = [
    local _binding _prefix _offset
    _prefix = "tool_do_action "
    _binding = (geteditbind $arg1 $arg2)
    _offset = (strstr $_binding $_prefix)

    if (> $_offset -1) [
        _offset = (+ $_offset (strlen $_prefix))
        _binding = (substr $_binding $_offset)
        result (at $_binding 0)
    ] [
        result ""
    ]
]

// 1:<key> 2:<name> 3:<key modifier mask>
toolbind = [
    local _old_action
    _old_action = (gettoolbind $arg1 $arg3)

    toolunbind $arg2

    if $_old_action [
        tool_set_bind_info $_old_action "" 0
    ]

    tool_set_bind_info $arg2 $arg1 $arg3

    editbind $arg1 [
        tool_do_action @arg2 $TOOL_ACTION_CTX_KEY
    ] $arg3
]

// 1:<category>
tool_action_add_category = [
    if (! (listhas $tool_actions_categories $arg1)) [
        append tool_actions_categories $arg1
        tool_actions_categories = (sortlist $tool_actions_categories a b [naturalsort $a $b])
        [tool_actions_@arg1] = []
    ]
]

// 1:<action name> 2:<prop>
tool_action_prop = [
    result [ (listassoc=s $@arg1 @arg2) ]
]

// 1:<action name> 2:<prop>
tool_action_get_prop = [
    listassoc=s $$arg1 $arg2
]

// 1:<name> 2:<properties>
tool_action = [
    local _newaction _cat
    _newaction = []

    append _newaction [name @arg1]
    append _newaction $arg2

    if (! (listhas $tool_actions $arg1)) [
        append tool_actions $arg1
    ]

    $arg1 = $_newaction

    if (! (tool_has_bind_info $arg1)) [
        defsvarp [tb_@arg1] ""
    ]

    _cat = @(tool_action_prop [$arg1] category)

    tool_action_add_category $_cat
    append [tool_actions_@_cat] $tool_actions_num

    tool_actions_num = (+ $tool_actions_num 1)
]
